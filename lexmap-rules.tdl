; More general contrast between native and generic le's: 
; PRED is a string for native (open-class) and a type for generics

; Throw out generics whenever native entries are available:
; distinguished by value of +ONSET, which is con_or_voc for native
; words, and unk_onset for generics.
; - Exception: keep unknown-proper-name if token has initial capital
; letter (hence has [+CLASS initcap]) even if native entry exists.
;

filter-generic-lowercase := lexmap-rule &
  [ +CONTEXT < [ SYNSEM.PHON.ONSET con_or_voc ] >,
    +INPUT   < [ SYNSEM.PHON.ONSET unk_onset,
		 ORTHTYPE loweralpha ] >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].

filter-dup-generic-noun := lexmap-rule &
  [ +CONTEXT < [ SYNSEM.PHON.ONSET unk_onset,
		 TOKENS.FIRST.+TNT.+TAGS.FIRST "NN" ] >,
    +INPUT   < [ SYNSEM.PHON.ONSET unk_onset,
		 TOKENS.FIRST.+TNT.+TAGS.FIRST "FW" ] >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].

; If generic_mass_count, discard simple generic_mass
;
filter-generic-mass-noun := lexmap-rule &
  [ +CONTEXT < [ SYNSEM [ PHON.ONSET unk_onset,
			  LOCAL.AGR.DIV - ],
		 TOKENS.FIRST.+TNT.+TAGS.FIRST "NN" ] >,
    +INPUT   < [ SYNSEM [ PHON.ONSET unk_onset,
			  LOCAL.AGR [ PNG.PN 3s, DIV + ] ],
		 TOKENS.FIRST.+TNT.+TAGS.FIRST "NN" ] >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].

; Discard plural if alternative, and FORM does not end in 's'
;
filter-generic-pl-noun-2 := lexmap-rule &
  [ +CONTEXT < [ SYNSEM [ PHON.ONSET unk_onset,
			  LOCAL.AGR.DIV - ],
		 TOKENS.FIRST [ +FORM "/.*!s/",
				+TNT.+TAGS.FIRST "NN" ] ] >,
    +INPUT   < [ SYNSEM [ PHON.ONSET unk_onset,
			  LOCAL.AGR [ PNG.PN 3p, DIV + ] ],
		 TOKENS.FIRST.+TNT.+TAGS.FIRST "NN" ] >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].

filter-proper-for-noun := lexmap-rule &
  [ +CONTEXT < [ SYNSEM [ PHON.ONSET unk_onset,
			  LOCAL.AGR [ PNG.PN 3s, DIV + ] ],
		 TOKENS.FIRST.+TNT.+TAGS.FIRST "NN" ] >,
    +INPUT   < [ SYNSEM [ PHON.ONSET unk_onset,
			  LKEYS.KEYREL.PRED named_unk_rel ] ] >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].

filter-genericcaps-name-pn := lexmap-rule &
  [ +CONTEXT < [ SYNSEM [ PHON.ONSET con_or_voc,
			  LKEYS.KEYREL.PRED abstr_named_np_rel ] ] >,
    +INPUT   < [ SYNSEM.PHON.ONSET unk_onset,
		 ORTHTYPE initcap ] >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].

filter-genericcaps-name-ne := lexmap-rule &
  [ +CONTEXT < [ ORTHTYPE namedentity ] >,
    +INPUT   < [ SYNSEM [ PHON.ONSET unk_onset,
			  LKEYS.KEYREL.PRED named_unk_rel ] ] >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].

filter-genericcaps-name-title := lexmap-rule &
  [ +CONTEXT < [ SYNSEM title_synsem & [ PHON.ONSET con_or_voc ] ] >,
    +INPUT   < [ SYNSEM [ PHON.ONSET unk_onset,
			  LKEYS.KEYREL.PRED named_unk_rel ] ] >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].

filter-genericcaps-name-dir := lexmap-rule &
  [ +CONTEXT < n_-_c-dir_le >,
    +INPUT   < n_-_pn-caps-unk_le >,
    +OUTPUT  < >,
    +POSITION "I1=C1" ].
