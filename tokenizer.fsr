;;; -*- mode: fundamental; coding: utf-8; indent-tabs-mode: t; -*-


;;;
;;;
;;;
;;;
;;;
;;;
;;;
;;;
;;;


;;
;; preprocessor rules versioning; auto-maintained upon CVS (or SVN) check-in
;; 
@$Date: 2008/08/02 16:50:39 $

;;
;; tokenization pattern: after normalization, the string will be broken up at
;; each occurrence of this pattern; the pattern match itself is deleted.
;;
:[ \t]+

;;;
;;; string rewrite rules: all matches, over the entire string, are replaced by
;;; the right-hand side; grouping (using `(' and `)') in the pattern) and group
;;; references (`\1' for the first group, et al.) carry over part of the match.
;;;

;;
;; pad the full string with trailing and leading whitespace; makes matches for
;; word boundaries a little easier down the road; also, squash multiple spaces.
;;
!^(.+)$								 \1 
!  +								 

;;
;; a set of `mark-up modules', often replacing mark-up character entitities
;; with actual UniCode characters (e.g. |&mdash;| or |---|), or just ditching
;; mark-up that has no bearing on parsing for now (e.g. most wiki mark-up).
;; these modules can be activated selectively by name in the REPP environment
;; or the top-level call into REPP.
;;
>xml
>latex
>wiki

;;
;; two special cases involving periods: map ASCII ellipsis (|...|) to a single
;; UniCode character (|…|), and convert |..| between numbers into an n-dash,
;; i.e. a numeric range (typically tokenized off, i.e. |42| |–| |43|).
;;
!([^.])\.\.\.([^.])						\1 … \2
!([0-9.]) *\.\. *([0-9.])					\1 – \2


;;
;; some UniCode characters force token boundaries: m-dash, n-dash, ellipsis.
;;
!([–—…])							 \1 

;;
;; deviating from the PTB conventions, we use one-character double quote marks
;; (i.e. |“| and |"| instead of |``| and |''|).  when expecting LaTeX mark-up,
;; maybe, one could interpret |''| as a proper closing quote (|”|), but people
;; are not very consistent about their quotes, even when writing LaTeX.  thus,
;; we distinguish proper opening quotes (|“| and |‘|) and ambiguous quotes (|"|
;; and |'|).  do this prior to tokenizing off other characters (group #1 below)
;; to make adjacent whitespace detection possible, e.g. in |"yes!"|.
;;
!``								“
!`								‘
!''								"

;;
;; remove space after initial |O'| and |L'|, i.e. irish and romance names, to
;; avoid stripping off their apostrophes.
;;
! ([OlL])' 							 \1'

;;
;; the ERG opts to squash some variation in punctuation (even though probably
;; we should preserve the contrast between opening and closing quotes, as it
;; would unambiguously correspond to prefix vs. suffix positions).  for now, 
;; accomodate the way the grammar expects things to come into the parser.
;;
![‘’‚‹›`]							'
![“”„«»]							"

;;
;; a new REPP facility: named groups and iterative group calls.  there are a
;; number of characters that PTB tokenizes off (unconditionally, it seems, in
;; the original `tokenizer.sed'), though not when they are parts of names or
;; NE patterns, e.g. |AT&T| or |http://www.emmtee.net/?foo.php&bar=42|.  thus,
;; we only want these as separate tokens when they are preceded or followed by
;; whitespace; this leaves a problem with, say, |http://www.emmtee.net/|, where
;; one would have to apply NE recognition (what used to be `ersatzing') _prior_
;; to tokenization.
;;
;; either way, because characters we want to tokenize off might be `clustered'
;; with each other, e.g. |(42%), |, the notion of adjacent whitespace needs to
;; apply transitively through such clusters.  it seems an iterative group is
;; the most straightforward way of getting that effect.  the rules from the
;; group will be applied repeatedly (in order) at the time the group is called
;; (by means of the `>' operator), until there are no further matches.  we need
;; to be careful to avoid indefinite recursion within the group, i.e. not add
;; duplicate spaces.  thus, ditch multiple spaces initially.
;;
;; at this point, we exclude a few punctuation characters from this policy, in
;; part because that is the PTB approach (|-| and |/|), in part because they
;; can be prefixes or suffixes of one-token named entities, i.e. |<| and |>| in
;; URLs and email addresses.  to work around these, we may need a string-level
;; `ersatzing' facility, associating a sub-string (that can be unambiguously 
;; identified by surface properties, e.g. a URL) with an identifier of a token
;; class.
;;
;; like in the original PTB script, periods are only tokenized off in sentence-
;; final position, possibly followed only by closing quote marks or parentheses.
;;
!  +							 
#1
!([^ ])([][(){}?!,;:@#$€¢£¥%&“‘'"]) ([^ ]|$)			\1 \2 \3
!([^ ])\. ([])}'" ]*)$						\1 . \2
!(^|[^ ]) ([][(){}?!,;:@#$€¢£¥%&“‘'"])([^ ])			\1 \2 \3
#

>1

;;
;; any word-final apostrophe, by now, should be separated (e.g. |abrams'| -->
;; |abrams '|).  which only leaves contracted forms, including the undesirable
;; PTB ones, e.g. |don't| --> |do n't| and |cannot| --> |can not| (but not the
;; more obscure ones: |gimme|, |lemme|, |'tis|, |wanna|, et al.
;;
!([^ ])'([dDmMsS]) 						\1 '\2 
!([^ ])'(ll|LL|re|RE|ve|VE) 					\1 '\2 
!([^ ])(n't|N'T) 						\1 \2 
! (can)(not)|(CAN)(NOT) 					 \1 \2 
