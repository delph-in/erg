;;; -*- Mode: tdl; Coding: utf-8; -*-
;;;
;;;  Copyright Emily M. Bender 2003-2005
;;;  All Rights Reserved.
;;;  No use or redistribution without permission.
;;;
;;;  mal-letypes.tdl
;;;
;;;  mal-letypes for Arboretum project.
;;;

mal_v_inf_cp_comp_le := main_verb_mliszt &
   [ SYNSEM mal_v_inf_cp_comp_verb &
       [ LOCAL.CAT.VAL.COMPS < [ OPT - ] > ],
     GENRE robust ].

mal_va_does_le := do_pres & 
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ < [ --SIND.PNG png & [ PN 3s ] ] >,
    GENRE robust ].

mal_va_doesnt_neg_pres_le := do_aux_neg_pres & 
  [ SYNSEM.LOCAL.AGR.PNG png & [ PN 3s ],
    GENRE robust ].

mal_va_dont_neg_pres_le := do_aux_neg_pres &
  [ SYNSEM.LOCAL.AGR.PNG png & [ PN -3s ],
    GENRE robust].

mal_va_do_fin_le := do_pres &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ < [ --SIND.PNG png & [ PN -3s ] ] >,
    GENRE robust].

mal_va_has_le := has_aux_lex_ent &
  [ GENRE robust ].

mal_va_have_fin_le := have_fin_aux_lex_ent &
  [ GENRE robust ].

mal_v_vp_has-n_le := have_aux_neg_lex_entry & have_pres & 
  [ SYNSEM.LOCAL.AGR.PNG.PN -3s,
    GENRE robust ].

mal_v_vp_have-f-n_le := have_aux_neg_lex_entry & have_pres & 
  [ SYNSEM.LOCAL.AGR.PNG.PN 3s,
    GENRE robust ].

mal_vc_there_synsem := there_cop_verb &
  [ LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.MINORS 
					      [ ALTMIN def_udef_some_a_q_rel,
						NORM no_rel ] ].
mal_vc_there_is_le := vc_there_are_lexent &
  [ GENRE robust,
    ALTS.VPELLIP -,
    SYNSEM mal_vc_there_synsem ].

mal_vc_there_are_le := vc_there_is_lexent &
  [ GENRE robust,
    ALTS.VPELLIP -,
    SYNSEM mal_vc_there_synsem ].

mal_vc_there_was_le := vc_there_were_lexent &
  [ GENRE robust,
    ALTS.VPELLIP -,
    SYNSEM mal_vc_there_synsem ].

mal_vc_there_were_le := vc_there_was_lexent &
  [ GENRE robust,
    ALTS.VPELLIP -,
    SYNSEM mal_vc_there_synsem ].

mal_v_np-xp_is-n_le := be_are_neg_contr_lex_entry & be_th_cop_neg &
  [ GENRE robust ].

mal_v_np-xp_are-n_le := be_is_neg_contr_lex_entry & be_th_cop_neg &
  [ GENRE robust ].

mal_v_np-xp_was-n_le := be_were_neg_contr_lex_entry & be_th_cop_neg &
  [ GENRE robust ].

mal_v_np-xp_were-n_le := be_was_neg_contr_lex_entry & be_th_cop_neg &
  [ GENRE robust ].

mal_vc_prd_is_le := vc_prd_are_lexent &
  [ ALTS.VPELLIP -,
    GENRE robust ].

mal_vc_prd_are_le := vc_prd_is_lexent &
  [ ALTS.VPELLIP -,
    GENRE robust ].

mal_vc_prd_was_le := vc_prd_were_lexent &
  [ ALTS.VPELLIP -,
    GENRE robust ].

mal_vc_prd_were_le := vc_prd_was_lexent &
  [ ALTS.VPELLIP -,
    GENRE robust ].

; Treat robust "be" and "been" as VFORM prp, so they trigger the robust
; subj-head rule which needs to avoid allowing ordinary base form verbs
; (identical to non-3sg) and ordinary past participles (identical to 
; past tense).
mal_vc_prd_be_le := vc_prd_be_lexent &
  [ SYNSEM.LOCAL.CAT.HEAD.VFORM prp,
    ALTS.VPELLIP -,
    GENRE robust ].

mal_vc_np_be_le := vc_np_be_lexent &
  [ SYNSEM.LOCAL.CAT.HEAD.VFORM prp,
    ALTS.VPELLIP -,
    GENRE robust ].

mal_vc_prd_been_le := vc_prd_been_lexent &
  [ SYNSEM.LOCAL.CAT.HEAD.VFORM prp,
    ALTS.VPELLIP -,
    GENRE robust ].

mal_vc_np_been_le := vc_np_been_lexent &
  [ SYNSEM.LOCAL.CAT.HEAD.VFORM prp,
    ALTS.VPELLIP -,
    GENRE robust ].

mal_det_div_le := det_word_nonque &
  [ SYNSEM [ LOCAL [ CAT.VAL.SPR < [ LOCAL.CAT.HEAD.MINORS.MIN
                                                     just_only_deg_rel ] >,
                     AGR.PNG.PN 3s ],
             LKEYS.KEYREL.ARG0 [ PNG png & [ PN 3s ],
                                 DIV + ],
	     MODIFD hasmod ],
    GENRE robust ].

; 'they/that'
mal_n_deictic_pro_le := n_deictic_pro_lexent &
  [ SYNSEM [ LOCAL.CAT.HEAD.MINORS.NORM norm_rel,
             LKEYS.KEYREL.ARG0.PNG png & [ PN 3p,
                                           GEN neut ] ] ].

mal_det_pl_le := det_pl_lexent &
  [ GENRE robust ].

mal_n_plur_ppcomp_le := n_plur_ppcomp_lexent &
  [ GENRE robust ].

; Restrict to lexical idiom NPs, as in "take care of ..."
v_np-pp_e-idm_le := main_verb & 
  [ SYNSEM empty_prep_trans_verb & 
	   [ LOCAL.CAT.VAL.COMPS < [ OPT -,
	                             LEX + ], [ OPT - ] >,
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ],
    IDIOM + ].
