;;; -*- Mode: tdl; Coding: utf-8; -*-
;;;
;;;  Copyright Emily M. Bender 2003-2005
;;;  All Rights Reserved.
;;;  No use or redistribution without permission.
;;;
;;;  mal-letypes.tdl
;;;
;;;  mal-letypes for Arboretum project.
;;;

mal_v_inf_cp_comp_le := main_verb_mliszt &
   [ SYNSEM mal_v_inf_cp_comp_verb &
       [ LOCAL.CAT.VAL.COMPS < [ OPT - ] > ],
     GENRE robust ].

; (ERB 2003-10-23) For subject equi verbs, with the inifinitival
; marker "to" dropped

mal_v_subj_equi_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_bse_verb &
            [ LOCAL.CAT.VAL.COMPS < [ OPT - ] > ],
    GENRE robust ].

mal_va_does_le := do_pres & 
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ < [ --SIND.PNG png & [ PN 3sg ] ] >,
    GENRE robust ].

mal_va_doesnt_neg_pres_le := do_aux_neg_pres & 
  [ SYNSEM.LOCAL.AGR.PNG png & [ PN 3sg ],
    GENRE robust ].

mal_va_dont_neg_pres_le := do_aux_neg_pres &
  [ SYNSEM.LOCAL.AGR.PNG png & [ PN non3sg ],
    GENRE robust].

mal_va_do_fin_le := do_pres &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ < [ --SIND.PNG png & [ PN non3sg ] ] >,
    GENRE robust].

mal_va_has_le := has_aux_lex_ent &
  [ GENRE robust ].

mal_va_have_fin_le := have_fin_aux_lex_ent &
  [ GENRE robust ].

mal_vc_there_is_le := vc_there_are_lexent &
  [ GENRE robust ].

mal_vc_there_are_le := vc_there_is_lexent &
  [ GENRE robust ].

mal_vc_prd_is_le := vc_prd_are_lexent &
  [ GENRE robust ].

mal_vc_prd_are_le := vc_prd_is_lexent &
  [ GENRE robust ].

mal_det_div_le := det_word_nonque &
  [ SYNSEM [ LOCAL [ CAT.VAL.SPR < [ LOCAL.CAT.HEAD.MINORS.MIN
                                                     just_only_deg_rel ] >,
                     AGR.PNG.PN 3sg ],
             LKEYS.KEYREL.ARG0 [ PNG png & [ PN 3sg ],
                                 DIV + ],
	     MODIFD hasmod ],
    GENRE robust ].

; 'they/that'
mal_n_deictic_pro_le := n_deictic_pro_lexent &
  [ SYNSEM [ LOCAL.CAT.HEAD.MINORS.NORM norm_rel,
             LKEYS.KEYREL.ARG0.PNG png & [ PN 3per,
                                           GEN neut ] ] ].

mal_det_pl_le := det_pl_lexent &
  [ GENRE robust ].

mal_n_plur_ppcomp_le := n_plur_ppcomp_lexent &
  [ GENRE robust ].