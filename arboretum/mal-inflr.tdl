;;; -*- Mode: tdl; Coding: utf-8; -*-
;;;
;;;  Copyright Emily M. Bender 2003-2005
;;;  All Rights Reserved.
;;;  No use or redistribution without permission.
;;;
;;;  mal-inflr.tdl
;;;
;;;  mal inflectional lr instances for Arboretum project.
;;;

;; Disagreement rules, one for each direction.  Pair synsem with
;; "wrong" forms: The dog bark/the dogs barks.
;;
;; Constrain this one to only apply to pronominal subject, to avoid spurious
;; analysis using both this rule and the mal_plur_noun_irule
non_third_sg_fin_v_rbst :=
%suffix (!s !ss) (!ss !ssses) (ss sses) (!ty !ties) (ch ches) (sh shes) (x xes) (z zes)
lex_rule_infl_affixed &
[ GENRE robust,
  ND-AFF +,
  SYNSEM.LOCAL non_third_sg_fin_verb &
	[ CAT.VAL.SUBJ < [ --MIN pron_rel ] > ],
  RNAME "MN3S" ].

third_sg_fin_v_rbst := lex_rule_infl_affixed &
[ GENRE robust,
  ND-AFF -,
  ORTH #stem,
  DTR [ ORTH #stem,
	SYNSEM.LOCAL.CAT.HEAD mal_verb & [ --MAL3SG + ] ],
  SYNSEM.LOCAL third_sg_fin_verb,
  RNAME "M3S" ].


;; DPF 10-May-04 - We take plural marking on noun as intended (not to be
;; corrected), so only have one mal infl-rule for nouns to correct 
;; 'two dog bark' to 'two dogs bark'.  We will need a statistical preference 
;; mechanism to choose one of the two possible corrections for 'dog bark':
;; either 'dogs bark' or 'a/the dog barks'.  But have to have both, since
;; 'he bark' should presumably get corrected to 'he barks' (not 'they bark').
;; DPF 07-feb-10 - Changed DTR..MIN norm_nom_rel to reg_nom_rel, so it also
;; applies to measure nouns, as in "they ran twenty six mile."
;; Note that CASE on dtr must be nom to avoid spurious ambiguity for e.g.
;; direct objects where there is no constraint on number.
;; DPF 30-mar-10 - Restrict this to only apply when the determiner is overt, 
;; to avoid spurious ambiguity for e.g. "she told her brother borrow her book"
;; where `brother' formerly got made a plural.  So now we assume that number
;; on nouns is as the writer intended, unless there is a determiner present,
;; which might indicate competing number, as in "most thing are important"
;; the verb to match robustly when needed.

plur_noun_irule_rbst := lex_rule_infl_affixed &
[ GENRE robust,
  ND-AFF -,
  ORTH #stem,
  SYNSEM.LOCAL.CAT.HEAD.--BARE +,
  DTR [ ORTH #stem,
	SYNSEM.LOCAL.CAT [ HEAD [ MINORS.MIN reg_nom_rel,
				  CASE nom ],
			   VAL.SPR < canonical_synsem > ] ],
  SYNSEM.LOCAL plur_noun,
  RNAME "MPLN" ].

#|
mal_mass_noun_orule :=
%suffix (!s !ss) (!ss !ssses) (ss sses) (!ty !ties) (ch ches) (sh shes) (x xes) (z zes)
lex_rule_infl_affixed &
[ GENRE robust,
  ND-AFF +,
  SYNSEM.LOCAL mass_noun ].
|#

; For |Kim, Abrams(,) and Browne|
w_paren_comma-nf_plr :=
%suffix (!. !.\(,\))
punctuation_comma_rule.
