; Restrict to lexical idiom NPs, as in "take care of ..."
v_np-pp_e-idm_le := main_verb & 
  [ SYNSEM empty_prep_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -, 
			               LEX +,
				       LOCAL.CAT.HEAD noun &
					   [ MINORS.MIN bare_nom_rel ] ], 
				     [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ],
    IDIOM + ].

;; Block reflexive pronouns (indeed, any) for second NP, to avoid silly parse
;; for "He cooked the dinner himself"
v_np-np_only_le := main_verb & 
  [ ALTS.DATIVE -,
    SYNSEM ditrans_only_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -,
				       MODIFD.RPERIPH bool ], 
				     [ OPT -,
				       --MIN basic_nonpro_rel,
				       MODIFD.RPERIPH bool ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; Prevent misanalysis of |He gave his math students homework.|
;; and |he buys his cats cat toys|
;; Also exclude pronouns for second NP.
;;
v_np-np_le := main_verb & 
  [ SYNSEM ditrans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -,
	                               MODIFD.RPERIPH bool ], 
	                             [ OPT -,
				       --MIN basic_nonpro_rel,
	                               MODIFD.RPERIPH bool ] > ],
	     LKEYS [ --OCOMPKEY #ocmin,
		     --+OCOMPKEY #ocmin ] ] ].

;; Pronoun as second NP
v_np-np_le_rbst := main_verb & 
  [ ALTS.DATIVE -,
    SYNSEM ditrans_only_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -,
	                               MODIFD.RPERIPH bool,
				       NONLOC.SLASH 0-dlist ], 
	                             [ OPT -,
				       --MIN pron_rel,
	                               MODIFD.RPERIPH bool ] > ],
	     LKEYS [ --OCOMPKEY #ocmin,
		     --+OCOMPKEY #ocmin ] ],
   GENRE robust ].

;; DPF 2017-04-03 - Added COMPS..MOD..MODIFD.RPERIPH constraint to prevent 
;; robust analysis of |the milk went bad|.
;;
v_adv_le := norm_basic_main_verb &
  [ SYNSEM adv_verb & 
      [ LOCAL.CAT.VAL.COMPS.FIRST.LOCAL.CAT.HEAD.MOD.FIRST.MODIFD.RPERIPH + ],
    INFLECTD - ].

;; Prevent rare passives to avoid spurious analysis of e.g. 
;; |I arrived felt bored|
v_np-prd_oeq_le := main_verb_mliszt & 
  [ SYNSEM obj_equi_non_trans_prd_verb ].
