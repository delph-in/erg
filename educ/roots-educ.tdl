;; DPF 2019-02-12 - Removed RITAL - since it blocks |We arise at two o' clock."|
root_robust := phrase &
  [ INFLECTD +,
    SYNSEM basic_root_synsem &
             [ LOCAL [ CAT [ HEAD verb &
				  [ MOD *anti_list*,
				    VFORM fin_or_bse_or_imp ],
			     MC + ],
                       CONJ cnil ] ],
    GENRE robust,
    ORTH root_orth ].

;; DPF 2019-02-12 - Removed RITAL - since it blocks |We arise at two o' clock."|
root_robust_s := phrase &
  [ INFLECTD +,
    SYNSEM basic_root_synsem &
             [ LOCAL [ CAT [ HEAD verb_or_conj-s & 
				  [ MOD *anti_list*,
				    VFORM fin_or_bse_or_imp ],
			     VAL.SUBJ *anti_list*,
			     MC + ],
		       CONT.HOOK.INDEX.SF #sf,
                       CONJ cnil ],
	       PUNCT [ RPUNCT comma_or_clause_or_pair_or_no_punct &
		              [ PSF #sf ] ] ],
    GENRE robust,
    ORTH root_orth ].

;; Redefine this to exclude robust daughters, to avoid unwanted ambiguity for
;; e.g. |bees sting|
;;
root_lex := sign &
  [ SYNSEM basic_lex_synsem &
           [ LEX +,
             LOCAL [ CAT.HEAD non_frag &
			      [ AUX na ],
                     CONJ cnil ],
             NONLOC.SLASH.LIST < >,
             PUNCT.RPUNCT clause_or_no_punct ],
    INFLECTD +,
    GENRE standard,
    ORTH root_orth ].

;; Likewise for fragments - exclude robust subconstituents for now
;;
root_inffrag := sign &
  [ SYNSEM root_frag_synsem,
    GENRE standard,  
    ORTH root_orth ].

;; "largest trees"
;; Exclude "first he marked ..."
root_robust_nbar_frag := sign &
  [ SYNSEM [ LOCAL [ CAT [ HEAD non_partn &
				[ MINORS [ MIN gen_nom_nbar_rel,
					   ALTMIN def_explicit_q_rel ],
				  --FRAG + ],
			   VAL [ SPR *cons* & [ FIRST.OPT - ],
				 COMPS *null* ] ],
		     CONJ cnil_or_lexconj ],
	     NONLOC.SLASH.LIST < > ],
    GENRE robust,
    ORTH root_orth ].
