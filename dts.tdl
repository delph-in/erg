;; DPF 2013-06-21 (with oe) - The feature --DT is the bundle of properties 
;; related to DT dependency annotation, passed in from the +HD attribute on
;; tokens, and passed up through constructions from one distinguished daughter
;; (treated in such annotations as the sometimes arbitrarily assigned 
;; 'dependency head' of each phrase) to the mother.

cat_min1 :+
 [ --DT token_head ].

basic_word :+
  [ SYNSEM.LOCAL.CAT.--DT #dt,
    TOKENS.+LAST.+TRAIT.+HD #dt ].

basic_root_synsem :+
  [ LOCAL.CAT.--DT.+LL.-CTYPE- "ROOT" ].


;; The following constraints determine which daughter in a
;; construction will be treated as the `head' for the purposes of
;; projecting bilexical dependencies in a derivation tree.  Unary
;; syntactic constructions and all lexical rules trivially point to
;; the single daughter.  Binary headed phrases point to the head
;; daughter, and other binary phrases record a sometimes arbitrary
;; decision.

;; Unary phrases

basic_unary_phrase :+
  [ SYNSEM.LOCAL.CAT.--DT #dt,
    ARGS.FIRST.SYNSEM.LOCAL.CAT.--DT #dt ].

;; Lexical rules

lex_rule_supermost :+
  [ SYNSEM.LOCAL.CAT.--DT #dt,
    ARGS.FIRST.SYNSEM.LOCAL.CAT.--DT #dt ].

;;; Binary headed phrases

basic_binary_headed_phrase :+
  [ HD-DTR.SYNSEM.LOCAL.CAT.--DT.+TI #ti,
    NH-DTR sign & [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ],
    RNAME #ctype ].

dt_head_first_phrase := phrase &
  [ SYNSEM.LOCAL.CAT.--DT #dt,
    ARGS < [ SYNSEM.LOCAL.CAT.--DT #dt & [ +TI #ti ] ],
	   [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ] >,
    RNAME #ctype ].

dt_head_final_phrase := phrase &
  [ SYNSEM.LOCAL.CAT.--DT #dt,
    ARGS < [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ],
	   [ SYNSEM.LOCAL.CAT.--DT #dt & [ +TI #ti ] ] >,
    RNAME #ctype ].

basic_head_initial :+ dt_head_first_phrase.
basic_head_marker_phrase :+ dt_head_first_phrase.
head_final :+ dt_head_final_phrase.
adj_head_inv_phrase :+ dt_head_final_phrase.

;; Non-headed phrase types

np_pred_phr :+ dt_head_final_phrase.
basic_n_v-or-adj_cmpnd_phr :+ dt_head_final_phrase.
measure_np_phrase :+ dt_head_final_phrase.
measure_np_symbol_phrase :+ dt_head_first_phrase.
num_noun_sequence_phrase :+ dt_head_first_phrase.
fw_sequence_left_phrase :+ dt_head_first_phrase.
fw_sequence_right_phrase :+ dt_head_first_phrase.
basic_coord_phr :+ dt_head_first_phrase.
adj_noun_coord_rule :+ dt_head_first_phrase.
noun_adj_coord_rule :+ dt_head_final_phrase.
filler_head_rel_rule :+ dt_head_final_phrase.
basic_run_on_np_rule :+ dt_head_first_phrase.
np_s_phrase :+ dt_head_final_phrase.
frag_l_mod_np_rule :+ dt_head_final_phrase.
frag_l_mod_pp_rule :+ dt_head_final_phrase.
frag_l_mod_adv_rule :+ dt_head_final_phrase.
basic_frag_r_mod_np_rule :+ dt_head_first_phrase.
frag_r_mod_nbar_rule :+ dt_head_first_phrase.
frag_r_mod_scop_pp_rule :+ dt_head_first_phrase.
frag_r_mod_ap_rule :+ dt_head_first_phrase.
#|
basic_conj_red_c_c_rule :+ dt_head_first_phrase.

basic_conj_red_x_m_rule :+
  [ SYNSEM.LOCAL.CAT.--DT #dt,
    ARGS < [ SYNSEM.LOCAL.CAT.--DT #dt & [ +TI #ti ] ],
	   [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ],
	   [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ],
	   [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ] >,
    RNAME #ctype ].

basic_conj_nov_subj_rule :+
  [ SYNSEM.LOCAL.CAT.--DT #dt,
    ARGS < [ SYNSEM.LOCAL.CAT.--DT #dt & [ +TI #ti ] ],
	   [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ],
	   [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ],
	   [ SYNSEM.LOCAL.CAT.--DT [ +LL #ctype, +TG #ti ] ] >,
    RNAME #ctype ].
|#
