;; Treat comma-spliced sentences as robust
;; DPF 2016-03-01 - Exclude fragments on either side, to avoid spurious analyses
;;
cl-cl_runon-cma_c := run_on_coord_comma_rule &
  [ ARGS < [ SYNSEM [ LEX -,
                      LOCAL.CAT.HEAD verb ] ],
	   [ SYNSEM [ LEX -,
		      MODIFD notmod_or_rmod,
		      LOCAL [ CAT.HEAD verb,
		              CONT.HOOK.INDEX.SF prop ] ] ] >,
    RNAME snsc,
    GENRE robust ].

;; Also treat all runons with period as robust, but not semicolons
;; DPF 2016-03-01 - Exclude fragments on either side, to avoid spurious analyses
;;
cl-cl_runon_c := run_on_coord_rule &
  [ ARGS < [ SYNSEM [ PUNCT.RPUNCT semicol_punct,
                      LOCAL.CAT.HEAD verb ] ],             
           [ SYNSEM.LOCAL.CAT.HEAD verb ] >,
    RNAME sns ].

;; DPF 2016-03-01 - Exclude fragments on either side, to avoid spurious analyses
;;
cl-cl_runon-period_c := run_on_coord_rule &
  [ ARGS < [ SYNSEM [ PUNCT.RPUNCT clause_punct,
                      LOCAL.CAT.HEAD verb ] ],             
           [ SYNSEM.LOCAL.CAT.HEAD verb ] >,
    RNAME snsp,
    GENRE robust ].

;; |snow-covered house| with oblig hyphen
n-v_j-cpd_c := n_v_cmpnd_rule &
  [ RNAME nvc,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT hyphen_sgl ].

;; |snow covered house| with no hyphen
n-v_j-cpd_c_rbst := n_v_cmpnd_rule &
  [ RNAME nvc,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT no_punct,
    GENRE robust ].

;; |dog-happy kid| with oblig hyphen
n-j_j-t-cpd_c := n_adj_cmpnd_rule &
  [ RNAME natc,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT hyphen_sgl ].

;; |dog happy kid| with no hyphen
n-j_j-t-cpd_c_rbst := n_adj_cmpnd_rule &
  [ RNAME natc,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT no_punct,
    GENRE robust ].

;; |dog-friendly family| with oblig hyphen
n-j_j-cpd_c := n_adj_intr_cmpnd_rule &
  [ RNAME naic,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT hyphen_sgl ].

;; |dog friendly family| with no hyphen
n-j_j-cpd_c_rbst := n_adj_intr_cmpnd_rule &
  [ RNAME naic,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT no_punct,
    GENRE robust ].

;; |dog barked|
hdn_bnp_c_rbst := bare_np_sg_phrase_robust &
  [ RNAME bnpr ].

;; Split NP top coord into two, so we can insist on a comma for leftmost in
;; multiple coord, to avoid |cats dogs and mice|
;;
;; Prevent bare singular as conjunct: |Note that _MATH_ and _MATH_|
np-np_crd-t_c := np_coord_top_eg_rule &
  [ RNAME ctnp,
    ARGS < [ SYNSEM [ LOCAL.CAT.HEAD.--BARE -,
                      PUNCT.RPUNCT comma_or_semi_or_pair_or_no_punct ] ],
           [ SYNSEM.LOCAL [ CAT.HEAD.--BARE -,
	     		    CONJ lex-conj ] ] > ].

np-np_crd-t-mult_c := np_coord_top_eg_rule &
  [ ARGS < [ SYNSEM.PUNCT.RPUNCT comma_punct ],
           [ SYNSEM.LOCAL.CONJ phr-conj ] >,
    RNAME ctnp ].


;; Enforce Oxford comma in three-part coord of NPs and of VPs
;;
np-np_crd-im_c := np_coord_mid_ig_rule &
  [ RNAME cmni,
    GENRE robust ].
vp-vp_crd-nfin-im_c := vp_coord_nonfin_mid_ig_phr &
  [ RNAME cmvpi,
    GENRE robust ].
vppr-vppr_crd-im_c := vp_predp_coord_mid_ig_phr &
  [ RNAME cmvpy,
    GENRE robust ].
pp-pp_crd-im_c := pp_coord_mid_ig_phr &
  [ RNAME cmppi,
    GENRE robust ].

; "One books"
num_det_1_rbst := mal_num_det_rule_1 &
  [ RNAME ndr1 ].

; "Twenty book"
num_det_2_rbst := mal_num_det_rule_2 &
  [ RNAME ndr2 ].

#|
; |equation _REF_| => |Equation _REF_|
np-hdn_ttl-cpd_c := np_title_cmpnd_rule &
  [ ARGS.FIRST.ORTH.CLASS.+CASE capitalized,
    RNAME npnt ].

np-hdn_ttl-cpd_c_rbst := np_title_cmpnd_rule &
  [ ARGS < [ ORTH.CLASS.+CASE non_capitalized ],
           [ ORTH.CLASS.+CASE mixed,
	     SYNSEM.LOCAL.CONT.HOOK.INDEX.PNG.PN 3s ] >,
    GENRE robust,
    RNAME npntl ].

np-hdn_ttl-cpd-pl_c := np_title_cmpnd_pl_rule &
  [ ARGS.FIRST.ORTH.CLASS.+CASE capitalized,
    RNAME nptp ].

np-hdn_ttl-cpd-pl_c_rbst := np_title_cmpnd_pl_rule &
  [ ARGS < [ ORTH.CLASS.+CASE non_capitalized ],
           [ ORTH.CLASS.+CASE mixed,
	     SYNSEM.LOCAL.CONT.HOOK.INDEX conj_ref-ind ] >,
    GENRE robust,
    RNAME nptpl ].
|#

; |admiring Kim we arise| - robust if no comma
aj-hd_scp_c := adjh_s_rule &
  [ RNAME mhs,
    NH-DTR.SYNSEM [ PUNCT.RPUNCT comma_punct,
                    LEX - ] ].

aj-hd_scp-lex_c := adjh_s_rule &
  [ RNAME mhs,
    NH-DTR.SYNSEM [ PUNCT.RPUNCT pair_or_no_punct,
                    LEX + ] ].

aj-hd_scp_c_rbst := adjh_s_rule &
  [ RNAME mhs,
    NH-DTR.SYNSEM [ PUNCT.RPUNCT pair_or_no_punct,
                    LEX - ],
    HD-DTR.SYNSEM.LEX -,
    GENRE robust ].

;; Discard binary fragment rules
aj-np_frg_c := never_unify_rule.
aj-np_int-frg_c := never_unify_rule.
aj-pp_frg_c := never_unify_rule.
aj-pp_i-frg_c := never_unify_rule.
aj-r_frg_c := never_unify_rule.
np-aj_frg_c := never_unify_rule.
np-aj_rorp-frg_c := never_unify_rule.
np-aj_j-frg_c := never_unify_rule.
nb-aj_frg_c := never_unify_rule.
pp-aj_frg_c := never_unify_rule.
j-aj_frg_c := never_unify_rule.
j_frg_c := never_unify_rule.

hdn-np_app-nbr_c := never_unify_rule.
np-hdn_cty-cpd_c := never_unify_rule.
cl_adv_c := never_unify_rule.