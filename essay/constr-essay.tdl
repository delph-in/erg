;; Treat comma-spliced sentences as robust
cl-cl_runon-cma_c := never_unify_rule.
cl-cl_runon-cma_c_rbst := run_on_coord_comma_rule &
  [ ARGS < [ SYNSEM.LEX - ], 
	   [ SYNSEM [ LEX -,
		      MODIFD notmod_or_rmod,
		      LOCAL.CONT.HOOK.INDEX.SF prop ] ] >,
    RNAME snsc,
    GENRE robust ].

;; Prevent robust-comma-marked fragments as left dtr
;;
cl-cl_runon_c := run_on_coord_rule &
  [ ARGS < [ SYNSEM [ LOCAL.CAT.HEAD verb,
		      LEX - ] ],
	   [ SYNSEM [ LOCAL.CAT.HEAD verb_or_frag,
		      LEX - ] ] >,
    RNAME sns ].

;; |snow-covered house| with oblig hyphen
n-v_j-cpd_c := n_v_cmpnd_rule &
  [ RNAME nvc,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT hyphen_sgl ].

;; |snow covered house| with no hyphen
n-v_j-cpd_c_rbst := n_v_cmpnd_rule &
  [ RNAME nvc,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT no_punct,
    GENRE robust ].

;; |dog-happy kid| with oblig hyphen
n-j_j-t-cpd_c := n_adj_cmpnd_rule &
  [ RNAME natc,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT hyphen_sgl ].

;; |dog happy kid| with no hyphen
n-j_j-t-cpd_c_rbst := n_adj_cmpnd_rule &
  [ RNAME natc,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT no_punct,
    GENRE robust ].

;; |dog-friendly family| with oblig hyphen
n-j_j-cpd_c := n_adj_intr_cmpnd_rule &
  [ RNAME naic,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT hyphen_sgl ].

;; |dog friendly family| with no hyphen
n-j_j-cpd_c_rbst := n_adj_intr_cmpnd_rule &
  [ RNAME naic,
    ARGS.FIRST.SYNSEM.PUNCT.RPUNCT no_punct,
    GENRE robust ].

;; |dog barked|
hdn_bnp-rbst_c := bare_np_sg_phrase_robust &
  [ RNAME bnpr ].

;; Prevent bare singular as conjunct: |Note that _MATH_ and _MATH_|
np-np_crd-t_c := np_coord_top_eg_rule &
  [ RNAME ctnp,
    ARGS < [ SYNSEM.LOCAL.CAT.HEAD.--BARE - ],
           [ SYNSEM.LOCAL.CAT.HEAD.--BARE - ] > ].


;; Enforce Oxford comma in three-part coord of NPs and of VPs
;;
np-np_crd-im_c := never_unify_rule.
np-np_crd-im_c_rbst := np_coord_mid_ig_rule &
  [ RNAME cmni,
    GENRE robust ].
vp-vp_crd-nfin-im_c := never_unify_rule.
vp-vp_crd-nfin-im_c_rbst := vp_coord_nonfin_mid_ig_phr &
  [ RNAME cmvpi,
    GENRE robust ].
vppr-vppr_crd-im_c := never_unify_rule.
vppr-vppr_crd-im_c_rbst := vp_predp_coord_mid_ig_phr &
  [ RNAME cmvpy,
    GENRE robust ].
pp-pp_crd-im_c:= never_unify_rule.
pp-pp_crd-im_c_rbst := pp_coord_mid_ig_phr &
  [ RNAME cmppi,
    GENRE robust ].
