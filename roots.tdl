;;; -*- Mode: TDL; Package: DISCO -*-
;;;
;;;  Copyright (c) 1994-2004
;;;    Dan Flickinger, Rob Malouf, Emily M. Bender
;;;    see erg/licence.txt for conditions
;;;
;;;  roots.tdl
;;;

; The instances in this file are needed by the parser to know which phrase 
; types to accept as successful parses of the full input string.

root_strict := phrase &
  [ INFLECTD +,
    ROBUST -,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verbal &
                                [ TAM ind_or_mod_subj_tam],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS < > ],
                           MC + ],
                     CONT [ HOOK.INDEX.E.TENSE real_tense,
                            MSG message ],
                     CONJ cnil ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ],
             PUNCT.RPUNCT clause_or_no_punct ] ].

; Copy of above, but with ROBUST +
root_strict_robust := phrase &
  [ INFLECTD +,
    ROBUST +,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verbal &
                                [ TAM ind_or_mod_subj_tam ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS < > ],
                           MC + ],
                     CONT.HOOK.INDEX.E.TENSE real_tense,
                     CONJ cnil ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ],
             ROOT - ] ].

; DPF 30-Nov-99 - Removed VAL.COMPS < >, to allow words in, and added
; CONJ cnil to block unwanted partial conjunct analyses.
; DPF 9-Mar-01 - Removed [ MOOD strict_sort ] to allow run-on sentences with
; mixed mood, as in "I would arrive he left".
; DPF 10-Sep-01 - Added SPR *olist* to block nbars.

root_phr := sign &
  [ SYNSEM phr_synsem &
	   [ LOCAL [ CAT [ HEAD [ INV -,
                                  TAM.MOOD ind_or_mod_subj,
                                  KEYS.KEY norm_rel ],
			   VAL [ COMPS < >,
                                 SPR *olist* ] ],
		     CONJ cnil,
                     CONT.MSG no_msg ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ],
             LEX -,
             ROOT - ],
    INFLECTD +,
    ROBUST -,
    POSSCL - ].

; Copy of above, but with ROBUST +
root_phr_robust := sign &
  [ SYNSEM phr_synsem &
	   [ LOCAL [ CAT [ HEAD [ INV -,
                                  TAM.MOOD ind_or_mod_subj,
                                  KEYS.KEY norm_rel ],
			   VAL [ COMPS < >,
                                 SPR *olist* ] ],
		     CONJ cnil,
                     CONT.MSG no_msg ],
	     NONLOC [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ],
             ROOT -,
             LEX - ],
    INFLECTD +,
    ROBUST +,
    POSSCL - ].

root_lex := sign &
  [ SYNSEM [ LEX +,
             LOCAL [ CAT [ HEAD.AUX na,
                           VAL.COMPS < > ],
                     CONJ cnil ] ],
    INFLECTD +,
    POSSCL - ].

; DPF 09-Jan-00 - Removed MSG message from root_conj, to admit eg "or on 
; Tuesday"

root_conj := sign &
  [ SYNSEM [ LOCAL [ CAT.VAL.COMPS < >,
		     CONJ lex-conj & [ CHEAD.LEFT.FIRST cnil ] ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ],
             ROOT - ],
    INFLECTD +,
    POSSCL - ].

root_subord := sign &
  [ SYNSEM [ LOCAL [ CAT [ HEAD prep,
			   VAL.COMPS < > ],
                     CONT.MSG message,
		     CONJ cnil ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ],
             ROOT - ],
    INFLECTD +,
    POSSCL - ].

root_gap := sign &
  [ SYNSEM [ LOCAL.CAT.HEAD no_head,
             ROOT + ] ].

; Removed KEY relation, since distinguish nocop_vp by [KEY no_rel]

root_nocop_id := phrase & 
  [ INFLECTD +,
    ROBUST -,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verb &
                                [ PRD -,
                                  VFORM fin,
                                  MOD < anti_synsem >,
                                  KEYS.KEY no_rel ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS < > ],
                           MC na_or_+ ],
                     CONT.HOOK.INDEX.E.TENSE no_tense,
                     CONJ cnil ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ],
             ROOT - ] ].

; Copy of above, but with ROBUST +
root_nocop_id_robust := phrase & 
  [ INFLECTD +,
    ROBUST +,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verb &
                                [ PRD -,
                                  VFORM fin,
                                  MOD < anti_synsem >,
                                  KEYS.KEY no_rel ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS < > ],
                           MC na_or_+ ],
                     CONT.HOOK.INDEX.E.TENSE no_tense,
                     CONJ cnil ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ],
             ROOT - ] ].

root_nocop_prd := phrase & 
  [ INFLECTD +,
    ROBUST -,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verb &
                                [ PRD -,
                                  VFORM fin,
                                  MOD < synsem >,
                                  KEYS.KEY no_rel ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS < > ],
                           MC na_or_+ ],
                     CONT.HOOK.INDEX.E.TENSE no_tense,
                     CONJ cnil ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ],
             ROOT - ] ].

; Copy of above, but with ROBUST +
root_nocop_prd_robust := phrase & 
  [ INFLECTD +,
    ROBUST +,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verb &
                                [ PRD -,
                                  VFORM fin,
                                  MOD < synsem >,
                                  KEYS.KEY no_rel ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS < > ],
                           MC na_or_+ ],
                     CONT.HOOK.INDEX.E.TENSE no_tense,
                     CONJ cnil ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ],
             ROOT - ] ].

root_eheader := phrase & 
  [ INFLECTD +,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verbal &
                                [ TAM ind_or_mod_subj_tam ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS < > ],
                           MC - ],
                     CONT [ HOOK.INDEX ref-ind,
                            MSG message ],
                     CONJ cnil ],
             NONLOC [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ],
             ROOT - ] ].

root_frag := sign &
  [ SYNSEM synsem &
	   [ LOCAL [ CAT.HEAD frag,
                     CONJ cnil ],
             NONLOC.SLASH 0-dlist & [ LIST < > ] ] ].

root_non_idiom := sign &
  [ IDIOM - ].