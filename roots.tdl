;;; -*- Mode: TDL; Package: DISCO -*-
;;;
;;;  Copyright Daniel Flickinger 1994-1999
;;;  All Rights Reserved.
;;;  No use or redistribution without permission.
;;;
;;;  roots.tdl
;;;

; The instances in this file are needed by the PAGE parser
; to know which phrase types to accept as successful parses of the full 
; input string.  They should be the only instances of phrase that fail to 
; assign some other status  (e.g. 'rule or 'lex-entry), so they alone get to 
; be of status 'root-node (assigned on type sign), which is what the parser 
; requires.  This is obscure, but works.

root_strict := phrase &
  [ INFLECTED +,
    ROOT -,
    ROBUST -,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verbal & strict_type &
                                [ TAM ind_or_mod_subj_tam],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS *olist* ],
                           MC + ],
                     CONT.INDEX.E.TENSE real_tense,
                     CONJ cnil,
                     KEYS.MESSAGE 1-dlist ],
              NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ] ] ].

; Copy of above, but with ROBUST +
root_strict_robust := phrase &
  [ INFLECTED +,
    ROOT -,
    ROBUST +,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verbal & strict_type &
                                [ TAM ind_or_mod_subj_tam ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS *olist* ],
                           MC + ],
                     CONT.INDEX.E.TENSE real_tense,
                     CONJ cnil ],
              NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ] ] ].

; DPF 30-Nov-99 - Removed VAL.COMPS *olist*, to allow words in, and added
; CONJ cnil to block unwanted partial conjunct analyses.
; DPF 9-Mar-01 - Removed [ MOOD strict_sort ] to allow run-on sentences with
; mixed mood, as in "I would arrive he left".
; DPF 10-Sep-01 - Added SPR *olist* to block nbars.

root_phr := sign &
  [ SYNSEM phr_synsem &
	   [ LOCAL [ CAT [ HEAD strict_type &
				[ INV -,
                                  TAM.MOOD ind_or_mod_subj ],
			   VAL [ COMPS *olist*,
                                 SPR *olist* ] ],
		     CONJ cnil,
		     KEYS [ KEY relation,
                            MESSAGE 0-dlist ] ],
	     NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ],
             LEX -* ],
    ROOT -,
    INFLECTED +,
    ROBUST -,
    POSSCL - ].

; Copy of above, but with ROBUST +
root_phr_robust := sign &
  [ SYNSEM phr_synsem &
	   [ LOCAL [ CAT [ HEAD strict_type &
				[ INV -,
                                  TAM.MOOD ind_or_mod_subj ],
			   VAL [ COMPS *olist*,
                                 SPR *olist* ] ],
		     CONJ cnil,
		     KEYS [ KEY relation,
                            MESSAGE 0-dlist ] ],
	     NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ],
             LEX -* ],
    ROOT -,
    INFLECTED +,
    ROBUST +,
    POSSCL - ].

root_lex := sign &
  [ SYNSEM [ LEX +,
             LOCAL.CONJ cnil ],
    INFLECTED +,
    POSSCL - ].

; DPF 09-Jan-00 - Removed MESSAGE 1-dlist from root_conj, to admit eg "or on Tuesday"

root_conj := sign &
  [ SYNSEM [ LOCAL [ CAT.VAL.COMPS *olist*,
		     CONJ lex-conj & [ CHEAD.LEFT.FIRST cnil ] ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ] ],
    ROOT -,
    INFLECTED +,
    POSSCL - ].

root_subord := sign &
  [ SYNSEM [ LOCAL [ CAT [ HEAD prep,
			   VAL.COMPS *olist* ],
		     KEYS.MESSAGE 1-dlist,
		     CONJ cnil ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ] ],
    ROOT -,
    INFLECTED +,
    POSSCL - ].

root_gap := sign &
  [ ROOT +,
    SYNSEM.LOCAL.CAT.HEAD no_head ].

; Removed KEY relation, since distinguish nocop_vp by [KEY no_rel]

root_nocop_id := phrase & 
  [ INFLECTED +,
    ROOT -,
    ROBUST -,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verb & strict_type &
                                [ PRD -,
                                  VFORM fin,
                                  MOD < anti_synsem > ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS *olist* ],
                           MC na_or_+ ],
                     KEYS.KEY no_rel,
                     CONT.INDEX.E.TENSE no_tense,
                     CONJ cnil ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ] ] ].

; Copy of above, but with ROBUST +
root_nocop_id_robust := phrase & 
  [ INFLECTED +,
    ROOT -,
    ROBUST +,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verb & strict_type &
                                [ PRD -,
                                  VFORM fin,
                                  MOD < anti_synsem > ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS *olist* ],
                           MC na_or_+ ],
                     KEYS.KEY no_rel,
                     CONT.INDEX.E.TENSE no_tense,
                     CONJ cnil ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ] ] ].

root_nocop_prd := phrase & 
  [ INFLECTED +,
    ROOT -,
    ROBUST -,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verb & strict_type &
                                [ PRD -,
                                  VFORM fin,
                                  MOD < synsem > ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS *olist* ],
                           MC na_or_+ ],
                     KEYS.KEY no_rel,
                     CONT.INDEX.E.TENSE no_tense,
                     CONJ cnil ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ] ] ].

; Copy of above, but with ROBUST +
root_nocop_prd_robust := phrase & 
  [ INFLECTED +,
    ROOT -,
    ROBUST +,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verb & strict_type &
                                [ PRD -,
                                  VFORM fin,
                                  MOD < synsem > ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS *olist* ],
                           MC na_or_+ ],
                     KEYS.KEY no_rel,
                     CONT.INDEX.E.TENSE no_tense,
                     CONJ cnil ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ] ] ].

root_eheader := phrase & 
  [ INFLECTED +,
    ROOT -,
    POSSCL -,
    SYNSEM [ LOCAL [ CAT [ HEAD verbal & strict_type &
                                [ TAM ind_or_mod_subj_tam ],
                           VAL [ SUBJ *olist*,
                                 SPR *olist*,
                                 COMPS *olist* ],
                           MC - ],
                     CONT.INDEX ref-ind,
                     CONJ cnil,
                     KEYS.MESSAGE 1-dlist ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist & [ LIST < > ],
                         QUE 0-dlist & [ LIST < > ] ] ] ].

