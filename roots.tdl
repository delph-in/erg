;;; -*- Mode: TDL; Package: DISCO -*-
;;;
;;;  Copyright Daniel Flickinger 1994-1999
;;;  All Rights Reserved.
;;;  No use or redistribution without permission.
;;;
;;;  roots.tdl
;;;

; The instances in this file are needed by the PAGE parser
; to know which phrase types to accept as successful parses of the full 
; input string.  They should be the only instances of phrase that fail to 
; assign some other status  (e.g. 'rule or 'lex-entry), so they alone get to 
; be of status 'root-node (assigned on type sign), which is what the parser 
; requires.  This is obscure, but works.

root_strict := phrase & 
  [ ROOT +,
    SYNSEM.LOCAL.CAT.HEAD verbal ].

; DPF 30-Nov-99 - Removed VAL.COMPS *olist*, to allow words in, and added
; CONJ cnil to block unwanted partial conjunct analyses.

root_phr := sign &
  [ SYNSEM phr_synsem &
	   [ LOCAL [ CAT [ HEAD strict_type &
				[ INV -,
                                  TAM.MOOD ind_or_mod_subj & strict_sort ],
			   VAL.COMPS *olist* ],
		     CONJ cnil,
		     CONT.TOP-H handle,
		     KEYS.MESSAGE 0-dlist ],
	     NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ],
	     LEX - ],
    ROOT -,
    INFLECTED + ].

root_lex := sign &
  [ SYNSEM.LEX +,
    INFLECTED + ].

; DPF 09-Jan-00 - Removed MESSAGE 1-dlist from root_conj, to admit eg "or on Tuesday"

root_conj := sign &
  [ SYNSEM [ LOCAL [ CAT.VAL.COMPS *olist*,
		     CONT.TOP-H handle,
		     CONJ complex-conj & [ CHEAD.LEFT.FIRST cnil ] ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ] ],
    ROOT -,
    INFLECTED + ].

root_subord := sign &
  [ SYNSEM [ LOCAL [ CAT [ HEAD prep,
			   VAL.COMPS *olist* ],
		     CONT.TOP-H handle,
		     KEYS.MESSAGE 1-dlist,
		     CONJ cnil ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
			 REL 0-dlist & [ LIST < > ] ] ],
    ROOT -,
    INFLECTED + ].

root_gap := sign &
  [ ROOT +,
    SYNSEM.LOCAL.CAT.HEAD adv ].


