;; This type is for "only if" as a patch given that the transfer machinery
;; currently does not allow reassignment of the top-level INDEX attribute,
;; where in the paraphrase of "if A then B" as "A only if B" the main clause
;; shifts.  So here we propagate the new complement's INDEX to be the main
;; one.
;;
p_cp_s-rev_le := noncqrs-h-nab &
  [ SYNSEM basic_subconj_cp_synsem &
	   [ LOCAL [ CAT [ HEAD.PRD -,
			   VAL [ SPR < [ --MIN just_only_deg_rel ] >,
				 COMPS < [ LOCAL 
					  [ CAT s_cat_unspec & 
						[ MC +,
						  HEAD verb & [ VFORM fin ] ],
					    CONT.HOOK.INDEX #ind &
						[ SF prop-or-comm ] ],
					   --SIND.E.TENSE real_tense,
					   PUNCT.RPUNCT.PSF #iforce,
					   NONLOC.SLASH 0-dlist,
					   OPT - ] > ],
			   POSTHD + ],
		     CONT.HOOK.INDEX #ind ],
	     LKEYS.KEYREL.ARG0.SF #iforce ] ].


aj_pp-cp_i-it-brdg_le := norm_adj_word &
  [ SYNSEM adj_atrans_cp_bridge_synsem &
           [ LOCAL.CAT.VAL.COMPS < [ OPT - ] > ] ].

adj_atrans_cp_bridge_synsem := basic_adj_synsem & isect_synsem & basic_two_arg &
			       prd_synsem & cp_addin_tam_pn &
  [ LOCAL [ CAT [ HEAD basic_adj & [ MINORS [ MIN adj_rel,
					      ALTMIN norm_rel ],
				     MOD < [ LOCAL.CAT.HEAD no_head ] > ],
		  VAL [ COMPS < synsem & #comp &
				[ LOCAL [ CONT.HOOK [ LTOP #chand,
						      INDEX #ind &
							    [ SF basic-prop ] ],
					  CONJ cnil,
					  CAT s_cat_fin_or_inf_v_c & 
						    [ HEAD comp ] ],
				  NONLOC.SLASH.LIST *cplist* ] >,
			SPR.FIRST.LOCAL.CONT.HOOK.XARG #arg0,
			KCMP #comp & [ PUNCT.LPUNCT no_punct ] ] ],
	    CONT [ HOOK [ INDEX #ind,
			  XARG it-ind ],
		   RELS <! relation !>,
		   HCONS <! qeq & [ HARG #arg, LARG #chand ] !>,
		   ICONS <! !> ] ],
    LKEYS [ KEYREL it_adj_arg1_relation & [ ARG1 handle & #arg,
					    ARG0 #arg0 & non_conj_event ],
	    --+ARGIND it-ind ] ].


