;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Macro definitions
;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

:begin :template.

satp ($head=head) := synsem &
  [ LOCAL local &
	  [ CONJ cnil,
	    CAT [ HEAD $head,
		  VALENCE [ SUBJ < >,
			    COMPS *olist*,
			    SPR *olist* ] ] ] ].

;; DPF (30-Apr-98) In satp, SUBJ must be < >, not *olist*, since we need to
;; have SUBJ < > unify with the filler in filler_head phrases, and the filler is
;; constrained to be SUBJ *prolist* which has a distinct synsem type (pro_ss)
;; which is constrained to be [OPT -].

; cp complement has to be MOD empty to exclude relative clauses

; ERB (21-01-98) Moving MOD into HEAD... will create problems if this
; is to be used for CP[to] complements as well, as these will not
; be MOD no-mod.  Yet another problem is that to_c_prop must be
; ROOT na so that it can feed extrasubj.  I conclude that we do
; not want to use one macro for both CP[to] and CP[that].

; ERB (03-02-98) Selected CPs can no longer be ROOT - because subjh_decl
; has to be ROOT + in order to keep it from being the modifier in the
; head-mod construction.  So this macro is now changed to ROOT bool,
; INV -.

cp ($vform=vform) := synsem & @satp($head=verbal) &		     
  [ LOCAL.CAT [ HEAD [ VFORM $vform,
		       MOOD ind_or_mod_subj,
		       INV - ],
		ROOT bool ] ].

nomp ($case=case,$cont=mrs) := @satp($head=nominal) &
  [ LOCAL [ CAT [ HEAD strict_head &
		       [ CASE $case ],
		  ROOT na ],
	    CONT $cont ] ].

np ($case=case,$cont=mrs) := @satp($head=supnoun) &
  [ LOCAL [ CAT.HEAD strict_head &
		     [ CASE $case ],
	    CONT $cont ] ].

; Only used as MOD value for relative clauses, so type LOCAL, not SYNSEM
nbar ($cont=nom-obj) := local &
  [ CAT [ HEAD noun &
	       [ MOD no-mod ],
	  VALENCE [ SUBJ < >,
		    COMPS *olist*,
		    SPR < synsem > ] ],
    CONT $cont,
    ROOT na ].

third-sg-np ( ) := @nomp ( ) & 
  [ LOCAL.CONT.INDEX.PNG 3sg ].

prd ($subj=synsem) :=
  [ LOCAL local &
	  [ CAT [ HEAD [ PRD + ],
		  VALENCE [ SUBJ < $subj >,
			    SPR *olist*,
			    COMPS *olist* ],
		  ROOT na ] ] ].

vp ($vform=vform) :=
  [ LOCAL local &
	  [ CONJ cnil,
	    CAT [ HEAD verbal & strict_head &
		     [ VFORM $vform,
		       INV - ],
		  VALENCE [ SUBJ < synsem >,
			    COMPS *olist* ] ] ] ].
pp () := synsem &
  [ LOCAL local &
	  [ CAT [ HEAD prep & [ PRD - ],
		  VALENCE [ SPR *olist*,
			    COMPS *olist* ],
		  ROOT na ] ] ].

:end :template.
