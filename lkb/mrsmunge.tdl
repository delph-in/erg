;;; type declarations for munge rules 

general_mrs_rule := *avm* &
[ CONTEXT mrs,
  OUTPUT *top* ] .

mrs_rule :=  general_mrs_rule &
[ INPUT mrs,
  CONTEXT mrs,
  OUTPUT mrs ] .

generator_rule :=  general_mrs_rule &
[ CONTEXT mrs,
  OUTPUT string ] .

;;; ALEX's BITS

; good_time_rel and bad_time_rel are relations which take two arguments: a handel and a time.  

good_time_rel := basic_nom_rel.

bad_time_rel := basic_nom_rel.

;;; this is a hacky equals relation for getting the
;;; good-time time at the top handle to equal the bound variable in the re-write 
;;; rules for adding good-time and bad-time to MRSs.

alex_equals_rel := relation &
[ALEXARG1 individual,
 ALEXARG2 individual].

;;; alex_after_rel is used when good-time is a constant that should
;;; be specified to be temporally after a time variable
;;; introduced in the MRS.

alex_after_rel := temp_loc_rel.

;;; alex_temp_inc_rel is part of the `temporal reasoning' component 
;;; (i.e., the rewrite rules which ensure that 
;;; in "two o'clock on Tuesday", there is something in the MRS that 
;;; stipulates that the "two o'clock" is temporally 
;;; included in Tuesday.

alex_temp_inc_rel := alex_equals_rel.

;;; alex_every_q_rel is the quantifier that is used to re-write 
;;; "No <time-expression> is good" into "Every <time-expression> is
;;; not good" (i.e., where alex_neg_rel is given narrow scope relative
;;; to the quantifier alex_every_rel).

alex_every_q_rel := quant_rel.

alex_neg_rel := neg_rel.

;;; contrast_rel is the contrast relation triggered by things
;;; like "I prefer afternoons".  It's a two argument relation,
;;; taking individuals as its values.  The rules are written so
;;; that the second individual is a complement of the first one.
;;; That is, if the first argument denotes afternoons, the second
;;; argument denotes the complement of afternoons.

contrast_rel := basic_nom_rel &
	       [CONTRASTWITH individual].

;;; complement_of_rel is the complements relation referred to above:

complement_of_rel := basic_nom_rel & 
		      [COMPLEMENTED individual].


