;;; This file contains patches to the tdl files - i.e. changes which
;;; are necessary to make the TDL files work in the LKB which
;;; shouldn't be made to the TDL files themselves

;;; redefines stuff in fundamentals.tdl

*olist* :< *list*.

*ocons* := *olist* & *cons* &
  [ FIRST unexpressed & [ OPT + ],
    REST  *olist* ].

*onull* := *olist* & *null*.


*gaplist* :< *list*.

*gapcons* := *gaplist* & *cons* &
  [ FIRST gap,
    REST  *null* ].

*gapnull* := *gaplist* & *null*.


*unexplist* := *list*.

*unexpcons* := *unexplist* & *cons* &
  [ FIRST unexpressed,
    REST  *null* ].


*unexpnull* := *unexplist* & *null*.



*prolist* := *list*.

*procons* := *prolist* & *cons* &
  [ FIRST pro_ss &
	  [ NON-LOCAL.SLASH <! !> ],
    REST *null* ].

*pronull* := *prolist* & *null*.


; LABEL and META used for labeling nodes in parse trees
; AAC - and they must have their features declared ...

label := synsem &
   [ LABEL-NAME symbol ],
    status: label.

meta := synsem &
  [  META-PREFIX symbol,
     META-SUFFIX symbol ],
    status: meta.


;;; redefine head 
;;; because this is one place where deliberate advantage is being taken of TDL sloppiness

head :=  head_min &
  [ MOD mod_local,
    PRD bool,
    MOOD mood,
    CASE case,
    POSS bool,
    INV bool,
    VFORM vform,
    AUX bool ].

;;; poss-able now becomes redundant, but keep it to avoid more changes

poss-able := *avm*.


lex_entry := word &
  [ STEM < #stem >,
    ARGS < [ STEM < #stem > ] >,
    INFLECTED - ],
  status: lex-entry.

basic_multi_lex_entry := word &
  [ INFLECTED - ],
  status: c-lex-entry.
