;;; Hey, emacs(1), this is -*- Mode: TDL; Package: LKB; -*- got it?

;;; Verb particles (transitive and intransitive)

look+up := verb_prtcl_mtr &
[ INPUT.RELS <! [ PRED "_look_v_up_rel" ],
                [ PRED _up_p_sel_rel ] !>,
  OUTPUT.RELS <! [ PRED "_find_v_1_rel" ] !> ].

carry+on := verb_prtcl_mtr &
[ INPUT.RELS <! [ PRED "_carry_v_on_rel" ],
                [ PRED _on_p_sel_rel ] !>,
  OUTPUT.RELS <! [ PRED "_continue_v_1_rel" ] !> ].

;keep+on := verb_prtcl_cp_mtr &
;[ INPUT [ RELS <! [ PRED "_keep_v_on_rel" ],
;                  [ PRED _on_p_sel_rel ],
;                  relation !> ],
;  OUTPUT.RELS <! [ PRED "_continue_v_2_rel" ], relation !> ].

; Alas, have to stipulate PRED of complement CP's verb - see mrs.tdl notes
keep+on := verb_prtcl_cp_mtr &
[ INPUT [ RELS <! [ PRED "_keep_v_on_rel" ],
                  [ PRED _on_p_sel_rel ], relation !> ],
  OUTPUT.RELS <! [ PRED "_continue_v_2_rel" ], [ PRED "_hire_v_rel" ] !> ].

; Alas, have to stipulate PRED of complement CP's verb - see mrs.tdl notes
get+around+to := verb_prtcl_pp_ger_mtr &
[ INPUT [ RELS <! [ PRED "_get_v_around_rel" ],
                  [ PRED _around_p_sel_rel ],
                  [ PRED _to_p_sel_rel ],
                  relation, relation, relation, [ PRED "_leave_v_1_rel" ] !> ],
  OUTPUT.RELS <! [ PRED "_leave_v_1_rel" ],
                 [ PRED "_eventually_r_rel" ] !> ].

pass+on+to := verb_prtcl_pp_mtr &
[ INPUT [ RELS <! [ PRED "_pass_v_on_rel" ],
                  [ PRED _on_p_sel_rel ],
                  [ PRED _to_p_sel_rel ] !> ],
  OUTPUT.RELS <! [ PRED "_give_v_1_rel" ] !> ].

give+back := verb_np_prtcl_np_mtr &
[ INPUT [ RELS <! [ PRED "_give_v_back_rel" ],
                  [ PRED _back_p_sel_rel ] !> ],
  OUTPUT.RELS <! [ PRED "_return_v_rel" ],
                 [ PRED _to_p_sel_rel ] !> ].

catch+on := verb_prtcl_mtr &
[ INPUT.RELS <! [ PRED "_catch_v_on_rel" ],
                [ PRED _on_p_sel_rel ] !>,
  OUTPUT.RELS <! [ PRED "_realize_v_rel" ] !> ].

;;; Idioms

;;;
;;; _fix_me_
;;; really, given what we know about the grammar, matching on the HCONS for the
;;; `bare_div' quantifier is, let's say, superfluous (to put it mildly), but to
;;; have at least one example showing off some of the transfer capabilities.
;;;
take+immediate+advantage := monotonic_mtr &
[ INPUT [ RELS <! [ PRED "_take_v_i_rel", LBL #h1,
                    ARG0 #e2, ARG1 #x3, ARG2 #x4, ARG3 #x5 ],
                  [ PRED bare_div_q_rel, ARG0 #x4, RSTR #h6 ],
                  [ PRED "_advantage_n_irel", LBL #h7, ARG0 #x4 ],
                  [ PRED "_immediate_j_rel", LBL #h7, ARG0 event, ARG1 #x4 ],
                  [ PRED _of_p_sel_rel, LBL #h1, ARG0 event, ARG2 #x5 ] !>,
          HCONS <! qeq & [ HARG #h6, LARG #h7 ] !> ],
  OUTPUT.RELS <! [ PRED "_benefit_v_from_rel", LBL #h1,
                   ARG0 #e2, ARG1 #x3, ARG2 #x5 ],
                 [ PRED "_immediately_r_rel", LBL #h1, ARG1 #e2 ],
                 [ PRED _from_p_sel_rel, LBL #h1, 
                   ARG0 event & [ E.TENSE nontense ], ARG2 #x5 ] !> ].

take+advantage := monotonic_mtr &
[ INPUT [ RELS <! [ PRED "_take_v_i_rel", LBL #h1,
                    ARG0 #e2, ARG1 #x3, ARG2 #x4, ARG3 #x5 ],
                  [ PRED bare_div_q_rel, ARG0 #x4, RSTR #h6 ],
                  [ PRED "_advantage_n_i_rel", LBL #h7, ARG0 #x4 ],
                  [ PRED _of_p_sel_rel, LBL #h1, ARG0 event, ARG2 #x5 ] !>,
          HCONS <! qeq & [ HARG #h6, LARG #h7 ] !> ],
  OUTPUT.RELS <! [ PRED "_benefit_v_from_rel", LBL #h1,
                   ARG0 #e2, ARG1 #x3, ARG2 #x5 ],
                 [ PRED _from_p_sel_rel, LBL #h1, 
                   ARG0 event & [ E.TENSE nontense ], ARG2 #x5 ] !> ].

keep+close+tabs := monotonic_mtr &
[ INPUT [ RELS <! [ PRED "_keep_v_i_rel", LBL #h1,
                    ARG0 #e2, ARG1 #x3, ARG2 #x4, ARG3 #x5 ],
                  [ PRED bare_div_q_rel, ARG0 #x4, RSTR #h6 ],
                  [ PRED "_close_j_rel", LBL #h7, ARG0 event, ARG1 #x4 ],
                  [ PRED "_tabs_n_i_rel", LBL #h7, ARG0 #x4 ],
                  [ PRED _on_p_sel_rel, LBL #h1, ARG0 event, ARG2 #x5 ] !>,
          HCONS <! qeq & [ HARG #h6, LARG #h7 ] !> ],
  OUTPUT.RELS <! [ PRED "_watch_v_1_rel", LBL #h1,
                   ARG0 #e2, ARG1 #x3, ARG2 #x5 ],
                 [ PRED "_close_r_rel", LBL #h1, ARG1 #e2 ] !> ].

keep+tabs := monotonic_mtr &
[ INPUT [ RELS <! [ PRED "_keep_v_i_rel", LBL #h1,
                    ARG0 #e2, ARG1 #x3, ARG2 #x4, ARG3 #x5 ],
                  [ PRED bare_div_q_rel, ARG0 #x4, RSTR #h6 ],
                  [ PRED "_tabs_n_i_rel", LBL #h7, ARG0 #x4 ],
                  [ PRED _on_p_sel_rel, LBL #h1, ARG0 event, ARG2 #x5 ] !>,
          HCONS <! qeq & [ HARG #h6, LARG #h7 ] !> ],
  OUTPUT.RELS <! [ PRED "_watch_v_1_rel", LBL #h1,
                   ARG0 #e2, ARG1 #x3, ARG2 #x5 ] !> ].

;;; Determinerless PPs

on+edge := detlesspp_mtr &
[ INPUT.RELS <! [ PRED _on_p_rel ],
                [ PRED "_edge_n_rel" ],
                relation !>,
  OUTPUT.RELS <! [ PRED "_nervous_j_rel" ] !> ].

at+long+last := detlesspp+adj_mtr &
[ INPUT.RELS <! [ PRED _at_p_rel ],
                [ PRED "_long_j_rel" ],
                [ PRED "_last_n_rel" ],
                relation !>,
  OUTPUT [ RELS <! [ PRED "_finally_r_rel" ] !>,
           HCONS <! !> ] ].

at+last := detlesspp_mtr &
[ INPUT.RELS <! [ PRED _at_p_rel ],
                [ PRED "_last_n_rel" ],
                relation !>,
  OUTPUT.RELS <! [ PRED "_finally_r_rel" ] !> ].


at+close+range := detlesspp+adj_mtr &
[ INPUT.RELS <! [ PRED _at_p_rel ],
                [ PRED "_close_j_rel" ],
                [ PRED "_range_n_1_rel" ],
                relation !>,
  OUTPUT.RELS <! [ PRED "_from_p_rel", ARG2 #arg0 ],
                 [ PRED _a_q_rel, ARG0 #arg0, RSTR #lbl ],
                 [ LBL #lbl, PRED "_distance_n_rel", ARG0 #arg0 ],
                 [ LBL #lbl, PRED "_small_j_rel", 
                   ARG0 event & [ E.ASPECT.PROGR -, 
                                  E.ASPECT.PERF -,
                                  E.ASPECT.STATIVE - ], 
                   ARG1 #arg0 ] !>].


;;; Light verbs

give+brief+demo_mtr := light_verb_mtr &
[ INPUT.RELS <! [ PRED "_give_v_1_rel" ],
                [ LBL #lbln, PRED "_demo_n_rel", ARG0 #inst, ARG1 #arg1 ],
                [ PRED _of_p_sel_rel ], 
                [ PRED _a_q_rel ],
                [ LBL #lbln, PRED "_brief_j_rel", ARG0 event, ARG1 #inst ] !>,
  OUTPUT.RELS <! [ LBL #lbl, PRED "_demo_v_rel", ARG0 #arg0, ARG2 #arg1 ],
                 [ LBL #lbl, PRED "_briefly_r_rel", ARG1 #arg0 ] !> ].

give+demo_mtr := light_verb_mtr &
[ INPUT.RELS <! [ PRED "_give_v_1_rel" ],
                [ PRED "_demo_n_rel", ARG1 #arg1 ],
                [ PRED _of_p_sel_rel ], 
                [ PRED _a_q_rel ] !>,
  OUTPUT.RELS <! [ PRED "_demo_v_rel", ARG2 #arg1 ] !> ].

give+talk_mtr := light_verb_mtr &
[ INPUT.RELS <! [ PRED "_give_v_1_rel" ],
                [ PRED "_talk_n_rel", ARG1 #arg1 ],
                [ PRED _on_p_sel_rel ], 
                [ PRED _a_q_rel ] !>,
  OUTPUT.RELS <! [ LBL #lbl, PRED "_talk_v_about_rel", ARG0 #arg0 ],
                 [ LBL #lbl, PRED _about_p_rel, ARG1 #arg0, ARG2 #arg1 ] !> ].

