;;; -*- Mode: tdl; Coding: utf-8; -*-
;;;
;;;  Copyright (c) 1994-2018
;;;    Dan Flickinger, Rob Malouf, Emily M. Bender
;;;    see LICENSE for conditions
;;;
;;;  letypes.tdl
;;;
;;;  Lexical entry types (yield of lextypes hierarchy)
;;;
;;;  Created by Dan Flickinger, 15-June-1996
;;;
;;;  $Id: letypes.tdl 7479 2010-02-21 23:11:30Z danf $


; In the following type names, complements marked with "*" in the type names 
; are optional. 

v_-_it_le := main_verb & 
  [ SYNSEM atrans_verb ].

v_vp_it_le := main_verb & 
  [ SYNSEM atrans_inf_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

v_cp_le := main_verb & 
  [ SYNSEM cp_intrans_verb ].

v_cp_comp_le := main_verb & 
  [ SYNSEM cp_intrans_verb_comp ].

v_cp_fin-inf_le := main_verb & 
  [ SYNSEM cp_fin_inf_intrans_verb ].

v_cp_fin-inf-q_le := main_verb & 
  [ SYNSEM cp_ques_fin_inf_intrans_verb ].

v_cp_fin-inf-q-mc_le := main_verb & 
  [ SYNSEM cp_ques_fin_inf_mc_verb ].

;; DPF 2018-04-29 - Prevent these from undergoing the  CP-frag lexical rule,
;; to avoid apparently spurious ambiguity.
;;
v_cp_prop_le := main_verb & 
  [ SYNSEM cp_prop_intrans_verb,
    ALTS.CPFRAG - ].

v_cp_nfin_le := main_verb & 
  [ SYNSEM cp_prop_intrans_nfin_verb ].

; "find": "I find that Kim has left" but "*I find Kim has left"
v_cp_comp-prop_le := main_verb & 
  [ SYNSEM cp_only_prop_intrans_verb ].

; look/seem like
v_cp_like_le := main_verb & 
  [ SYNSEM cp_like_intrans_verb ].

;; inform
v_np-cp_le := main_verb & 
  [ SYNSEM np_trans_cp_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ] ] ].

;; DPF 2016-08-26 - This type is subsumed by the more general 
;; v_cp_fin-inf-q-mc_le which also admits *we asked them what to do*
;; DPF 2017-10-07 - Well, no.  This type takes two complements, an NP and a CP,
;; while v_cp_fin-inf-q-mc_le only takes one complement, a CP.  Sigh.
;; DPF 2020-04-21 - Block cp-frag lexical rule, since it ignores whether the
;; first (NP) complement is obligatory.
;;
; ask
v_np-cp_q_le := main_verb & 
  [ SYNSEM np_trans_cp_ques_verb &
	   [ LOCAL.CAT.VAL.COMPS.FIRST.OPT - ],
    ALTS.CPFRAG - ].

; persuade
v_np-cp_prop_le := main_verb & 
  [ SYNSEM np_trans_cp_prop_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ] ] ].

; tell: tell me whether to stay
v_np-cp_fin-inf_le := main_verb & 
  [ SYNSEM np_trans_cp_fin_or_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ] ] ].

; ask: asked me whether to stay/whether he should jump
v_np-cp_fin-inf-q_le := main_verb & 
  [ SYNSEM np_trans_cp_fin_or_inf_verb &
	   [ LOCAL.CAT.VAL.COMPS < [ OPT - ], [ --SIND.SF ques ] > ] ].

; ask, prefer 'ask that he be released'
v_cp_sbjnct_le := main_verb & 
  [ SYNSEM cp_bse_intrans_verb ].

;; Hack for Sherlock, archaic English
; remark 'That be the house, he remarked'
v_cp_sbjnct-xtr_le := main_verb & 
  [ SYNSEM cp_bse_intrans_xtra_verb ].

; ask, prefer 'ask that he be released'
v_cp_sbjnct-fin_le := main_verb & 
  [ SYNSEM cp_sbjnct_intrans_verb ].

; wait
v_cp_inf-only_le := main_verb & 
  [ SYNSEM for_to_verb ].

; try
v_vp_seq_le := main_verb & 
  [ SYNSEM subj_equi_verb ].

; refrain from
v_vp_seq-from_le := main_verb & 
  [ SYNSEM subj_equi_from_verb ].

v_prd_seq_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_prd_verb ].

v_prd_seq-idm_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_prd_adj_verb,
    IDIOM + ].

; run low (on X)
v_prd_seq-va_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_prd_v_a_verb ].

; run low (on X)
v_ap_seq_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_prd_adj_verb ].

; want into
v_pp_seq_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_prd_prep_verb ].

v_pp_seq-e_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_empty_prep_verb ].

; stop working
; go shopping
v_vp_seq-prp_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_prp_verb ].

; help
v_vp_seq-bse_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_bse_verb ].

; 'I will go read', 'Go read' '*He went read'
v_vp_seq-go_le := main_verb_mliszt & 
  [ SYNSEM subj_equi_bse_or_n3sg_verb ].

; 'We needn't stay' but "*He needs not stay", "*We will need not stay"
; cf. "He needn't stay" (irreg)
v_vp_ssr-n3sg_le := main_verb_newltop &
  [ SYNSEM subj_rais_bse_n3sg_verb ].

; "need not"
v_vp_ssr-n3sg-r_le := aux_verb_word_super &
  [ INFLECTD +,
    SYNSEM subj_rais_bse_n3sg_adv_verb ].

; have yet to
v_p-vp_ssr_le := ssr_particle_v_lexent &
  [ SYNSEM.LKEYS [ --COMPKEY #cmin,
                   --+COMPKEY #cmin ] ].

; they try and find it
v_vp_seq-and_le := main_verb & 
  [ SYNSEM subj_equi_and_fin_verb ].

; he will try and find it
v_vp_seq-and-bse_le := main_verb & 
  [ SYNSEM subj_equi_and_bse_verb ].

; couldn't help but notice
v_vp_seq-but_le := main_verb & 
  [ SYNSEM subj_equi_but_verb ].

v_np-vp_oeq_le := main_verb & 
  [ SYNSEM obj_equi_verb ].

v_np-vp_oeq-ntr_le := main_verb & 
  [ SYNSEM obj_equi_non_trans_verb ].

; help
v_np-vp_oeq-bse_le := main_verb_mliszt & 
  [ SYNSEM obj_equi_bse_verb ].

v_np-vp_oeq-psv_le := main_verb & 
  [ SYNSEM obj_equi_trans_psv_verb ].

v_np-prd_oeq_le := main_verb_mliszt & 
  [ SYNSEM obj_equi_prd_verb ].

v_np-ap_oeq_le := main_verb_mliszt & 
  [ SYNSEM obj_equi_prd_adj_verb ].

; have x ready
; have x in stock
; I want x confirmed
; get x ready
v_np-prd_oeq-ntr_le := main_verb_mliszt & 
  [ SYNSEM obj_equi_non_trans_prd_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].


; I have X to get rid of
v_np-vpslnp_oeq_le := main_verb & 
  [ SYNSEM obj_equi_vpslash_prd_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

; prevent X from Ving
v_np-vp_oeq-from_le := main_verb & 
  [ SYNSEM obj_equi_from_verb ].


; promise
v_np-vp_aeq-ntr_le := main_verb & 
  [ SYNSEM anom_equi_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT + ] ] ].

; use X to do Y
v_np-vp_aeq_le := main_verb & 
  [ SYNSEM anom_equi_trans_verb ].

; ask X to be allowed to Y
v_np-vp_aeq-psv_le := main_verb & 
  [ SYNSEM anom_equi_psv_verb ].

; kim took too long to arrive
v_np-vp_aeq-noel_le := main_verb & 
  [ SYNSEM anom_equi_oblig_verb ].

; have trouble sleeping
v_np-vp_aeq-prp_le := main_verb_mliszt & 
  [ SYNSEM anom_equi_prp_verb ].

; turn out to
v_p-vp_seq_le := main_verb & 
  [ SYNSEM particle_inf_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

; keep on driving
v_p-vp_prp_le := main_verb_mliszt & 
  [ SYNSEM particle_prp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_p-cp_le := main_verb & 
  [ SYNSEM particle_cp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_p-pp*-cp_le := main_verb & 
  [ SYNSEM particle_pp_cp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_p-vp_oeq_le := main_verb & 
  [ SYNSEM particle_oeq_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_pp-vp_oeq_le := main_verb & 
  [ SYNSEM oeq_pp_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ],
	     LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

v_pp-vp_oeq-bse_le := main_verb & 
  [ SYNSEM oeq_pp_bse_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ],
	     LKEYS [ --COMPKEY #cmin,
		     --+COMPKEY #cmin ] ] ].

v_pp*-vp_oeq_le := main_verb & 
  [ SYNSEM oeq_pp_inf_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                      --+COMPKEY #cmin ] ] ].

;; leave it to kim to arrange the meeting
v_it-pp-vp_oeq_le := main_verb & 
  [ SYNSEM expl_pp_inf_oeq_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; arrange (with kim) to meet her
v_pp-vp_seq_le := main_verb & 
  [ SYNSEM pp_inf_seq_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ] > ] ] ].

;; arrange it with kim to meet her
v_it-pp-vp_seq_le := main_verb & 
  [ SYNSEM expl_pp_inf_seq_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; *it bothers kim that sally left*; and by lex rule: *it bothers kim to go*
v_np-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_np_cp_verb ].

; ticks NP off that S
v_np-p-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_np_ptcl_cp_verb ].

;; That Kim lost suggests that Sandy won. 
;; It suggests that Sandy won that Kim lost.
v_cp-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_cp_cp_verb ].

; cost
v_np-np-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_np_np_cp_inf_verb ].

; make (?)
v_np-np-cp_it-ob_le := main_verb & 
  [ SYNSEM expl_it_subj_np_np_cp_verb ].

; matter
v_np-pp*-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_np_pp_cp_verb ].

; take: 'took him ten minutes to leave'
v_np*-np-vp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_np_np_vp_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ], [ ] > ] ] ].

; take: 'took (him) until Friday to leave'
v_np*-pp-cp_it-s-inf_le := main_verb & 
  [ SYNSEM expl_it_subj_np_pp_cp_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ], [ ] > ] ] ].

; 'it advances our cause (a lot) to take this step"
; "it will cost us (a lot) to win"
v_np-np*-vp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_np_np_vp_inf_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], unexpressed, [ ] > ] ] ].

; call
v_it-pp-cp_le := main_verb & 
  [ SYNSEM np_expl_pp_cp_verb ].

; cost/take
v_np-np-vpslnp_tgh_le := main_verb & 
  [ SYNSEM obj_equi_vpslash_four_arg_verb ].

; make (only)
v_np-prd-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_np_prdp_cp_verb ].

;; it seems (to kim) that sandy left
v_pp*-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_pp*_cp_verb ].

;; it occurred *(to me) that Sandy left
v_pp-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_pp_cp_verb ].

;; it looks (to kim) like Sandy left
v_pp*-cp_like_le := main_verb & 
  [ SYNSEM expl_it_subj_pp_cp_like_verb ].

;; It doesn't matter who stays
v_pp*-cp_it-unsp_le := main_verb & 
  [ SYNSEM expl_it_subj_pp_cp_unsp_verb ].

;; It dawned on Kim that ...
v_pp-cp_it-s-e_le := main_verb & 
  [ SYNSEM expl_it_subj_ppnoto_cp_verb ].

; it turns out that kim arrived
v_p-cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_prtcl_cp_verb ].

; It follows that ...
v_cp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_cp_verb ].

; it would help/wouldn't hurt to know ...
v_vp_it-s_le := main_verb & 
  [ SYNSEM expl_it_subj_vp_verb ].

; bet
v_np-np-cp_le := main_verb & 
  [ SYNSEM np_np_cp_verb ].

; report to
v_pp*-cp_le := norm_basic_main_verb & 
  [ INFLECTD -,
    SYNSEM pp_cp_fin_prop_verb &
	   [ LOCAL.CAT.VAL.COMPS.FIRST.OPT + ] ].

; say of NP that S
v_pp-cp_le := norm_basic_main_verb & 
  [ INFLECTD -,
    SYNSEM pp_cp_fin_prop_verb &
	   [ LOCAL.CAT.VAL.COMPS.FIRST.OPT - ] ].

; say to
v_pp*-cp_fin-imp_le := norm_basic_main_verb & 
  [ INFLECTD -,
    SYNSEM pp_cp_fin_unspec_verb ].


; predict
v_pp*-cp_unsp_le := main_verb & 
  [ SYNSEM pp_cp_fin_pq_verb ].

v_pp*-cp_fin_le := main_verb & 
  [ SYNSEM pp*_cp_fin_verb ].

v_pp-cp_fin_le := main_verb & 
  [ SYNSEM pp_cp_fin_verb,
    ALTS.INVQ - ].

v_pp*-cp_inf_le := main_verb & 
  [ SYNSEM pp*_cp_inf_verb ].

; 'see to it that X'
v_pp-it-cp_le := main_verb & 
  [ SYNSEM pp_expl_cp_verb ].

; would like
v_np-vp_sor-ntr_le := main_verb_mliszt & 
  [ SYNSEM sor_non_trans_verb ].

v_np-vp_sor_le := main_verb_mliszt & 
  [ SYNSEM sor_verb ].

v_np-vp_bse_le := main_verb_mliszt & 
  [ SYNSEM sorb_verb ].

; make: 'kim made it clear that sandy arrived'
v_np-prd_sor_le := main_verb_mliszt & 
  [ SYNSEM sor_prd_verb ].

v_np-np_le := main_verb & 
  [ SYNSEM ditrans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
	     LKEYS [ --OCOMPKEY #ocmin,
		     --+OCOMPKEY #ocmin ] ] ].

; 'leave (bequeath)'
v_np-np*_le := main_verb & 
  [ SYNSEM ditrans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT + ] > ] ] ].

v_np*-np_le := main_verb & 
  [ SYNSEM ditrans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT + ] > ] ] ].

v_np-np_only_le := main_verb & 
  [ ALTS.DATIVE -,
    SYNSEM ditrans_only_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np*-np*_only_le := main_verb & 
  [ ALTS.DATIVE -,
    SYNSEM ditrans_only_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT + ] > ] ] ].

v_np-np_idm_le := main_verb & 
  [ ALTS.DATIVE -,
    SYNSEM ditrans_only_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ],
    IDIOM + ].

;; talk
v_pp*-pp*_le := main_verb & 
  [ SYNSEM double_pp_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT + ] > ] ] ].

v_pp-pp_le := main_verb & 
  [ SYNSEM double_pp_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_pp-pp_e_le := main_verb & 
  [ SYNSEM double_pp_empty_pp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np*-pp*-ppto_le := main_verb & 
  [ SYNSEM np_trans_double_ppto_verb & 
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT + ] ] ].

v_np-pp*-ppto_le := main_verb & 
  [ SYNSEM np_trans_double_ppto_verb &
	   [ LOCAL.CAT [ VAL.COMPS.FIRST.OPT - ] ] ].

v_np-pp*-ppfor_le := main_verb & 
  [ SYNSEM np_trans_double_ppfor_verb ].

v_pp_e_le := main_verb & 
  [ SYNSEM empty_prep_intrans_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_pp_e3_le := main_verb & 
  [ SYNSEM empty_prep_intrans_arg3_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_pp_refl-idm_le := main_verb & 
  [ SYNSEM empty_prep_intrans_idiom_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].


v_np-pp_e_le := main_verb & 
  [ SYNSEM empty_prep_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np*-pp_e_le := main_verb & 
  [ SYNSEM empty_prep_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np-pp*_e-imp_le := main_verb & 
  [ SYNSEM empty_prep_trans_verb & 
	   [ LOCAL [ CAT [ HEAD [ VFORM fin ],
                           VAL [ SUBJ < [ LOCAL.CONT.HOOK.INDEX #ind ] >,
                                 COMPS < [ OPT - ], [ OPT + ] > ] ],
                     AGR #ind ],
             LKEYS [ KEYREL.ARG1 #ind & [ PNG.PN 1 ] ] ] ].

; take Kim for granted
v_np-pp_e-idm_le := main_verb & 
  [ SYNSEM empty_prep_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -, 
				       LOCAL.CAT.HEAD basic_noun ],
				     [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ],
    IDIOM + ].

v_np-pp_e-refl-idm_le := main_verb & 
  [ SYNSEM empty_prep_trans_refl_idiom_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np-pp_e-refl2-idm_le := main_verb & 
  [ SYNSEM empty_prep_trans_refl_refl_idm_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

; came into being
v_pp_e-idm_le := main_verb & 
  [ SYNSEM empty_prep_intrans_verb & 
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ],
    IDIOM + ].

; 'interest kim in'
v_np-pp_e-ntr_le := main_verb & 
  [ SYNSEM empty_prep_non_trans_verb ].

;; 'prefer it if ...'
v_it-cp_le := main_verb &
  [ SYNSEM expl_obj_cp_verb ].

;; 'hate it that ...'
v_it-cp_prop-c_le := main_verb &
  [ SYNSEM expl_obj_cpcmp_verb ].

;; 'take it (that) ...'
v_it-cp_prop_le := main_verb &
  [ SYNSEM expl_obj_cpprop_verb ].

; 'make it clear that ...'
v_it-prd_le := main_verb_mliszt &
  [ SYNSEM expl_obj_prd_verb ].

v_np_le := main_verb & 
  [ SYNSEM np_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

v_np*_le := main_verb & 
  [ SYNSEM np_trans_verb ].

;; DPF 2017-04-27 - No longer see why we would restrict these verbs (often
;; alternating with inchoatives) to exclude gerund complements, since we
;; need them for e.g. *they increased spending*.  So changed [HEAD basic_noun]
;; to supnoun on complement.
;; DPF 2020-05-14 - Perhaps a confusion about the name: noger here means the
;; verb itself does not undergo the (nominal) gerund lexical rule.  But we do
;; need a lexical verb type that excludes gerund complements, as for *seem*
;; in *He seemed arriving*, and does not passivize.
;;
v_np_noger_le := main_verb & 
  [ ALTS [ NGER -,
	   VADJ - ],
    SYNSEM np_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -,
				       LOCAL.CAT.HEAD supnoun ] > ] ] ].

v_np*_noger_le := main_verb & 
  [ ALTS [ VADJ -,
           NGER - ],
    SYNSEM np_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT +,
				       LOCAL.CAT.HEAD supnoun ] > ] ] ].

v_np_do-be_le := basic_main_verb_sans_key &
  [ SYNSEM do_be_verb &
	   [ LOCAL.CAT.HEAD [ VFORM non_prespart,
			      INV - ],
	     LKEYS.KEYREL.ARG2.IFORM non_prespart ],
    INFLECTD - ].


v_np_do-ing-be_le := basic_main_verb_sans_key &
  [ INFLECTD -,
    SYNSEM do_be_verb &
	   [ LOCAL.CAT.HEAD [ VFORM prp,
			      INV - ],
	     LKEYS.KEYREL.ARG2.IFORM prp ] ].

v_np_done-be_le := basic_main_verb_sans_key &
  [ SYNSEM do_be_verb_passive &
	   [ LOCAL.CAT.HEAD.INV -,
	     LKEYS.KEYREL.ARG2.IFORM non_prespart ],
    INFLECTD + ].

v_np*_nfin_le := main_verb & 
  [ SYNSEM np_trans_nfin_verb ].

v_np_n3s-bse_le := main_verb & 
  [ SYNSEM np_non_trans_verb &
           [ LOCAL bse_or_non3sg_verb &
		   [ CAT.VAL.COMPS < [ OPT - ] > ] ] ].

v_np_refl-idm_le := main_verb_mliszt & 
  [ SYNSEM np_trans_refl_verb,
    IDIOM + ].

v_p-np_refl-idm_le := main_verb_mliszt & 
  [ SYNSEM np_particle_idiom_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ],
    IDIOM + ].

v_np_sg-idm_le := v_np_idm_lexent &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.AGR.PNG.PN 3s ] > ].

v_np_pl-idm_le := v_np_idm_lexent &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.AGR.PNG.PN 3p ] > ].

; 'elected him president'
v_np-nb_le := main_verb_mliszt & 
  [ SYNSEM ditrans_np_nbar_verb ].

; 'see picture, page 2'
v_nb-np*_imp_le := main_verb_mliszt & 
  [ SYNSEM ditrans_nbar_np_verb ].

v_pp_e-nfin_le := main_verb & 
  [ SYNSEM empty_prep_intrans_bse_verb ].

v_np-pp_le := main_verb_mliszt & 
  [ SYNSEM prep_trans_verb & 
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; takes dir_state_modable_rel oblique complement.
;; put (takes variety of PP complements, so avoid chart dependency filter)
v_np-pp_prop_le := main_verb_mliszt & 
  [ SYNSEM prep_trans_dir_verb ].

;; change one's mind about
v_np-pp_nmod_le := main_verb_mliszt &
  [ SYNSEM prep_trans_verb_nmod &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np-pp_dir_le := main_verb_mliszt & 
  [ SYNSEM prep_trans_dir_noprop_verb ].

v_np-pp_oeq-as_le := main_verb &
  [ SYNSEM np_as_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np_ntr_le := main_verb & 
  [ SYNSEM np_non_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

v_np*_ntr_le := main_verb & 
  [ SYNSEM np_non_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ] > ] ] ].

v_np_ntr-noger_le := main_verb & 
  [ SYNSEM np_non_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT -,
				       LOCAL.CAT.HEAD basic_noun] > ] ] ].

; 'become'
v_np_id_le := main_verb & 
  [ SYNSEM np_non_trans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

; 'become president'
v_nb_le := main_verb_mliszt & 
  [ SYNSEM nbar_verb ].

; 'become obvious that Kim arrived.'
v_prd_ssr-va_le := main_verb_mliszt & 
  [ SYNSEM ssr_prdp_verb ].

; 'stood guard'
v_nb_idm_le := main_verb & 
  [ SYNSEM np_non_trans_nb_verb,
    IDIOM + ].

v_np-pp_seq-as_le := main_verb_mliszt & 
  [ SYNSEM np_as_seq_verb &
	   [ LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_p_le := main_verb & 
  [ SYNSEM particle_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_p_3only_le := main_verb & 
  [ SYNSEM particle_3only_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].
v_p-np_le := main_verb & 
  [ SYNSEM np_particle_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].
v_np-p_le := main_verb & 
  [ SYNSEM np_particle_verb &
	   [ LKEYS [ --COMPKEY #ocmin,
                     --+COMPKEY #ocmin ] ],
    ALTS.NPPART - ].

v_np-p_idm_le := main_verb & 
  [ SYNSEM np_particle_idiom_verb &
	   [ LKEYS [ --COMPKEY #ocmin,
                     --+COMPKEY #ocmin ] ],
    ALTS.NPPART -,
    IDIOM + ].

v_p-prd_le := main_verb & 
  [ SYNSEM particle_prd_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].
v_p-pp_le := main_verb & 
  [ SYNSEM particle_pp_verb ].

v_p-pp_e_le := main_verb & 
  [ SYNSEM particle_empty_pp_verb &
	   [ LOCAL.CAT [ VAL.COMPS < synsem, [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_p-pp*_e_le := main_verb & 
  [ SYNSEM particle_empty_pp_verb &
	   [ LOCAL.CAT [ VAL.COMPS < synsem, [ OPT + ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

; 'boils it down to'
v_p-np-pp_to_le := main_verb & 
  [ SYNSEM np_particle_ppto_verb ].

; 'made up of'
v_p-np-pp_of_le := main_verb & 
  [ SYNSEM np_particle_ppof_verb ].

v_p-np-pp_for_le := main_verb & 
  [ SYNSEM np_particle_ppfor_verb ].

v_p-np-pp_from_le := main_verb & 
  [ SYNSEM np_particle_ppfrom_verb ].

v_p-np-pp_in_le := main_verb & 
  [ SYNSEM np_particle_ppin_verb ].

v_p-np-pp_on_le := main_verb & 
  [ SYNSEM np_particle_ppon_verb ].

v_p-np-pp_as_le := main_verb & 
  [ SYNSEM np_particle_ppas_verb ].

v_np-p-np_le := main_verb & 
  [ SYNSEM np_particle_np_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_np-p-cp_idm_le := main_verb & 
  [ SYNSEM np_particle_cp_verb &
	   [ LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ],
    IDIOM + ].

v_np-p-cp_le := main_verb & 
  [ SYNSEM np_particle_cp_verb &
	   [ LOCAL.CAT.VAL.KCMP.LOCAL.CAT s_cat_unspec,
	     LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_np-p-vp_le := main_verb & 
  [ SYNSEM np_particle_vp_verb &
	   [ LOCAL.CAT.VAL.KCMP.LOCAL.CAT [ HEAD.VFORM inf,
					    VAL.SUBJ < synsem > ],
	     LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_pp_arg_le := main_verb  & 
  [ SYNSEM prep_intrans_event_verb ].

v_pp_gap_le := main_verb  & 
  [ SYNSEM prep_intrans_gap_verb ].

v_pp_le := main_verb  & 
  [ SYNSEM prep_intrans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].

v_pp*_dir_le := main_verb  & 
  [ SYNSEM prep_intrans_dir_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ] > ] ] ].

v_pp_dir_le := main_verb  & 
  [ SYNSEM prep_intrans_dir_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ] > ] ] ].

v_pp*_le := main_verb & 
  [ SYNSEM prep_intrans_verb & 
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ] > ] ] ].

v_pp_unacc_le := main_verb  & 
  [ SYNSEM prep_intrans_unacc_verb ].

v_vp_ssr_le := main_verb & 
  [ SYNSEM ssr_verb ].

v_vp_ssr-prp_le := main_verb & 
  [ SYNSEM ssr_prp_verb ].

; 'have to': no imperative "*Have to leave!"
v_vp_ssr-nimp_le := main_verb & 
  [ SYNSEM ssr_noimp_verb ].

v_pp-vp_ssr_le := main_verb_mliszt & 
  [ SYNSEM ssr_pp_inf_verb ].

; DPF 29-aug-07 - These verbs like "become" supposedly don't combine with
; predicative VPs, but stative passives often seem to be fine, 
; as in ; "This picture became damaged/associated with Picasso" or
; "We became inspired by the music."  Rather than adding a lexical rule to
; convert them to predicative adjs, consider a HEAD supertype of basic_adj
; and passive-verb.  FIX.

v_ap-pp_seq_le := main_verb_mliszt & 
  [ SYNSEM seq_ap_pp_verb ].

v_pp-pp_seq_le := main_verb_mliszt & 
  [ SYNSEM seq_pp_pp_verb ].

v_prd-pp_seq_le := main_verb_mliszt & 
  [ SYNSEM seq_aorp_pp_verb,
    ALTS.VADJ - ].

; feel good about
v_ap-pp_about_le := main_verb_mliszt & 
  [ SYNSEM seq_prdp_pp_about_verb ].

v_np-pp_to_le := main_verb & 
  [ SYNSEM empty_to_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np-pp_to-unsp_le := main_verb & 
  [ SYNSEM empty_prep_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT - ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin,
                     --OCOMPKEY _to_p_sel_rel & #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

;; DPF 2018-12-20 - This type prevented N-V compounds as in "snow-bound" since
;; the optional selected-for PP blocks these.  So discard this type, and unfold
;; its instances into two types v_np-pp_to_le and v_np_le.
;;

v_np-pp*_to_le := main_verb & 
  [ SYNSEM empty_to_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT - ], [ OPT + ] > ],
             LKEYS [ --COMPKEY #cmin,
                     --+COMPKEY #cmin ] ] ].


v_np*-pp_to_le := main_verb & 
  [ SYNSEM empty_to_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT - ] > ],
             LKEYS [ --OCOMPKEY #ocmin,
                     --+OCOMPKEY #ocmin ] ] ].

v_np*-pp*_to_le := main_verb & 
  [ SYNSEM empty_to_trans_verb &
	   [ LOCAL.CAT [ VAL.COMPS < [ OPT + ], [ OPT + ] > ] ] ].

v_-_unacc_le := main_verb & 
  [ SYNSEM unacc_verb ].

v_-_le := main_verb & 
  [ SYNSEM unerg_verb ].

v_-_imp_le := main_verb & 
  [ SYNSEM unerg_imp_verb ].

v_-_bse_le := main_verb & 
  [ SYNSEM unerg_bse_verb ].

v_np_poss_le := main_verb & 
  [ SYNSEM poss_verb ].

v_np_poss-cx_le := main_verb & 
  [ SYNSEM poss_verb &
	   [ LOCAL [ CAT [ HEAD.TAM.TENSE present,
			   VAL.COMPS < canonical_synsem > ],
		     AGR.PNG.PN -3s ] ] ].

;; DPF 19-feb-10 - Prevent "got there" via COMPS..NORM no_rel.
;; DPF 07-apr-10 - No longer remember why we have this, given the ordinary
;; NP-complement get_v2.  It does not appear in any of the treebanked data,
;; so let's try living without it, to avoid spurious ambiguity for e.g.
;; "he has got cats"

v_np_got_le := norm_nonconj_word &
  [ SYNSEM poss_got_verb ].
			  
v_cp_rkey-bse_le := v_cp_prop_raise_key_lexent &
  [ SYNSEM.LOCAL bse_verb ].

v_cp_rkey-3sg_le := v_cp_prop_raise_key_lexent &
  [ SYNSEM cp_prop_raise_key_3sg_verb ].

v_cp_rkey-n3sg_le := v_cp_prop_raise_key_lexent &
  [ SYNSEM cp_prop_raise_key_n3sg_verb ].

v_cp_rkey-pst_le := v_cp_prop_raise_key_lexent &
  [ SYNSEM cp_prop_raise_key_pst_verb ].

v_cp_rkey-prp_le := v_cp_prop_raise_key_lexent &
  [ SYNSEM cp_prop_raise_key_prp_verb ].

v_cp_rkey-psp_le := v_cp_prop_raise_key_lexent &
  [ SYNSEM cp_prop_raise_key_psp_verb ].

v_adv_le := norm_basic_main_verb &
  [ SYNSEM adv_verb,
    INFLECTD - ].

v_np-adv_le := norm_basic_main_verb &
  [ SYNSEM np_adv_verb,
    INFLECTD - ].

; 'made': "Kim made sandy arrive' but "Sandy was made TO arrive'
v_vp_sorb-psv_le := norm_basic_main_verb &
  [ SYNSEM sor_psv_verb,
    INFLECTD - ].

; 'faded'
v_-_psv_le := norm_basic_main_verb &
  [ SYNSEM passive_unerg_synsem,
    INFLECTD + ].

; 'born'
v_-_psv-prd_le := norm_basic_main_verb &
  [ INFLECTD +,
    SYNSEM passive_unerg_synsem,
    ALTS.ATTRJ - ].

; 'surrounded'
v_-_psv-with_le := norm_basic_main_verb &
  [ INFLECTD +,
    SYNSEM passive_unerg_with_synsem,
    ALTS.ATTRJ - ].

;; DPF 2016-09-26 - Removed NORM norm_rel because we want 
;; *the Chicago-based team* where the n-v_j-cpd_c rule requires its second dtr
;; to be NORM no_rel.  Maybe FIX whatever that constraint was doing.
;; 
; 'faded'
v_-_psv-att_le := norm_basic_main_verb &
  [ SYNSEM passive_unerg_synsem &
	   [ LOCAL.CAT [ VAL.SUBJ < canonical_synsem >,
			 POSTHD - ],
	     NONLOC.SLASH 0-dlist ],
    INFLECTD + ].

; 'horseback riding'
v_-_prp_le := norm_basic_main_verb &
  [ SYNSEM unerg_prp_verb,
    INFLECTD + ].

;; 'put' - "That was well put", but block in n-v compounds like "*idea-put"
;; Also block depictives via OPT -
;;
v_-_psv-ncnh_le := norm_basic_main_verb &
  [ INFLECTD +,
    SYNSEM passive_unerg_synsem &
	   [ LOCAL.CAT.HEAD [ MINORS.NORM norm_rel ],
	     OPT - ] ].

; 'unevaluated (by Abrams)'
v_-_psv-neg_le := main_verb_newltop &
  [ SYNSEM passive_unerg_neg_synsem,
    INFLECTD + ].

v_np_thr-s_le := main_verb & 
  [ SYNSEM there_verb_nohc_synsem ].

v_np_locinv-mv_le := loc_inv_mv_word
.

v_np_locinv-is_le := loc_inv_be_word &
  [ SYNSEM.LOCAL [ CAT.HEAD.TAM.TENSE present,
                   AGR.PNG.PN 3s ] ].

v_np_locinv-are_le := loc_inv_be_word &
  [ SYNSEM.LOCAL [ CAT.HEAD.TAM.TENSE present,
                   AGR.PNG.PN 3p ] ].

v_np_locinv-was_le := loc_inv_be_word &
  [ SYNSEM.LOCAL [ CAT.HEAD.TAM.TENSE past,
                   AGR.PNG.PN 3s ] ].

v_np_locinv-were_le := loc_inv_be_word &
  [ SYNSEM.LOCAL [ CAT.HEAD.TAM.TENSE past,
                   AGR.PNG.PN 3p ] ].

v_np_locinv-be_le := basic_loc_inv_be_word &
  [ SYNSEM.LOCAL.CAT.HEAD [ VFORM bse,
			    TAM.TENSE no_tense ] ].

;; May he always be blessed
;;
v_np-vp_imp_le := main_verb_mliszt & 
  [ SYNSEM sorb_verb &
	   [ LOCAL.CAT.HEAD.VFORM imp_vform ] ].

;; DPF 2018-08-24 - For some reason, PET's flop will not compile this file
;; without the following 99-character line preceded by a semicolon:
;; (and similarly for auxverbs.tdl but not lextypes.tdl)
;xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
