;;; -*- Mode: tdl; Coding: utf-8; -*-

;;;
;;; Copyright (c) 2012 -- 2012 Stephan Oepen (oe@ifi.uio.no); 
;;; see `LICENSE' for conditions.
;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; processing the (emerging) Grammar Markup Language (GML)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;
;; _fix_me_
;; to get started on bracketed inputs, we assume there are no token ambiguities
;; at this stage (which will be true when working off REPP results, but is not
;; guaranteed to be true in general, i.e. for other sources of parser input).
;;                                                              (31-oct-12; oe)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; an experiment: a gazetteer of named entities
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

dow_jones_industrial_average_tmr := four_four_ne_tmt &
[ +INPUT < [ +FORM "Dow", +TRAIT [ +LB #lb1, +RB #rb1 ] ],
           [ +FORM "Jones", +TRAIT [ +LB #lb2, +RB #rb2 ] ],
           [ +FORM "Industrial", +TRAIT [ +LB #lb3, +RB #rb3 ] ],
           [ +FORM "Average", +TRAIT [ +LB #lb4, +RB #rb4 ] ] >,
  +OUTPUT < [ +TRAIT [ +LB < npnm, npnm, npnm . #lb1 >, +RB #rb1 ] ],
            [ +TRAIT [ +LB #lb2, +RB < npnm . #rb2 > ] ],
            [ +TRAIT [ +LB #lb3, +RB < npnm . #rb3 > ] ],
            [ +TRAIT [ +LB #lb4, +RB < npnm . #rb4 > ] ] > ].

dow_jones_tmr := two_two_ne_tmt &
[ +INPUT < [ +FORM "Dow", +TRAIT [ +LB #lb1, +RB #rb1 ] ],
           [ +FORM "Jones", +TRAIT [ +LB #lb2, +RB #rb2 ] ] >,
  +OUTPUT < [ +TRAIT [ +LB < np-hdn . #lb1 >, +RB #rb1 ] ],
            [ +TRAIT [ +LB #lb2, +RB < np-hdn . #rb2 > ] ] > ].

default_lb_tmr := one_one_tmt &
[ +INPUT < [ +FORM #form, +CLASS #class,
             +TRAIT [ +LB anti_bracket_list, +RB #rb ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #class,
              +TRAIT [ +LB bracket_null, +RB #rb ],
              +PRED #pred, +CARG #carg, +TNT #tnt ] > ].

default_rb_tmr := one_one_tmt &
[ +INPUT < [ +FORM #form, +CLASS #class,
             +TRAIT [ +LB #lb, +RB anti_bracket_list ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #class,
              +TRAIT [ +LB #lb, +RB bracket_null ],
              +PRED #pred, +CARG #carg, +TNT #tnt ] > ].

prefix_gml_bracket_tmr := two_one_tmt &
[ +INPUT < [ +FORM ^⌊\[⌋$ ], 
           [ +FORM #form & ^[^⌊¦⌋]+$, +CLASS #class, 
             +TRAIT [ +LB #lb, +RB #rb ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #trait, 
              +TRAIT [ +LB [ FIRST ctype, REST #lb ], +RB #rb ], 
              +PRED #pred, +CARG #carg, +TNT #tnt ] > ].

suffix_gml_bracket_tmr := two_one_tmt &
[ +INPUT < [ +FORM #form & ^[^⌊¦⌋]+$, +CLASS #class, 
             +TRAIT [ +LB #lb, +RB #rb ] ,
             +PRED #pred, +CARG #carg, +TNT #tnt ],
           [ +FORM ^⌊\]⌋$ ] >,
  +OUTPUT < [ +FORM #form, +CLASS #trait, 
              +TRAIT [ +LB #lb, +RB [ FIRST ctype, REST #rb ] ], 
              +PRED #pred, +CARG #carg, +TNT #tnt ] > ].
