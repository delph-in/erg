;;; -*- Mode: tdl; Coding: utf-8; -*-

;;;
;;; Copyright (c) 2012 -- 2012 Stephan Oepen (oe@ifi.uio.no); 
;;; see `LICENSE' for conditions.
;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; processing the (emerging) Grammar Markup Language (GML)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;
;; _fix_me_
;; to get started on bracketed inputs, we assume there are no token ambiguities
;; at this stage (which will be true when working off REPP results, but is not
;; guaranteed to be true in general, i.e. for other sources of parser input).
;;                                                              (31-oct-12; oe)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; an experiment: a gazetteer of (very) high-certainty named entities
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

dow_jones_industrial_average_tmr := four_four_ne_tmt &
[ +INPUT < [ +FORM "Dow" ], [ +FORM "Jones" ], 
           [ +FORM "Industrial" ], [ +FORM "Average" ] >,
  +OUTPUT < [ +TRAIT [ +LB < npnm, npnm, npnm > ] ],
            [ +TRAIT [ +LB <>, +RB < npnm > ] ],
            [ +TRAIT [ +LB <>, +RB < npnm > ] ],
            [ +TRAIT [ +RB < npnm > ] ] > ].

dow_jones_tmr := two_two_ne_tmt &
[ +INPUT < [ +FORM "Dow" ], [ +FORM "Jones" ] >,
  +OUTPUT < [ +TRAIT [ +LB < npnm > ] ],
            [ +TRAIT [ +RB < npnm > ] ] > ].

default_lb_tmr := one_one_tmt &
[ +INPUT < [ +FORM #form, +CLASS #class,
             +TRAIT [ +UW #uw, +LB anti_bracket_list, +RB #rb ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #class,
              +TRAIT [ +UW #uw, +LB bracket_null, +RB #rb ],
              +PRED #pred, +CARG #carg, +TNT #tnt ] > ].

default_rb_tmr := one_one_tmt &
[ +INPUT < [ +FORM #form, +CLASS #class,
             +TRAIT [ +UW #uw, +LB #lb, +RB anti_bracket_list ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #class,
              +TRAIT [ +UW #uw, +LB #lb, +RB bracket_null ],
              +PRED #pred, +CARG #carg, +TNT #tnt ] > ].

prefix_gml_bracket_tmr := two_one_tmt &
[ +INPUT < [ +FORM ^⌊\[⌋$ ], 
           [ +FORM #form & ^[^⌊¦⌋]+$, +CLASS #class, 
             +TRAIT [ +UW #uw, +LB #lb, +RB #rb ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #trait, 
              +TRAIT [ +UW #uw, +LB [ FIRST ctype, REST #lb ], +RB #rb ], 
              +PRED #pred, +CARG #carg, +TNT #tnt ] > ].

suffix_gml_bracket_tmr := two_one_tmt &
[ +INPUT < [ +FORM #form & ^[^⌊¦⌋]+$, +CLASS #class, 
             +TRAIT [ +UW #uw, +LB #lb, +RB #rb ] ,
             +PRED #pred, +CARG #carg, +TNT #tnt ],
           [ +FORM ^⌊\]⌋$ ] >,
  +OUTPUT < [ +FORM #form, +CLASS #trait, 
              +TRAIT [ +UW #uw, +LB #lb, +RB [ FIRST ctype, REST #rb ] ], 
              +PRED #pred, +CARG #carg, +TNT #tnt ] > ].
