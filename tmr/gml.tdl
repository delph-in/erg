;;; -*- Mode: tdl; Coding: utf-8; -*-

;;;
;;; Copyright (c) 2012 -- 2014 Stephan Oepen (oe@ifi.uio.no); 
;;; see `LICENSE' for conditions.
;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; processing of the (emerging) Grammar Markup Language (GML)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;; DPF 2014-08-01 - Added a rule to add a bridging lexical entry into each
;; leaf cell for use in filling lexical gaps when doing robust full-forest 
;; treebanking
;;
#|
bridging_lexent_tmr := token_mapping_rule &
[ +CONTEXT < [ +FORM #form, +CLASS #class, +TRAIT #trait,
	       +ID #id, +FROM #from, +TO #to,
	       +PRED #pred, +CARG #carg, +TNT.+TAGS.FIRST string ] >,
  +INPUT < >,
  +OUTPUT < [ +FORM #form, +CLASS #class, +TRAIT #trait,
	      +ID #id, +FROM #from, +TO #to,
              +PRED #pred, +CARG #carg, +TNT [ +TAGS < >, +PRBS < > ] ] >,
  +POSITION "O1@C1" ].
|#

;;
;; _fix_me_
;; to get started on bracketed inputs, we assume there are no token ambiguities
;; at this stage (which will be true when working off REPP results, but is not
;; guaranteed to be true in general, i.e. for other sources of parser input).
;;                                                              (31-oct-12; oe)

default_lb_tmr := one_one_tmt &
[ +INPUT < [ +FORM #form, +CLASS #class,
             +TRAIT [ +UW #uw, +LB anti_bracket_list, +RB #rb, +HD #hd ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #class,
              +TRAIT [ +UW #uw, +LB bracket_null, +RB #rb, +HD #hd ],
              +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +CONTEXT < > ].

default_rb_tmr := one_one_tmt &
[ +INPUT < [ +FORM #form, +CLASS #class,
             +TRAIT [ +UW #uw, +LB #lb, +RB anti_bracket_list, +HD #hd ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #class,
              +TRAIT [ +UW #uw, +LB #lb, +RB bracket_null, +HD #hd ],
              +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +CONTEXT < > ].

prefix_gml_bracket_tmr := two_one_tmt &
[ +INPUT < [ +FORM ^⌊\(⌋$ ], 
           [ +FORM #form & ^[^⌊¦⌋]+$, +CLASS #class, 
             +TRAIT [ +UW #uw, +LB #lb, +RB #rb, +HD #hd ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #trait, 
              +TRAIT [ +UW #uw, 
                       +LB < ctype . #lb >, +RB #rb, +HD #hd ], 
              +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +CONTEXT < > ].

suffix_gml_bracket_tmr := two_one_tmt &
[ +INPUT < [ +FORM #form & ^[^⌊¦⌋]+$, +CLASS #class, 
             +TRAIT [ +UW #uw, +LB #lb, +RB #rb, +HD #hd ] ,
             +PRED #pred, +CARG #carg, +TNT #tnt ],
           [ +FORM ^⌊\)⌋$ ] >,
  +OUTPUT < [ +FORM #form, +CLASS #trait, 
              +TRAIT [ +UW #uw, 
                       +LB #lb, +RB < ctype . #rb >, +HD #hd ], 
              +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +CONTEXT < > ].

head_gml_tmr := two_one_tmt &
[ +INPUT < [ +FORM ^⌊↔¦([^⌊¦⌋]+)¦([^⌊¦⌋]+)⌋$ ], 
           [ +FORM #form, +CLASS #class, 
             +TRAIT [ +UW #uw, +LB #lb, +RB #rb ],
             +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #trait, 
              +TRAIT [ +UW #uw, +LB #lb, +RB #rb,
                       +HD [ +LL.-CTYPE- "${uc(I1:+FORM:1)}",
                             +TG "${I1:+FORM:2}" ] ],
              +PRED #pred, +CARG #carg, +TNT #tnt ] >,
  +CONTEXT < > ].
