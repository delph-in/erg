;;; -*- Mode: tdl; Coding: utf-8; -*-

;;;
;;; Copyright (c) 2009 -- 2010 Stephan Oepen (oe@ifi.uio.no); 
;;; see `LICENSE' for conditions.
;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; an experiment: a gazetteer of named entities
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

dow_jones_industrial_average_tmr := token_mapping_rule &
[ +INPUT < [ +FORM #form1 & "Dow", +CLASS #class1,
             +TRAIT [ +LB #lb1, +RB #rb1 ],
             +PRED #pred1, +CARG anti_string, +ID #id1, 
             +FROM #from1, +TO #to1, +TNT #tnt1 ],
           [ +FORM #form2 & "Jones", +CLASS #class2,
             +TRAIT [ +LB #lb2, +RB #rb2 ],
             +PRED #pred2, +CARG #carg2, +ID #id2, 
             +FROM #from2, +TO #to2, +TNT #tnt2 ],
           [ +FORM #form3 & "Industrial", +CLASS #class3,
             +TRAIT [ +LB #lb3, +RB #rb3 ],
             +PRED #pred3, +CARG #carg3, +ID #id3, 
             +FROM #from3, +TO #to3, +TNT #tnt3 ],
           [ +FORM #form4 & "Average", +CLASS #class4, 
             +TRAIT [ +LB #lb4, +RB #rb4 ],
             +PRED #pred4, +CARG #carg4, +ID #id4, 
             +FROM #from4, +TO #to4, +TNT #tnt4 ] >,
  +OUTPUT < [ +FORM #form1, +CLASS #class1, 
              +TRAIT [ +LB < np-hdn, np-hdn, np-hdn . #lb1 >, +RB #rb1 ],
              +PRED #pred1, +CARG non_string, +ID #id1, 
              +FROM #from1, +TO #to1, +TNT #tnt1 ],
            [ +FORM #form2, +CLASS #class2, 
              +TRAIT [ +LB #lb2, +RB [ FIRST np-hdn, REST #rb2 ] ],
              +PRED #pred2, +CARG #carg2, +ID #id2, 
              +FROM #from2, +TO #to2, +TNT #tnt2 ],
            [ +FORM #form3, +CLASS #class3,
              +TRAIT [ +LB #lb3, +RB [ FIRST np-hdn, REST #rb3 ] ],
              +PRED #pred3, +CARG #carg3, +ID #id3, 
              +FROM #from3, +TO #to3, +TNT #tnt3 ],
            [ +FORM #form4, +CLASS #class4, 
              +TRAIT [ +LB #lb4, +RB [ FIRST np-hdn, REST #rb4 ] ],
              +PRED #pred4, +CARG #carg4, +ID #id4, 
              +FROM #from4, +TO #to4, +TNT #tnt4 ] >,
  +POSITION "I1<I2<I3<I4, O1@I1, O2@I2, O3@I3, O4@I4" ].

reset_carg_tmr := one_one_tmt &
[ +INPUT < [ +FORM #form, +CLASS #class, +TRAIT #trait, 
             +PRED #pred, +CARG non_string, +TNT #tnt ] >,
  +OUTPUT < [ +FORM #form, +CLASS #class, +TRAIT #trait, 
              +PRED #pred, +CARG string, +TNT #tnt ] > ].


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; a second pass at lightweight NEs, now that we have further split up tokens
;;; at hyphens and dashes.
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;
;;; numerals, including some sub-sets (days of the month or years).  in some of
;;; the rules |1| is deliberately excluded, because (unlike everyone else) it
;;; requires singular agreement.  hence, we assume that all usages of |1| need
;;; be accounted for in the native lexicon, including as hours or days of the
;;; month.
;;;

;;
;; (candidate) days of the month: |2| to |9|, |10| to |29|, |30|, and |31|
;;
card_or_dom_ne_tmr := ne_tmt &
[ +INPUT < [ +FORM ^([2-9]|[1-2][0-9]|3[01])$ ] >,
  +OUTPUT < [ +CLASS card_or_dom_ne ] > ].

;;
;; (candidate) years: |950|, |1805|, |1957|, |2005|, et al.
;;
card_or_year_ne_tmr := ne_tmt &
[ +INPUT < [ +FORM ^[12]?[0-9]{3}$ ] >,
  +OUTPUT < [ +CLASS card_or_year_ne ] > ].

;;
;; any sequence of digits, with optional sign and optional decimal point.  to
;; accept measure phrases like |900-pound|, where at this point |900-| is one
;; token, allow an optional trailing hyphen.
;;
card_ne_1_tmr := ne_tmt &
[ +INPUT < [ +FORM ^[-+±~]?([2-9]|[1-9][0-9][0-9]*)(\.|-)?$ ] >,
  +OUTPUT < [ +CLASS card_ne ] > ].

;;
;; floating point numbers, with optional sign and at least one decimal.  again
;; for measures phrases, allow optional hyphen: |1.5-inch pianist|.
;; DPF 2012-05-27 - Generalized this class to also allow treatment of |8.10|
;; as a proper name, as in |Ubuntu 8.10|.
;;
;; DPF 2012-09-19 - In some contexts such as the WSJ, an expression like 
;; |stocks rose 0.3 point| is accepted with a sub-one decimal value and a 
;; singular measure noun.  So we split the old rule into two, one for
;; decimal numbers 1.0 or greater (and the negative equivalent), and the
;; other for decimals with a 0 before the decimal.
;;
#|
card_ne_2_tmr := ne_tmt &
[ +INPUT < [ +FORM ^[-+±~]?[0-9]*\.[0-9]+$ ] >,
  +OUTPUT < [ +CLASS card_or_proper_ne ] > ].
|#

card_ne_2_a_tmr := ne_tmt &
[ +INPUT < [ +FORM ^[-+±~]?[1-9]+\.[0-9]+$ ] >,
  +OUTPUT < [ +CLASS card_or_proper_ne ] > ].

card_ne_2_b_tmr := ne_tmt &
[ +INPUT < [ +FORM ^[-+±~]?[0]*\.[0-9]+$ ] >,
  +OUTPUT < [ +CLASS card_or_decimal_ne ] > ].

;;
;; US-style or German separators, optional sign and decimals: e.g. |23,000.-|
;;
card_ne_3_tmr := ne_tmt &
[ +INPUT < [ +FORM ^[-+±]?[1-9][0-9]{0,2}([,.][0-9]{3})+([,.]([0-9]*|-))?$ ] >,
  +OUTPUT < [ +CLASS card_ne ] > ].

;;
;; ordinals, mostly parallel to the numerals
;;
ord_or_dom_ne_tmr := ne_tmt &
[ +INPUT < [ +FORM ^([23]?(1st|2nd|3rd)|(2?[1-9]|1[04-9]|20|30|31)th)$ ] >,
  +OUTPUT < [ +CLASS ord_or_dom_ne ] > ].

ord_ne_tmr := ne_tmt &
[ +INPUT < [ +FORM ^[0-9]*((^|[^1])(1st|2nd|3rd)|(11|12|13|[04-9])th)$ ] >,
  +OUTPUT < [ +CLASS ord_ne ] > ].

;;
;; _fix_me_
;; another pass at the decades, for cases like |mid-1800s|; see `ne1.tdl' for
;; the original rules.  to clean these up, we would need something like the
;; 'group call' mechanism in REPP.                            (27-aug-11; oe)
;;
decade_ne_3_tmr := ne_tmt &
[ +INPUT < [ +FORM ^(?:1[0-9])?[0-9]0[sS]$ ] >,
  +OUTPUT < [ +CLASS plur_ne ] > ].

decade_ne_4_tmr := two_one_tmt &
[ +INPUT < [ +FORM ^((1[0-9])?[0-9]0)$, 
             +TRAIT #trait, +CLASS non_ne,
             +PRED #pred, +CARG #carg, +TNT.+MAIN #main ],
           [ +FORM ^('[sS])$, +CLASS non_ne ] >,
  +OUTPUT < [ +FORM "${I1:+FORM:1}${I2:+FORM:1}", 
              +TRAIT #trait, +CLASS plur_ne,
              +PRED #pred, +CARG #carg, +TNT null_tnt & [ +MAIN #main ] ] > ].

