;;; -*- Mode: tdl; Coding: utf-8; -*-


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; join clitic 's to their hosts
;;; experimental rule set by sweaglesw (14-nov-2011)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

initial_caps := chart_mapping_rule &
[ +CONTEXT < >,
  +INPUT < [ ORTH < ^([a-z])(.*)$ > ] >,
  +OUTPUT < [ ORTH < "${uc(I1:ORTH.FIRST:1)}${I1:ORTH.FIRST:2}" > ] >,
  +POSITION "^<I1, O1@I1",
;; show off the new +JUMP feature: when this rule fires, jump to another rule.
;; we know there will only be one possible firing of this rule, so don't waste time looking for more match locations...
  +JUMP "split_mwe" ].

split_mwe := chart_mapping_rule &
[ +CONTEXT < >,
  +INPUT < [ ORTH < #first . #rest & *cons* > ] >,
  +OUTPUT < [ ORTH < #first > ], [ ORTH #rest ] >,
  +POSITION "I1@O1, I1@O2, O1<O2" ].

suffix_clitic := chart_mapping_rule &
[ +CONTEXT < >,
  +INPUT < [ ORTH < ^(.*)$ > ],
             [ ORTH < ^('.*)$ > ] >,
  +OUTPUT < [ ORTH < "${I1:ORTH.FIRST:1}${I2:ORTH.FIRST:1}" > ] >,
  +POSITION "I1<I2, O1@I1, O1@I2" ].

join_hyphenated := chart_mapping_rule &
[ +CONTEXT < >,
  +INPUT < [ ORTH < ^(.*)-$ > ], [ ORTH < ^(.*)$ > ] >,
  +OUTPUT < [ ORTH < "${I1:ORTH.FIRST:1}-${I2:ORTH.FIRST:1}" > ] >,
  +POSITION "I1<I2, O1@I1, O1@I2" ].

#|	;; a target for jumping to the end of post-processing, if we need one.
last_rule := chart_mapping_rule &
[ +INPUT < [ ORTH < "never_match_me" > ] >,
  +OUTPUT < > ].
|#
