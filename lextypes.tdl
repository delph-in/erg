;;; -*- Mode: TDL; Package: DISCO -*-
;;;
;;;  Copyright Daniel Flickinger 1994-1999
;;;  All Rights Reserved.
;;;  No use or redistribution without permission.
;;;
;;;  lextypes.tdl
;;;
;;;  Lexical types (i.e., word classes)
;;;
;;;  Rob Malouf, 3-Nov-1994
;;;
;;;  $Id$

;:message "Lexical types".



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; LEXICAL DEFAULTS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

nonslash := word &
  [ SYNSEM.NON-LOCAL.SLASH 0-dlist ].

; Lexical types that may have subtypes or instances with nonempty QUE values.

nonque := word &
  [ SYNSEM.NON-LOCAL.QUE 0-dlist ].  

nonrel := word &
  [ SYNSEM.NON-LOCAL.REL 0-dlist ].  

nonconj := word &
  [ SYNSEM.LOCAL.CONJ cnil ].

mcna := word &
  [ SYNSEM.LOCAL.CAT.MC na ].

nonmsg := word &
  [ SYNSEM.LOCAL.KEYS.MESSAGE 0-dlist ].

; Exceptions to this default include neg-contracted auxiliaries.
topkey := word &
  [ SYNSEM.LOCAL [ KEYS.KEY #key,
		   CONT.--TOPKEY #key ] ].

; Most words combine with their complements (if any) to produce a phrase,
; but a few, like "twenty" as in "twenty-two" and "five" as in "five o'clock",
; produce a sign which is still a word -- one with [ SYNSEM lex_synsem ].

hc-to-phr := word &
  [ SYNSEM.LOCAL.CAT.HC-LEX -* ].

basic_nontopkey := nonconj & nonslash & nonrel &
     	     mcna & non_affix_bearing.

; nontopkey_word's have all the defaults except topkey.
nontopkey := basic_nontopkey & nonque & nonmsg.

; hcons_amalg_word's have all the defaults EXCEPT non_affix_bearing,
; nonslash, nonrel, nonque.
hcons_amalg_word := mcna &
	    nonconj &
	    nonmsg &
	    topkey &
	    hc-to-phr &
  [ SYNSEM.LOCAL.KEYS.KEY.LABEL *cons* ].

basic_norm_word := hcons_amalg_word & nonque & nonslash & nonrel.

basic_norm_no_affix_word := basic_norm_word & non_affix_bearing.

; norm_word's have all the defaults except non_affix_bearing
norm_word := basic_norm_word &
  [ SYNSEM lex_synsem ].
	     
norm_no_affix_word := norm_word & non_affix_bearing.  

norm_mod_no_affix_word := nonconj & nonslash & nonrel & mcna & topkey &
		 non_affix_bearing &
   [ SYNSEM lex_synsem &
	    [ LOCAL [ CAT.HEAD.MOD < [ LOCAL.KEYS.MESSAGE #msg ] >,
		      KEYS.MESSAGE #msg ] ] ].

norm_mod_nonque_no_affix_word := norm_mod_no_affix_word & nonque.

basic_mod_no_affix_notopkey_word := nonconj & nonslash & nonrel &
		 non_affix_bearing.

norm_mod_no_affix_notopkey_word := basic_mod_no_affix_notopkey_word &
   [ SYNSEM lex_synsem &
	    [ LOCAL [ CAT.HEAD.MOD < [ LOCAL.KEYS.MESSAGE #msg ] >,
		      KEYS.MESSAGE #msg ] ] ].

; ERB (04-10-96) I have added nonmsg to the following types, as
; appropriate.

; que_word's have all the defaults EXCEPT nonque & topkey.
que_word := nonconj & non_affix_bearing &
	    mcna &
	    nonslash &
	    nonrel &
	    nonmsg &
	    hc-to-phr.

; rel_word's have all the defaults EXCEPT nonrel.
rel_word := nonque & non_affix_bearing &
	    mcna &
            nonslash &
	    nonconj &
	    nonmsg &
	    topkey &
	    hc-to-phr.

; ERB (04-10-96) msg_word's have all the defaults EXCEPT nonmsg, nonslash,
; mcna, and topkey.
msg_word := nonque & non_affix_bearing &
	    nonrel &
	    nonconj &
	    hc-to-phr.

norm_msg_word := msg_word & nonslash & mcna & topkey.

hcons_amalg_basic_affixed_word := hcons_amalg_word & 
  [ INFLECTED - ].

hcons_amalg_affixed_word := hcons_amalg_word & 
  [ INFLECTED - ].

hcons_amalg_non_affixed_word := hcons_amalg_word & non_affix_bearing.

amalg_word := mcna & 
	    nonconj &
	    nonmsg &
            non_affix_bearing &
	    topkey &
	    hc-to-phr.

msg_amalg_word := nonconj &
            non_affix_bearing &
	    topkey &
	    hc-to-phr.

; hc_word's repeat all the defaults EXCEPT hc-to-phr, which means they can decide
; to build head_complement structures which are still words (with 
; [ SYNSEM lex_synsem ].  Also, they are not inherently themselves 
; [ SYNSEM lex_synsem ], so we don't use inheritance at the moment, but replicate
; the inheritable properties, for an experiment.

hc_word := word_or_lexrule &
  [ SYNSEM [ LOCAL [ CAT.MC na,
		     CONT.--TOPKEY #key,
		     KEYS [ KEY #key & [ LABEL *cons* ],
			    MESSAGE 0-dlist ],
		     CONJ cnil ],
	     NON-LOCAL [ QUE 0-dlist,
			 REL 0-dlist,
			 SLASH 0-dlist ] ],
    ROOT - ].

;;;;;
;; Non-local amalgmation types
;;

basic_zero_arg := lex_synsem &
  [ LOCAL.ARG-S < >,
    NON-LOCAL [ SLASH 0-dlist,
		REL 0-dlist,
		QUE 0-dlist ] ].

zero_arg := basic_zero_arg &
  [ LOCAL.CONT.H-CONS <! !> ].

basic_one_arg := canonical_synsem &
  [ LOCAL.ARG-S < [ NON-LOCAL [ SLASH #slash,
				REL #rel,
				QUE #que ],
		    LOCAL.CONT.INDEX individual ] >,
    NON-LOCAL [ SLASH #slash,
		REL #rel,
		QUE #que ] ].

one_arg := basic_one_arg &
  [ LOCAL.CONT.H-CONS <! !> ].

basic_two_arg := lex_synsem &
  [ LOCAL.ARG-S < [ NON-LOCAL [ SLASH [ LIST #smiddle,
					LAST #slast ],
				REL [ LIST #rmiddle,
				      LAST #rlast ],
				QUE [ LIST #qmiddle,
				      LAST #qlast ] ],
		    LOCAL.CONT.INDEX individual ],
		  [ NON-LOCAL [ SLASH [ LIST #sfirst,
					LAST #smiddle ],
				REL [ LIST #rfirst,
				      LAST #rmiddle ],
				QUE [ LIST #qfirst,
				      LAST #qmiddle ] ],
		    LOCAL.CONT.INDEX individual ] >,
    NON-LOCAL [ SLASH [ LIST #sfirst,
			LAST #slast ],
		REL [ LIST #rfirst,
		      LAST #rlast ],
		QUE [ LIST #qfirst,
		      LAST #qlast ] ] ].

two_arg := basic_two_arg &
  [ LOCAL.CONT.H-CONS <! !> ].

basic_three_arg := lex_synsem &
  [ LOCAL [ ARG-S < [ NON-LOCAL [ SLASH [ LIST #smiddle2,
					  LAST #slast ],
				  REL [ LIST #rmiddle2,
					LAST #rlast ],
				  QUE [ LIST #qmiddle2,
					LAST #qlast ] ],
		      LOCAL.CONT.INDEX individual ],
                    [ NON-LOCAL [ SLASH [ LIST #sfirst,
					  LAST #smiddle1 ],
				  REL [ LIST #rfirst,
					LAST #rmiddle1 ],
				  QUE [ LIST #qfirst,
					LAST #qmiddle1 ] ],
		      LOCAL.CONT.INDEX individual ],
                    [ NON-LOCAL [ SLASH [ LIST #smiddle1,
					  LAST #smiddle2 ],
				  REL [ LIST #rmiddle1,
					LAST #rmiddle2 ],
				  QUE [ LIST #qmiddle1,
					LAST #qmiddle2 ] ],
		      LOCAL.CONT.INDEX individual ] > ],
    NON-LOCAL [ SLASH [ LIST #sfirst,
			LAST #slast ],
		REL [ LIST #rfirst,
		      LAST #rlast ],
		QUE [ LIST #qfirst,
		      LAST #qlast ] ] ].

three_arg := basic_three_arg &
  [ LOCAL.CONT.H-CONS <! !> ].

basic_four_arg := lex_synsem &
  [ LOCAL [ ARG-S < [ NON-LOCAL [ SLASH [ LIST #smiddle3,
					  LAST #slast ],
				  REL [ LIST #rmiddle3,
					LAST #rlast ],
				  QUE [ LIST #qmiddle3,
					LAST #qlast ] ],
		      LOCAL.CONT.INDEX individual ],
		    [ NON-LOCAL [ SLASH [ LIST #sfirst,
					  LAST #smiddle1 ],
				  REL [ LIST #rfirst,
					LAST #rmiddle1 ],
				  QUE [ LIST #qfirst,
					LAST #qmiddle1 ] ],
		      LOCAL.CONT.INDEX individual ],
		    [ NON-LOCAL [ SLASH [ LIST #smiddle1,
					  LAST #smiddle2 ],
				  REL [ LIST #rmiddle1,
					LAST #rmiddle2 ],
				  QUE [ LIST #qmiddle1,
					LAST #qmiddle2 ] ],
		      LOCAL.CONT.INDEX individual ],
		    [ NON-LOCAL [ SLASH [ LIST #smiddle2,
					  LAST #smiddle3 ],
				  REL [ LIST #rmiddle2,
					LAST #rmiddle3 ],
				  QUE [ LIST #qmiddle2,
					LAST #qmiddle3 ] ],
		      LOCAL.CONT.INDEX individual ] > ],
    NON-LOCAL [ SLASH [ LIST #sfirst,
			LAST #slast ],
		REL [ LIST #rfirst,
		      LAST #rlast ],
		QUE [ LIST #qfirst,
		      LAST #qlast ] ] ].

four_arg := basic_four_arg &
  [ LOCAL.CONT.H-CONS <! !> ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; SUBCATEGORIZATIONS -- synsems for verbs, adjectives, prepositions, nouns
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

nomod_local := local &
  [ CAT.HEAD.MOD < > ].

nomod_synsem := lex_synsem &
  [ LOCAL.CAT [ HEAD.MOD < >,
		VAL.SUBJ < > ] ].

scopal_mod :< local_min.
intersective_mod :< local_min.

scopal_mod_local := scopal_mod & local.
int_mod_local := intersective_mod & local.

;; DPF 27-May-99 - Changed [MC na] in NOMP_CAT to [MC na_or_-] to allow 
;; acc-verbal-gerunds to be NP complements, even though they are built via the 
;; head_subj rule.
;; DPF 18-Nov-99 - Removed PRD + since this prevented e.g. "ten past five", where
;; "ten" is [PRD -], and "past" requires a nomp_cat specifier.
;; DPF 07-Jan-01 - We would like to capture the generalization that nominal
;; phrases which have been assigned case (nominative or accusative) must also
;; be [ MESSAGE 0-dlist ] to block, e.g., spurious analyses for verbal gerunds
;; which have been built via the subj-head rule.  But for now have to stipulate
;; this for each occurrence in subcats.
;; DPF 21-Mar-01 - Made COMPS for nomp_cat be < > rather than *olist* to avoid
;; spurious ambiguity for partitives, as in "some arrived", since they have to
;; undergo the n_optcomp pumping rule in order to be able to take a specifier
;; as in "almost all arrived".

nomp_cat_min :< cat_min.

nomp_cat := nomp_cat_min & cat &
  [ HEAD nominal & strict_type &
	 [ POSS -,
	   TAM.MOOD ind_or_mod_subj ],
    VAL [ SUBJ *olist*,
	  SPR *olist*,
	  COMPS < > ],
    MC na_or_- ].

; DPF (8-Jan-99) Added accusative and nominative subtypes of nomp_cat, since 
; nominative ones are not 'mobile' - see fundamentals.tdl

nomp_cat_nom_min :< nomp_cat_min.
nomp_cat_acc_min :< nomp_cat_min.

nomp_cat_nom := nomp_cat_nom_min & nomp_cat &
  [ HEAD.CASE nom ].

nomp_cat_acc := nomp_cat_acc_min & nomp_cat &
  [ HEAD mobile &  [ CASE acc ] ].

np_cat_nom_min :< nomp_cat_nom_min.
np_cat_acc_min :< nomp_cat_acc_min.

np_cat_nom := nomp_cat_nom & np_cat_nom_min &
  [ HEAD noun ].

np_cat_acc := nomp_cat_acc & np_cat_acc_min &
  [ HEAD noun ].

;; DPF (30-Apr-98) In saturated phrase subcats, SUBJ must be < >, not *olist*,
;; since we need to have SUBJ < > unify with the filler in filler_head phrases,
;; and the filler is constrained to be SUBJ *prolist* which has a distinct 
;; synsem type (pro_ss) which is constrained to be [OPT -].
;; DPF 27-May-99 - Having eliminated the need for *prolist* to be OPT -, we
;; can now make saturated phrases require SUBJ to be *olist* rather than < >, 
;; which means verbal gerunds can have an optional SUBJ but still themselves 
;; be subjects or complements.
;; DPF (28-Jul-98) Added MOD < > to exclude non-finite relative clauses, which
;; are otherwise not distinguishable from nomp's.  Maybe also needed to 
;; exclude the second (modifier) entries for temporal NPs like "Tuesday".
;; DPF 09-Jun-99 - Removed MOD < > (see above), since relative clauses are now
;; all marked with non-empty REL, and because this constraint was falsely 
;; blocking "one to three pm" as an NP complement, because it has a 
;; non-empty MOD.

s_cat_unspec := cat &
  [ HEAD verbal & strict_type,
    VAL [ SUBJ *olist*,
	  COMPS *olist* ] ].

s_cat_v_c := s_cat_unspec &
  [ HEAD.INV -,
    VAL.SPR *olist*,
    MC - ].

s_cat_fin_or_inf_unspec := s_cat_unspec &
  [ HEAD.VFORM fin_or_inf ].

s_cat_fin_unspec := s_cat_fin_or_inf_unspec &
  [ HEAD [ VFORM fin,
           TAM ind_or_mod_subj_tam ] ].

s_cat_inf_unspec := s_cat_fin_or_inf_unspec &
  [ HEAD [ VFORM inf,
           TAM ind_or_mod_subj_tam ] ].

s_cat_fin_or_inf_v_c := s_cat_v_c & s_cat_fin_or_inf_unspec &
  [ HEAD.TAM.MOOD ind_or_mod_subj & strict_sort ].

s_cat_fin_v_c := s_cat_v_c & s_cat_fin_unspec &
  [ HEAD.TAM.MOOD ind_or_mod_subj & strict_sort ].

s_cat_inf_v_c := s_cat_v_c & s_cat_inf_unspec &
  [ HEAD.TAM.MOOD ind_or_mod_subj & strict_sort ].

; Removed HEAD comp, to allow "I demand he be here"
s_cat_bse := s_cat_v_c &
  [ HEAD.VFORM bse ].

s_cat := s_cat_unspec &
  [ HEAD verb ].

s_cat_fin := s_cat & s_cat_fin_unspec &
  [ HEAD.INV -,
    MC + ].

s_cat_fin_or_imp := s_cat &
  [ HEAD [ VFORM fin_or_imp,
	   TAM.MOOD ind_or_mod_subj ],
    MC + ].

n_or_v_cat := cat &
  [ HEAD nominal_or_verbal & strict_type,
    VAL [ SPR < synsem_min & [ LOCAL.KEYS.KEY quant_or_deg_rel ] >,
          COMPS *olist* ] ].

vp_cat := n_or_v_cat &
  [ HEAD verbal &
	 [ INV - ],
    VAL.SUBJ < synsem_min >,
    MC na ].

vp_inf_cat := vp_cat &
  [ HEAD [ VFORM inf,
	   PRD - ] ].

vp_bse_cat := vp_cat &
  [ HEAD verb &
	 [ VFORM bse_only,
	   PRD - ] ].

; DPF 10-Jun-99 - Removed MOD < > (cf note in templates.tdl)
; DPF 11-Jul-99 - Added restriction that SPR be [HEAD det] to prevent adjectives
; from modifying partitives and odd nouns like hour_prep words.
; DPF 28-Aug-99 - Removed SPR: OPT - to allow relative clauses to modify
; generic-pro's like "anything" and still have the result look like an NP.
; DPF 10-May-01 - Removed [SPR..HEAD det] to allow modifiers of generic_pro
; entries as in "everyone important to me"

nbar_cat := n_or_v_cat &
  [ HEAD noun &
	 [ POSS - ],
    VAL [ SUBJ < >,
          COMPS < > ],
    MC na ].

adverbee_cat := cat &
  [ HEAD adverbee & strict_type ].

prd_cat := cat &
  [ HEAD strict_type & 
         [ PRD +,
           TAM.TENSE no_tense ],
    VAL [ SUBJ < synsem >,
	  SPR *olist*,
	  COMPS *olist* ],
    MC na ].

pp_cat := cat &
  [ HEAD prep_or_modnp & [ PRD - ],
		  VAL [ SPR *olist*,
			COMPS < > ],
    MC na ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Linking Types
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Linking types specify the relationship between valence positions and 
;; semantic roles.

linking_type :< lex_synsem.

atrans_lt := linking_type &
  [ LOCAL [ CAT.VAL.SUBJ < [ LOCAL local_min &
				   [ CONT.INDEX it-ind ] ] >,
	    KEYS.KEY no_role_rel ] ].

arg1_subj_lt := linking_type &
  [ LOCAL [ CAT.VAL.SUBJ < [ LOCAL local_min &
				   [ CONT.INDEX #subjind & non_expl-ind,
                                     AGR.DIVISIBLE strict_sort ] ] >,
	    KEYS.KEY arg1_rel & [ ARG1 #subjind ] ] ].

unacc_lt := linking_type &
  [ LOCAL [ CAT.VAL.SUBJ < [ LOCAL local_min & 
                                   [ CONT [ INDEX #subjind & 
                                                  non_expl-ind ],
                                     AGR.DIVISIBLE strict_sort ] ] >,
            KEYS.KEY arg3_event_rel & [ ARG3 #subjind ] ] ].

arg13_lt := arg1_subj_lt & 
  [ LOCAL [ CAT.VAL.COMPS 
                    < [ LOCAL local_min &
                            [ CONT [ TOP #top,
                                     INDEX #objind & non_expl-ind ] ] ], ... >,
            CONT.TOP #top,
	    KEYS.KEY arg13_rel & [ ARG3 #objind ] ] ].

trans_lt :< arg13_lt.

arg14_lt := arg1_subj_lt & 
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
				    [ KEYS [ KEY verb_aspect_rel,
					     MESSAGE 1-dlist ],
				      CONT.TOP #mhand ] ], ... >,
	    KEYS.KEY arg14_rel & [ ARG4 #mhand ] ] ].

; "suppose" which makes its KEY same as its complement's KEY
; - wouldn't need this if had defaults

alt_arg14_lt := linking_type &
  [ LOCAL [ CAT.VAL [ SUBJ < [ LOCAL local_min &
				    [ CONT.INDEX #subjind & non_expl-ind ] ] >,
		      COMPS < [ LOCAL local_min &
				      [ KEYS [ KEY verb_aspect_rel,
					       MESSAGE 1-dlist ],
					CONT.TOP #mhand ] ], ... > ],
	    KEYS.ALTKEY arg14_rel & [ ARG1 #subjind,
				      ARG4 #mhand ] ] ].

; Maybe only for main verb "have" (AAC internal comment moved)

poss_lt := linking_type &         
  [ LOCAL [ CAT.VAL [ SUBJ < [ LOCAL local_min &
				     [ CONT [ TOP #top,
					      INDEX #subjind & ref-ind ] ] ] >,
		      COMPS 
                       < [ LOCAL local_min &
                               [ CONT [ TOP #top,
                                        INDEX #objind & ref-ind ] ] ], ... > ],
	    CONT [ TOP #top,
		   LISZT <! arg13_rel & 
			  [ ARG1 #subjind,
			    ARG3 #objind ] !> ] ] ].

expl_obj_lt := arg1_subj_lt &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL local_min & [ CONT.INDEX it-ind ] ], ... > ].

expl_obj_cp_lt := expl_obj_lt &
  [ LOCAL [ CAT.VAL.COMPS < synsem, 
			    [ LOCAL local_min &
				    [ KEYS.MESSAGE 1-dlist,
				      CONT.TOP #mhand ] ],
			    ... >,
	    KEYS.KEY arg14_rel & [ ARG4 #mhand ] ] ].

particle_cp_lt := arg1_subj_lt &
  [ LOCAL [ CAT.VAL.COMPS < *top*, [ LOCAL local_min &
				      [ KEYS [ KEY verb_aspect_rel,
					       MESSAGE 1-dlist ],
					CONT.TOP #mhand ] ], ... >,
	    KEYS.KEY arg14_rel & [ ARG4 #mhand ] ] ].

ditrans_lt := arg1_subj_lt &
  [ LOCAL [ CAT.VAL.COMPS 
                  < [ LOCAL local_min &
                           [ CONT [ TOP #top,
                                    INDEX #obj2ind & non_expl-ind ] ] ], 
                    [ LOCAL local_min &
                           [ CONT [ TOP #top,
                                    INDEX #objind & non_expl-ind ] ] ], ... >,
	    CONT.TOP #top,
	    KEYS.KEY arg123_rel & [ ARG2 #obj2ind,
				    ARG3 #objind ] ] ].

tritrans_lt := ditrans_lt &
  [ LOCAL [ CAT.VAL.COMPS < *top*, *top*, 
			    [ LOCAL local_min &
				 [ KEYS [ KEY v_event_rel,
					  MESSAGE 1-dlist ],
				   CONT.TOP #mhand ] ],
				... >,
	    KEYS.KEY arg1234_rel & [ ARG4 #mhand ] ] ].

basic_prep_intrans_lt := linking_type &
  [ LOCAL [ CAT.VAL.COMPS 
			< [ LOCAL local_min &
				  [ CAT.HEAD.TAM.TENSE no_tense,
                                    KEYS.KEY prep_rel & independent_rel,
				    CONT.INDEX #index ] ],
			  ... >,
	    CONT [ INDEX #index,
		   LISZT.LIST < #key, ... > ],
	    KEYS.KEY #key ] ].

prep_intrans_unerg_lt := basic_prep_intrans_lt & arg1_subj_lt.
prep_intrans_lt := prep_intrans_unerg_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.TOP #top ], ... >,
            CONT [ TOP #top,
                   LISZT <! relation !> ] ] ].
prep_intrans_unacc_lt := basic_prep_intrans_lt & unacc_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.TOP #top ], ... >,
            CONT [ TOP #top,
                   LISZT <! relation !> ] ] ].
prep_intrans_nosubj_lt := prep_intrans_unerg_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.TOP #top ] >,
            KEYS.KEY.HANDEL #top ] ].

;; DPF 14-Sept-00 - This type does not conform to our assumption that only 
;; handles, indices, and external arguments of COMPS synsems can be referred 
;; to in semantic construction.  But we believe we have a better analysis on 
;; the way which collapses these collocation constructions into a more general
;; account of multi-word expressions, so we allow the hack for now.

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

empty_prep_intrans_lt := arg1_subj_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
				      [ KEYS.KEY prep_rel & selected_rel &
						     [ ARG3 #objind ],
					CONT.TOP #top ] ], ... >,
	    CONT.TOP #top,
	    KEYS.KEY arg13_rel & [ ARG3 #objind ] ] ].
		   

; Argument coercion as in "Kim suggests on Friday"
prep_intrans_event_lt := arg1_subj_lt &
  [ LOCAL [ CAT.VAL.COMPS 
			 < [ LOCAL local_min &
				   [ CAT.HEAD.TAM.TENSE no_tense,
                                     KEYS.KEY prep_rel & independent_rel,
				     CONT [ TOP #top,
					    INDEX #event ] ] ], ... >,
	    CONT [ TOP #top,
		   LISZT <! #key !> ],
	    KEYS.KEY #key & arg12_rel & [ ARG2 #event ] ] ].

basic_prep_trans_lt := arg13_lt &
  [ LOCAL [ CAT.VAL.COMPS < synsem,
			    [ LOCAL local_min &
				    [ CAT.HEAD [ MOD < synsem_min >,
                                                 TAM.TENSE no_tense ],
				      KEYS.KEY prep_rel & independent_rel ] ],
                            ... >,
            CONT.LISZT.LIST < #key, ... >,
            KEYS.KEY #key ] ].

; put
prep_trans_lt := basic_prep_trans_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.INDEX #objind ], 
                            [ LOCAL.CONT [ TOP #ctop,
                                           INDEX #objind ] ], ... >,
            CONT [ LISZT <! relation, hypo_rel & [ HANDEL #hand,
                                                   SOA #arghand ] !>,
                   H-CONS <! qeq & [ SC-ARG #arghand,
                                     OUTSCPD #ctop ] !> ],
            KEYS.KEY.ARG4 #hand ] ].

; buy from
prep_trans_nomsg_lt := basic_prep_trans_lt &
  [ LOCAL [ CAT.VAL.COMPS < synsem,
                            [ LOCAL.CONT [ TOP #ctop,
                                           INDEX #index ] ], ... >,
            CONT [ TOP #ctop,
                   INDEX #index,
                   LISZT <! relation !>,
                   H-CONS <! !> ] ] ].

; See discussion of empty_prep_intrans_lt above
;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

empty_prep_trans_lt := arg13_lt &
  [ LOCAL [ CAT.VAL.COMPS < *top*, [ LOCAL local_min &
				       [ KEYS.KEY prep_rel & selected_rel &
						      [ ARG3 #objind ],
					 CONT.TOP #top ] ], ... >,
	    CONT.TOP #top,
	    KEYS.KEY arg123_rel & [ ARG2 #objind ] ] ].
		   

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

basic_prep_particle_intrans_lt := arg1_subj_lt &
  [ LOCAL [ CAT.VAL.COMPS < *top*, [ LOCAL local_min &
					 [ CAT.HEAD.TAM.TENSE no_tense,
                                           KEYS.KEY prep_rel & [ ARG3 #arg3 ],
                                           CONT [ TOP #top,
						  INDEX #event ] ] ], ... >,
	    CONT.TOP #top,
	    KEYS.KEY arg13_rel & [ EVENT #event,
				   ARG3 #arg3 ] ] ].
		   
prep_particle_intrans_lt := basic_prep_particle_intrans_lt &
  [ LOCAL.CAT.VAL.COMPS < synsem, [ LOCAL.KEYS.KEY independent_rel ] > ].

empty_prep_particle_intrans_lt := basic_prep_particle_intrans_lt &
  [ LOCAL.CAT.VAL.COMPS < synsem, [ LOCAL.KEYS.KEY selected_rel ] > ].

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

prep_particle_trans_lt := arg13_lt &
  [ LOCAL [ CAT.VAL.COMPS < synsem,
                            synsem &
                              [ LOCAL.KEYS.KEY selected_rel ], 
                            synsem &
			      [ LOCAL local_min & 
                                      [ CAT.HEAD.TAM.TENSE no_tense,
                                        KEYS.KEY prep_rel & independent_rel &
                                                 [ ARG #event ],
                                        CONT.TOP #top ] ], ... >,
            CONT [ TOP #top,
                   INDEX #event ] ] ].
		   
cp_trans_lt := arg13_lt &
  [ LOCAL [ CAT.VAL.COMPS < *top*, 
			    [ LOCAL local_min &
				 [ KEYS [ KEY v_event_rel,
					  MESSAGE 1-dlist ],
				   CONT.TOP #mhand ] ], 
			    ...>,
	    KEYS.KEY arg134_rel & [ ARG4 #mhand ] ] ].

; "explain to Kim that ..."
pp_cp_lt := empty_prep_intrans_lt &
  [ LOCAL [ CAT.VAL.COMPS < *top*,
			    [ LOCAL local_min &
				  [ KEYS [ KEY v_event_rel,
					   MESSAGE 1-dlist ],
				    CONT.TOP #mhand ] ],
			    ... >,
	    KEYS.KEY arg134_rel & [ ARG4 #mhand ] ] ].

double_pp_lt := arg1_subj_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
				    [ CAT.HEAD.TAM.TENSE no_tense,
                                      KEYS.KEY prep_rel & independent_rel,
				      CONT [ TOP #top,
					     INDEX #event ] ] ], 
			    [ LOCAL local_min &
				    [ CAT.HEAD.TAM.TENSE no_tense,
                                      KEYS.KEY prep_rel & independent_rel,
                                      CONT [ TOP #top,
                                             INDEX #event ] ] ], ... >,
	    CONT [ TOP #top,
		   INDEX #event ] ] ].

arg13_double_pp_lt := arg13_lt &
  [ LOCAL [ CAT.VAL.COMPS < synsem,
                            [ LOCAL local_min &
				    [ CAT.HEAD.TAM.TENSE no_tense,
                                      KEYS.KEY prep_rel & independent_rel,
				      CONT [ TOP #top,
					     INDEX #event ] ] ], 
			    [ LOCAL local_min &
				    [ CAT.HEAD.TAM.TENSE no_tense,
                                      KEYS.KEY prep_rel & independent_rel,
                                      CONT [ TOP #top,
                                             INDEX #event ] ] ], ... >,
	    CONT [ TOP #top,
		   INDEX #event ] ] ].

s_equi_lt := linking_type &
  [ LOCAL [ CAT.VAL.SUBJ < [ LOCAL local_min &
				   [ CONT [ TOP #top,
					    INDEX #index & ref-ind ] ] ] >,
	    CONT.TOP #top,
	    KEYS.KEY arg14_rel & [ arg1 #index ] ] ].
		   

equi_prd_lt := linking_type & 
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL [ KEYS.KEY independent_rel,
				      CONT.E-INDEX #event ],
	    CONT.LISZT <! relation !>,
	    KEYS.KEY.ARG4 #event ] ].

; ERB (14-12-97) Subj_equi verbs now require that their complement have
; a pro_ss subject.  This might be redundant if some other supertype of
; all equi verbs requires hypo_rel on the complement.

; ERB (21-01-98) This pro_ss needs to be constrained to be unslashed.

subj_equi_lt := s_equi_lt &
  [ LOCAL.CAT.VAL [ SUBJ < [ LOCAL.CONT.INDEX #index ] >,
		    --KEYCOMP.LOCAL local_min &
				    [ CAT.VAL.SUBJ 
                                       < pro_ss &
                                         [ LOCAL.CONT.INDEX #index,
                                           NON-LOCAL.SLASH 0-dlist ] > ] ] ].

; 'try to leave'
subj_equi_inf_lt := subj_equi_lt &
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL local_min &
				    [ KEYS.MESSAGE 1-dlist & <! hypo_rel !>,
				      CONT.TOP #hand ],
	    KEYS.KEY.ARG4 #hand ] ].

subj_equi_prp_lt := subj_equi_lt &
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL local_min &
				    [ CONT.TOP #hand ],
	    CONT [ LISZT <! relation, hypo_rel & [ HANDEL #chand,
                                                   SOA #shand ] !>,
                   H-CONS <! qeq &
			   [ SC-ARG #shand,
			     OUTSCPD #hand ] !> ],
	    KEYS.KEY.ARG4 #chand ] ].

; ERB (14-12-97) comp's subj is a pro_ss.  This is probably redundant since
; the to_c_prop will say that its subject is a pro_ss.

; ERB (21-01-98) The pro_ss needs to be constrained to be unslashed.

; 'persuade Kim to leave'
obj_equi_lt := arg1_subj_lt &
  [ LOCAL [ CAT.VAL [ COMPS < [ LOCAL local_min &
				    [ CONT [ TOP #top,
					     INDEX #index & non_expl-ind ] ] ],
			      #comp &
			      [ LOCAL local_min &
				      [ CAT.VAL.SUBJ 
					 < pro_ss &
                                           [ LOCAL.CONT.INDEX #index,
                                             NON-LOCAL.SLASH 0-dlist ] > ] ] >,
		      --KEYCOMP #comp ],
	    CONT.TOP #top,
	    KEYS.KEY arg134_rel & [ ARG3 #index ] ] ].

; 'persuade Kim to leave'
obj_equi_inf_lt := obj_equi_lt &
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL local_min &
				    [ CONT.TOP #hand,
				      KEYS.MESSAGE 1-dlist ],
	    KEYS.KEY.ARG4 #hand ] ].

prd_comp_lt := linking_type &
  [ LOCAL [ CAT.VAL [ SUBJ < [ LOCAL.CONT nom-obj ] >,
		      --KEYCOMP.LOCAL [ CONT.TOP #chand,
                                        KEYS.KEY independent_rel ] ],
            CONT [ LISZT <! #key, hypo_rel & [ HANDEL #arghand,
                                               SOA #soa ] !>,
		   H-CONS <! qeq & [ SC-ARG #soa,
                                     OUTSCPD #chand ] !> ],
	    KEYS.KEY #key & [ ARG4 #arghand ] ] ].

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

pp_inf_lt := arg1_subj_lt &
 [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
				     [ KEYS.KEY.ARG3 #arg2 ] ],
			   [ LOCAL local_min &
				     [ CONT.TOP #hand ] ] >,
	   KEYS.KEY arg124_rel & [ ARG2 #arg2,
				   ARG4 #hand ] ] ].

; 'consider Kim wise'

obj_equi_prd_lt := prd_comp_lt & obj_equi_lt &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL local_min &
				  [ KEYS.KEY basic_nom_rel,
				    CONT.INDEX #index ] ],
			  [ LOCAL local_min &
				  [ CAT.VAL.SUBJ 
                                    < pro_ss &
                                      [ LOCAL local_min &
                                             [ CONT.INDEX #index ],
                                        NON-LOCAL.SLASH 0-dlist ] > ] ] > ].

; 'promise Kim to leave'
anom_equi_lt := s_equi_lt &
  [ LOCAL [ CAT.VAL 
		   [ SUBJ < [ LOCAL.CONT.INDEX #subjind ] >,
		     COMPS < [ LOCAL local_min &
				 [ CONT [ TOP #top,
                                          INDEX #objind & non_expl-ind ] ] ],
			     [ LOCAL local_min &
				[ CAT.VAL.SUBJ < pro_ss &
						 [ LOCAL local_min &
						   [ CONT.INDEX #subjind ],
                                                   NON-LOCAL.SLASH 0-dlist ] >,
				  KEYS.MESSAGE 1-dlist,
				  CONT.TOP #hand ] ] > ],
	    CONT.TOP #top,
	    KEYS.KEY arg134_rel &
		    [ ARG4 #hand,
		      ARG3 #objind ] ] ].

; 'turn out unreliable' (also resultatives like 'turned up drunk')
anom_equi_prd_lt := equi_prd_lt & s_equi_lt &
  [ LOCAL [ CAT.VAL 
		[ SUBJ < [ LOCAL.CONT [ TOP #chand,
                                        INDEX #index ] ] >,
                  --KEYCOMP #comp,
		  COMPS < *top*,
			  #comp &
			  [ LOCAL local_min &
				  [ CAT.VAL.SUBJ < pro_ss &
						   [ LOCAL local_min &
						      [ CONT.INDEX #index ],
                                                     NON-LOCAL.SLASH 0-dlist]>,
				    CONT.TOP #chand ] ] > ] ] ].

; 'turn out to be red'
anom_equi_particle_lt := s_equi_lt &
  [ LOCAL [ CAT.VAL 
		[ SUBJ < [ LOCAL.CONT.INDEX #index ] >,
		  COMPS < *top*,
			  [ LOCAL local_min &
				  [ CAT.VAL.SUBJ < pro_ss &
						   [ LOCAL local_min &
						     [ CONT.INDEX #index ],
                                                     NON-LOCAL.SLASH 0-dlist]>,
				    KEYS.MESSAGE 1-dlist,
				    CONT.TOP #hand ] ] > ],
	    KEYS.KEY.ARG4 #hand ] ].

basic_ssr_expr_lt := linking_type &
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL.CONT.TOP #chand,
	    CONT.H-CONS <! qeq &
			   [ SC-ARG #arghand,
			     OUTSCPD #chand ] !>,
	    KEYS.KEY arg24_event_rel & [ ARG4 #arghand ] ] ].

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing
;; JB 9/01 fixed from ARG4 to ARG2

ssr_expr_lt := basic_ssr_expr_lt &
  [ LOCAL [ CAT.VAL [ --KEYCOMP #comp,
		      COMPS < [ LOCAL.KEYS.KEY.ARG3 #arg4 ], 
			      #comp, ... > ],
	    KEYS.KEY.ARG2 #arg4 ] ].

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing 
;; fixed LOCAL.KEYS.KEY to ARG2 being linked

ssr_prdp_expr_lt := basic_ssr_expr_lt &
  [ LOCAL [ CAT.VAL [ SUBJ < [ LOCAL.CONT nom-obj ] >,
                      --KEYCOMP #comp,
                      COMPS < #comp,
                              [ LOCAL [ KEYS.KEY _to_rel_s &
                                                 [ ARG3 #arg2 ],
                                        CONT.TOP #top ] ],
                              ... > ],
            CONT [ TOP #top,
		   LISZT <! #key !> ],
	    KEYS.KEY #key & [ ARG2 #arg2 & non_expl-ind ] ] ].

sor_lt := linking_type &
  [ LOCAL [ CAT.VAL [ SUBJ < [ LOCAL local_min &
				     [ CONT [ TOP #top,
					      INDEX #index ] ] ] >,
                      COMPS < synsem & [ LOCAL local_min &
                                               [ CONT.TOP #top ] ], 
                              [ LOCAL local_min &
                                      [ CONT.TOP #chand ] ] > ],
	    CONT [ TOP #top,
		   H-CONS <! qeq &
			   [ SC-ARG #arghand,
			     OUTSCPD #chand ] !> ],
	    KEYS.KEY arg14_rel & [ ARG4 #arghand,
				   ARG1 #index ] ] ].

expl_it_subj_lt := linking_type &
  [ LOCAL [ CAT.VAL [ SUBJ < [ LOCAL local_min & [ CONT [ TOP #hand,
							  INDEX it-ind ] ] ] >,
		      COMPS < synsem, #comp, ... >,
		      --KEYCOMP #comp & 
			   [ LOCAL local_min &
				   [ KEYS.MESSAGE 1-dlist,
				     CONT.TOP #chand ] ] ],
	    CONT.H-CONS <! !>,
	    KEYS.KEY arg4_rel &
		    [ HANDEL #hand,
		      ARG4 #chand ] ] ].

expl_it_subj_np_cp_lt := expl_it_subj_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
                                    [ CONT.INDEX #arg3 ] ], ... >,
            KEYS.KEY arg34_event_rel &
		    [ ARG3 #arg3 ] ] ].

expl_it_subj_pp_cp_lt := expl_it_subj_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
                                    [ KEYS.KEY.ARG3 #arg2 ] ], ... >,
            KEYS.KEY arg24_event_rel &
		    [ ARG2 #arg2 ] ] ].

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

expl_pp_inf_lt := arg1_subj_lt &
 [ LOCAL [ CAT.VAL [ COMPS < [ LOCAL local_min &
				     [ CONT.INDEX it-ind ] ],
			       [ LOCAL local_min &
				     [ KEYS.KEY.ARG3 #arg2,
				       CONT.TOP #top ] ],
			       [ LOCAL local_min &
				     [ CONT.TOP #hand ] ] > ],
	   CONT.TOP #top,
	   KEYS.KEY arg124_rel & [ ARG2 #arg2,
				   ARG4 #hand ] ] ].

; 'leave it to Kim to find the answer'
;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

expl_pp_inf_oeq_lt := expl_pp_inf_lt &
 [ LOCAL.CAT.VAL.COMPS < *top*,
			 [ LOCAL.KEYS.KEY.ARG3 #arg3 ],
			 [ LOCAL.KEYS.KEY arg1_rel & [ ARG1 #arg3 ] ] > ].

; 'arrange with Kim to meet her'
;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

pp_inf_seq_lt := pp_inf_lt &
 [ LOCAL.CAT.VAL [ SUBJ < [ LOCAL local_min &
				  [ CONT.INDEX #arg1 ] ] >,
		   COMPS < *top*,
			  [ LOCAL.KEYS.KEY arg1_rel & [ ARG1 #arg1 ] ] > ] ].

; 'arrange it with Kim to meet her'
;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

expl_pp_inf_seq_lt := expl_pp_inf_lt &
 [ LOCAL.CAT.VAL [ SUBJ < [ LOCAL local_min &
				  [ CONT.INDEX #arg1 ] ] >,
		   COMPS < *top*,
			   *top*,
			   [ LOCAL.KEYS.KEY arg1_rel & [ ARG1 #arg1 ] ] > ]].

; suffice
atrans_inf_lt := linking_type &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
				  [ CONT.TOP #hand ] ] >,
	    KEYS.KEY arg14_rel & [ ARG4 #hand ] ] ].

particle_NP_lt := arg1_subj_lt & 
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL [ CONT.TOP #top,
                                      KEYS.KEY selected_rel ] ],
                            [ LOCAL local_min &
				  [ CONT [ TOP #top,
                                           INDEX #index & 
                                               non_expl-ind ] ] ], ... >,
            CONT.TOP #top,
	    KEYS.KEY arg13_rel & [ ARG3 #index ] ] ].

NP_particle_lt := arg1_subj_lt & 
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
                                    [ CONT [ TOP #top,
                                             INDEX #index & non_expl-ind ] ] ],
			      [ LOCAL.CONT.TOP #top ], ... >,
	    CONT.TOP #top,
	    KEYS.KEY arg13_rel & [ ARG3 #index ] ] ].

particle_np_pp_lt := particle_np_lt &
  [ LOCAL [ CAT.VAL.COMPS < synsem,
                            synsem,
                            synsem &
			      [ LOCAL local_min & 
                                      [ CAT.HEAD.TAM.TENSE no_tense,
                                        KEYS.KEY prep_rel & independent_rel &
                                                 [ ARG #event ],
                                        CONT.TOP #top ] ], ... >,
            CONT [ TOP #top,
                   INDEX #event ] ] ].
		   
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Val Types
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Valence types specify the number and type of subcategorized complements.
  
valence_type :< canonical_synsem.

unsat_subst := valence_type &
  [ LOCAL.CAT.VAL.SUBJ < synsem_min & [ LOCAL [ CAT nomp_cat_min,
                                                CONJ cnil ] ] > ].

trans_or_intrans_subst :< unsat_subst.

intrans_subst := one_arg & trans_or_intrans_subst &
  [ LOCAL.CAT.VAL.COMPS < > ].

unsat_two_arg_subst := unsat_subst &
  [ LOCAL [ CAT.VAL.COMPS < synsem & [ LOCAL local_min &
					     [ KEYS.KEY #ckey ] ], ... >,
	    KEYS.--COMPKEY #ckey ] ].

two_arg_subst := unsat_two_arg_subst &
  [ LOCAL.CAT.VAL.COMPS < *top* > ].

; For verbs that passivize.
; 21-Apr-00 Changed np_cat_acc_min to nomp_cat_acc_min for e.g. "kim liked 
; hiring abrams"
; DPF 29-May-01 - Removed COMPS..MESSAGE 0-dlist, since this blocked
; acc-gerunds as in "I resent you giving him that"
trans_subst := unsat_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     CONJ cnil ] ], ... > ].
  
np_trans_subst := trans_subst & two_arg_subst & two_arg & 
                  trans_or_intrans_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem > ].
  
; For verbs with nominal complement but that don't passivize.
non_trans_subst := unsat_two_arg_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ KEYS.MESSAGE 0-dlist,
					     CONJ cnil ] ], ... > ].
  
np_non_trans_subst := non_trans_subst & two_arg_subst & 
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL.CAT nomp_cat_acc_min ] > ].

cp_intrans_subst := two_arg_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < #comp & synsem &
			    [ LOCAL [ CAT s_cat_v_c,
				      CONJ cnil ] ] > ] ].
  
prep_intrans_subst := two_arg_subst & 
  [ LOCAL.CAT.VAL.COMPS < synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].

unsat_three_arg_subst := unsat_two_arg_subst &
  [ LOCAL [ CAT.VAL.COMPS < synsem, 
			    synsem & [ LOCAL local_min &
					  [ KEYS.KEY #ckey ] ], ... >,
	    KEYS.--OCOMPKEY #ckey ] ].

three_arg_subst := unsat_three_arg_subst & three_arg.
basic_three_arg_subst := unsat_three_arg_subst & basic_three_arg.

three_arg_trans_subst := unsat_three_arg_subst & three_arg & trans_subst.
basic_three_arg_trans_subst := unsat_three_arg_subst & basic_three_arg & 
			       trans_subst.
three_arg_nontrans_subst := unsat_three_arg_subst & three_arg & 
                            non_trans_subst.

three_arg_nontrans_raising_subst := unsat_three_arg_subst & basic_three_arg &
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     KEYS.MESSAGE 0-dlist,
					     CONJ cnil ] ], ... > ].

prep_trans_subst := basic_three_arg_trans_subst & 
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     KEYS.MESSAGE 0-dlist,
					     CONJ cnil ] ], 
			  synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].
  
prep_non_trans_subst := three_arg_nontrans_subst & 
  [ LOCAL.CAT.VAL.COMPS < lex_synsem & [ LOCAL [ CAT nomp_cat_acc_min,
						 KEYS.MESSAGE 0-dlist,
                                                 CONJ cnil ],
                                         LEX +],
			  synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].
  
; For idioms like "take hold of"
prep_nbar_subst := three_arg_nontrans_subst & 
  [ LOCAL.CAT.VAL.COMPS < lex_synsem & [ LOCAL [ CAT nbar_cat,
						 KEYS.MESSAGE 0-dlist,
                                                 CONJ cnil ],
                                         LEX +],
			  synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].
  
prd_trans_subst := basic_three_arg_trans_subst & 
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     KEYS.MESSAGE 0-dlist,
					     CONJ cnil ] ],
			  synsem & 
			  [ LOCAL [ CAT prd_cat,
				    CONJ cnil ] ] > ].

prd_non_trans_subst := basic_three_arg_subst & non_trans_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     KEYS.MESSAGE 0-dlist,
					     CONJ cnil ] ], 
			  synsem & 
			  [ LOCAL [ CAT prd_cat,
				    CONJ cnil ] ] > ].

ditrans_subst := three_arg_trans_subst & 
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     KEYS.MESSAGE 0-dlist,
					     CONJ cnil ] ], 
			  synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     CONJ cnil,
					     KEYS [ KEY nonpro_rel,
						    MESSAGE 0-dlist ] ] ] > ].
  
; 'inform Kim that S'
cp_trans_subst := three_arg_trans_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					       KEYS.MESSAGE 0-dlist,
					       CONJ cnil ] ],
			    #comp & synsem &
			    [ LOCAL [ CAT s_cat_v_c,
				      CONJ cnil ] ] > ] ]. 
  
pp_cp_subst := three_arg_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < synsem &
			    [ LOCAL [ CAT pp_cat,
				      CONJ cnil ] ],
			    #comp & synsem &
			    [ LOCAL [ CAT s_cat_fin_v_c,
				      CONJ cnil ] ] > ] ].
  
np_comp_subst := three_arg_trans_subst & 
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     KEYS.MESSAGE 0-dlist,
					     CONJ cnil ] ],
			  [ LOCAL local_min &
				 [ CAT [ HEAD prep,
					 VAL [ SUBJ < synsem >,
					       COMPS *olist* ] ] ] ] > ].
; 'talk to Kim about Sandy'
double_pp_subst := three_arg_subst & 
  [ LOCAL.CAT.VAL.COMPS < synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ], 
			  synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].
  
np_trans_double_pp_subst := four_arg & trans_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem,
                          synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ], 
			  synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].
  
inf_or_prp_intrans_subst := unsat_two_arg_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < #comp & synsem &
			    [ LOCAL [ CAT vp_cat &
					  [ HEAD.VFORM inf_or_prp ],
				      CONJ cnil ] ] > ] ].

inf_intrans_subst := inf_or_prp_intrans_subst &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL local_min &
				  [ CAT.HEAD.VFORM inf ] ] > ].

inf_intrans_particle_subst := three_arg_subst &
  [ LOCAL [ CAT.VAL [ --KEYCOMP #comp,
                      COMPS < synsem &
			    [ LOCAL.KEYS.KEY selected_rel & [ HANDEL #hand ] ],
			    #comp & synsem &
			    [ LOCAL [ CAT vp_cat & [ HEAD.VFORM inf ],
				      CONJ cnil ] ] > ],
            KEYS.KEY.HANDEL #hand ] ].

; for "I will try and find it" - analysis following Pullum and Gazdar

inf_intrans_and_subst := unsat_two_arg_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < #comp & synsem &
			    [ LOCAL [ CAT vp_cat &
					  [ HEAD.VFORM inf_and ],
                                      CONJ cnil ] ] > ] ].

for_inf_intrans_subst := two_arg_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem & 
			  [ LOCAL [ CAT s_cat_v_c &
					[ HEAD.VFORM inf ],
				    CONJ cnil ] ] > ].

inf_trans_subst := three_arg_trans_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp, 
		    COMPS < *top*, #comp & synsem &
			           [ LOCAL [ CAT vp_inf_cat,
					     CONJ cnil ] ] > ] ].

inf_trans_raising_subst := basic_three_arg_trans_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp, 
		    COMPS < *top*, 
			    #comp & synsem &
			    [ LOCAL [ CAT vp_inf_cat,
				      CONJ cnil ] ] > ] ].

prp_intrans_subst := inf_or_prp_intrans_subst & basic_two_arg &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL local_min &
				  [ CAT.HEAD.VFORM prp ] ] > ].

; 'make, let, watch' don't passivize.  Some of these have an apparently
; corresponding passive, but with an ordinary 'to-VP' complement, not vform bse.
bse_nontrans_raising_subst := three_arg_nontrans_raising_subst & 
  [ LOCAL.CAT.VAL.COMPS < *top*, 
			  synsem &
			  [ LOCAL [ CAT vp_bse_cat,
				    CONJ cnil ] ] > ].

; want (doesn't passivize)
inf_non_trans_subst := basic_three_arg_subst & non_trans_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < synsem & [ LOCAL.CAT nomp_cat_acc_min ],
			    #comp & synsem &
			    [ LOCAL [ CAT vp_inf_cat,
				      CONJ cnil ] ] > ] ].

pp_inf_subst := unsat_three_arg_subst &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < synsem &
			    [ LOCAL [ CAT pp_cat,
				      CONJ cnil,
				      KEYS.KEY selected_rel ] ],
			    #comp & synsem &
			    [ LOCAL [ CAT vp_inf_cat,
				      CONJ cnil ] ] > ] ].

; We treat raising as the identification of the CONT attribute of the SUBJ and
; the COMPS.SUBJ, rather than the whole synsem, in part to enable the 
; underspecification of the base/non3sg lex-entry for verbs, where otherwise
; CASE information would clash in examples like "Kim believes Sandy to be ready."
; In fact, we only identify the INDEX of the two subjects - see comments by
; sor_inf_subst.
; 
; DPF 17-Nov-99 - The TOP handle of the SUBJ must be identified with the
; KEY.HANDEL of the ssr_subst predicate, since contracted negation auxiliary
; verbs have the TOP be the neg_rel's handle, but the subject as in 
; "I can't leave" should have its HANDEL identified with that of the
; can_rel, not the neg_rel.

ssr_subst := two_arg_subst &
  [ LOCAL.CAT.VAL [ SUBJ < [ LOCAL local_min & [ CONT.INDEX #sind ] ] >,
                    COMPS < [ LOCAL local_min &
                                    [ CAT.VAL [ SUBJ < [ LOCAL.CONT.INDEX #sind,
                                                         NON-LOCAL [ SLASH 0-dlist,
                                                                     REL 0-dlist,
                                                                     QUE 0-dlist ] ] >,
                                                COMPS *olist* ],
                                      KEYS.MESSAGE 0-dlist ] ] > ] ].

;;; AAC - added to avoid overspecific glb types

ssr_two_arg_subst := ssr_subst & basic_two_arg.

ssr_inf_subst := ssr_subst & inf_intrans_subst.

ssr_pp_inf_subst :=  basic_three_arg_subst &
  [ LOCAL [ CAT.VAL 
	   [ SUBJ < [ LOCAL.CONT [ TOP #top,
				   INDEX #subj ] ] >,
	     COMPS < synsem &
		     [ LOCAL [ CAT pp_cat,
			       CONJ cnil ] ] ,
		     [ LOCAL local_min &
			     [ CAT [ HEAD verbal & strict_type &
					  [ VFORM inf ],
				     VAL [ SUBJ < [ LOCAL.CONT.INDEX #subj,
						    NON-LOCAL [ SLASH 0-dlist,
								REL 0-dlist,
								QUE 0-dlist ]] >,
						   COMPS *olist* ] ],
					  KEYS.MESSAGE 0-dlist ] ] > ],
	    CONT.TOP #top ] ].

ssr_prdp_pp_subst := basic_three_arg_subst &
  [ LOCAL [ CAT.VAL 
              [ SUBJ < [ LOCAL local_min & [ CONT [ TOP #top,
						INDEX #subj ] ] ] >,
		COMPS < [ LOCAL local_min &
			  [ CAT [ HEAD.PRD +,
				  VAL [ SUBJ < [ LOCAL.CONT.INDEX #subj,
						 NON-LOCAL [ SLASH 0-dlist,
							     REL 0-dlist,
							     QUE 0-dlist ] ] >,
					SPR *olist*,
					COMPS *olist* ],
				  MC na ] ] ],
			synsem &
			[ LOCAL [ CAT pp_cat,
				  CONJ cnil ] ] > ],
	    CONT.TOP #top ] ].

expl_pp_cp_subst :=  basic_three_arg_subst &
  [ LOCAL [ CAT.VAL.COMPS < synsem &
			    [ LOCAL [ CAT pp_cat,
				      CONJ cnil ] ], 
			    synsem &
			           [ LOCAL [ CAT cat & [ HEAD comp,
							 VAL [ SUBJ *olist*,
							       COMPS *olist* ] ],
					     CONJ cnil ] ] >,
	    KEYS.--COMPKEY _to_rel_s ] ].

expl_np_cp_subst :=  basic_three_arg_subst &
  [ LOCAL [ CAT.VAL.COMPS < synsem &
			    [ LOCAL [ CAT nomp_cat_acc_min,
				      CONJ cnil ] ], 
			    synsem &
			           [ LOCAL [ CAT cat & 
                                                 [ HEAD comp,
                                                   VAL.COMPS *olist* ],
                                             CONJ cnil ] ] > ] ].

expl_np_cp_fin_subst := expl_np_cp_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem, 
                          [ LOCAL.CAT.VAL.SUBJ *olist* ] > ].

expl_np_cp_inf_subst := expl_np_cp_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem, 
                          [ LOCAL.CAT [ HEAD.VFORM inf,
                                        VAL.SUBJ < synsem > ] ] > ].

; Raising synsems have to identify the LOCAL value of the raised synsem with
; that of the VP's subject's synsem, rather than identifying the synsems
; themselves, due to our lexical threading of SLASH: if the raised complement
; is extracted, it will be of type gap, and therefore have a non-empty SLASH,
; but we don't want the lower VP complement's subject to also then have a
; non-empty SLASH.  But cf. note above for subj-subj raising, where maybe we
; even want to restrict the identity to CONT only.
;   Indeed, this seems necessary, since CASE blocks unification for
; 'Kim made him sleep' where our (dual-purpose) 'sleep' requires its subject
; to be [CASE nom] (in order to block the sentence "them sleep"), but when its
; subject is raised to object as with "make", there's a conflict with the
; [CASE acc] on the object of "make".  So here again, we make the raising
; identity hold only for CONT.
; DPF (30-Apr-99) In fact, we now have to further restrict the identity to
; only the INDEX, since if we identify all of CONT, we get the handles of the
; higher NP and the VP's subject being identified, but since we also want to 
; lexically identify a predicate's handle with that of its arguments (subject to
; classes ; of lexical exceptions like for entries with CP complements), we get 
; an unwanted identity of the handles of the higher and lower verbs through the
; identity of the two NPs.  So if we can't sustain this minimal identification
; for equi and raising, we'll have to give up on identifying the handles of
; lexical entries and their arguments.  Possible trouble spots are locative
; inversions like "On the corner seemed to be an abandoned house".

sor_inf_subst := inf_trans_raising_subst & 
  [ LOCAL.CAT.VAL.COMPS < synsem &
			  [ LOCAL.CONT.INDEX #subj,
			    OPT - ], 
			  [ LOCAL local_min &
				  [ CAT.VAL.SUBJ 
						< [ LOCAL.CONT.INDEX #subj,
						    NON-LOCAL [ SLASH 0-dlist,
								REL 0-dlist,
								QUE 0-dlist ] ]>,
					KEYS.MESSAGE 0-dlist ],
				OPT - ] > ].

sor_bse_subst := bse_nontrans_raising_subst &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL.CONT.INDEX #subj,
			    OPT - ], 
			  [ LOCAL local_min &
				  [ CAT.VAL.SUBJ 
					     < [ LOCAL.CONT.INDEX #subj,
						 NON-LOCAL [ SLASH 0-dlist,
							     REL 0-dlist,
							     QUE 0-dlist ] ] > ],
			    OPT - ] > ].

sor_inf_non_trans_subst := inf_non_trans_subst & 
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL.CONT.INDEX #subj,
			    OPT - ],
			  [ LOCAL local_min &
				  [ CAT.VAL.SUBJ 
					     < [ LOCAL.CONT.INDEX #subj,
						 NON-LOCAL [ SLASH 0-dlist,
							     REL 0-dlist,
							     QUE 0-dlist ] ]>,
					KEYS.MESSAGE 0-dlist ],
				OPT - ] > ].

particle_subst := unsat_two_arg_subst &
  [ LOCAL [ CAT.VAL.COMPS < lex_synsem & 
			    [ LOCAL local_min &
				    [ CAT.HEAD.PRD -,
				      CONT.TOP #hand,
				      KEYS.KEY selected_rel ],
                              LEX +,
			      NON-LOCAL [ SLASH 0-dlist,
					  REL 0-dlist,
					  QUE 0-dlist ] ], ... >,
	    KEYS.KEY [ HANDEL #hand ] ] ].

;; DPF (29-Jan-99) Modified particle_NP_subst to exclude pronominal NP objects,
;; to block *Kim looked up it"

generic_particle_NP_subst := particle_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem,
			  synsem & 
                          [ LOCAL [ CAT nomp_cat_acc_min,
                                    CONJ cnil,
                                    KEYS [ KEY nonpro_rel,
                                           MESSAGE 0-dlist ] ] ], ... > ].

particle_NP_subst := generic_particle_NP_subst & three_arg &
  [ LOCAL.CAT.VAL.COMPS < synsem, synsem > ].

particle_NP_pp_subst := generic_particle_NP_subst & four_arg &
  [ LOCAL.CAT.VAL.COMPS < synsem, 
                          synsem,
			  synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].

generic_NP_particle_subst := unsat_subst & 
  [ LOCAL [ CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					       KEYS.MESSAGE 0-dlist,
					       CONJ cnil ] ],
			    lex_synsem &
			      [ LOCAL local_min &
				      [ CAT.HEAD.PRD -,
					KEYS.KEY #ckey & selected_rel ],
                                LEX +,
				NON-LOCAL [ SLASH 0-dlist,
					    REL 0-dlist,
					    QUE 0-dlist ] ], ... >,
	    KEYS.--COMPKEY #ckey ] ].

NP_particle_subst := generic_NP_particle_subst & trans_subst & three_arg &
  [ LOCAL.CAT.VAL.COMPS < synsem, synsem > ].

particle_plus_subst := particle_subst & three_arg_subst.

particle_prd_subst := particle_plus_subst & 
  [ LOCAL.CAT.VAL.COMPS < *top*, 
			  synsem & 
			  [ LOCAL [ CAT prd_cat,
				    CONJ cnil ] ] > ].

; 'turn out to be'
particle_inf_subst := particle_plus_subst & 
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < *top*, 
			    #comp & synsem &
			    [ LOCAL [ CAT vp_inf_cat,
				      CONJ cnil ] ] > ] ].

particle_pp_subst := particle_plus_subst & 
  [ LOCAL.CAT.VAL.COMPS < *top*, 
			  synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].

; 'find out that S'
particle_cp_subst := particle_plus_subst & 
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < *top*, 
			    #comp & synsem &
			    [ LOCAL [ CAT s_cat_fin_v_c,
				      CONJ cnil ] ] > ] ].

;; 'leave it to Kim to schedule the meeting'
unsat_four_arg_subst := unsat_three_arg_subst &
  [ LOCAL.CAT.VAL.COMPS < synsem, 
			  synsem,
			  synsem, ... > ].

; 'leave it to Kim to find the answer'
expl_pp_inf_subst := unsat_four_arg_subst & trans_subst & four_arg &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < *top*, 
			    synsem &
			    [ LOCAL [ CAT pp_cat,
				      CONJ cnil ] ] ,
			    #comp & synsem &
			    [ LOCAL [ CAT vp_inf_cat,
				      CONJ cnil ] ] > ] ].

;; 'put kim through to Sandy'
np_particle_pp_subst := generic_NP_particle_subst & four_arg &
  [ LOCAL.CAT.VAL.COMPS < synsem, 
			  synsem, 
			  synsem &
			  [ LOCAL [ CAT pp_cat,
				    CONJ cnil ] ] > ].


;; 'bet Kim a fortune that Sandy would win'
;; DPF (19-Oct-98) Added hack to block extraction from the CP complement, since
;; don't know how to avoid topicalized imperative parse for "Abrams bet Browne
;; Chiang slept with subject of "slept" topicalized.

np_np_cp_subst := unsat_four_arg_subst & trans_subst & four_arg &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					       KEYS.MESSAGE 0-dlist,
					       CONJ cnil ] ],
			    synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					       KEYS.MESSAGE 0-dlist,
					       CONJ cnil ] ],
			    #comp & synsem &
			    [ LOCAL [ CAT s_cat_fin_v_c,
				      CONJ cnil ],
			      NON-LOCAL.SLASH 0-dlist ] > ] ].

; E.g., "I'm not doing too badly"

adv_subst := unsat_two_arg_subst &
  [ LOCAL [ CAT.VAL.COMPS < lex_synsem & 
			    [ LOCAL local_min &
				    [ CAT.HEAD adv,
				      CONT [ TOP #hand,
					     INDEX #event ],
				      KEYS [ KEY adv_rel,
					     MESSAGE 0-dlist ] ],
                              LEX + ] >,
	    KEYS.KEY [ HANDEL #hand,
		       EVENT #event ] ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; VERB SYNSEMS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Verb synsems should inherit from one valence type and one linking type.
;; The type verb_synsem is given a non-empty SPR value so VPs can be modified
;; by PPs, but (hack) the synsem is of type anti_synsem to block unification, to
;; avoid ever picking up an actual specifier.
;;   Added VIT links for the moment, to satisfy VIT requirements for visibility
;; of TENSE and MOOD information.  (Actually, moved VITTENSE specification to
;; the various inflectional affixes in lexrules.tdl, to accommodate the collapse
;; of the base and fin-non3sg verb forms.
;; DPF (8-Jul-98) Added QUE 0-dlist, REL 0-dlist to block pied piping in verb
;; phrases (taking advantage of lexical amalgamation of these features, together
;; with our treatment of subject-WH as extraction (where only local features are
;; propagated in SLASH).

verb_synsem := lex_synsem &
  [ LOCAL [ STEMHEAD vstem,
            AGR #agr,
            CAT [ HEAD verb* & [ TAM.MOOD #mood ],
		  VAL [ SUBJ < synsem_min & #subj &
			       [ LOCAL local_min &
				       [ AGR #agr ] ] >,
			SPR < anti_synsem >,
			COMPS #comps ] ],
	    CONT [ INDEX #event & [ E.MOOD #mood ],
                   E-INDEX #event ],
	    ARG-S < #subj . #comps > ],
    NON-LOCAL [ QUE 0-dlist,
		REL 0-dlist ] ].

atrans_verb := verb_synsem & intrans_subst & atrans_lt.

unerg_verb := verb_synsem & intrans_subst & arg1_subj_lt.
  
unacc_verb := verb_synsem & intrans_subst & unacc_lt.
  
; For at least main verb "have" (e.g, "Kim has some rice")
poss_verb := verb_synsem & two_arg_subst & two_arg & poss_lt &
  [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					     KEYS.MESSAGE 0-dlist,
					     CONJ cnil ] ] > ].
  
np_trans_verb := verb_synsem & np_trans_subst & trans_lt.
  
np_non_trans_verb := verb_synsem & np_non_trans_subst & two_arg & trans_lt.

np_non_trans_unacc_verb := verb_synsem & np_non_trans_subst & two_arg & 
			   unacc_lt.

basic_prep_intrans_verb := verb_synsem & prep_intrans_subst & basic_two_arg.

;; come to
prep_intrans_verb := basic_prep_intrans_verb & prep_intrans_lt & two_arg.

;; move to
prep_intrans_unacc_verb := basic_prep_intrans_verb & prep_intrans_unacc_lt
                           & two_arg.
;; thanks (hack)
prep_intrans_nosubj_verb := basic_prep_intrans_verb & prep_intrans_nosubj_lt.

;; look for
;;
empty_prep_intrans_verb := verb_synsem & prep_intrans_subst & two_arg & 
			   empty_prep_intrans_lt.

;; suggest on Tuesday
;;
prep_intrans_event_verb := verb_synsem & prep_intrans_subst & two_arg & 
			   prep_intrans_event_lt.

;; put, move
;;
prep_trans_verb := verb_synsem & prep_trans_subst & prep_trans_lt.
  
;; buy (from)
prep_trans_nomsg_verb := verb_synsem & prep_trans_subst & prep_trans_nomsg_lt.

;; thanks
basic_prep_trans_verb := verb_synsem & prep_trans_subst & basic_prep_trans_lt.

;; remind-of
;;
empty_prep_trans_verb := verb_synsem & prep_trans_subst & empty_prep_trans_lt &
  [ LOCAL.CONT.H-CONS <! !> ].

;; interest him in X
;;
empty_prep_non_trans_verb := verb_synsem & prep_non_trans_subst & 
			     empty_prep_trans_lt.

;; get-hold-of
;;
empty_prep_nbar_verb := verb_synsem & prep_nbar_subst & 
			     empty_prep_trans_lt.

;; regard Kim as tall
np_comp_verb := verb_synsem & np_comp_subst & obj_equi_lt &
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL.CONT.TOP #top,
	    CONT.TOP #top ] ].

;; strike Kim as tall
np_comp_obj_verb := verb_synsem & np_comp_subst & 
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.INDEX #index ],
			  [ LOCAL.CONT.E-INDEX #event ] >,
	    KEYS.KEY arg13_rel & [ ARG3 #event,
				   ARG1 #index ] ] ].


; DPF 4-Jun-01 - Changed dative-to to be contentful "to", in order to sustain
; 'underspecified' semantics for "kim sent it to Sandy/Paris".  Verbs like 
; "hand" are now of type empty_to_trans_verb.
to_trans_verb := prep_trans_nomsg_verb &
  [ LOCAL.KEYS.--OCOMPKEY _to_rel ].
  
empty_to_trans_verb := empty_prep_trans_verb &
  [ LOCAL.KEYS.--OCOMPKEY _to_rel_cm ].

ditrans_verb := verb_synsem & ditrans_subst & ditrans_lt.
  
ditrans_only_verb := verb_synsem & ditrans_subst & ditrans_lt &
  [ LOCAL.CAT.VAL.COMPS < synsem, synsem > ].

; 'talk to Kim about Sandy'
double_pp_verb := verb_synsem & double_pp_subst & double_pp_lt.

; 'change the meeting from Tuesday to Wednesday'
np_trans_double_pp_verb := verb_synsem & np_trans_double_pp_subst & 
                           arg13_double_pp_lt.

; Raising verbs which take an infinitival complement restrict it to being
; [ MESSAGE 0-dlist ], which ensures that the VP will be headed by the 
; non-propositional "to" complementizer, which identifies its handle with
; that of its VP complement.

ssr_verb := verb_synsem & ssr_inf_subst & basic_two_arg &
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL.CONT.TOP #chand,
	    CONT.H-CONS <! qeq &
			   [ SC-ARG #arghand,
			     OUTSCPD #chand ] !>,
	    KEYS.KEY.ARG4 #arghand ] ].

ssr_pp_inf_verb := verb_synsem & ssr_pp_inf_subst & ssr_expr_lt.

ssr_prdp_pp_verb := verb_synsem & ssr_prdp_pp_subst & ssr_prdp_expr_lt.

sor_verb := verb_synsem & sor_inf_subst & sor_lt.

sorb_verb := verb_synsem & sor_bse_subst & sor_lt.

sor_non_trans_verb := verb_synsem & sor_inf_non_trans_subst & sor_lt.

subj_equi_prp_verb := verb_synsem & prp_intrans_subst & subj_equi_prp_lt.

; "K became quiet"
subj_prd_verb := verb_synsem & ssr_two_arg_subst & prd_comp_lt &
  [ LOCAL.CAT.VAL [ COMPS < synsem & #comp &
                            [ LOCAL [ CAT prd_cat,
                                      CONJ cnil ] ] >,
                    --KEYCOMP #comp ] ].

; "K got hired"
subj_equi_prd_verb := subj_prd_verb & subj_equi_lt.

obj_equi_prd_verb := verb_synsem & prd_trans_subst & obj_equi_prd_lt.

obj_equi_non_trans_prd_verb := verb_synsem & prd_non_trans_subst & 
			       obj_equi_prd_lt.

; ERB (07-10-96) I am working with the verb 'know' as a prototype for a verb
; that selects by the semantic type (for now expressed as a feature) of its
; sentential complement rather than by the value of CMP or some such hack.  In
; order to do so, I have changed the types around here.  In particular, I am
; adding a new type cp_prop+ques_intrans_verb that takes a sentential
; complement but doesn't care if its a proposition or a question (for verbs
; like 'know').  A further axis of variation is the VFORM of the complement
; (fin vs. inf).  Since my analysis of 'know' never makes use of the resolved
; subtypes (e.g., cp_prop_intrans_verb) I am not adding to the resolved
; subtypes, and just making the old ones inherit from the finite side of
; things for now.  The future inf_cp_prop_intrans_verb will probably be the
; type of subject equi verbs. A further future modification would involve the
; addition of cp_command_intrans_verb and cp_command+prop_intrans_verb for
; verbs like 'insist'.
;
; DPF (19-Jun-98) This type cannot itself simply add the complement CP's 
; (underspecified) message to its LISZT, since the generation algorithm requires
; that no relation on LISZT will ever get more specialized during parsing.  So 
; we make MESSAGE be list-valued (the empty list for all but complementizers
; and clauses), and have the Head-Complement rule append the values of MESSAGE
; from both of its daughters, since the msg comes from the head if it's a
; complementizer, and from the non-head if the head is a verb.

basic_cp_prop+ques_verb := verb_synsem &
  [ LOCAL [ CAT.VAL [ --KEYCOMP 
			  [ LOCAL local_min &
				  [ CAT [ HEAD verbal & strict_type &
					       [ INV - ] ],
				    KEYS.MESSAGE 1-dlist &
					<! message !> ] ] ],
	    CONT.LISZT.LIST < relation, ... > ] ].

cp_prop+ques_verb := basic_cp_prop+ques_verb &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE <! prop_ques_rel !> ].

fin_cp_prop+ques_verb := cp_prop+ques_verb &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.CAT s_cat_fin_unspec ].

fin_or_inf_cp_prop+ques_verb := cp_prop+ques_verb &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.CAT s_cat_fin_or_inf_v_c ].

bse_cp_verb := cp_prop+ques_verb &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.CAT s_cat_bse ].

;; DPF 4/21/98 - Moved [ COMPS.FIRST.LOCAL.CAT.MC - ] from cp_ques_intrans_verb
;; to cp_intrans_verb, to block spurious parse for e.g. "Kim knows who left"
;; JB 9/01 This info seems to have been moved up to s_cat_v_c instead at some 
;; point.

cp_intrans_verb := fin_cp_prop+ques_verb & cp_intrans_subst & two_arg &
		   arg14_lt &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT.HEAD.TAM.MOOD ind_or_mod_subj ] > ].

; 'know'
cp_fin_inf_intrans_verb := fin_or_inf_cp_prop+ques_verb & cp_intrans_subst &
                              two_arg & arg14_lt.

cp_prop_intrans_verb := cp_intrans_verb &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL.KEYS.MESSAGE 1-dlist &
					       <! prpstn_rel !> ] > ].

cp_ques_intrans_verb := cp_intrans_verb &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL.KEYS.MESSAGE 1-dlist &
					       <! int_rel !> ] > ].

;; suppose
cp_prop_raise_key_verb := fin_cp_prop+ques_verb & cp_intrans_subst & two_arg &
			  alt_arg14_lt &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL [ KEYS [ KEY #key,
					     MESSAGE 1-dlist &
						     <! prpstn_rel !> ] ] ] >,
	    CONT.LISZT.LIST < #altkey, ... >,
	    KEYS [ KEY #key & mod_role_rel,
		   ALTKEY #altkey ] ] ].

;; tell Kim that/whether S
;; inform Kim that S
np_trans_cp_verb := fin_cp_prop+ques_verb & cp_trans_subst & cp_trans_lt.

np_trans_cp_fin_or_inf_verb := fin_or_inf_cp_prop+ques_verb & cp_trans_subst & 
                               cp_trans_lt.

;; bet Kim ten cents that S
np_np_cp_verb := fin_cp_prop+ques_verb & np_np_cp_subst & tritrans_lt.

;; explain to Kim that S
pp_cp_verb := fin_cp_prop+ques_verb & pp_cp_subst & pp_cp_lt &
  [ LOCAL.CAT.VAL.COMPS < synsem, 
			  [ LOCAL.KEYS.MESSAGE 1-dlist &
					       <! prpstn_rel !> ] > ].

;; ask Kim whether S
np_trans_cp_ques_verb := fin_cp_prop+ques_verb & cp_trans_subst & cp_trans_lt &

  [ LOCAL.CAT.VAL.COMPS < synsem, [ LOCAL.KEYS.MESSAGE 1-dlist &
				    <! int_rel !> ] > ].

;; persuade Kim that S
np_trans_cp_prop_verb := fin_cp_prop+ques_verb & cp_trans_subst & cp_trans_lt &

  [ LOCAL.CAT.VAL.COMPS < synsem, 
			  [ LOCAL.KEYS.MESSAGE 1-dlist &
					       <! prpstn_rel !> ] > ].

;; ask that Kim be released
cp_bse_intrans_verb := bse_cp_verb & cp_intrans_subst & two_arg &
		   arg14_lt.

subj_equi_verb := cp_prop+ques_verb & inf_intrans_subst & two_arg & 
                  subj_equi_inf_lt.

;; try and find it
subj_equi_and_verb := cp_prop+ques_verb & inf_intrans_and_subst & two_arg & 
                      subj_equi_inf_lt.

;; have yet to find it
subj_equi_particle_verb := cp_prop+ques_verb & inf_intrans_particle_subst & 
                           subj_equi_inf_lt.

obj_equi_verb := cp_prop+ques_verb & inf_trans_subst & obj_equi_inf_lt.

oeq_pp_inf_verb := verb_synsem & pp_inf_subst & three_arg & obj_equi_inf_lt &
		   empty_prep_intrans_lt.

pp_inf_seq_verb := cp_prop+ques_verb & pp_inf_subst & three_arg & pp_inf_seq_lt.

anom_equi_verb := cp_prop+ques_verb & inf_non_trans_subst & anom_equi_lt &
  [ LOCAL.CONT.H-CONS <! !> ].

anom_equi_trans_verb := cp_prop+ques_verb & inf_trans_subst & anom_equi_lt &
  [ LOCAL.CONT.H-CONS <! !> ].

particle_inf_verb := cp_prop+ques_verb & particle_inf_subst & 
		     anom_equi_particle_lt.

particle_cp_verb := fin_cp_prop+ques_verb & particle_cp_subst & 
		    particle_cp_lt.

expl_it_subj_pp_cp_verb := basic_cp_prop+ques_verb & expl_pp_cp_subst & 
		     expl_it_subj_pp_cp_lt.

expl_it_subj_np_cp_verb := basic_cp_prop+ques_verb & expl_np_cp_subst & 
                           expl_it_subj_np_cp_lt.

expl_it_subj_np_cp_fin_verb := basic_cp_prop+ques_verb & 
                               expl_np_cp_fin_subst & expl_it_subj_np_cp_lt.

expl_it_subj_np_cp_inf_verb := basic_cp_prop+ques_verb & 
                               expl_np_cp_inf_subst & expl_it_subj_np_cp_lt.

expl_pp_inf_oeq_verb := cp_prop+ques_verb & expl_pp_inf_subst & 
			expl_pp_inf_oeq_lt.

expl_pp_inf_seq_verb := cp_prop+ques_verb & expl_pp_inf_subst & 
			expl_pp_inf_seq_lt.

atrans_inf_verb := cp_prop+ques_verb & inf_intrans_subst & two_arg & 
		   atrans_inf_lt.

for_to_verb := verb_synsem & for_inf_intrans_subst & two_arg & trans_lt.

particle_verb := verb_synsem & particle_subst & arg1_subj_lt & two_arg &
  [ LOCAL.CAT.VAL.COMPS < *top* > ].

particle_NP_verb := verb_synsem & particle_NP_subst & particle_NP_lt.

NP_particle_verb := verb_synsem & NP_particle_subst & NP_particle_lt.

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

NP_prep_particle_verb := NP_particle_verb &
  [ LOCAL [ CAT.VAL.COMPS < synsem, [ LOCAL.KEYS.KEY.ARG3 #index ], ...>,
	    KEYS.KEY.ARG3 #index ] ].

particle_prd_verb := verb_synsem & particle_prd_subst & anom_equi_prd_lt.

particle_pp_verb := verb_synsem & particle_pp_subst & 
		    prep_particle_intrans_lt.

particle_empty_pp_verb := verb_synsem & particle_pp_subst & 
		    empty_prep_particle_intrans_lt.

np_particle_pp_verb := verb_synsem & np_particle_pp_subst & prep_particle_trans_lt.

particle_np_pp_verb := verb_synsem & particle_np_pp_subst & particle_np_pp_lt.

; prefer it if ...
expl_obj_cp_verb := fin_cp_prop+ques_verb & cp_trans_subst & expl_obj_cp_lt.

adv_verb := verb_synsem & adv_subst & arg1_subj_lt & two_arg &
  [ LOCAL [ CAT.VAL.COMPS < *top* >,
	    CONT.LISZT.LIST < #key, ... >,
	    KEYS.KEY #key ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Modifier synsems
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; When changing, remember to also modify verb_participle_affix in lexrules.tdl
isect_synsem := canonical_synsem &
  [ LOCAL [ CAT.HEAD.MOD < [ LOCAL intersective_mod ] >,
	    CONT.TOP #hand,
	    KEYS.KEY.HANDEL #hand ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; VERBS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; The semantics of a main verb consists of the "main" handel for the sentence
; and a list of relations.  A pointer to the main or head relation in the list
; is kept in the KEY attribute.
;
; DPF 26-Jan-01 - Changed HEAD.INV - to HEAD.INV -* to allow for coordination
; of inverted and non-inverted sentences.  Worry that this swap has been made
; before, but next time we'll know why.

main_verb_sans_key := nonconj & hc-to-phr &
  [ SYNSEM.LOCAL [ CAT.HEAD verb* & [ AUX -*,
				      INV -* ],
		   CONT.LISZT.LIST < v_event_rel, ... >,
                   KEYS [ KEY.LABEL *cons*,
                          ALTKEY role_rel ] ] ].

;;; AAC - all main_verbs have single element LISZTs

main_verb := main_verb_sans_key & topkey & mcna & nonmsg &
  [ INFLECTED -,
    SYNSEM.LOCAL [ KEYS.KEY #key,
		   CONT.LISZT <! #key !> ] ].

main_verb_std := main_verb.

main_verb_mliszt := main_verb_sans_key & topkey & mcna & nonmsg &
  [ INFLECTED -,
    SYNSEM.LOCAL [ KEYS.KEY #key,
		   CONT.LISZT.LIST < #key, ... > ] ].

main_verb_mliszt_infl := main_verb_sans_key & topkey &
  [ INFLECTED +,
    SYNSEM.LOCAL [ KEYS.KEY #key,
		   CONT.LISZT.LIST < #key, ... > ] ].


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; AUXILIARY VERBS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Auxiliary verbs are subject-to-subject raising verbs which also raise the 
; content of the VP complement, possibly tacking an additional relation on the
; end.  They also raise the context of the VP complement, for now at least to
; propagate tense information.
; AUX verbs are further classified by the type of VP complement they 
; select for.  Also, AUX verbs optionally take a neg (post-head) specifier.

aux_verb := verb_synsem &
  [ LOCAL [ CAT.HEAD.TAM #tam,
	    CONT.INDEX.E #tam ] ].
		
aux_verb_ssr := aux_verb & ssr_subst & basic_two_arg &
  [ LOCAL.CAT.VAL [ --KEYCOMP #comp,
		    COMPS < #comp, ... > ] ].

aux_verb_word_super := mcna & nonconj & nonmsg & hc-to-phr &
  [ INFLECTED +,
    SYNSEM aux_verb_ssr ]. 

aux_verb_word := aux_verb_word_super &
  [ SYNSEM.LOCAL.CAT [ HEAD.AUX +*,
		       POSTHEAD + ] ].

bse_aux_verb_word := aux_verb_word &
  [ SYNSEM.LOCAL local_min &
		 [ CAT [ VAL [ SUBJ < [ OPT - ] >,
			       COMPS < synsem &
				       [ LOCAL [ CAT vp_bse_cat,
						 CONJ cnil ],
					 OPT - ] > ] ] ] ].

;; ought, going
inf_aux_verb_word := aux_verb_word_super & 
  [ SYNSEM aux_verb_ssr &
	   [ LOCAL local_min &
		 [ CAT [ HEAD.AUX -,
                         POSTHEAD +,
			 VAL.COMPS < synsem &
				     [ LOCAL [ CAT vp_inf_cat,
					       CONJ cnil ],
				       OPT - ] > ] ] ] ].

;; Only for auxiliary "have"
psp_aux_verb_word := aux_verb_word &
  [ SYNSEM aux_verb_ssr &
	   [ LOCAL [ CAT.VAL.COMPS < synsem &
				     [ LOCAL [ CAT vp_cat &
						   [ HEAD verb &
							  [ VFORM psp ] ],
					       CONT.TOP #hand,
					       CONJ cnil ],
				       OPT - ] >,
		     KEYS.KEY [ HANDEL #hand ] ] ] ].
; Only for the copula

prd_aux_verb_word := aux_verb_word &
  [ SYNSEM aux_verb_ssr &
	   [ LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
					   [ CAT [ HEAD.PRD +,
						   VAL [ SUBJ < synsem >,
							 COMPS *olist*,
							 SPR *olist* ],
						   MC na ],
                                             CONT.E-INDEX #event,
					     KEYS.KEY independent_rel ],
				       OPT - ] >,
		     CONT.INDEX #event ] ] ].

raise_cont := word_or_lexrule &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS < [ LOCAL local_min &
					   [ CONT [ TOP #hand,
						    INDEX #index ] ] ], ... >,
		   CONT [ TOP #hand,
			  INDEX #index,
			  LISZT <! !> ] ] ].

add_cont := word_or_lexrule &
  [ SYNSEM.LOCAL [ KEYS.KEY #key,
		   CONT.LISZT.LIST < relation & #key, ... > ] ].

; Real modal verbs are auxiliary verbs which select a base form complement.
; They only exist in finite forms.

generic_modal_verb_word := aux_verb_word_super &
  [ SYNSEM aux_verb_ssr &
	   [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL.CONT.TOP #chand,
		     CONT [ INDEX #event,
			    H-CONS.LIST < qeq &
					   [ SC-ARG #arghand,
					     OUTSCPD #chand ], ... > ],
		     KEYS.KEY [ EVENT #event,
				ARG4 #arghand ] ] ] ].

fin_modal_verb_word := generic_modal_verb_word &
  [ SYNSEM.LOCAL [ CAT [ HEAD [ PRD -,
				VFORM fin*,
                                TAM ind_or_mod_subj_tam ],
			 POSTHEAD +,
			 VAL.SUBJ < synsem & [ LOCAL [ CAT nomp_cat_nom_min,
						       KEYS.MESSAGE 0-dlist,
						       CONJ cnil ],
					       OPT - ] > ],
		   CONT psoa ] ].

pres_modal_verb_word := fin_modal_verb_word & 
  [ SYNSEM.LOCAL.CAT.HEAD.TAM [ TENSE present*,
				ASPECT no_aspect* ] ].

; Assume for now that all modals have simple present tense semantics,
; ignoring the habitual past readings possible for "would" and "could" -
; these will have to be separate lexical entries.

modal_verb_word := pres_modal_verb_word & bse_aux_verb_word &
  [ SYNSEM.LOCAL.CAT [ HEAD [ VFORM fin*,
                              PRD -,
                              MOD < [ LOCAL intersective_mod &
                                            [ CAT nbar_cat ] ] > ],
                       VAL.COMPS.FIRST.LOCAL.CAT vp_bse_cat ] ].

pos_modal_verb_word := modal_verb_word & topkey &
  [ SYNSEM.LOCAL [ CAT.HEAD.AUX +*,
		   CONT [ LISZT <! #key !>,
			  H-CONS <! qeq !> ],
		   KEYS.KEY #key & [ LABEL *cons* ] ] ].

ought_verb_word := pres_modal_verb_word & inf_aux_verb_word &
  [ SYNSEM.LOCAL.CAT.HEAD.MOD < [ LOCAL intersective_mod &
                                        [ CAT nbar_cat ] ] > ].

pos_ought_verb_word := ought_verb_word & topkey &
  [ SYNSEM.LOCAL [ KEYS.KEY #key,
		   CONT [ LISZT <! #key !>,
			  H-CONS <! qeq !> ] ] ].

past_ought_verb_word := fin_modal_verb_word & inf_aux_verb_word & topkey &
  [ SYNSEM.LOCAL [ CAT.HEAD [ TAM [ TENSE past*,
                                    ASPECT no_aspect* ],
                              MOD < [ LOCAL intersective_mod &
                                            [ CAT nbar_cat ] ] > ],
                   CONT [ LISZT <! #key !>,
			  H-CONS <! qeq !> ],
		   KEYS.KEY #key ] ].

basic_quasimodal_verb_word := aux_verb_word_super &
  [ SYNSEM [ LOCAL [ CAT.VAL [ SUBJ < [ OPT - ] >,
                               COMPS.FIRST.LOCAL.CONT [ TOP #hand,
                                                        INDEX #event ] ],
                     CONT [ TOP #hand,
			    INDEX #event,
			    LISZT <! !>,
			    H-CONS <! !> ],
		     KEYS.KEY.EVENT #event ] ] ]. 

quasimodal_word := basic_quasimodal_verb_word & inf_aux_verb_word &
  [ SYNSEM.LOCAL.CAT.HEAD.MOD < > ].

quasimodal_psp_word := inf_aux_verb_word & generic_modal_verb_word & topkey &
  [ SYNSEM.LOCAL [ CAT.HEAD [ MOD < >,
			      VFORM psp,
			      TAM.TENSE present*,
			      PRD -],
		   CONT.LISZT <! #key !>,
		   KEYS.KEY #key ] ].

quasimodal_bse_word := basic_quasimodal_verb_word & bse_aux_verb_word &
  [ SYNSEM.LOCAL.CAT.HEAD [ VFORM prp,
                            PRD +,
                            MOD < > ] ].

; DPF 09-Jan-00 - Removed SLASH 0-dlist from COMPS.FIRST, since it was blocking
; e.g. "When's he leaving"

contracted_aux_word :< word.

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; NOUNS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;       TYPE                 ISA                what's it for?
;       nominal_synsem       lex_synsem         all nouns
;       xpro_synsem          nominal_synsem     pro v. non-pro dimension
;       xref_synsem          nominal_synsem     ref v. non-ref dimension
;       pronominal_synsem    xpro_synsem        all pronouns
;       nonpronominal_synsem xpro_synsem        all non-pronominal nouns
;       ref_synsem           xref_synsem        personal pro's & nouns
;       non_ref_synsem       xref_synsem        it, there, wh-pronouns
;       ref_pro_synsem       pronominal_synsem  personal pronouns
;                            & ref_synsem
;       non_ref_pro_synsem   pronominal_synsem  it, there, wh-pronouns
;                            & non_ref_synsem
;       noun_synsem          nonpronominal_synsem all non-pro nouns
;                            & ref_synsem 

; The "default" statement that most nouns identify their AGR and INDEX
; will live on noun_synsem and non_ref_pro_synsem, and then be restated for
; personal pronouns other than my special 'they'.  (All this because I can't
; be bothered now to set up a special default type...) 

basic_nom_synsem := lex_synsem & 
  [ LOCAL [ STEMHEAD nstem,
            CAT.HEAD nominal,
	    CONT nom-obj ] ].

nominal_synsem := basic_nom_synsem &
  [ LOCAL.CAT.HEAD.POSS - ].

xpro_synsem :< nominal_synsem.
xref_synsem :< nominal_synsem.

; DPF 23-Jul-99 - Add missing MOD < >.  Hmm.

pronominal_synsem := xpro_synsem &
  [ LOCAL [ CAT [ HEAD noun* &
		       [ MOD < > ],
		  VAL.SUBJ < > ] ] ].

nonpronominal_synsem :< xpro_synsem.

; AAC - added following types for verbal_gerund, poss_clitic etc

nonpro_nomod_synsem := nomod_synsem & nonpronominal_synsem.
nonpro_onearg_synsem := nonpronominal_synsem & basic_one_arg.
nomod_onearg_synsem := nomod_synsem & one_arg.
nonpro_nomod_onearg_synsem := nonpro_nomod_synsem & basic_one_arg.

ref_synsem := xref_synsem &
  [ LOCAL [ KEYS.KEY #key,
	    CONT [ INDEX #inst & ref-ind,
		   LISZT.LIST < basic_nom_rel & #key & 
				    [ INST #inst ], ... > ] ] ].

non_ref_synsem := xref_synsem & nomod_synsem & zero_arg &
  [ LOCAL.CAT.VAL [ SPR < >,
		    COMPS < > ] ].

ref_pro_synsem := pronominal_synsem & ref_synsem & nomod_synsem.

non_ref_pro_synsem := pronominal_synsem & non_ref_synsem &
  [ LOCAL [ AGR #agr,
	    CONT.INDEX #agr ] ].

noun_synsem := nonpronominal_synsem & ref_synsem &
  [ LOCAL [ AGR #agr,
	    CAT.HEAD noun*,
	    CONT.INDEX #agr ] ].

;;
;; Common nouns
;;
;; DPF 16-Nov-00 - Added SPR.NON-LOCAL.SLASH 0-dlist - don't know how we did
;; without this up to now.

basic_common_noun_synsem := noun_synsem &
  [ LOCAL [ ARG-S < #spr . #comps >,
	    CAT.VAL [ SPR < #spr &  synsem &
			    [ LOCAL local_min &
				    [ CAT [ HEAD det,
					    VAL [ SUBJ < >,
						  SPR *olist*,
						  COMPS *olist* ] ] ],
                              NON-LOCAL.SLASH 0-dlist,
			      OPT - ] >,
		      COMPS #comps ] ] ].

common_noun_synsem := basic_common_noun_synsem &
  [ LOCAL.STEMHEAD countnstem ].

common_noun_nocomp_synsem := common_noun_synsem & basic_one_arg &
  [ LOCAL.CAT.VAL.COMPS < > ].

basic_mass_or_count_synsem := basic_common_noun_synsem & nonpro_nomod_synsem &
  [ LOCAL [ STEMHEAD masscountnstem,
            CONT.LISZT <! relation !> ] ].

mass_or_count_nocomp_synsem := basic_mass_or_count_synsem & one_arg.

noun_nocomp_synsem := common_noun_nocomp_synsem & nonpro_nomod_onearg_synsem.
noun_nonpro_nomod_synsem := basic_common_noun_synsem & nonpro_nomod_synsem.
noun_two_arg_nomod_synsem := noun_nonpro_nomod_synsem & basic_two_arg.

basic_noun_ppcomp_synsem := basic_common_noun_synsem &
  [ LOCAL [ CAT.VAL.COMPS < expressed_synsem &
                            [ LOCAL local_min &
				    [ CAT [ HEAD prep & [ PRD -,
                                                          TAM.TENSE no_tense ],
					    VAL.COMPS *olist* ],
                                      CONT [ TOP #hand,
                                             INDEX #nind ],
				      KEYS.KEY #ckey ] ], ... >,
            CONT.INDEX #nind,
	    KEYS [ KEY.HANDEL #hand,
		   --COMPKEY #ckey ] ] ].

noun_nondeverb_ppcomp_synsem := basic_noun_ppcomp_synsem &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.KEYS.KEY.ARG3 #index ] >,
            KEYS.KEY diadic_nom_rel & [ ARG3 #index ] ] ].

noun_ppcomp_synsem := noun_nondeverb_ppcomp_synsem & nonpro_nomod_synsem &
                      common_noun_synsem.

noun_deverb_ppcomp_synsem := basic_noun_ppcomp_synsem & nonpro_nomod_synsem &
                             common_noun_synsem & basic_two_arg.

noun_ppcomp_two_arg_synsem := noun_ppcomp_synsem & two_arg &
  [ MODIFIED notmod ].

temp_noun_ppcomp_synsem := noun_ppcomp_synsem &
  [ LOCAL [ CAT.VAL [ COMPS < [ LOCAL.KEYS.KEY _of_rel,
				OPT + ], ... >,
                      SUBJ < >,
		      SPR < [ OPT - ] > ],
	    CONT.LISZT <! relation !>,
	    KEYS.KEY temp_abstr_rel ] ].

temp_noun_ppcomp_two_arg_synsem := temp_noun_ppcomp_synsem & two_arg.

; For e.g. "hotel" as in "the hotel Marriott" or "the Marriott hotel Hannover"c
basic_noun_npcomp_synsem := noun_two_arg_nomod_synsem &
  [ LOCAL [ CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					       CONJ cnil,
					       KEYS [ KEY abstr_named_rel,
						      MESSAGE 0-dlist ],
					       CONT [ TOP #top,
                                                      INDEX #cindex ] ] ] >,
            CONT [ TOP #top,
		   INDEX #index,
		   LISZT <! relation, 
			    unspec_rel &
			    [ HANDEL #hand,
			      HINST #index,
			      NHINST #cindex ] !>,
                   H-CONS <! !> ],
	    KEYS.KEY.HANDEL #hand ] ].

common_noun_npcomp_synsem := basic_noun_npcomp_synsem &
  [ LOCAL [ STEMHEAD countnstem,
            CAT.VAL.COMPS < [ OPT - ] > ] ].

common_noun_numcomp_synsem := noun_two_arg_nomod_synsem &
  [ LOCAL [ STEMHEAD countnstem,
            CAT.VAL.COMPS < synsem & [ LOCAL [ CAT [ HEAD intadj,
                                                     VAL [ SPR *olist*,
                                                           COMPS *olist* ] ],
					       CONJ cnil,
					       KEYS.KEY integer_rel &
                                                   [ HANDEL #hand ],
                                               CONT.INDEX #index ],
                                       OPT - ] >,
            KEYS.KEY.HANDEL #hand,
            CONT [ INDEX #index,
		   LISZT <! reg_nom_rel !>,
                   H-CONS <! !> ] ] ].

; ERB (14-12-97) It might be cleaner in some sense for pro_ss to have that
; pron_rel as its key and then have this construction and others like it
; take the value of that KEY and put it on their LISZT.  Also, if we
; ever move to lexical amalgamation of relations, the pron_rel should get
; incorporated automatically. ... So I'm going to try to do it that way.

; ERB (14-12-97) We just decided that there shouldn't be a pron_rel here.
; The feature PRONTYPE is enough to distinguish the instance introduced
; by the pro_ss as one that doesn't have to be bound.

; ERB (21-01-98) The pro_ss should be unslashed.

basic_mass_noun_synsem := basic_common_noun_synsem & nonpro_nomod_synsem &
  [ LOCAL [ STEMHEAD massnstem,
            CONT [ INDEX [ PNG png & [ PN 3sg* ],
                           DIVISIBLE +* ] ] ] ].

scoping_noun_synsem := noun_two_arg_nomod_synsem &
  [ LOCAL [ CAT.VAL.--KEYCOMP [ LOCAL [ CONT.TOP #hand,
					KEYS.MESSAGE 1-dlist ] ],
	    KEYS.KEY basic_hcomp_nom_rel & [ ARG4 #hand ] ] ].

;; a way to meet

common_noun_vpcomp_synsem := scoping_noun_synsem & common_noun_synsem &
  [ LOCAL [ CAT.VAL [ --KEYCOMP #comp,
		      COMPS < #comp & phr_synsem &
			      [ LOCAL [ CAT vp_inf_cat &
					 [ VAL.SUBJ 
					    < pro_ss &
					      [ LOCAL.CONT.INDEX #inst,
						NON-LOCAL.SLASH 0-dlist ] > ],
					KEYS.MESSAGE 1-dlist &
						       <! hypo_rel !>,
					CONJ cnil ],
				OPT - ] > ],
	    CONT [ LISZT <! relation, 
                          def_rel & [ RESTR #rhand,
                                      BV #inst ],
                          pron_rel &
			  [ HANDEL #prohand,
			    INST #inst & [ PRONTYPE zero_pron ] ] !>,
                   H-CONS <! qeq & [ SC-ARG #rhand,
                                     OUTSCPD #prohand ] !> ] ] ].

common_noun_cpcomp_synsem := scoping_noun_synsem &
  [ LOCAL [ STEMHEAD countnstem,
            CAT.VAL [ --KEYCOMP #comp,
		      COMPS < #comp & synsem &
			    [ LOCAL [ CAT.HEAD comp & strict_type,
				      CONJ cnil ],
			      OPT - ] > ],
	    CONT [ LISZT <! relation !>,
                   H-CONS <! !> ] ] ].

common_noun_cpcomp_fin_synsem := common_noun_cpcomp_synsem &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT s_cat_fin_v_c ] > ].

common_noun_cpcomp_inf_synsem := common_noun_cpcomp_synsem &
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT s_cat_inf_v_c ] > ].

mass_noun_synsem := basic_mass_noun_synsem & one_arg &
  [ LOCAL [ CAT.VAL.COMPS < >,
            CONT.LISZT <! relation !> ] ].

mass_noun_ppcomp_synsem := basic_mass_noun_synsem & two_arg &
                           noun_nondeverb_ppcomp_synsem &

  [ LOCAL.CONT.LISZT <! relation !> ].

mass_or_count_ppcomp_synsem := basic_mass_or_count_synsem & two_arg &
                               noun_nondeverb_ppcomp_synsem.

mass_noun_npcomp_synsem := basic_mass_noun_synsem & basic_noun_npcomp_synsem.

; One of the relations in LISZT is the support relation nominalize_rel 
; which is a subtype of nom_rel, but also introduces the ARG4 attribute 
; for the handle of the verbal relation.  The other rel is the verbal relation.

deverbal_noun_synsem := noun_nonpro_nomod_synsem &
  [ LOCAL [ STEMHEAD countnstem,
            KEYS.ALTKEY #altkey,
	    CONT [ LISZT <! nominalize_rel & [ ARG4 #arghand ],
                          hypo_rel & [ HANDEL #arghand,
                                       SOA #soahand ],
                          relation & #altkey & [ HANDEL #vhand ] !>,
                   H-CONS <! qeq & [ SC-ARG #soahand,
                                     OUTSCPD #vhand ] !> ] ] ].

deverbal_intr_noun_synsem := deverbal_noun_synsem & noun_nocomp_synsem.

deverbal_trans_noun_synsem := deverbal_noun_synsem & 
                              noun_deverb_ppcomp_synsem &
  [ LOCAL.CAT.VAL.COMPS < [ OPT + ] > ].

deverbal_mass_noun_synsem := deverbal_noun_synsem & nonpro_nomod_onearg_synsem &
  [ LOCAL [ CAT.VAL.COMPS < >,
	    CONT.INDEX [ PNG png & [ PN 3sg* ],
			 DIVISIBLE +* ] ] ].

deverbal_mass_noun_ppcomp_synsem := deverbal_trans_noun_synsem &
  [ LOCAL.CONT.INDEX [ PNG png & [ PN 3sg* ],
		       DIVISIBLE +* ] ].

; PRD feature used as a hack to avoid partitives in noun-noun compounds,
; pending a better analysis.
; DPF 9-Sept-99 - Changed N-N block to be based on non-empty ALTKEY, rather than PRD.
; The handle of the of-NP should probably be an argument of the part_of relation,
; as the value of a feature SET.  But for now we simply identify the handles of
; the part_of and the of_NP.

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

basic_partitive_noun_synsem := noun_synsem &
  [ LOCAL [ CAT [ HEAD.MOD < >,
		  VAL [ SUBJ < >,
			--KEYCOMP 
                             [ LOCAL local_min &
					[ CAT [ HEAD prep & 
						     [ PRD -,
                                                       TAM.TENSE no_tense ],
						VAL.COMPS *olist* ],
                                          CONT [ TOP #nhand,
                                                 INDEX #index ],
					  KEYS.KEY #ckey & _of_rel &
					      [ ARG3 #cind ] ],
				  NON-LOCAL.SLASH 0-dlist,
				  OPT + ] ] ],
            CONT [ INDEX #index,
		   H-CONS <! qeq & [ SC-ARG #phand,
				     OUTSCPD #nhand ] !> ],
	    KEYS [ KEY part_of_rel & 
                       [ HANDEL #nhand,
                         ARG3 #cind & non_expl-ind ],
		   ALTKEY quant_or_wh_rel &
                       [ BV #index,
                         RESTR #phand ],
		   --COMPKEY #ckey ] ] ].

;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing

partitive_noun_synsem := basic_partitive_noun_synsem &
  [ LOCAL [ CAT.VAL [ --KEYCOMP #comp,
                    COMPS < #comp >,
                    SPR  < [ LOCAL local_min &
				       [ CAT.VAL [ SPR  *olist*,
						   COMPS *olist* ],
                                         CONT.TOP #hand,
					 KEYS.KEY degree_rel &
                                                 [ DARG #darg ] ],
                                 NON-LOCAL [ QUE 0-dlist,
					     REL 0-dlist ],
				 OPT + ] > ],
            KEYS.ALTKEY [ HANDEL #hand,
                          DIM #darg ] ] ].

partitive_num_synsem :< basic_partitive_noun_synsem.

basic_intr_noun_word := hcons_amalg_basic_affixed_word &
  [ SYNSEM common_noun_nocomp_synsem &
	   [ LOCAL.CONT [ LISZT <! relation !>,
                          H-CONS <! !> ] ] ].

n_intr_lex_entry := basic_intr_noun_word &
  [ SYNSEM noun_nocomp_synsem & 
	   [ LOCAL [ CAT.VAL.SPR < [ LOCAL.KEYS.KEY quant_or_wh_rel ] >,
		     KEYS.KEY non_temp_nom_rel ],
	     MODIFIED notmod ] ].

n_intr_le :< n_intr_lex_entry.
n_intr_lowprio_le :< n_intr_lex_entry.

n_intr_temp_le := basic_intr_noun_word & 
  [ SYNSEM noun_nocomp_synsem &
	   [ LOCAL [ CAT.VAL.SPR < [ LOCAL.KEYS.KEY quant_or_wh_rel ] >,
		     KEYS.KEY temp_abstr_rel ],
             MODIFIED notmod ] ].

irreg_intr_noun_le := basic_intr_noun_word &
  [ SYNSEM noun_nocomp_synsem & 
	   [ LOCAL [ CAT.VAL.SPR < [ LOCAL.KEYS.KEY quant_or_wh_rel ] >,
		     KEYS.KEY basic_nom_rel ],
	     MODIFIED notmod ] ].

noun_ppcomp_word := hcons_amalg_affixed_word &
  [ SYNSEM noun_ppcomp_two_arg_synsem &
	   [ LOCAL [ CAT.VAL.COMPS < [ OPT + ] >,
		     CONT.LISZT <! relation !> ],
	     MODIFIED notmod ] ].

n_ppcomp_le :< noun_ppcomp_word.

n_ppof_le := noun_ppcomp_word &
  [ SYNSEM.LOCAL.KEYS.--COMPKEY _of_rel ].

noun_ppin_le := noun_ppcomp_word &
  [ SYNSEM.LOCAL.KEYS.--COMPKEY _in_rel ].

noun_ppto_le := noun_ppcomp_word &
  [ SYNSEM.LOCAL.KEYS.--COMPKEY _to_rel ].

noun_ppfor_le := noun_ppcomp_word &
  [ SYNSEM.LOCAL.KEYS.--COMPKEY _for_rel ].

basic_n_temp_ppcomp_lexent := hcons_amalg_affixed_word &
  [ SYNSEM temp_noun_ppcomp_two_arg_synsem ].

n_temp_ppcomp_le := basic_n_temp_ppcomp_lexent &
  [ SYNSEM.MODIFIED notmod ].

n_day_of_week_le := basic_n_temp_ppcomp_lexent &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS < [ LOCAL.KEYS.--COMPKEY 
                                        non_day_diadic_modable_rel ] >,
                   KEYS [ KEY dofw_rel,
                          ALTKEY basic_nom_rel ] ] ].

n_month_le := basic_n_temp_ppcomp_lexent &
  [ SYNSEM [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.KEYS.--COMPKEY yofc_rel ] >,
                     KEYS [ KEY mofy_rel,
                            ALTKEY basic_nom_rel ] ],
             MODIFIED notmod ] ].

; "way" as in "find a way to do that" -- was used in VM, but analysis changed
; so this type not currently in use.  Its semrel.tdl entry was as follows:
;   _way_rel := hcomp_nom_rel &
;     [ INST.SORT entity ].

n_vpcomp_le := hcons_amalg_affixed_word &
  [ SYNSEM common_noun_vpcomp_synsem &
	   [ MODIFIED notmod ] ].

n_cpcomp_fin_le := hcons_amalg_affixed_word &
  [ SYNSEM common_noun_cpcomp_fin_synsem &
	   [ MODIFIED notmod ] ].

; procedure
n_cpcomp_inf_le := hcons_amalg_affixed_word &
  [ SYNSEM common_noun_cpcomp_inf_synsem &
	   [ MODIFIED notmod ] ].

n_npcomp_le := hc_word & 
  [ INFLECTED -,
    SYNSEM common_noun_npcomp_synsem &
	   [ LOCAL.CAT.HC-LEX +,
	     MODIFIED notmod ] ].

; Leave unmarked for INFLECTED to allow "a $ 500 check" (where $ unifies with
; [INFLECTED -].
n_numcomp_le := mcna & nonconj & nonmsg & topkey & hc-to-phr &
  [ SYNSEM common_noun_numcomp_synsem &
	   [ LOCAL.KEYS.KEY.LABEL *cons*,
             MODIFIED notmod ] ].

deverbal_noun_intr_le := hcons_amalg_affixed_word &
  [ SYNSEM deverbal_intr_noun_synsem &
	   [ MODIFIED notmod ] ].

; For e.g. "meetings", where the singular form is a gerund, which is of type
; word, but we can't yet do plural affixation on words, only on lexemes.
deverbal_noun_intr_plural_le := amalg_word &
  [ SYNSEM deverbal_intr_noun_synsem & 
	   [ LOCAL.CONT.INDEX.PNG png & [ PN 3pl* ],
	     MODIFIED notmod ] ].

deverbal_noun_trans_word := hcons_amalg_affixed_word &
  [ SYNSEM deverbal_trans_noun_synsem &
	   [ MODIFIED notmod ] ].

n_mass_le := hcons_amalg_basic_affixed_word &
  [ SYNSEM mass_noun_synsem &
	   [ MODIFIED notmod ] ].

n_mass_lowprio_le := hcons_amalg_basic_affixed_word &
  [ SYNSEM mass_noun_synsem &
	   [ MODIFIED notmod ] ].

n_mass_count_le := hcons_amalg_basic_affixed_word &
  [ SYNSEM mass_or_count_nocomp_synsem &
	   [ MODIFIED notmod ] ].

n_mass_ppcomp_le := hcons_amalg_basic_affixed_word &
  [ SYNSEM mass_noun_ppcomp_synsem &
	   [ MODIFIED notmod ] ].

n_mass_count_ppcomp_le := hcons_amalg_basic_affixed_word &
  [ SYNSEM mass_or_count_ppcomp_synsem &
	   [ MODIFIED notmod ] ].

; "late fall 2000"
n_mass_npcomp_le := hcons_amalg_basic_affixed_word &
  [ SYNSEM mass_noun_npcomp_synsem &
	   [ MODIFIED notmod ] ].

deverbal_massn_le := amalg_word &
  [ SYNSEM deverbal_mass_noun_synsem &
	   [ MODIFIED notmod ] ].

deverbal_massn_pp_le := amalg_word &
  [ SYNSEM deverbal_mass_noun_ppcomp_synsem &
	   [ MODIFIED notmod ] ].

n_plur_le := amalg_word &
  [ SYNSEM noun_nocomp_synsem &
	   [ LOCAL.CONT [ INDEX.PNG png & [ PN 3pl* ],
			  LISZT <! relation !>,
                          H-CONS <! !> ],
	     MODIFIED notmod ] ].

n_plur_ppcomp_le := amalg_word &
  [ SYNSEM noun_ppcomp_two_arg_synsem &
	   [ LOCAL.CONT [ INDEX.PNG png & [ PN 3pl* ],
			  LISZT <! relation !> ],
	     MODIFIED notmod ] ].

n_part_le := que_word &
  [ SYNSEM partitive_noun_synsem &
	   [ LOCAL [ CONT.LISZT <! #key, #altkey !>,
                     KEYS [ KEY #key,
                            ALTKEY #altkey ] ],
             NON-LOCAL.QUE 0-dlist,
	     MODIFIED notmod ] ].

;;
;; Lexical NPs
;;
;;   (proper names, pronouns, names of weekdays, ...)

basic_np_synsem := ref_synsem &
  [ LOCAL.CAT [ HEAD noun*,
                VAL [ SPR < >,
                      SUBJ < > ] ] ].

basic_np_word := basic_nontopkey & hc-to-phr &
  [ SYNSEM basic_np_synsem ].

non_affixed_np := basic_nontopkey & hc-to-phr & nonque & nonmsg &
  [ SYNSEM [ MODIFIED notmod,
             LOCAL.CAT.VAL.COMPS < > ] ].

np_word := basic_np_word & non_affixed_np.

np_synsem := noun_synsem & nomod_synsem.

basic_np_sing_synsem := basic_np_synsem &
  [ LOCAL [ KEYS.KEY.HANDEL #keyhand,
            CONT [ LISZT.LIST < basic_nom_rel &
                                [ INST #ind ],
                                quant_rel &
                                [ BV #ind,
                                  RESTR #rhand ], ... >,
                   H-CONS <! qeq & [ SC-ARG #rhand,
                                     OUTSCPD #keyhand ] !> ] ] ].

basic_np_sing_word := basic_np_word &
  [ SYNSEM basic_np_sing_synsem ].

np_sing_synsem := basic_np_sing_synsem & np_synsem &
  [ LOCAL [ CAT.VAL.COMPS < >,
            CONT.LISZT <! relation, def_np_rel !> ],
    MODIFIED notmod ].

np_sing_word := basic_np_sing_word & nonque & nonmsg &
  [ SYNSEM np_sing_synsem ].

; DPF 26-Mar-01 - Added MODIFIED.PERIPH + to prevent (restrictive) post-nom
; modifiers of bare proper names, as in "*Abrams by Browne arrived." though
; this will also block "Abrams from New York just called".  Not clear what
; the contrast is due to.

n_proper_lexent := non_affixed_np &
  [ SYNSEM noun_nocomp_synsem & 
	   [ LOCAL [ CAT.VAL.SPR < [ LOCAL.KEYS.KEY quant_or_wh_rel ] >,
                     CONT [ INDEX.PNG png & [ PN 3sg* ],
                            LISZT <! relation !>,
                            H-CONS <! !> ],
		     KEYS [ KEY named_rel,
                            ALTKEY basic_nom_rel ],
                     AGR.DIVISIBLE -* ],
             MODIFIED.PERIPH + ] ].

n_proper_le :< n_proper_lexent.
n_proper_lowprio_le :< n_proper_lexent.

proper_n_synsem := np_sing_synsem &
  [ LOCAL [ KEYS.KEY gen_named_rel,
            CONT.INDEX.PNG png & [ PN 3sg* ] ] ].

; Add ALTKEY to block these in left position of noun-noun compounds
n_proper_abb_le := np_sing_word & 
  [ SYNSEM proper_n_synsem &
           [ LOCAL.KEYS [ KEY named_abb_rel,
                          ALTKEY pron_rel ] ] ].

; "here, there" as in "get out of here/there" 
; Make SYNSEM pronominal_synsem to block N-N cmpnds like "the there meeting"
; Added PRD + so appositives with left member np_adv won't join in N-N-cmpnds.
; DPF 23-Oct-00 - Since pronominal_synsem no longer constrains left member of
; noun-noun compounds, use alternate hack of making ALTKEY not unify with
; the [ALTKEY no_rel] constraint in the n-n-cmpnd rule.

np_adv_synsem := basic_np_sing_synsem & pronominal_synsem &
  [ LOCAL.KEYS.ALTKEY relation ].

basic_np_adv_word := basic_np_sing_word & nonmsg &
  [ SYNSEM np_adv_synsem &
	   [ LOCAL [ CONT [ INDEX #ind & [ PNG png & [ PN 3sg* ] ],
                            LISZT.LIST < relation, #altkey, ... > ],
                     KEYS.ALTKEY #altkey,
		     AGR #ind ] ] ].

; DPF 4-Apr-01 - Made ALTKEY be def_explicit_rel to block these guys as left 
; members of compounds as in "*the there books"
np_adv_word := basic_np_adv_word & nonque &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS < >,
                   KEYS.ALTKEY def_explicit_rel ] ].

n_adv_le :< np_adv_word.

n_adv_arg_le := basic_np_adv_word & nonque &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS < synsem &
                                   [ LOCAL [ CAT np_cat_acc_min,
                                             CONT.INDEX #intarg ],
                                     OPT + ] >,
                   CONT.LISZT <! [ HANDEL #hand,
                                   INST #extarg,
                                   LABEL #label ], 
                                 relation,
                                 [ HANDEL #hand,
                                   EVENT.E.TENSE no_tense,
                                   ARG #extarg,
                                   ARG3 #intarg,
                                   LABEL #label ] !> ] ].

; DPF 4-Apr-01 - ALTKEY was abstr_def_rel,
n_comp_adv_le := np_adv_word &
  [ SYNSEM.LOCAL [ CONT.LISZT <! modable_nom_rel & 
                               [ HANDEL #hand, 
				 INST #inst ],
                               relation,
			       arg_rel &
			       [ HANDEL #hand,
				 ARG #inst ],
                               comp_rel &
                               [ HANDEL #hand,
                                 ARG #inst ] !> ] ].

; DPF 27-Apr-00 - Added [CASE acc] to block spurious parse for "where is kim"
n_wh_adv_le := basic_np_adv_word &
  [ SYNSEM [ LOCAL [ CAT [ HEAD.CASE acc,
                           VAL.COMPS < > ],
                     CONT.LISZT <! relation, which_rel !> ],
             NON-LOCAL.QUE 1-dlist &
		         [ LIST < handle > ] ] ].

; Lexical NP plurals - maybe only in British English? ("IBM are hiring again.")
;
np_pl_word := np_word &
  [ SYNSEM np_synsem &
	   [ LOCAL.CONT.INDEX.PNG png & [ PN 3pl* ] ] ].

np_ersatz_synsem := lex_synsem &
  [ LOCAL [ CAT [ HEAD.MOD < >,
                  VAL [ SPR < >,
			SUBJ < >,
			COMPS < > ] ],
            KEYS.KEY #key & [ HANDEL #hand ],
	    CONT nom-obj &
		[ TOP #hand,
		  INDEX ref-ind &
		      [ PNG png & [ PN 3sg* ] ],
		  LISZT.LIST < #key, ... >,
		  H-CONS <! !> ] ] ].

np_word_no_quant := non_affixed_np &
  [ SYNSEM np_ersatz_synsem ].

pers_pro_synsem := basic_np_synsem & ref_pro_synsem &
  [ LOCAL.CAT.VAL.COMPS < > ].

personal_pro := np_word &
  [ SYNSEM pers_pro_synsem &
	   [ LOCAL.CONT [ LISZT <! pron_rel & [ HANDEL #prohand,
                                                INST #inst ],
                                   def_rel & [ RESTR #rhand,
                                               BV #inst ] !>,
                          H-CONS <! qeq & [ SC-ARG #rhand,
                                            OUTSCPD #prohand ] !> ] ] ].

n_pers_pro_noagr_le :< personal_pro.

n_pers_pro_le := personal_pro & 
  [ SYNSEM.LOCAL [ AGR #agr,
		   CONT [ INDEX #agr &
			        [ PRONTYPE std_pron ] ] ] ].

n_refl_pro_le := personal_pro &
  [ SYNSEM.LOCAL [ CAT.HEAD.CASE acc,
		   AGR #agr,
		   CONT.INDEX #agr,
		   KEYS.KEY pron_rel &
			    [ INST.PRONTYPE refl ] ] ].

n_recip_pro_le := np_word &
  [ SYNSEM ref_pro_synsem &
	   [ LOCAL [ CAT.HEAD.CASE acc,
		     AGR #agr,
		     CONT [ INDEX #agr,
			    LISZT <! recip_pro_rel &
                                     [ INST.PRONTYPE recip ] !>,
                            H-CONS <! !> ] ] ] ].


; 'yours'
; DPF 15-Apr-00 = Changed KEY to be pron_rel rather than def_np_rel, since
; e.g. prepositions want a non_temp_nom_rel as their argument.

n_poss_pro_le := basic_nontopkey & nonmsg & hc-to-phr &
  [ SYNSEM nomod_synsem &
	   [ LOCAL [ CAT [ HEAD noun*,
                           VAL [ SPR < >,
				 COMPS < >,
				 SPEC < anti_synsem > ] ],
                     AGR #index,
		     CONT [ INDEX #index,
			    LISZT <! pron_rel & #key & 
				     [ HANDEL #prohand,
				       INST #eind &
					      [ PRONTYPE std_pron ] ],
                                     def_rel &
				     [ BV #eind,
				       RESTR #rhand ],
				     def_np_rel &
				     [ BV #index,
				       RESTR #phand ],
				     basic_poss_rel & #altkey & 
				     [ HANDEL #phand,
				       ARG3 #eind,
				       ARG #index ] !>,
			    H-CONS <! qeq & [ SC-ARG #rhand,
                                              OUTSCPD #prohand ] !> ],
		     KEYS [ KEY #key,
			    ALTKEY #altkey ] ],
	     MODIFIED notmod ] ].

; Expletive pronouns "there" and "it"
n_expl_it_le := nontopkey &
  [ SYNSEM non_ref_pro_synsem &
	   [ LOCAL [ KEYS.KEY no_rel,
		     CONT [ INDEX it-ind,
			    LISZT <! !> ] ],
	     MODIFIED notmod ] ].

n_expl_there_le := nontopkey &
  [ SYNSEM non_ref_pro_synsem &
	   [ LOCAL [ KEYS.KEY no_rel,
		     CONT [ INDEX there-ind,
			    LISZT <! !> ] ],
	     MODIFIED notmod ] ].
			  

; ERB (31-10-96) A hack:  I want the wh_interrogs to be able to
; get their hands on the which_rel so they can put it on the PARAMS
; list, but QSTORE isn't working yet.  So I am making the QUE value
; of wh_pro_word, wh_adverb_word, and wh_poss_word be the handle of
; the which_rel, so that this gets passed up in such a way that I can see it.

; DPF (3-May-99) We can't just put the handle of the which_rel in QUE, since
; in coordinate structures like "which book and which record did sandy buy?"
; we want to unify the NON-LOCALs of "which book" and "which record", but this
; would mean that the handles of the two which_rel's would be wrongly identified.
; So for now I'm just putting in an unbound handle - we'll have to sort out
; with Ivan what the right solution is.

; who, what

n_wh_pro_le := que_word &
  [ SYNSEM ref_pro_synsem &
	 [ LOCAL [ CAT.VAL [ SPR < >,
			     COMPS < > ],
                   AGR #index,
                   CONT [ INDEX #index & [ PNG png & [ PN 3sg* ] ],
			  LISZT <! [ HANDEL #nhand ],
				   which_rel & [ BV #index,
						 RESTR #rhand ] !>,
			  H-CONS <! qeq &
				    [ SC-ARG #rhand,
				      OUTSCPD #nhand ] !> ] ],
	   NON-LOCAL.QUE 1-dlist &
		         [ LIST < handle > ],
	   MODIFIED notmod ] ].

n_rel_pro_lexent := rel_word & 
  [ SYNSEM pronominal_synsem &
	 [ LOCAL [ CAT [ HEAD.MOD < >,
			 VAL [ SUBJ < >,
			       SPR < >,
			       COMPS < > ] ],
                   AGR #index,
		   CONT [ TOP #hand,
			  INDEX #index,
			  LISZT <! !>,
			  H-CONS <! !> ],
		   KEYS [ KEY non_temp_nom_rel,
                          ALTKEY relation ] ],
           NON-LOCAL.REL 1-dlist &
		         [ LIST < [ TOP #hand,
				    INDEX #index ] > ] ] ].

n_rel_pro_le := n_rel_pro_lexent &
  [ SYNSEM [ MODIFIED notmod,
             LOCAL.KEYS.KEY reg_nom_rel ] ].

; Unmarked for MODIFIED, so can undergo temp_np rule to get "the day that I
; arrived"
n_rel_pro_nonwh_le := n_rel_pro_lexent &
  [ SYNSEM.LOCAL [ CAT.HEAD.PRD -,
                   KEYS.KEY modable_nom_rel ] ].

; DPF 28-Aug-99 - SPR..KEY no_rel prevents these from undergoing bare_np rule
; DPF 09-Jan-00 - In fact, can now eliminate bogus SPR, since picking up
; post-head adjuncts as complements.

generic_pro_adv_word := que_word &
  [ SYNSEM np_synsem &
	   [ LOCAL [ CAT.VAL [ SPR < [ LOCAL local_min &
                                             [ CONT.TOP #khand,
                                               KEYS.KEY degree_rel & 
                                                        [ DARG #darg ] ],
                                       NON-LOCAL [ QUE 0-dlist,
                                                   REL 0-dlist ],
                                       OPT + ] >,
                               COMPS < > ],
		     CONT [ LISZT <! nom_rel,
				   #altkey & quant_rel !>,
			    H-CONS <! qeq &
				    [ SC-ARG #rhand,
				      OUTSCPD #nhand ] !> ],
		     KEYS [ KEY [ HANDEL #nhand,
				  INST #inst & [ PNG png & [ PN 3sg* ] ] ],
			    ALTKEY #altkey & 
				      [ HANDEL #khand,
                                        BV #inst,
					RESTR #rhand,
                                        DIM #darg ] ] ],
	     NON-LOCAL.QUE 0-dlist ] ].

n_generic_pro_adv_le :< generic_pro_adv_word.

n_generic_pro_le := generic_pro_adv_word &
  [ SYNSEM.MODIFIED notmod ].

n_deictic_pro_le := nontopkey &
  [ SYNSEM ref_pro_synsem &
	   [ LOCAL [ CAT.VAL [ SPR < >,
			       COMPS < > ],
		     CONT [ INDEX #index,
			    LISZT <! generic_nom_rel, 
				     demonstrative_rel & #altkey &
				     [ BV #ind,
				       RESTR #rhand ] !>,
			    H-CONS <! qeq &
				    [ SC-ARG #rhand,
				      OUTSCPD #hand ] !> ],
		     AGR #index,
		     KEYS [ KEY [ HANDEL #hand,
					INST #ind ],
			    ALTKEY #altkey ] ],
	     MODIFIED notmod ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Synsems for Auxiliary verb lexical rules
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; ERB (05-10-97) Making this specific to question auxes.  See notes near sai
; in syntax.tdl.
; DPF 26-Jan-01 - Changed HEAD.INV + to HEAD.INV +* to allow for coordination
; of inverted and non-inverted sentences.  Prevent yes-no question rule from
; applying to main verbs by making inverted auxiliaries [MC na], so they have
; to undergo the yes-no rule.

sai_synsem := lex_synsem &
  [ LOCAL [ CAT [ HEAD verb* & [ INV +*,
				 AUX +*,
                                 PRD -,
                                 MOD < [ LOCAL intersective_mod &
                                               [ CAT.VAL.SPR *cons*,
						 KEYS.KEY no_rel ] ] >,
                                TAM ind_or_mod_subj_tam ],
                  VAL.COMPS #comps & < [ NON-LOCAL.SLASH 0-dlist ], ... >,
                  POSTHEAD +,
		  MC na ],
	    KEYS.MESSAGE 0-dlist,
	    ARG-S #comps ] ].

tag_synsem := lex_synsem &
  [ LOCAL [ CAT [ HEAD verb &
		       [ AUX -,
			 INV -,
			 TAM ind_or_mod_subj_tam & [ TENSE #tense ],
			 MOD < [ LOCAL scopal_mod &
				       [ CAT s_cat_fin &
					     [ HEAD.TAM.TENSE #tense & 
                                                              strict_sort,
                                               MC + ],
					 AGR #agr &
					     [ PNG png & [ GEN strict_sort,
							   PN strict_sort ] ],
					 CONT [ TOP #modhand,
						INDEX #ind ],
					 KEYS [ KEY #key,
						MESSAGE 1-dlist &
						    <! #presup & 
						     prpstn_rel !> ] ],
				 NON-LOCAL [ SLASH 0-dlist,
					     REL 0-dlist,
					     QUE 0-dlist ] ] > ],
		  POSTHEAD +,
		  VAL [ SUBJ < >,
			SPR < >,
			COMPS < pronominal_synsem &
				[ OPT -,
				  LOCAL [ CAT.VAL.SPEC < >,
					  AGR #tagagr,
					  CONT [ INDEX #agr ],
                                          KEYS.ALTKEY no_rel ] ] > ],
		  MC + ],
	    AGR #tagagr,
	    CONT [ TOP #nehand,
		   INDEX #ind,
		   H-CONS <! qeq &
			   [ SC-ARG #soahand,
			     OUTSCPD #modhand ] !> ],
	    KEYS [ KEY #key,
		   MESSAGE 1-dlist &
			 <! ne_rel &
			    [ HANDEL #nehand,
			      SOA #soahand,
			      PARAMS <! !> ] !> ],
	    CTXT.PRESUP <! #presup !> ] ].

loc_inv_synsem := lex_synsem &
  [ LOCAL [ CAT [ VAL [ SUBJ < >,
                        SPR < >,
                        COMPS < [ OPT -,
                                  LOCAL.KEYS.KEY nonpro_rel ], ... > ],
                  MC + ],
            CONT [ TOP #hand,
                   H-CONS <! qeq & [ SC-ARG #soahand,
                                     OUTSCPD #vhand ] !> ],
            KEYS [ KEY.HANDEL #vhand,
                   MESSAGE <! prpstn_rel &
                            [ HANDEL #hand,
                              SOA #soahand ] !> ] ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; ADJECTIVES
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; (See prep_synsem definition for discussion of ARG attribute.)

; Predicative adjectives used to introduce an EVENT attribute in a separate 
; support relation, as did predicative PPs, but now each introduces its own
; EVENT attribute directly in the KEY relation.

; Assume that if adjectives have a specifier, it will be either a measure-NP,
; as in "two feet tall', 'a week later"; or a degree_adverb like "very".

; Since most adjectives are underspecified for being predicative, we have to be
; sure that when they act as modifiers, the unexpressed subject's SLASH value 
; gets terminated (given lexical SLASH amalgamation).  We can't just make the
; adjective's SUBJ be 'unexpressed' since it must unify with the real subject
; in a raising construction.  So we identify the SLASH of the MOD value with
; that of the SUBJ - only one of the two will be realized syntactically.

; Most but not all adjectives can be modifiers, so the most basic adj_synsem
; type does not introduce the MOD feature, to allow for adjectives like
; "impossible" as in "it is impossible to find Kim".

;; DPF 3-Feb-01 - Removed SPR..COMPS *olist* to allow for equatives like
;; "as tall as you" where first "as" combines with "tall" and passes up its
;; as-complement.
;; DPF 5-Apr-01 - Added [PERIPH na] to interact correctly with adj-head rule 
;; and noun-noun-cmpnd rule, to block "*towel rusty rack" - the adj-head rule 
;; makes its PERIPH value that of the adjunct dtr, and the n-n-cmpnd rule 
;; blocks all but [PERIPH -] head-dtr.

;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing

basic_adj_synsem_lex_or_phrase := canonical_synsem &
  [ LOCAL [ STEMHEAD astem,
            CAT [ HEAD adj* & [ TAM #tam ],
		  VAL [ SUBJ < [ LOCAL [ CAT nomp_cat_min,
					 CONJ cnil ] ] >,
			SPR < synsem &
			      [ LOCAL local_min &
				      [ CAT [ VAL.SPR *olist*,
					      MC na ],
					KEYS.KEY degree_rel &
						       [ DARG #darg ] ],
                                NON-LOCAL.SLASH 0-dlist,
				OPT + ] > ] ],
            KEYS.KEY [ EVENT.E #tam,
                       DIM #darg ] ],
    MODIFIED.PERIPH na ].

basic_adj_synsem := basic_adj_synsem_lex_or_phrase & lex_synsem &
  [ LOCAL [ ARG-S < #subj . < #spr . #comps > >,
            CAT.VAL [ SUBJ < #subj >,
                      SPR < #spr >,
                      COMPS #comps ],
	    CONT.LISZT.LIST < #key, ... >,
            KEYS.KEY #key ] ].

;; We assume for now, counterfactually, that all modifying adjectives are
;; intersective.

;; ERB (03-02-98) Maybe should add MOD < [ LOCAL.SPR.OPT ] > - to keep 
;; adjectives from modifying partitives.

;; DPF (16-Feb-98) Made SUBJ.INDEX be 'ref-ind' rather than non_expl-ind,
;; to ensure that it gets discourse-bound if the adjective is used in a fragment.

;; DPF (12-Jul-98) Constrain adjectives to only modify nominals with obligatory
;; specifiers (which distinguishes "common" from "proper" nominals).

adj_synsem_lex_or_phrase := basic_adj_synsem_lex_or_phrase & isect_synsem &
  [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL [ CAT nbar_cat,
				       CONT.INDEX #ind & ref-ind,
				       CONJ cnil ] ] >,
		  VAL.SUBJ < [ LOCAL.CONT.INDEX individual ] > ],
            CONT [ INDEX #ind,
                   E-INDEX #event ],
	    KEYS.KEY.EVENT #event ] ].

adj_synsem := basic_adj_synsem & adj_synsem_lex_or_phrase.

; For measure-nouns used as adjectives, as in "ten foot pole"
adj_synsem_phr := adj_synsem_lex_or_phrase & phr_synsem &
  [ LOCAL.CAT [ HEAD [ PRD -,
                       MOD < [ LOCAL.KEYS.ALTKEY no_rel ] > ],
                VAL.COMPS < > ] ].

nomod_adj_synsem := basic_adj_synsem &
  [ LOCAL.CAT.HEAD.MOD < > ].

nonprd_synsem := lex_synsem &
  [ LOCAL.CAT.HEAD.PRD - ].

prd_synsem := lex_synsem &
  [ LOCAL.CAT.HEAD.PRD + ].

intrans_adj_synsem := adj_synsem & two_arg &
  [ LOCAL.CAT [ VAL.COMPS < >,
		POSTHEAD - ] ].

attr_adj_synsem := intrans_adj_synsem & nonprd_synsem &
  [ NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                QUE 0-dlist & [ LIST < > ],
                REL 0-dlist & [ LIST < > ] ] ].

pred_adj_synsem := adj_synsem & prd_synsem &
  [ LOCAL [ CONT.INDEX #ind,
            KEYS.KEY.ARG #ind ] ].

;;; FIX
intrans_pred_adj_synsem := intrans_adj_synsem &
  [ LOCAL [ CAT.HEAD.PRD +,
            CONT.INDEX #ind,
            KEYS.KEY.ARG #ind ] ].

;;; We should have a trans_adj_attr_synsem too, for 'easy' and the likes ...

pred_adj_three_arg_synsem := pred_adj_synsem & three_arg.
adj_three_arg_synsem := adj_synsem & three_arg.

trans_adj_pred_synsem := pred_adj_three_arg_synsem &
  [ LOCAL [ CAT.VAL.COMPS < synsem &
			    [ LOCAL [ CAT pp_cat,
				      CONJ cnil,
				      CONT.TOP #top,
                                      KEYS.KEY #ckey & selected_rel &
                                               [ ARG3 #ind ] ] ] >,
            CONT.TOP #top,
            KEYS [ KEY.ARG3 #ind,
                   --COMPKEY #ckey ] ] ].

irreg_trans_adj_pred_synsem := pred_adj_three_arg_synsem &
  [ LOCAL [ CAT.VAL.COMPS < synsem &
			    [ LOCAL [ CAT np_cat_acc_min,
                                      KEYS.KEY #ckey ] ] >,
            KEYS.--COMPKEY #ckey ] ].


; e.g. 'able' - Can't use equi_intrans_lt since it specifies an arg13_rel, 
; which is incompatible with adj_rel.  Also, the subj index of adjectives is 
; bound to the ARG role, not the ARG1 role of equi verbs; the ARG role is used
; to give a uniform treatment of semantics of noun modification for adjectives.

reg_adj_equi_synsem := pred_adj_synsem & inf_intrans_subst & three_arg &
  [ LOCAL [ CAT.VAL [ SUBJ < [ LOCAL.CONT.INDEX #subjind ] >,
		      COMPS < [ LOCAL [ CAT.VAL.SUBJ 
					 < [ LOCAL local_min &
                                                   [ CONT.INDEX #subjind ] ] >,
                                        CONT.TOP #hand,
                                        KEYS.MESSAGE <! message !> ],
				OPT + ] > ],
	    KEYS.KEY adj_arg4_rel &
			 [ ARG #subjind,
			   ARG4 #hand ] ] ].


scoping_adj_synsem := basic_adj_synsem & prd_synsem &
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL [ KEYS.MESSAGE 1-dlist,
				      CONT.TOP #hand ],
	    KEYS.KEY basic_adj_arg4_rel & [ ARG4 #hand ] ] ].

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

reg_adj_atrans_synsem := nomod_adj_synsem & scoping_adj_synsem & pp_inf_subst &
			 basic_four_arg &
  [ LOCAL 
    [ CAT.VAL [ SUBJ < [ LOCAL [ CONT.INDEX it-ind ] ] >,
                COMPS < [ LOCAL [ CONT [ TOP #ahand,
                                         INDEX #ind ],
                                  KEYS.KEY _for_rel_s & [ ARG3 #expr ] ],
                          OPT + ],
                        [ LOCAL.CAT.VAL.SUBJ < [ LOCAL.CONT.INDEX #sind,
                                                 NON-LOCAL.SLASH 0-dlist ] >,
                         OPT - ], ...> ],
      CONT [ INDEX #ind,
	     LISZT <! relation, 
		      pron_rel &
		      [ HANDEL #prohand,
			INST #sind & [ PRONTYPE zero_pron ] ],
                      def_rel &
                    [ BV #sind,
                      RESTR #rhand ] !>,
             H-CONS <! qeq & [ SC-ARG #rhand,
                               OUTSCPD #prohand ] !> ],
      KEYS.KEY it_adj_arg24_rel &
		     [ HANDEL #ahand,
		       ARG2 #expr ] ] ].


;; DPF (9-Mar-99) It appears that there are no CP-taking adjectives which 
;; select for a 'like-CP' of the kind some verbs take (e.g. 'It sounds like
;; Kim will leave').  So we constrain the --KEYCOMP to be prop_ques_rel.
;; But there are adjectives (like 'doubtful, unclear, unsure') which can take 
;; a whether-CP, so we introduce a subtype of reg_adj_cp_synsem for those that 
;; only take a 'that-CP', and similarly for expletive-it subject-taking 
;; adjectives.
;; JB 9/01 Also added types for those that only take whether/if-CP's, those 
;; that take whether/if- and that-CP's but not for...to-CP's, and those that
;; take that-CP's and for...to-CP's but not whether/if-CP's.


reg_adj_cp_synsem := scoping_adj_synsem & nomod_adj_synsem & cp_intrans_subst &
		     three_arg &
  [ LOCAL [ CAT.VAL.--KEYCOMP.LOCAL
	                       [ CAT s_cat_fin_or_inf_v_c,
				 KEYS.MESSAGE <! prop_ques_rel !> ],
	    CONT.LISZT <! relation !>,
	    KEYS.KEY adj_arg4_rel ] ].

; 'afraid'  'Kim is afraid that Sandy left'
;           '*Kim is afraid whether Sandy left'
;           '*It is afraid that/whether Sandy left'

reg_adj_that_cp_synsem := reg_adj_cp_synsem &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE 1-dlist &
					       <! prpstn_rel !> ].
; 'uncertain': '?Kim is uncertain that Sandy left' 
;              'Kim is uncertain whether Sandy left' 
;              'It is uncertain that Sandy left'
;              'It is uncertain whether Sandy left'

reg_adj_whether_cp_synsem := reg_adj_cp_synsem &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE 1-dlist &
					       <! int_rel !> ].
; 'glad': 'I'm glad that Kim is going.'
;         '*I'm glad whether Kim is going.'
;         'I'm glad for Kim to go.'

reg_adj_propl_cp_synsem := reg_adj_cp_synsem &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE 1-dlist &
					       <! propositional_rel !> ].

; 'certain': 'Kim is certain that Sandy left.'
;            'Kim is not certain whether Sandy left.'
;            '*Kim is (not) certain for Sandy to leave.' 

reg_adj_non-inf_cp_synsem := reg_adj_cp_synsem &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE 1-dlist &
					       <! prpstn_or_int_rel !> ].


reg_adj_atrans_cp_synsem := reg_adj_cp_synsem &
  [ LOCAL [ CAT.VAL.SUBJ < [ LOCAL.CONT.INDEX it-ind ] > ] ].

reg_adj_atrans_that_cp_synsem := reg_adj_atrans_cp_synsem &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE 1-dlist &
					       <! prpstn_rel !> ].

; 'unclear' '*Kim is unclear that/whether Sandy left'
;           'It is unclear that Sandy left'
;           'It is unclear whether Sandy left'
; note: BNC doesn't bear out Dan's judgements, so 'unclear' is restricted to 
; taking _whether_ (-JB)

reg_adj_atrans_whether_cp_synsem := reg_adj_atrans_cp_synsem &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE 1-dlist &
					       <! int_rel !> ].

; 'necessary': 'It's necessary that Kim goes.'
;              '*It's necessary whether Kim goes.'
;              'It's necessary for Kim to go.'

reg_adj_atrans_propl_cp_synsem := reg_adj_atrans_cp_synsem &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE 1-dlist &
					       <! propositional_rel !> ].

; 'obvious' '*Kim is obvious that/whether Sandy left'
;           'It is obvious that Sandy left'
;           '*It is obvious whether Sandy left'
;   but cf. 'It is not obvious whether Sandy left' ***Needs fixing***
;   - 'certain' currently overgenerates (takes non-inf of either _that_ or
;     _whether_ type)

reg_adj_atrans_non-inf_cp_synsem := reg_adj_atrans_cp_synsem &
  [ LOCAL.CAT.VAL.--KEYCOMP.LOCAL.KEYS.MESSAGE 1-dlist &
					       <! prpstn_or_int_rel !> ].

adj_word := hcons_amalg_word &
  [ SYNSEM adj_synsem &
           [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL.CONT.INDEX #ind ] >,
                           VAL.SUBJ < [ LOCAL.CONT.INDEX #ind ] > ],
                     CONT.LISZT <! abstr_adj_rel !>,
                     KEYS.KEY.ARG #ind ] ] ].

reg_adj_word := adj_word & 
  [ INFLECTED - ].

reg_intrans_adj := reg_adj_word & 
  [ SYNSEM intrans_adj_synsem ].

adj_intrans_le :< reg_intrans_adj.
adj_intrans_lowprio_le :< reg_intrans_adj.

adj_attr_intrans_le := reg_adj_word &
  [ SYNSEM attr_adj_synsem ].

adj_pred_intrans_le := reg_intrans_adj & 
  [ SYNSEM intrans_pred_adj_synsem ].

; For unlikely adjectives, e.g. 'in' and 'on'
adj_pred_intrans_lowprio_le := reg_intrans_adj & 
  [ SYNSEM intrans_pred_adj_synsem ].

intrans_adj_oddsem := norm_no_affix_word &
  [ SYNSEM intrans_adj_synsem &
           [ LOCAL [ CONT.INDEX #ind,
                     KEYS.KEY.ARG #ind ] ] ].

;; doctor's appointment
;;; AAC - changed SYNSEM LOCAL CONT LISZT FIRST INST to ARG
adj_compound_le := intrans_adj_oddsem &
  [ SYNSEM attr_adj_synsem &
	 [ LOCAL 
	   [ CAT.HEAD.MOD < [ LOCAL.CONT [ TOP #hand,
					   INDEX #nind ] ] >,
	     CONT [ TOP #hand,
		    LISZT <! relation & [ ARG #aind ],
			     unspec_rel & [ HANDEL #hand,
					    HINST #nind,
					    NHINST #aind ] !> ] ] ] ].

;; {OPT +] is on reg_trans_adj and not on trans_adj_synsem because the 
;; comparison_lr uses trans_adj_synsem and requires OPT -. Same for semantics.

adj_trans_lexent := reg_adj_word &
  [ SYNSEM trans_adj_pred_synsem &
	   [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.TOP #top ] >, 
		     CONT [ TOP #top,
			    LISZT <! relation !> ] ] ] ].

adj_trans_le := adj_trans_lexent &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ OPT + ] > ].

adj_trans_oblig_le := adj_trans_lexent &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ OPT - ] > ].

; These 'irregular' types are for multi-word adjectives, which cannot
; be affix-bearing under the current regime.  They should be eliminated
; once we do better with multi-words.

irreg_adj_word := adj_word & non_affix_bearing.

adj_irreg_attr_le := irreg_adj_word &
  [ SYNSEM attr_adj_synsem &
	  [ LOCAL [ CAT.VAL.SUBJ 
			  < [ LOCAL.CONT [ TOP #hand,
					   INDEX #ind & non_expl-ind ] ] >,
		    KEYS.KEY [ HANDEL #hand,
                               ARG #ind ] ] ] ].

adj_irreg_pred_intrans_le := irreg_adj_word &
  [ SYNSEM intrans_pred_adj_synsem &
	  [ LOCAL [ CAT [ HEAD.PRD +,
			  VAL.SUBJ
			  < [ LOCAL.CONT [ TOP #hand,
					   INDEX #ind & non_expl-ind ] ] > ],
		    KEYS.KEY [ HANDEL #hand,
			       ARG #ind ] ] ] ].


adj_irreg_np_trans_le := irreg_adj_word &
  [ SYNSEM irreg_trans_adj_pred_synsem &
	   [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.INDEX #index,
				       OPT - ] >,
		     CONT.LISZT <! [ ARG3 #index ] !> ] ] ].


adj_reg_equi_le := hcons_amalg_affixed_word &
  [ INFLECTED -,
    SYNSEM reg_adj_equi_synsem &
	   [ LOCAL.CONT.LISZT <! abstr_adj_rel !> ] ].

adj_reg_atrans_le := hcons_amalg_affixed_word &
  [ SYNSEM reg_adj_atrans_synsem ].

adj_reg_atrans_medprio_le := hcons_amalg_affixed_word &
  [ SYNSEM reg_adj_atrans_synsem ].

reg_adj_atrans_cp_word := hcons_amalg_affixed_word &
  [ SYNSEM reg_adj_atrans_cp_synsem ].

adj_reg_atrans_cp_le := reg_adj_atrans_cp_word &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ OPT - ] > ].

adj_reg_atrans_that_cp_le := reg_adj_atrans_cp_word &
  [ SYNSEM reg_adj_atrans_that_cp_synsem ].

adj_reg_atrans_non-inf_cp_le := reg_adj_atrans_cp_word &
  [ SYNSEM reg_adj_atrans_non-inf_cp_synsem ].

adj_reg_atrans_whether_cp_le := reg_adj_atrans_cp_word &
  [ SYNSEM reg_adj_atrans_whether_cp_synsem ].

adj_reg_atrans_propl_cp_le := reg_adj_atrans_cp_word &
  [ SYNSEM reg_adj_atrans_propl_cp_synsem ].

reg_adj_cp_word := hcons_amalg_affixed_word &
  [ SYNSEM reg_adj_cp_synsem &
	   [ LOCAL [ CAT.VAL.SUBJ < [ LOCAL.CONT.INDEX ref-ind & #ind ] >,
		     KEYS.KEY.ARG #ind ] ] ].

adj_reg_cp_le := reg_adj_cp_word & 
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ OPT - ] > ].

adj_reg_that_cp_le := reg_adj_cp_word &
  [ SYNSEM reg_adj_that_cp_synsem &
           [ LOCAL.CAT.VAL.COMPS < [ OPT - ] > ] ].

adj_reg_whether_cp_le := reg_adj_cp_word &
  [ SYNSEM reg_adj_whether_cp_synsem &
           [ LOCAL.CAT.VAL.COMPS < [ OPT - ] > ] ].

adj_reg_propl_cp_le := reg_adj_cp_word &
  [ SYNSEM reg_adj_propl_cp_synsem &
           [ LOCAL.CAT.VAL.COMPS < [ OPT - ] > ] ].

adj_reg_non-inf_cp_le := reg_adj_cp_word &
  [ SYNSEM reg_adj_non-inf_cp_synsem &
           [ LOCAL.CAT.VAL.COMPS < [ OPT - ] > ] ].


; How is Sandy?  How does Tuesday look?
; We adopt the following second-order quantification semantics for these:
;    which (p, property(p), look_seem(proposition_to_property(adj(x),p)))
; where we exploit a type-shifting relation to change the proposition "adj(x)"
; into the associated property "the set of entities of which adj(x) is true",
; so the "which" operator can quantify over that property.

;; DPF 14-Apr-00 - Changed SUBJ.CAT from nomp_cat_nom_min to nomp_cat_min
;; since [CASE nom] conflicts with the CASE hack on pro_ss to prevent the
;; latter from undergoing subject xxtraction (see ERB notes for pro_ss).

adj_wh_le := nonconj & non_affix_bearing & mcna & nonslash & 
                        nonrel & nonmsg &
  [ SYNSEM lex_synsem &
      [ LOCAL [ STEMHEAD astem,
		ARG-S #subj,
		CAT [ POSTHEAD +,
		      HEAD adj* & [ MOD < > ],
		      VAL [ SUBJ #subj &
				 < synsem &
				   [ LOCAL [ CAT nomp_cat_min,
					     CONJ cnil,
					     CONT.INDEX #ind ] ] >,
			    SPR < >,
			    COMPS < > ] ],
		CONT [ TOP #top,
		       INDEX #ind,
		       LISZT <! unspec_adj_rel &
			        [ HANDEL #ahand,
				  ARG #ind ],
			        which_rel & [ BV #pind,
					      RESTR #hand ],
			        property_rel & [ HANDEL #hand,
						 INST #pind ],
			        prpstn_to_prop_rel & #key &
			        [ HANDEL #top,
				  PRPSTN #ahand,
				  PROP #pind ] !>,
		       H-CONS <! !> ],
		KEYS.KEY #key ],
	NON-LOCAL.QUE 1-dlist &
	              [ LIST < handle > ] ] ].

; two-hour meeting, two-foot tall
measure_adj_attr_synsem := zero_arg & isect_synsem &
  [ LOCAL [ CAT [ HEAD adj* &
		       [ PRD -,
			 MOD < [ LOCAL [ CAT nbar_cat,
					 CONT.INDEX #ind,
					 CONJ cnil ] ] > ],
		  VAL [ SUBJ < >,
			SPR < synsem &
			      [ LOCAL local_min &
				      [ CAT [ VAL [ SPR *olist*,
						    COMPS *olist* ],
					      MC na ],
					CONT [ TOP #hand,
                                               INDEX #inst ],
					KEYS.KEY const_rel ],
				OPT - ] >,
			COMPS < > ] ],
	    CONT mrs &
		 [ LISZT <! meas_adj_rel & #key &
			    [ HANDEL #hand,
			      ARG #ind,
			      ARG3 #inst ],
			    nom_rel & #altkey & [ HANDEL #hand,
						  INST #inst ] !> ],
	    KEYS [ KEY #key,
		   ALTKEY #altkey ] ] ].

adj_meas_le := norm_no_affix_word &
 [ SYNSEM measure_adj_attr_synsem ].

compar_superl_adj_word := hcons_amalg_non_affixed_word &
  [ SYNSEM adj_synsem &
           [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL.CONT.INDEX #ind ] >,
                           VAL.SUBJ < [ LOCAL.CONT.INDEX #ind ] > ],
                     CONT.LISZT <! [ HANDEL #hand,
                                     ARG #ind ],
                                 #altkey &
                                 [ HANDEL #hand,
                                   ARG #ind ] !>,
                     KEYS.ALTKEY #altkey ] ] ].

basic_compar_adj_word := compar_superl_adj_word &
  [ SYNSEM.LOCAL [ CAT.VAL.SPR < [ LOCAL.KEYS.KEY much_deg_rel ] >,
                   KEYS.ALTKEY comp_rel ] ].

adj_comp_le := basic_compar_adj_word &
  [ SYNSEM intrans_adj_synsem ].

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

adj_comp_trans_le := basic_compar_adj_word &
  [ SYNSEM trans_adj_pred_synsem &
	   [ LOCAL [ CAT.VAL.COMPS < [ LOCAL [ KEYS.KEY.ARG3 #index,
					       CONT.TOP #top ],
				       OPT +] >, 
                     KEYS.KEY.ARG3 #index,
		     CONT.TOP #top ] ] ].


adj_more_less_le := basic_compar_adj_word &
  [ SYNSEM adj_three_arg_synsem &
	   [ LOCAL [ CAT.VAL [ SUBJ #subj & *olist*,
                               SPR *olist*,
			       COMPS < lex_synsem &
					 [ LOCAL 
                                           [ CAT [ HEAD adj &
                                                        [ TAM.TENSE no_tense ],
                                                   VAL [ SUBJ #subj,
                                                         SPR *olist*,
                                                         COMPS < > ] ],
                                             CONT [ TOP #top,
                                                    INDEX #ind ] ],
                                           LEX +,
					   OPT -] > ], 
		     KEYS.KEY more_less_adj_rel,
                     CONT [ TOP #top,
                            INDEX #ind ] ] ] ].

basic_superl_adj_word := compar_superl_adj_word &
  [ SYNSEM.LOCAL [ CAT.VAL.SPR < [ LOCAL.KEYS.KEY very_deg_rel ] >,
                   KEYS.ALTKEY superl_rel ] ].

adj_superl_le := basic_superl_adj_word &
  [ SYNSEM intrans_adj_synsem ].

adj_most_least_le := basic_superl_adj_word &
  [ SYNSEM adj_three_arg_synsem &
	   [ LOCAL [ CAT.VAL [ SUBJ #subj & *olist*,
                               SPR *olist*,
			       COMPS < lex_synsem &
					 [ LOCAL 
                                           [ CAT [ HEAD adj &
                                                        [ TAM.TENSE no_tense ],
                                                   VAL [ SUBJ #subj,
                                                         SPR *olist*,
                                                         COMPS < > ] ],
                                             CONT [ TOP #top,
                                                    INDEX #ind ] ],
                                           LEX +,
					   OPT -] > ], 
		     KEYS.KEY most_least_adj_rel,
                     CONT [ TOP #top,
                            INDEX #ind ] ] ] ].

;; Titles like 'mister', 'professor' which modify proper names
;; DPF 5-Apr-01 - Changed MOD..KEY to abstr_named_rel from named_rel, to
;; include integers, as in "number ten"

title_synsem := basic_zero_arg & 
  [ LOCAL [ CAT [ HEAD adv &
		       [ MOD < [ LOCAL intersective_mod &
				       [ CAT nbar_cat,
					 CONJ cnil,
					 CONT.INDEX #nind,
					 KEYS.KEY abstr_named_rel & 
                                                  [ HANDEL #nhand ] ],
                                 MODIFIED notmod_or_rmod ] > ],
		  VAL [ SUBJ < >,
			SPR < >,
			COMPS < > ] ],
	    CONT [ INDEX #ind,
		   LISZT <! #key & 
                          [ HANDEL #khand,
                            INST #ind ],
                            udef_rel & 
                          [ BV #ind,
                            RESTR #rhand ],
                          title_id_rel &
                          [ HANDEL #nhand,
                            ARG1 #ind,
                            ARG3 #nind ] !>,
                   H-CONS <! qeq &
                           [ SC-ARG #rhand,
                             OUTSCPD #khand ] !> ],
            KEYS [ KEY #key ] ] ].

n_title_le := nontopkey &
  [ SYNSEM title_synsem &
	   [ LOCAL.CAT.POSTHEAD - ] ].

n_post_title_le := norm_no_affix_word &
  [ SYNSEM title_synsem &
	   [ LOCAL.CAT.POSTHEAD + ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; PREPOSITIONS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Most prepositions can be modifiers.  If they act as a modifier, we'll assume
; they have no subject (leaving for some distinct analysis Carl's "A unicorn
; at the helm, the ship sailed into the horse latitudes."  This lets us use a
; single attribute, ARG, for the index of the phrase modified, or for the
; index of the subject of the PP, in a predicative construction.

; Prepositions also have a distinguished role ARG3 which has as value the
; index of the object of the preposition.  We could introduce distinct role
; names for each preposition (e.g. "ON" for on_rel, "IN" for in-rel) but this
; would complicate the definition of the prep_synsem type, since it needs to
; know the role name to which it assigns the object's index.

; An EVENT attribute is needed for PPs (and other substs used predicatively)
; occurring as complements of the copula as in "the meeting is on Tuesday",
; but on this analysis not postnominally.  This EVENT role used to be
; introduced in a separate support_rel, supplied by the copula, but is now
; introduced directly in the relation introduced by the predicate (prep or
; adj or verb).

; Prepositions, like adjectives and participial verbs, can have either
; predicative or nonpredicative synsems, so each ordinary preposition
; (actually the PP headed by the P) can unify with either a pred. or non-pred.
; constraint, which resolves the underspecification.  One assumption here is 
; that there are no contexts which are underspecified for the PRD feature 
; for PPs.

; N.B. The LISZT value of basic_prep_synsem cannot be a singleton list, since
; adverbs like "when" inherit this synsem, and add additional rels lexically.

; Here we only amalgamate non-local features from the complements, not the
; subject.  This is so we don't get two identical gaps in an example like "Who
; does kim think is under the bed?".  On the other hand, binding theory
; suggests that the subjects of prepositions really are on the ARG-S list.

; DPF (14-Mar-99) Constrained prepositions to not allow pied-piping in
; free relative constructions, where the QUE value is an index rather than
; a handle.

basic_prep_synsem := canonical_synsem & 
  [ LOCAL [ CAT.HEAD n_or_p & [ TAM #tam ],
	    CONT.LISZT.LIST < norm_relation & [ EVENT.E #tam ], ... > ] ].

; DPF 18-Apr-01 - Removed ident. of ALTKEY with COMPS..KEY, since it prevents
; selection in v-np-pp verbs for a PP which is temporal or locative: want to
; block "When did kim put the book", but allow "Where did Kim put the book"
; We'll see where this constraint was being used.

basic1_prep_synsem := basic_prep_synsem & unsat_two_arg_subst &
  [ LOCAL [ CAT [ VAL.COMPS < synsem & 
			      [ LOCAL local_min &
				      [ KEYS.KEY #ckey,
					CONT.INDEX #objind ],
				NON-LOCAL.QUE.LIST *handlelist* ], ... >,
		  POSTHEAD + ],
	    CONT.LISZT.LIST < #key, ... >,
	    KEYS [ KEY #key & [ ARG3 #objind ],
		   --COMPKEY #ckey ] ] ].

prep_synsem := basic1_prep_synsem &
  [ LOCAL.CAT.HEAD prep* ].

; For predicative Ps and PPs like "when", which have a (non-expletive) NP subject

prep_p_synsem := basic_prep_synsem &
  [ LOCAL [ CAT.VAL [ SUBJ < synsem & #subj &
			       [ LOCAL [ CAT nomp_cat_min,
					 CONT.INDEX #subjind & event_or_index,
					 CONJ cnil ],
				 NON-LOCAL [ SLASH 0-dlist,
					     REL 0-dlist,
					     QUE 0-dlist ] ] >, 
			COMPS #comps ],
	    KEYS.KEY.ARG #subjind,
	    ARG-S < #subj . #comps > ] ].

;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing

prep_with_spr_synsem := basic_prep_synsem &
  [ LOCAL [ CAT.VAL.SPR < synsem & [ LOCAL local_min &
					   [ CAT [ HEAD adv,
						   VAL [ SPR *olist*,
							 COMPS *olist* ] ],
                                             CONT.TOP #khand,
					     KEYS.KEY degree_rel &
						 [ DARG #darg ] ],
				     NON-LOCAL.QUE 0-dlist ] >,
	    KEYS.KEY [ HANDEL #khand,
		       DIM #darg ] ] ].


; Ps and PPs that can be modifiers.
; Block PP modification of infinitival "to" - spurious ambiguity.
; Constrain MOD < [ LOCAL.CONT.INDEX > to be non_expl, for fragments like 
; "perhaps in a week"

basic_mod_n_or_vp_synsem := isect_synsem &
  [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL [ CAT n_or_v_cat &
                                           [ HEAD n_or_v,
					     MC #mc ],
				       CONT.INDEX #ind & non_expl,
				       CONJ cnil,
				       KEYS.MESSAGE #msg ] ] >,
                  MC #mc,
		  POSTHEAD + ],
	    CONT [ INDEX #ind,
                   E-INDEX #event ],
	    KEYS [ KEY [ ARG #ind,
                         EVENT #event ],
		   MESSAGE #msg ] ] ].

mod_n_or_vp_synsem := basic_mod_n_or_vp_synsem & prep_with_spr_synsem.

prep_p_mod_synsem := prep_p_synsem & mod_n_or_vp_synsem &
  [ LOCAL.CAT.VAL.SPR < [ OPT + ] > ].

prep_prd_or_nonprd_synsem := prep_synsem & prep_p_mod_synsem.

; Strict transitive prepositions which can be either predicative or non-pred:

basic_trans_prep_synsem := prep_prd_or_nonprd_synsem & basic_two_arg &
  [ LOCAL [ CAT.VAL [ SUBJ < [ NON-LOCAL [ SLASH 0-dlist,
                                             REL 0-dlist,
                                             QUE 0-dlist ] ] > ],
            CONT.LISZT.LIST < prep_rel, ... > ] ].

trans_prep_synsem := basic_trans_prep_synsem &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT [ TOP #top,
					   INDEX ref-ind ] ] >,
	    CONT [ TOP #top,
		   LISZT <! prep_rel !>,
		   H-CONS <! !> ] ] ].

ditrans_prep_synsem := prep_prd_or_nonprd_synsem & three_arg_subst &
  [ LOCAL [ CAT.VAL.COMPS < *top* & [ LOCAL.CONT.TOP #top ], *top* >,
	    CONT [ TOP #top,
		   LISZT <! prep_rel !> ] ] ].

basic_lex_pp_synsem := prep_p_synsem & phr_synsem &
  [ LOCAL [ ARG-S #subj,
	    CAT.VAL [ SUBJ #subj,
		      COMPS < > ] ] ].

lex_pp_synsem := basic_lex_pp_synsem & mod_n_or_vp_synsem & one_arg &
  [ LOCAL.CAT.HEAD prep* &
                   [ MOD < [ MODIFIED notmod_or_rmod ] > ] ].

lex_pp_post_synsem := basic_lex_pp_synsem & basic_mod_n_or_vp_synsem & 
                      one_arg &
  [ LOCAL [ CAT [ HEAD prep* &
                       [ MOD < [ MODIFIED notmod_or_rmod ] > ],
                  VAL.SPR < synsem & 
                            [ LOCAL local_min &
                                    [ CAT np_cat_acc,
                                      CONT.INDEX #index ],
                              OPT - ] > ],
            KEYS.KEY.ARG3 #index ] ].



; DPF 13-Oct-99 - Restricted MOD value to a comparative adj (lexical like "taller"
; or phrasal like "more important").

;; DPF 10-Oct-00 - MOD.KEYS.ALTKEY needs fixing

compar_adj_prep_synsem := prep_synsem & isect_synsem & lex_synsem &
  [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL [ CAT [ HEAD adj,
					     VAL.SPR < [ LOCAL.KEYS.KEY 
							      much_deg_rel ] >,
                                             MC na ],
                                       CONT [ TOP #top,
                                              INDEX #ind ],
                                       KEYS [ ALTKEY comp_rel &
                                                        [ ARG3 #arg ] ] ] ] >,
                  VAL [ SPR < >,
                        COMPS < [ LOCAL [ CAT [ HEAD noun,
						VAL [ SPR *olist*,
						      SUBJ *olist*,
						      COMPS < > ] ],
					  CONT [ TOP #top,
						 INDEX #arg ] ],
				  NON-LOCAL [ SLASH 0-dlist,
					      REL 0-dlist,
					      QUE 0-dlist ],
				  OPT - ] > ] ],
            CONT [ TOP #top,
                   INDEX #ind,
                   LISZT <! _compar_than_rel !>,
                   H-CONS <! !> ],
	    KEYS.MESSAGE <! !>,
            CONJ cnil ] ].

; Ordinary prepositions:

; Moved [ OPT - ] specification to subtypes, to allow "the day before" where
; the complement is missing.
; DPF (9-Mar-99) Made basic_prep_word not inherit from nonmsg, since it has
; its MESSAGE identified with that of its modifiee (via the head_modifier rule)
; and that modifiee will sometimes have a non-empty message, as in
; "Is kim happy in Berlin"

basic_prep_word := msg_amalg_word &
  [ SYNSEM trans_prep_synsem &
	 [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
						    KEYS.MESSAGE 0-dlist,
						    CONJ cnil ] ] > ] ].

; Changed ref-ind constraint to non_expl-ind so prep's can be used as particles
prep_word := basic_prep_word &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.CONT.INDEX non_expl-ind ] > ].

p_le := prep_word & 
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ OPT - ] > ].

p_reg_le := prep_word &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL local_min &
					 [ KEYS.KEY non_temp_nom_rel ],
				   OPT - ] > ].
p_nospec_le := prep_word & 
  [ SYNSEM.LOCAL.CAT.VAL [ SPR < unexpressed >,
                           COMPS < [ OPT - ] > ] ].

p_nogap_le := prep_word & 
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ OPT -,
                                   NON-LOCAL.SLASH 0-dlist ] > ].

p_nospec_nogap_le := prep_word & 
  [ SYNSEM.LOCAL.CAT.VAL [ SPR < unexpressed >,
                           COMPS < [ OPT -,
                                     NON-LOCAL.SLASH 0-dlist ] > ] ].

p_temp_le :=  msg_amalg_word &
  [ SYNSEM trans_prep_synsem &
	 [ LOCAL.CAT.VAL.COMPS < synsem & 
                                 [ LOCAL [ CAT [ HEAD nominal & strict_type &
                                                      [ CASE acc,
                                                        POSS - ],
                                                 VAL [ SUBJ *olist*,
                                                       SPR *olist*,
                                                       COMPS *olist* ],
                                                 MC na ],
                                           CONJ cnil ],
                                   OPT - ] > ] ].
; Used e.g. for "from", as in "from under the table"
; DPF 6-Apr-01 - Also used for "worth" as in "ten dollars worth of food"

p_ppcomp_lexent := msg_amalg_word &
  [ SYNSEM trans_prep_synsem &
	 [ LOCAL [ CAT.VAL.COMPS < synsem &
				   [ LOCAL local_min &
					   [ CAT [ HEAD prep,
						   VAL [ SPR *olist*,
							 COMPS < > ] ],
					     CONT.INDEX non_expl-ind,
					     CONJ cnil ] ] > ],
           NON-LOCAL.SLASH 0-dlist ] ].

; 'from'
p_ppcomp_le := p_ppcomp_lexent &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ OPT - ] > ].

; 'worth'

p_ppcomp*_le := p_ppcomp_lexent &
  [ SYNSEM.LOCAL [ CAT [ HEAD.MOD < [ LOCAL.CAT.HEAD noun ] >,
                         VAL.COMPS < [ OPT + ] > ],
                   KEYS [ KEY.ARG3 #arg,
                          --COMPKEY _of_rel_s & [ ARG3 #arg ] ] ] ].


; 'from now on', maybe 'from two to four'
p_ditrans_le := norm_msg_word &
  [ SYNSEM ditrans_prep_synsem &
	 [ LOCAL [ CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
						      KEYS.MESSAGE 0-dlist,
						      CONJ cnil ],
					      OPT - ], 
				   synsem & [ LOCAL local_min &
						    [ CAT [ HEAD.PRD -,
							    MC na ],
						      CONT.TOP #top ],
					      NON-LOCAL.QUE.LIST *handlelist* ] >,
		   CONT.TOP #top ] ] ].

p_cp_le := msg_amalg_word &
  [ SYNSEM basic_trans_prep_synsem &
      [ LOCAL [ CAT.VAL.COMPS < synsem &
				[ LOCAL 
				  [ CAT s_cat_fin_v_c & [ HEAD verb ],
				    CONJ cnil,
				    CONT [ TOP #mhand,
					   INDEX event & #event ],
				    KEYS.MESSAGE 1-dlist &
						 <! prpstn_rel !> ],
				  OPT - ] >,
		CONT [ TOP #mhand,
		       LISZT <! prep_rel !>,
		       H-CONS <! !> ],
		KEYS.KEY.ARG3 #event ] ] ].

;; DPF 10-Oct-00 - COMPS.KEYS.KEY needs fixing

p_pp_le := msg_amalg_word &
  [ SYNSEM basic_trans_prep_synsem &
      [ LOCAL [ CAT.VAL.COMPS < synsem &
				[ LOCAL [ CAT pp_cat,
					  CONJ cnil,
					  CONT.TOP #top,
                                          KEYS.KEY [ ARG3 #arg3 ] ] ] >,
		CONT [ TOP #top,
		       LISZT <! prep_rel !>,
		       H-CONS <! !> ],
                KEYS.KEY [ ARG3 #arg3 ] ] ] ].

#|
prep_spr_word := msg_amalg_word &
  [ SYNSEM prep_spr_synsem &
	   [ LOCAL.CAT.VAL [ SPR < [ OPT - ] >,
			     COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
							KEYS.MESSAGE 0-dlist,
							CONJ cnil ],
						OPT - ] > ] ] ].
|#

; Prepositions that don't modify nominals - e.g, 'until'
p_no_n_mod_le := prep_word &
  [ SYNSEM.LOCAL.CAT [ HEAD.MOD < [ LOCAL.CAT.VAL.SUBJ < synsem > ] >,
		       VAL.COMPS < [ OPT - ] > ] ].

; "Case-marking" prepositions, which cannot be modifiers
; and (let's assume) also cannot be modified, so stipulate [ SUBJ < > ].
;
; DPF 10-Jun-99 - But they can take specifiers: "Kim fell nearly to the bottom"

basic_prep_nomod_synsem := lex_synsem & prep_with_spr_synsem &
  [ LOCAL [ CAT [ HEAD prep* & [ PRD -,
				 MOD < > ],
		  VAL [ SPR < [ OPT + ] >,
                        COMPS < synsem & 
				[ LOCAL local_min &
					[ KEYS.KEY #ckey,
					  CONT [ TOP #top,
						 INDEX #ind ] ],
				  NON-LOCAL.QUE.LIST *handlelist*,
				  OPT - ] > ] ],
	    CONT [ TOP #top,
		   LISZT <! #key !> ],
	    KEYS [ KEY prep_rel & #key &
		       [ ARG3 #ind ],
		   --COMPKEY #ckey ] ] ].

prep_nosubj_synsem := basic_prep_nomod_synsem & one_arg & 
  [ LOCAL [ ARG-S #comps,
	    CAT.VAL [ SUBJ < >,
		      COMPS #comps ] ] ].

prep_nomod_synsem := basic_prep_nomod_synsem & prep_p_synsem & two_arg.

; Removed constraint on [COMPS.FIRST.MOD < >] to allow "of the second"
p_nomod_le := amalg_word &
  [ SYNSEM prep_nomod_synsem & 
	 [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
						    KEYS.MESSAGE 0-dlist,
						    CONJ cnil ] ] > ] ].

; nominal "of"
; DPF 11-Oct-00 - Changed to make INDEX be ARG rather than ARG3, like other
; prepositions.
; DPF 27-Mar-01 - Try not using this type, since need predicative "of" both
; for "Kim notified me of the error" and for "That picture is of Sandy"

#|
p_nomod_of_le := amalg_word &
  [ SYNSEM prep_nosubj_synsem & 
	   [ LOCAL [ CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
							KEYS.MESSAGE 0-dlist,
							CONJ cnil ] ] >,
		     KEYS.KEY.ARG #ind,
		     CONT.INDEX #ind ] ] ].
|#

; Lexical PPs

pp_word := basic_norm_no_affix_word &
  [ SYNSEM lex_pp_synsem &
	   [ LOCAL [ KEYS.KEY #key,
		     CONT.LISZT <! #key !> ] ] ].

; 'away'
pp_le := pp_word &
  [ SYNSEM.LOCAL.CAT.VAL.SPR < [ OPT + ] > ].

; ago
pp_post_le := basic_norm_no_affix_word &
  [ SYNSEM lex_pp_post_synsem &
	   [ LOCAL [ KEYS.KEY #key,
		     CONT.LISZT <! #key !> ] ] ].

; Prepositions with lexically idiosyncratic complements:

; DPF 08-Jan-01 - was built for "as of", but don't remember why, since
; it seems "as of" is an ordinary prep.
p_idiom_le := msg_amalg_word &
  [ SYNSEM trans_prep_synsem &
	   [ LOCAL [ CAT.VAL.COMPS < [ OPT - ], ... > ] ] ].

p_idiom_nomod_le := amalg_word &
  [ SYNSEM prep_nosubj_synsem &
	   [ LOCAL.CAT.VAL.COMPS < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
						      KEYS.MESSAGE 0-dlist,
						      CONJ cnil ] ] > ] ].

p_nbar_comp_lexent := msg_amalg_word &
  [ SYNSEM basic_trans_prep_synsem &
	   [ LOCAL [ CAT.VAL [ SPR < [ NON-LOCAL.SLASH 0-dlist ] >,
			       COMPS < lex_synsem &
				     [ OPT -,
				       LOCAL [ CAT [ HEAD noun,
						     VAL [ SPR < synsem &
							  [ NON-LOCAL.SLASH 0-dlist ] >,
							   COMPS *olist* ] ],
					       CONT.TOP #nhand,
                                               KEYS.ALTKEY no_rel ],
                                       NON-LOCAL.SLASH 0-dlist ] > ],
		     CONT [ LISZT <! prep_rel & [ ARG3 #arg3 ],
				     udef_rel & [ BV #arg3,
						  RESTR #rhand ] !>,
			    H-CONS <! qeq & [ SC-ARG #rhand,
					      OUTSCPD #nhand ] !> ] ] ] ].

p_nbar_comp_le :< p_nbar_comp_lexent.

; Added MOD..ALTKEY no_rel to prevent "a person" from modifying e.g. gerunds.
p_nbar_comp_nmod_le := p_nbar_comp_lexent &
  [ SYNSEM.LOCAL [ CAT.HEAD.MOD < [ LOCAL [ CAT.HEAD noun,
                                            KEYS.ALTKEY no_rel ] ] >,
                   KEYS.KEY.EVENT.E [ TENSE no_tense,
                                      ASPECT no_aspect ] ] ].

;; Temporal prepositions:

; Special type for "at" taking hours
;    'at five oclock'
;    'at fifteen to six'

#|
at_prep_word := msg_amalg_word &
  [ SYNSEM trans_prep_synsem &
	 [ LOCAL [ CAT.VAL.COMPS
		       < synsem & [ LOCAL [ CAT nomp_cat_acc_min,
					    KEYS.MESSAGE 0-dlist,
					    CONJ cnil,
					    KEYS.KEY hour_rel ],
				    OPT - ] > ] ] ].
|#

; Prepositions like "before" that take a prep_noun like "now, then".
;   'before now', 'since then'

before_prep_le := norm_msg_word &
  [ SYNSEM trans_prep_synsem &
	 [ LOCAL [ CAT.VAL.COMPS 
		 < [ LOCAL local_min &
			   [ CAT [ HEAD n_or_p & [ PRD - ],
				   VAL [ SPR *olist*,
					 COMPS *olist* ] ],
			     KEYS.KEY temp_loc_indep_rel ],
		     OPT - ], ... > ] ] ].

; "ten minutes to three" - "to" as head
; Use POSTHEAD to allow "half past three" and "three thirty" but block
;      "?thirty past three" and "*three half"
;; DPF (12-Jul-98) The specifier on hour_prep_words has to be [ OPT + ] in order
;; to block prenominal adjectives from attaching, since they distinguish proper
;; from common nouns by the optionality (or absence) of the specifier.  This 
;; distinction doesn't seem to work well here, but it's the only case, so perhaps
;; the analysis of "ten after three" should be reconsidered.
;; DPF (11-Jul-99) But this [OPT +] incorrectly predicts that "To five thirty suits
;; me" is good, and worse, it allows "to five thirty" to act like an NP in
;; appositive constructions.  So instead of this hack, constrain adjectives to
;; only modify nominals with a DET specifier.

n_hour_prep_le := norm_msg_word &
  [ SYNSEM nomod_onearg_synsem &
	 [ LOCAL [ CAT [ HEAD noun* &
			      [ POSS - ],
			 VAL [ SPR < synsem & [ LOCAL [ CAT [ HEAD intadj,
							      VAL.COMPS < > ],
							CONJ cnil,
							CONT.INDEX #minute,
							KEYS.KEY minute_rel ],
						OPT - ] >,
			       COMPS < synsem & 
				       [ LOCAL [ CAT nomp_cat_acc_min,
						 KEYS [ KEY numbered_hour_rel &
							      #ckey,
							MESSAGE 0-dlist ],
						 CONT [ TOP #top,
							INDEX #hour ],
						 CONJ cnil ],
					 OPT - ] > ] ],
		   CONT nom-obj &
		       [ TOP #top,
			 INDEX #ind & ref-ind &
			       [ PNG png & [ PN 3sg* ] ],
			 LISZT <! #key !> ],
		   KEYS [ KEY #key & hour_prep_rel &
				     [ INST #ind,
				       MINUTE-IND #minute,
				       HOUR-IND #hour ],
			  --COMPKEY #ckey ] ] ] ].

; ten pm to two am
; Tuesday to Friday
n_x_to_y_le := norm_msg_word &
  [ SYNSEM basic_two_arg &
	 [ LOCAL [ CAT [ HEAD noun* &
			      [ POSS -,
				MOD < > ],
			 VAL [ SUBJ < >,
                               SPR < synsem &
				     [ LOCAL [ CAT nomp_cat_acc_min,
					       CONJ cnil,
					       CONT.INDEX #arg1,
					       KEYS [ KEY temp_abstr_rel,
						      MESSAGE 0-dlist ] ],
				       OPT - ] >,
			       COMPS < synsem &
				       [ LOCAL [ CAT nomp_cat_acc_min,
						 CONJ cnil,
						 CONT.INDEX #arg2,
						 KEYS [ KEY temp_abstr_rel,
							MESSAGE 0-dlist ] ],
					 OPT - ] > ] ],
		   CONT nom-obj &
		       [ INDEX #index,
			 LISZT <! temp_rel & #key &
                                [ HANDEL #nhand,
                                  LABEL #label,
                                  INST #index ],
                                  udef_rel &
                                [ BV #index,
                                  RESTR #rhand ],
                                _from_rel &
				[ HANDEL #nhand,
                                  LABEL #label,
                                  EVENT.E.TENSE no_tense,
                                  ARG #index,
				  ARG3 #arg1 ],
				_until_rel &
				[ HANDEL #nhand,
                                  LABEL #label,
                                  EVENT.E.TENSE no_tense,
				  ARG #index,
				  ARG3 #arg2 ] !>,
			 H-CONS <! qeq & [ SC-ARG #rhand,
                                           OUTSCPD #nhand ] !> ],
		   KEYS.KEY #key ] ] ].

p_compar_than_le := norm_mod_nonque_no_affix_word &
  [ SYNSEM compar_adj_prep_synsem ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; ADVERBS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Adverbs modify VPs and APs either pre-head or post-head (or both).
; Sentence-initial adverbials are treated as extractions of post-VP modifiers,
; unless the adverbial is a pre-head modifier. Otherwise, it would be hard to
; block post-S attachment along with post-VP attachment, which would give 
; spurious ambiguity.

basic_adverb_synsem := basic_one_arg & nonprd_synsem &
  [ LOCAL [ CAT [ HEAD basic_adv &
		       [ MOD < [ LOCAL local_basic &
				       [ CAT.MC #mc,
                                         CONT.INDEX #vevent & individual,
					 KEYS.MESSAGE #msg,
					 CONJ cnil ] ] > ],
		  VAL [ SUBJ < >,
                        SPR #spr,
			COMPS < > ],
                  MC #mc ],
	    CONT [ INDEX #vevent,
		   LISZT.LIST < #key, ... > ],
	    KEYS [ KEY #key & abstr_adv_rel,
		   MESSAGE #msg ],
            ARG-S #spr ] ].

basic_scopal_adverb_synsem := basic_adverb_synsem &
  [ LOCAL [ CAT.HEAD.MOD < [ LOCAL scopal_mod &
				   [ CAT.HEAD adverbee & strict_type,
				     CONT.TOP #mhand ] ] >,
	    CONT.H-CONS.LIST < qeq &
				 [ SC-ARG #hand,
				   OUTSCPD #mhand ], ... >,
	    KEYS.KEY.ARG #hand & handle ] ].

basic_int_adverb_synsem := basic_adverb_synsem & isect_synsem &
  [ LOCAL [ CAT.HEAD.MOD < [ LOCAL [ CAT.MC na,
                                     CONT.INDEX #vevent ] ] >,	
            CONT.INDEX #vevent,
	    KEYS.KEY.ARG #vevent ] ].

;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing

spec_adverb_synsem := basic_adverb_synsem &
  [ LOCAL [ CAT.VAL.SPR < synsem & 
			  [ LOCAL local_min &
				  [ CAT [ HEAD adv,
					  VAL [ SUBJ < >,
						SPR *olist* ],
					  MC na ],
                                    CONT.TOP #hand,
				    KEYS.KEY degree_rel &
					[ DARG #darg ] ] ] >,
            KEYS.KEY [ HANDEL #hand,
		       DIM #darg ] ] ].

scopal_spec_adverb_synsem := basic_scopal_adverb_synsem & spec_adverb_synsem &
  [ LOCAL.CAT.HEAD adv ].
intersect_spec_adverb_synsem := basic_int_adverb_synsem & spec_adverb_synsem.

; VP, PP, AP
scopal_adverb_synsem := scopal_spec_adverb_synsem &
  [ LOCAL [ CAT.HEAD.MOD < [ LOCAL.CAT [ VAL [ SUBJ < synsem_min >,
					       COMPS *olist* ],
                                         MC na_or_- ] ] >,
	    CONT [ LISZT <! relation !>,
		   H-CONS <! qeq !> ] ] ].

basic_intersect_adverb_synsem := intersect_spec_adverb_synsem &
  [ LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT [ VAL [ SUBJ < synsem >,
					     COMPS *olist* ],
				       MC na_or_- ] ] > ].

intersect_adverb_synsem := basic_intersect_adverb_synsem &
  [ LOCAL [ CAT.HEAD adv & [ MOD < [ LOCAL.CAT adverbee_cat ] > ],
	    CONT [ LISZT <! relation !>,
                   H-CONS <! !> ] ] ].

; 'now' but not 'then' - appears after auxiliary as in "You are now ready"
intersect_adverb_auxv_synsem := basic_intersect_adverb_synsem &
  [ LOCAL.CAT [ HEAD lexadv & [ MOD < [ LOCAL.CAT adverbee_cat &
                                       [ HEAD v_or_g & strict_type ] ] > ],
                POSTHEAD - ] ].

; VP only
; Block adj modifiee to avoid spurious parse for "almost empty"
; DPF 09-Aug-99 - Relaxed MOD..HEAD value to be v_or_g_or_a because okay for 
; adjectives as pre-head modifiers ("an apparently difficult problem"), though
; not post-head.

scopal_vp_adverb_synsem := scopal_adverb_synsem &
  [ LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT.HEAD v_or_g_or_a & strict_type ] > ].

; Adverbs like "also" that either (pre) modify VPs or appear as complements of
; aux Vs.
; DPF 24-May-00 - Restrict MOD value to be no_aspect, to avoid spurious
; ambiguity for "kim is probably leaving"
; DPF 14-Oct-00 - But this prevents the desired parse for "Kim is not currently
; sleeping" since "not" should modify a pres-participle VP, not the gerund.  So
; as long as the gerund rule is a lexical rule, we're stuck with the ambiguity.

scopal_vp_aux_adverb_synsem := basic_scopal_adverb_synsem & spec_adverb_synsem &
  [ LOCAL [ CAT [ HEAD lexadv &
		       [ MOD < [ LOCAL.CAT [ HEAD v_or_g & strict_type,
					     VAL [ SUBJ < synsem >,
						   COMPS *olist* ],
					     MC na ] ] > ],
		  POSTHEAD - ],
	    CONT [ LISZT <! relation !>,
		   H-CONS <! qeq !> ] ] ].

comp_vp_adverb_synsem := basic_intersect_adverb_synsem &
  [ LOCAL [ CAT [ HEAD adv & [ MOD < [ LOCAL.CAT adverbee_cat ] > ],
		  POSTHEAD + ],
	    CONT [ LISZT <! #key &
			  [ HANDEL #hand,
			    ARG #ind ],
			  comp_rel &
			  [ HANDEL #hand,
			    ARG #ind ] !>,
                   H-CONS <! !> ],
	    KEYS.KEY #key ] ].

intersect_vp_adverb_synsem := intersect_adverb_synsem &
  [ LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT.HEAD v_or_g & strict_type ] > ].

; S (for pre-S scopal adverbs only)
;
; DPF 26-Jul-99 - Made MOD..HEAD be 'verb' instead of 'adverbee' to block
; "so then" with "then" as discourse adverb.

scopal_s_adverb_synsem := scopal_spec_adverb_synsem &
  [ LOCAL [ CAT.HEAD.MOD < [ LOCAL [ CAT [ HEAD verb & strict_type,
                                           VAL [ SUBJ *olist*,
                                                 COMPS *olist* ],
                                           MC bool ],
                                     KEYS.MESSAGE 1-dlist ] ] >,
	    CONT [ TOP #top,
		   LISZT <! relation, prpstn_rel &
			              [ HANDEL #top,
					SOA #soahand ] !>,
		   H-CONS <! qeq,
			     qeq &
			   [ SC-ARG #soahand,
			     OUTSCPD #khand ] !> ],
	    KEYS.KEY.HANDEL #khand ] ].

; S or VP (for pre-head intersective adverbs only) 
intersect_s_or_vp_adverb_synsem := intersect_spec_adverb_synsem &
  [ LOCAL [ CAT.HEAD adv &
                     [ MOD < [ LOCAL.CAT adverbee_cat &
                                         [ VAL.COMPS *olist* ] ] > ],
	    CONT [ LISZT <! relation !>,
                   H-CONS <! !> ] ] ].

; For adverbs like "maybe" which take no degree specifier
;
; DPF 26-Jul-99 - Made MOD..HEAD be 'verb' instead of 'adverbee' to block
; "so then" with "then" as discourse adverb.
scopal_s_adverb_nospec_synsem := basic_scopal_adverb_synsem &
  [ LOCAL [ CAT [ VAL.SPR < anti_synsem >,
		  HEAD adv &
			  [ MOD < [ LOCAL.CAT [ HEAD verb & strict_type,
						VAL [ SUBJ *olist*,
						      COMPS *olist* ],
						MC bool ] ] > ] ],
	    CONT [ TOP #top,
		   LISZT <! relation, prpstn_rel &
			              [ HANDEL #top,
					SOA #soahand ] !>,
		   H-CONS <! qeq,
			     qeq &
			   [ SC-ARG #soahand,
			     OUTSCPD #khand ] !> ],
	    KEYS.KEY.HANDEL #khand ] ].

intersect_s_or_vp_adverb_nospec_synsem := basic_int_adverb_synsem &
  [ LOCAL [ CAT [ VAL.SPR < anti_synsem >,
		  HEAD adv &
                       [ MOD < [ LOCAL.CAT adverbee_cat &
					 [ VAL.COMPS *olist* ] ] > ] ],
	    CONT [ LISZT <! relation !>,
                   H-CONS <! !> ] ] ].

adverb_word := norm_mod_no_affix_word &
  [ SYNSEM scopal_adverb_synsem & 
	   [ LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT.HEAD v_or_g_or_a & 
						   strict_type ] > ] ].
    
adv_le :< adverb_word.

; 'back' of 'back on the table'
adv_int_pp_le := norm_mod_no_affix_word &
  [ SYNSEM basic_intersect_adverb_synsem &
	   [ LOCAL [ CAT [ HEAD adv &
                                [ MOD < [ LOCAL.CAT cat &
                                           [ HEAD prep & strict_type ] ] >],
			   POSTHEAD - ],
		     CONT [ LISZT <! relation !>,
                            H-CONS <! !> ] ] ] ].

; DPF (27-Apr-99) Assume for now that S-initial adverbs can only modify [ INV - ]
; S's, in order to block "*Never does kim sleep?"
adv_s_pre_le := norm_mod_no_affix_notopkey_word &
  [ SYNSEM scopal_s_adverb_synsem &
	   [ LOCAL.CAT [ POSTHEAD -,
			 HEAD.MOD < [ LOCAL.CAT.HEAD.INV - ] > ] ] ].
    
adv_s_post_word_nospec_le := norm_mod_no_affix_notopkey_word &
  [ SYNSEM scopal_s_adverb_nospec_synsem &
	   [ LOCAL.CAT.POSTHEAD + ] ].

adv_s_pre_word_nospec_le := norm_mod_no_affix_notopkey_word &
  [ SYNSEM scopal_s_adverb_nospec_synsem &
	   [ LOCAL.CAT.POSTHEAD - ] ].
    
adv_s_le := norm_mod_no_affix_notopkey_word &
  [ SYNSEM scopal_s_adverb_synsem &
	   [ LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT.HEAD.INV - ] > ] ].

adv_pre_le := adverb_word & 
  [ SYNSEM.LOCAL.CAT.POSTHEAD - ].

adv_post_le := adverb_word & 
  [ SYNSEM.LOCAL.CAT.POSTHEAD + ].

vp_adverb_word := norm_mod_no_affix_word &
  [ SYNSEM scopal_vp_adverb_synsem ].
    
adv_vp_le := vp_adverb_word &
  [ SYNSEM.LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT.HEAD verb ] > ].

; 'please'
adv_vp_bse_le := vp_adverb_word &
  [ SYNSEM.LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT.HEAD verb & 
                                                 [ VFORM imp_vform ] ] > ].

adv_comp_vp_le := norm_mod_no_affix_word &
  [ SYNSEM comp_vp_adverb_synsem ].

adv_vp_aux_le := norm_mod_no_affix_word &
  [ SYNSEM scopal_vp_aux_adverb_synsem ].
    
; DPF 7-Jun-00 - To avoid apparently spurious ambiguity of attachment of
; VP-final scopal adverbs in e.g. "I have won too", require these adverbs to
; attach only to finite VP, forcing high attachment.

adv_vp_post_le := vp_adverb_word & 
  [ SYNSEM.LOCAL.CAT [ HEAD.MOD < [ LOCAL.CAT.HEAD v_or_g & [ VFORM fin ] ] >,
                       POSTHEAD + ] ].

adv_vp_pre_le := vp_adverb_word & 
  [ SYNSEM.LOCAL.CAT.POSTHEAD - ].

int_vp_adverb_word := norm_mod_no_affix_word &
  [ SYNSEM intersect_vp_adverb_synsem ].
    
int_vp_aux_adverb_word := norm_mod_no_affix_word &
  [ SYNSEM intersect_adverb_auxv_synsem ].
    
adv_int_vp_le := int_vp_adverb_word.

basic_adv_int_vp_aux_le := int_vp_aux_adverb_word.

adv_int_vp_aux_le := int_vp_aux_adverb_word &
  [ SYNSEM.LOCAL [ CAT.HEAD.MOD < [ LOCAL.CONT.TOP #top ] >,
                   CONT [ TOP #top,
                          LISZT <! #key !>,
                          H-CONS <! !> ],
                   KEYS.KEY #key ] ].

adv_int_vp_post_le := int_vp_adverb_word & 
  [ SYNSEM.LOCAL.CAT.POSTHEAD + ].

; ERB (31-10-96) See notes near n_wh_pro_le.
; where, when, how
pp_wh_le := nonconj & non_affix_bearing & mcna & nonslash & nonrel & 
		  topkey &
  [ SYNSEM prep_p_mod_synsem &
      [ LOCAL [ CAT [ HEAD prep* &
			   [ PRD bool,
			     MOD < [ LOCAL.CAT.HEAD v_or_g_or_a & 
						    strict_type ] > ],
		      VAL.COMPS < >,
		      POSTHEAD + ],
		CONT [ LISZT <! #key & [ ARG3 #objind,
                                         LABEL #label ],
			        which_rel & [ BV #objind,
					      RESTR #rhand ],
			        nom_rel & #altkey &
			                  [ HANDEL #nhand,
					    INST #objind,
                                            LABEL #label ] !>,
		       H-CONS <! qeq &
			       [ SC-ARG #rhand,
				 OUTSCPD #nhand ] !> ],
		KEYS [ KEY #key,
		       ALTKEY #altkey ] ],
	NON-LOCAL.QUE 1-dlist &
	      [ LIST < handle > ] ] ].
 
;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing						   
pp_rel_le := rel_word &
  [ SYNSEM basic_lex_pp_synsem &
      [ LOCAL [ CAT [ HEAD prep* & 
			   [ PRD +,
			     MOD < [ LOCAL [ CAT vp_cat &
						 [ HEAD verb ],
					     CONT.INDEX #event & event,
					     CONJ cnil,
					     KEYS.MESSAGE #msg ] ] > ],
		      VAL.SPR < synsem & [ LOCAL local_min &
					   [ CAT [ HEAD adv,
						   VAL [ SPR *olist*,
							 COMPS *olist* ] ],
                                             CONT.TOP #khand,
					     KEYS.KEY degree_rel &
						 [ DARG #darg ] ],
				     NON-LOCAL.QUE 0-dlist,
				     OPT + ] >,
		      POSTHEAD + ],
		CONT [ INDEX #event,
		       LISZT <! #key & [ ARG #event,
					 ARG3 #objind ],
			        def_np_rel & [ BV #objind,
					       RESTR #rhand ],
			        nom_rel & #altkey &
			                  [ HANDEL #nhand,
					    INST #objind ] !>,
		       H-CONS <! qeq &
			       [ SC-ARG #rhand,
				 OUTSCPD #nhand ] !> ],
		KEYS [ KEY #key & [ HANDEL #khand,
				    DIM #darg ],
		       ALTKEY #altkey,
		       MESSAGE #msg ] ],
	NON-LOCAL.REL 1-dlist & [ LIST < [ TOP #nhand,
					   INDEX #objind ] > ] ] ].

; 'on vacation', 'in bed'

prep_p_mod_phr_synsem := prep_p_mod_synsem & phr_synsem.
prep_nomod_phr_synsem := prep_p_synsem & phr_synsem &
  [ LOCAL.CAT.HEAD.MOD < > ].

pp_idiom_le := msg_amalg_word &
  [ SYNSEM prep_p_mod_phr_synsem &
	   [ LOCAL [ CAT [ HEAD prep*,
                           VAL [ SUBJ < [ NON-LOCAL [ SLASH 0-dlist,
                                                      REL 0-dlist,
                                                      QUE 0-dlist ] ] >,
                                 COMPS < > ] ],
                     KEYS [ KEY #key,
                            ALTKEY #altkey ],
                     CONT [ LISZT <! #key & [ ARG3 #inst ],
                                     #altkey & 
                                   [ HANDEL #nhand,
                                     INST #inst & ref-ind &
                                            [ PNG png & [ PN 3sg,
                                                          GEN neut ] ] ],
                                   udef_rel &
                                   [ BV #inst,
                                     RESTR #rhand ] !>,
                            H-CONS <! qeq &  [ SC-ARG #rhand,
                                               OUTSCPD #nhand ] !> ] ],
             NON-LOCAL [ SLASH 0-dlist,
                         REL 0-dlist,
                         QUE 0-dlist ] ] ].

pp_as_adj_le := msg_amalg_word &
  [ SYNSEM prep_nomod_phr_synsem &
	   [ LOCAL [ CAT [ HEAD prep*,
                           VAL [ SUBJ < [ NON-LOCAL [ SLASH 0-dlist,
                                                      REL 0-dlist,
                                                      QUE 0-dlist ] ] >,
                                 COMPS < > ] ],
                     KEYS [ KEY _as_comp_rel & [ ARG3 #pinst ],
                            ALTKEY #altkey ],
                     CONT [ LISZT <! #altkey & 
                                   [ ARG #pinst ],
                                   def_rel & [ RESTR #rhand,
                                               BV #pinst ],
                                   pron_rel & [ HANDEL #prohand,
                                                INST #pinst ] !>,
                            H-CONS <! qeq & [ SC-ARG #rhand,
                                              OUTSCPD #prohand ] !> ] ],
             NON-LOCAL [ SLASH 0-dlist,
                         REL 0-dlist,
                         QUE 0-dlist ] ] ].


adv_comparison_spec_le := norm_no_affix_word &
  [ SYNSEM nomod_synsem &
	  [ LOCAL [ CAT [ HEAD adv,
			  VAL [ COMPS < >,
				SPR < > ] ],
		    CONT [ TOP #hand,
			   INDEX #inst,
			   LISZT <! #key &
				    [ HANDEL #hand,
				      C-INST #inst ] !>,
                           H-CONS <! !> ],
		    KEYS.KEY #key ] ] ].
			   

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; DETERMINERS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Determiners must have the KEY relation be the quantifier, since nouns
;; select their determiner in part based on the type of the quantifier rel.
;; The basic determiner synsem has a non-null value for its specifier; this is 
;; to account for cases like "nearly all books, only his book, exactly which
;; book, almost every book".  
;;
;; Quantifiers need to accommodate expressions like
;; 'almost every book'.  One might expect to have the degree_rel of 'almost'
;; take the handle of 'every' as its ARG value, but this may interfere with the
;; treatment of scope which needs to identify the set of quantifier handles.
;; So we do this by adding separate lexical entries for these det-specs.

;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing

basic_det_synsem := nomod_synsem &
  [ LOCAL [ CAT [ HEAD det,
		  VAL [ SPR < [ LOCAL local_min &
				      [ CONT.TOP #khand,
                                        KEYS.KEY degree_rel & 
						     [ DARG #darg ] ],
				NON-LOCAL [ QUE 0-dlist,
					    REL 0-dlist ],
				OPT + ] >, 
			SPEC < [ LOCAL.CONT [ TOP #nhand,
					      INDEX #index ] ] >,
			COMPS < > ] ],
	    CONT nom-obj &
		[ INDEX #index,
		  LISZT.LIST < quant_or_wh_rel & #key, ... >,
		  H-CONS.LIST < qeq &
				[ SC-ARG #rhand,
				  OUTSCPD #nhand ], ... > ],
	    KEYS.KEY #key & [ HANDEL #khand,
			      BV #index,
			      RESTR #rhand,
			      DIM #darg ],
	    ARG-S < > ] ].

nonpart_det_synsem :< basic_det_synsem.

det_synsem := nonpart_det_synsem &
  [ LOCAL [ CONT [ LISZT <! quant_or_wh_rel !>,
                   H-CONS <! qeq !> ],
            KEYS.ALTKEY no_rel ] ].

part_det_synsem :< basic_det_synsem.

part_det_synsem_nonwh := part_det_synsem & 
  [ LOCAL.CONT.H-CONS <! qeq !>,
    NON-LOCAL.QUE 0-dlist ].

next_last_det_synsem := nonpart_det_synsem &
  [ LOCAL [ CAT.VAL.SPEC < [ LOCAL [ CONT.TOP #nhand,
				     KEYS.KEY temp_abstr_rel ],
                             MODIFIED notmod ] >,
	    CONT [ LISZT <! def_rel &
			    [ BV #ind & [ PNG png & [ PN 3sg ],
					  DIVISIBLE -* ] ],
			    relation & #altkey &
			    [ HANDEL #nhand,
			      ARG #ind ] !>,
		   H-CONS <! qeq !> ],
	    KEYS.ALTKEY #altkey ] ].

det_word := que_word &
  [ SYNSEM det_synsem & [ LOCAL.CAT.HEAD.POSS - ] ].

det_word_sing := det_word &
  [ SYNSEM.LOCAL.KEYS.KEY.BV [ PNG png & [ PN 3sg ],
			       DIVISIBLE -* ] ].

det_word_nonque := det_word & nonque.

det_pl_le := det_word_nonque &
  [ SYNSEM [ LOCAL.KEYS.KEY.BV.PNG png & [ PN 3pl ],
	     MODIFIED hasmod ] ].

det_sm_le := det_word_nonque &
  [ SYNSEM [ LOCAL.KEYS.KEY.BV.PNG png & [ PN 3sg ],
	     MODIFIED hasmod ] ].

det_plm_le := det_word &
  [ SYNSEM [ LOCAL.KEYS.KEY.BV.DIVISIBLE +*,
	     MODIFIED hasmod ] ].

det_next_last_le := que_word & nonque &
  [ SYNSEM next_last_det_synsem & 
	   [ LOCAL.CAT.HEAD.POSS -,
	     MODIFIED hasmod ] ].  

; DPF 20-Feb-01 - Use ALTKEY to block det-N analysis of "both kim and sandy"
pdet_word := que_word &
  [ SYNSEM part_det_synsem_nonwh &
	   [ LOCAL [ CAT.VAL.SPEC < [ LOCAL.KEYS.ALTKEY basic_nom_rel ] >,
                     CONT.LISZT <! quant_or_wh_rel !> ],
	     MODIFIED hasmod ] ].

pdet_word_nonque := pdet_word & nonque.

det_part_le :< pdet_word_nonque.

det_part_sg_le := pdet_word_nonque &
  [ SYNSEM.LOCAL.KEYS.KEY.BV [ PNG png & [ PN 3sg ],
                               DIVISIBLE -* ] ].

det_part_ms_le := pdet_word_nonque &
  [ SYNSEM.LOCAL.KEYS.KEY.BV [ PNG png & [ PN 3sg ],
                               DIVISIBLE +* ] ].

det_part_pl_le := pdet_word_nonque &
  [ SYNSEM.LOCAL.KEYS.KEY.BV.PNG png & [ PN 3pl ] ].

; This assumes (falsely) that all plurals are divisible (cf "oats", "clothes")
; It also incorrectly blocks coordinations like "Kim and all children" since 
; the alternative (DIVISIBLE +*) would fail to block "all book".
det_part_pl_mass_le := pdet_word_nonque &
  [ SYNSEM [ LOCAL.KEYS.KEY.BV.DIVISIBLE +,
             MODIFIED hasmod ] ].

det_part_unsp_le := que_word &
  [ SYNSEM part_det_synsem & 
	   [ LOCAL [ CAT.HEAD.POSS -,
		     CONT [ LISZT <! quant_or_wh_rel !>,
			    H-CONS <! qeq !> ] ],
	     NON-LOCAL.QUE 1-dlist &
		           [ LIST < handle > ] ] ].

pdet_word_pl_mass_wh := que_word &
  [ SYNSEM part_det_synsem & 
	   [ LOCAL [ CAT.HEAD.POSS -,
		     CONT [ INDEX #mainind,
                            LISZT <! udef_rel,
				 which_rel & [ BV #index,
					       RESTR #rhand ],
				 diadic_nom_rel & #altkey &
				   [ HANDEL #nhand,
				     INST #index ],
                                 unspec_rel &
                                   [ HANDEL #rhand, 
                                     HINST #mainind,
                                     NHINST #index ] !>,
			    H-CONS <! qeq,
				      qeq &
				      [ SC-ARG #rhand,
					OUTSCPD #nhand ] !> ],
		     KEYS.ALTKEY #altkey ],
             NON-LOCAL.QUE 1-dlist &
		           [ LIST < handle > ] ] ].

det_part_ms_wh_le := pdet_word_pl_mass_wh &
  [ SYNSEM.LOCAL.KEYS [ KEY.BV [ PNG png & [ PN 3sg ],
				 DIVISIBLE +* ],
			ALTKEY quantity_rel ] ].

det_part_pl_wh_le := pdet_word_pl_mass_wh &
  [ SYNSEM.LOCAL.KEYS [ KEY.BV.PNG png & [ PN 3pl ],
			ALTKEY number_rel ] ].

det_part_one_le := que_word &
  [ SYNSEM part_det_synsem_nonwh &
	   [ LOCAL [ CAT.VAL.SPEC < [ LOCAL.CONT.TOP #nomhand ] >,
		     CONT.LISZT <! udef_rel &
				   [ BV #ind & 
					[ PNG png & [ PN 3sg ],
					  DIVISIBLE -* ] ],
				   const_rel &
				   [ HANDEL #nomhand,
				     ARG #ind,
				     CONST_VALUE "1" ] !> ] ] ].


;; DPF 10-May-00 - We add subtypes of determiners to distinguish "We met every week"
;; from "*We met the week".  The distinction makes use of the feature MODIFIED,
;; where the head-specifier rule's mother's MODIFIED value is determined by the
;; specifier daughter, so each specifier has to say whether it passes on the 
;; MODIFIED value from its head, or supplies its own.  Thus "the" just passes up
;; the value from the head, "every" supplies its own 'hasmod' value, and "a"
;; supplies the opposite value "notmod", to block "*Kim arrived a big week".

; 'the'
det_le := det_word_nonque &
  [ SYNSEM [ LOCAL.CAT.VAL.SPEC < [ MODIFIED #modif ] >,
	     MODIFIED #modif ] ].

; 'half the time'
det_word_modable := det_word &
  [ SYNSEM.MODIFIED hasmod ].

det_wh_le :< det_word_modable.
det_modable_le := det_word_modable & nonque.

det_sing_nonque := det_word_sing & nonque.

; 'an'
det_sg_nomod_le := det_sing_nonque &
  [ SYNSEM [ MODIFIED notmod,
             LOCAL.CAT.VAL.SPR < anti_synsem > ] ].

; 'every'
det_sg_le := det_sing_nonque &
  [ SYNSEM.MODIFIED hasmod ].

;; We make the non-quantifier relation generically pron_rel, and have each
;; individual pronoun simply constrain the INST index of that pron_rel.
;;   This type is used for both WH "whose" and relative "whose", so it leaves 
;; both  QUE  and  REL  values unspecified, but supplies all other defaults.

poss_word := nonconj & non_affix_bearing & nonslash & nonmsg & 
	     mcna &
  [ SYNSEM.LOCAL [ CAT.HEAD.POSS +,
		   CONT nom-obj &
			[ INDEX #index,
			  LISZT.LIST < relation,
				       pro_poss_rel &
				       [ ARG #index ], ... > ] ] ].

; ERB (31-10-96) See notes near n_wh_pro_le.

det_wh_poss_le := poss_word & nonrel &
  [ SYNSEM nonpart_det_synsem &
	   [ LOCAL [ CAT.VAL.SPEC < [ LOCAL.CONT.TOP #nhand ] >,
		     CONT [ LISZT <! which_rel & #key,
				     relation & [ HANDEL #nhand,
						  ARG3 #ind ],
				     person_rel & [ HANDEL #phand,
						    INST #ind ],
				     def_rel & [ BV #ind,
                                                 RESTR #rhand2 ] !>,
			    H-CONS <! qeq, 
				      qeq &
				      [ SC-ARG #rhand2,
					OUTSCPD #phand ] !> ],
		     KEYS.KEY #key ],
	     NON-LOCAL.QUE 1-dlist &
			   [ LIST < handle > ] ] ].

nonwh_poss_word := poss_word & nonque &
  [ SYNSEM nonpart_det_synsem &
	   [ LOCAL [ CONT [ LISZT.LIST < relation, #altkey, ... >,
			    H-CONS.LIST < qeq, ... > ],
                     KEYS [ KEY abstr_def_rel,
			    ALTKEY #altkey ] ] ] ].

det_poss_le := nonwh_poss_word & nonrel &
  [ SYNSEM.LOCAL [ CAT.VAL.SPEC < [ LOCAL.CONT.TOP #hand ] >,
		   CONT [ LISZT <! relation & [ LABEL #label ],
				   [ HANDEL #hand,
				     ARG3 #ind ],
                                   def_rel & 
                                   [ RESTR #rhand,
                                     BV #ind ],
				   pron_rel &
				   [ HANDEL #prohand,
                                     LABEL #label,
				     INST #ind &
					  [ PRONTYPE std_pron ] ] !>,
                          H-CONS <! qeq & [ SC-ARG #rhand,
                                            OUTSCPD #prohand ] !> ] ] ].

det_rel_poss_le := nonwh_poss_word &
  [ SYNSEM [ LOCAL.CONT [ LISZT <! relation,
                                 [ HANDEL #hand,
                                   ARG3 #ind ] !>,
                          H-CONS <! qeq !> ],
	     NON-LOCAL.REL 1-dlist &
			   [ LIST < [ TOP #hand,
				      INDEX #ind ] > ] ] ].


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Free relatives
;
; One crucial property of free relatives is that they have a non-empty QUE
; value whose single list element is an index (rather than the handle which
; is employed for ordinary question WH-words).
; The other is that they encode the constraints for their S/XP sister in the
; free-relative construction, in their SLASH attribute.

free_rel_pro_word := nonconj & non_affix_bearing & mcna & nonrel & nonmsg &
		     hc-to-phr &
  [ SYNSEM synsem &
	   [ LOCAL [ CAT.VAL [ SPR < >,
			       SUBJ < >,
			       COMPS < > ],
		     AGR #ind,
		     CONT [ INDEX ref-ind & #ind & [ PNG.PN 3sg ],
			    LISZT.LIST < free_relative_ever_rel &
					 [ BV #ind,
					   RESTR #rhand ], 
					 [ HANDEL #nhand,
					   INST #ind ], ... >,
			    H-CONS <! qeq &
				    [ SC-ARG #rhand,
				      OUTSCPD #nhand ] !> ],
		     ARG-S < > ],
	     NON-LOCAL [ REL 0-dlist,
			 QUE 1-dlist & <! #ind !>,
			 SLASH 1-dlist &
                               <! [ CAT.VAL.COMPS < >,
                                    CONJ cnil ] !> ] ] ].

; whatever, whoever
n_freerel_pro_le := free_rel_pro_word &
  [ SYNSEM [ LOCAL [ CAT.HEAD noun* &
			      [ MOD < >,
				PRD - ],
		     CONT.LISZT <! relation & #altkey, #key !>,
		     KEYS [ KEY #key,
			    ALTKEY #altkey ] ],
	     NON-LOCAL.SLASH 1-dlist &
                   <! [ CAT.HEAD noun & [ MOD < > ],
                        AGR ref-ind & [ PNG.PN 3sg ] ] !> ] ].

; when, where
n_freerel_pro_adv_le := free_rel_pro_word &
  [ SYNSEM [ LOCAL [ CAT.HEAD noun* &
			      [ MOD < >,
                                PRD - ],
		     CONT.LISZT <! relation, #key & [ HANDEL #hand,
                                                      INST #ind ],
                                   loc_rel &
                                   [ HANDEL #hand,
                                     ARG #arg,
                                     ARG3 #ind ] !>,
		     KEYS [ KEY #key,
			    ALTKEY #key ] ],
	     NON-LOCAL.SLASH 1-dlist &
                   <! [ CAT.HEAD prep & 
                                 [ MOD < synsem &
                                         [ LOCAL.CONT.INDEX #arg ] > ] ] !>] ].

free_rel_det_word_gen := nonconj & non_affix_bearing & mcna & nonrel & nonmsg &
		     hc-to-phr &
  [ SYNSEM basic_det_synsem & 
	   [ LOCAL [ CAT [ HEAD.POSS -,
			   VAL.SPEC < [ LOCAL.CONT.TOP #althand ] > ],
		     CONT [ LISZT <! free_relative_ever_rel !>,
			    H-CONS <! qeq !> ],
		     KEYS.ALTKEY.HANDEL #althand  ],
	     NON-LOCAL [ QUE 1-dlist &
			     [ LIST < index > ],
			 SLASH 1-dlist &
			     <! [ CAT.HEAD n_or_p ] !> ] ] ].

; whatever, what
det_freerel_le := free_rel_det_word_gen &
  [ SYNSEM det_synsem & 
           [ LOCAL.CAT.HEAD.POSS -,
             NON-LOCAL.SLASH <! [ CAT.HEAD.CASE acc ] !> ] ].

; whichever
n_freerel_part_le := free_rel_det_word_gen &
  [ SYNSEM part_det_synsem ].


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Possessives
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Possessive "'s" takes an NP[-Poss] specifier and projects either an
;; NP[+Poss] phrase (for "the book of Kim's") or a DetP[+Poss] phrase (for
;; "Kim's book").  Either way, the content of the possessive phrase will be a
;; definite possessive quantifier.  The NP[+Poss] things also work pretty well
;; for things like "Kim's is under the desk" if we allow governed NPs to be
;; +Poss.  The tricky thing will be figuring out exactly where `bare
;; possessives' can show up.  Also, we need to add the restriction that the SPR
;; must be non-pronominal.  We cannot simply make the synsem of the SPR be
;; the type nonpronominal, since that NP might be either lexical or phrasal,
;; distinguished by the synsem types lex_synsem and phr_synsem.  So we use
;; the semantic KEY relation to identify nonpronominals.

;; Another question is what do we do with the scope
;; of quantifiers in the possessor?  This entry assumes that they always have
;; narrow scope.  That should be an empirical question, as the saying goes.

n_poss_clitic_le := mcna & nonconj & nonmsg & non_affix_bearing &
  [ SYNSEM nomod_synsem &
     [ LOCAL [ ARG-S < #spr >,
	       CAT [ HEAD poss-able &
			  [ POSS + ],
		     VAL [ SPR < #spr & synsem &
				 [ LOCAL [ CAT nomp_cat_min,
					   CONJ cnil,
					   KEYS.KEY nonpro_rel,
					   CONT.INDEX #pindex & individual ],
				   NON-LOCAL [ SLASH #slash,
					       REL #rel,
					       QUE #que ],
				   OPT - ] >,
			   COMPS < >,
			   SPEC < [ LOCAL.CONT [ TOP #phand,
						 INDEX #index ] ] > ] ],
	       CONT [ INDEX #index,
		      LISZT <! #key & def_rel & [ BV #index,
                                                  RESTR #rhand ],
			       #altkey & poss_rel & [ HANDEL #phand,
                                                      ARG3 #pindex,
                                                      ARG #index ] !>,
		      H-CONS <! qeq & [ SC-ARG #rhand,
					OUTSCPD #phand ] !> ],
	       KEYS [ KEY #key,
		      ALTKEY #altkey ] ],
       NON-LOCAL [ SLASH #slash,
		   REL #rel,
		   QUE #que ] ] ].

;; The possessive "of" takes an NP[+Poss] complement (e.g., "Kim's", "mine")
;; and modifies an N'[-Poss].  Unfortunately, the way the hierarchy of
;; preposition types is built, there wasn't any type this could inherit from.
;; We could probably rearrange things so this inherits some preposition
;; properties, but right now I'm too lazy.  Also, somehow we'll have to make
;; sure that this doesn't modify a pronoun.  The semantic manipulation here is
;; tricky - keep your eyes on the definite quantifier.  This def_rel provided
;; by the apostrophe-s (for its other life as a determiner), is dropped by the
;; possessive-of, analogous to nouns, verbs, and adjectives dropping the
;; semantics of the prepositions that can mark their complements.  This
;; dropping of the relation is effected by having the possessive-of neglect to
;; grab the handle of the def_rel for its complement, but grab instead the
;; handle (and instance variable) of the NP complement of the apostrophe-s.
;; Then the general semantic pruning mechanism will ignore the def_rel, since
;; its handle will not be a value in any other relation.

one_arg_isect := one_arg & isect_synsem.

;; DPF 10-Oct-00 - COMPS.KEYS.ALTKEY needs fixing - may be tricky since we
;; need to get to the n-bar's handle - not supposed to be visible at NP.
;; FIX??

p_poss_of_le := nonque & nonconj & nonslash & nonrel & mcna & non_affix_bearing &
	      hc-to-phr &
  [ SYNSEM one_arg_isect &
     [ LOCAL 
       [ ARG-S #comps,
	 CAT [ HEAD prep* & 
		    [ PRD -,
		      MOD < [ LOCAL [ CAT nbar_cat,
				      CONT [ TOP #hand,
					     INDEX #index ],
				      CONJ cnil,
				      KEYS.MESSAGE #msg ] ] > ],
	       VAL [ SUBJ < >,
		     SPR < >,
		     COMPS #comps & < synsem &
				      [ LOCAL [ CAT [ HEAD noun & strict_type &
							   [ POSS + ],
						      VAL [ SPR *olist*,
							    COMPS *olist* ],
						      MC na_or_- ],
						CONJ cnil,
						KEYS [ ALTKEY [ HANDEL #hand,
								ARG #index ]]],
					OPT - ] > ],
	       POSTHEAD + ],
	 CONT [ TOP #hand,
		INDEX #index,
		LISZT <! !> ],
	 KEYS [ KEY no_rel,
		MESSAGE #msg ] ] ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; SUBORDINATING CONJUNCTIONS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; These are not restricted to modifying only main VPs, since e.g. "if" needs
; to take scope over modals as in "I'd be pleased if we made an appointment."
; This lack of constraint means there will be structural ambiguity for 
; if-clauses, but not spurious - get different scopes of "if" and modals.
; But we restrict them to modifying only finite VPs or imperatives.

; DPF (10-Jan-99) Changed these to modify S rather than VP, since they don't
; appear to survive extraction from embedded clauses, nor do they appear inside
; an extraposed subject relative clause.

; DPF (10-Jan-98) For some reason, can't make the HEAD value prep* - fails to
; unify with strict_type in "Kim sleeps if Sandy sleeps".  Solve later.

; DPF 02-Oct-00 Changed subconj_inf_synsem to modify S rather than VP, in 
; order to make semantics consistent with other subord rels.

basic_subconj_synsem := nonprd_synsem &
  [ LOCAL [ CAT [ HEAD prep &
		       [ MOD < [ LOCAL scopal_mod &
				       [ CAT s_cat_fin_or_imp &
                                             [ MC #mc ],
					 CONT [ TOP #modtop,
						INDEX #ind ],
                                         KEYS.MESSAGE 1-dlist,
					 CONJ cnil ],
				 NON-LOCAL.SLASH 0-dlist ] > ],
		  VAL [ SUBJ < >,
			SPR < > ],
                  MC #mc ],
            CONT [ TOP #top,
		   INDEX #ind,
		   LISZT.LIST < subord_rel & #key &
                                [ HANDEL #stop,
                                  MAIN #main,
                                  SUBORD #subord ],
                                prpstn_rel & #msg &
                                [ HANDEL #top,
                                  SOA #soa ], ... >,
		   H-CONS.LIST < qeq & [ SC-ARG #main,
                                         OUTSCPD #modtop ],
                                 qeq & [ SC-ARG #subord ],
                                 qeq & [ SC-ARG #soa,
                                         OUTSCPD #stop ], ... > ],
            KEYS [ KEY #key,
		   MESSAGE <! #msg !> ] ] ].

basic_onearg_subconj_synsem := basic_subconj_synsem & basic_one_arg &
  [ LOCAL [ ARG-S #comps,
            CAT.VAL.COMPS #comps & < synsem &
                                     [ LOCAL.KEYS.KEY #ckey,
                                       NON-LOCAL [ SLASH 0-dlist,
                                                   REL 0-dlist,
                                                   QUE 0-dlist ],
                                       OPT - ] >,
            KEYS.--COMPKEY #ckey ] ].
                  

subconj_synsem := basic_onearg_subconj_synsem &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL [ CAT s_cat_fin,
                                      CONT [ TOP #ctop,
                                             INDEX event ],
                                      KEYS.MESSAGE 1-dlist &
                                                 <! prpstn_rel !> ] ] >,
            CONT [ LISZT <! relation, relation !>,
                   H-CONS <! qeq,
                             qeq & [ OUTSCPD #ctop ], 
                             qeq !> ] ] ].

subconj_inf_synsem := basic_onearg_subconj_synsem &
  [ LOCAL [ CAT.VAL.COMPS < [ LOCAL [ CAT vp_bse_cat &
                                          [ VAL [ SUBJ 
					        < unexpressed &
                                                  [ LOCAL.CONT.INDEX #sind ] >,
                                                  SPR *olist* ] ],
                                      CONT [ TOP #vptop,
                                             INDEX event ],
                                      KEYS.MESSAGE 0-dlist ] ] >,
            CONT [ LISZT <! subord_rel,
                            message,
                            hypo_rel & [ HANDEL #ctop,
                                         SOA #hsoa ],
                            def_rel & [ RESTR #rhand,
                                        BV #sind ],
                            pron_rel & [ HANDEL #prohand,
					 INST #sind &
						[ PRONTYPE zero_pron ] ] !>,
                   H-CONS <! qeq,
                             qeq & [ OUTSCPD #ctop ], 
                             qeq,
                             qeq & [ SC-ARG #hsoa,
                                     OUTSCPD #vptop ],
                             qeq & [ SC-ARG #rhand,
                                     OUTSCPD #prohand ] !> ] ] ].

subconj_prd_synsem := basic_onearg_subconj_synsem &
  [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL.AGR #sind ] >,
                  VAL.COMPS < [ LOCAL [ CAT prd_cat &
                                          [ VAL.SUBJ 
					     < unexpressed &
                                               [ LOCAL.CONT.INDEX #sind ] > ],
                                        CONT.TOP #prdtop ] ] > ],
            CONT [ LISZT <! subord_rel,
                            message,
                            hypo_rel & [ HANDEL #ctop,
                                         SOA #hsoa ] !>,
                   H-CONS <! qeq,
                             qeq & [ OUTSCPD #ctop ], 
                             qeq,
                             qeq & [ SC-ARG #hsoa,
                                     OUTSCPD #prdtop ] !> ] ] ].

subconj_if_synsem := basic_subconj_synsem & basic_two_arg &
  [ LOCAL [ ARG-S #comps,
            CAT [ HEAD.TAM.MOOD #mood,
                  VAL.COMPS < synsem &
                            [ LOCAL [ CAT.HEAD prep_or_modnp &
                                       [ TAM.TENSE no_tense,
                                         MOD < [ LOCAL intersective_mod ]>],
                                      CONT [ TOP #chand,
                                             INDEX #event ] ],
                              OPT + ] . #comps & 
                            < synsem &
                              [ LOCAL [ CAT np_cat_nom_min,
                                        CONT.INDEX #ind,
                                        AGR #agr,
                                        CONJ cnil ],
                                NON-LOCAL.SLASH 0-dlist,
                                OPT - ],
                              synsem &
                              [ LOCAL local_min &
                                      [ CAT [ HEAD verbal & strict_type &
                                                   [ VFORM fin,
                                                     TAM.MOOD #mood ],
                                              VAL [ SUBJ 
                                                    < synsem &
                                                      [ LOCAL 
                                                        [ AGR #agr,
                                                          CONT.INDEX #ind ]] >,
                                                    COMPS *olist* ] ],
                                        CONT [ TOP #chand,
                                               INDEX #event ],
                                        KEYS.MESSAGE 0-dlist ],
                                OPT - ] > > ],
            CONT [ LISZT <! subord_rel,
                            message,
                            prpstn_rel &
                            [ HANDEL #cmsg,
                              SOA #soa ] !>,
                   H-CONS <! qeq, 
                           qeq & [ OUTSCPD #cmsg ],
                           qeq,
                           qeq & [ SC-ARG #soa,
                                   OUTSCPD #chand ] !> ] ] ].

subconj_word := basic_mod_no_affix_notopkey_word & nonque &
  [ SYNSEM subconj_synsem ].
    
p_subconj_le := subconj_word &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT s_cat_fin ] > ].

p_subconj_if_indic_le := basic_mod_no_affix_notopkey_word & nonque &
  [ SYNSEM subconj_if_synsem &
           [ LOCAL.CAT.VAL.COMPS < synsem, 
                                   synsem, 
                                   [ LOCAL.CAT.HEAD.TAM 
                                                 ind_or_mod_subj_tam ] > ] ].

p_subconj_if_subjnct_le := basic_mod_no_affix_notopkey_word & nonque &
  [ SYNSEM subconj_if_synsem &
           [ LOCAL.CAT.VAL.COMPS < synsem, 
                                   synsem, 
                                   [ LOCAL.CAT.HEAD.TAM 
                                                 subjnct_tam ] > ] ].

p_subconj_inf_le := basic_mod_no_affix_notopkey_word & nonque &
  [ SYNSEM subconj_inf_synsem ].

p_subconj_prd_le := basic_mod_no_affix_notopkey_word & nonque &
  [ SYNSEM subconj_prd_synsem ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Coordinating conjunctions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; HACK: For efficiency, restrict conjunctions to only coordinate phrases which 
; are saturated for COMPS.  This falsely excludes "Kim bought and sold a car".

; DPF (25-Apr-98) Removed COMPS..HEAD subst constraint, to allow 
; "your and my offices"

; DPF 8-Jun-99 - Removed hack on saturated COMPS - was blocking e.g. "between
; eleven and one p.m."

; AAC I am not at all convinced that conj_word's shouldn't 
; be non_affix_bearing and INFLECTED -, but replicate existing 
; behaviour for now

conj_word := word &
  [ INFLECTED +,
    SYNSEM synsem &
     [ LOCAL [ CAT [ HEAD #head,
		     MC #mc,
		     VAL [ SUBJ #subj,
			   SPR #spr,
			   COMPS < synsem &
				   [ LOCAL local_min &
					   [ CAT [ HEAD #head,
						   MC #mc,
						   VAL [ SUBJ #subj,
							 SPR #spr,
							 COMPS #comps ],
                                                   HC-LEX #hclex ],
					     AGR.DIVISIBLE #div,
					     KEYS [ KEY #key,
						    MESSAGE #msg ] ],
                                     LEX #lex,
				     OPT - ] 
				   . #comps > ],
                     HC-LEX #hclex ],
               AGR.DIVISIBLE #div,
	       KEYS [ KEY #key,
		      MESSAGE #msg ] ],
       LEX #lex ] ].

; A coordinating conjunction needs to "prime" the CONJ.HANDELS and
; CONJ.INDICES lists, so that the right side of a binary coordination looks
; like an iterated coordination.

conj_complex_le := conj_word &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS.FIRST.LOCAL.CONT.E-INDEX #ind,
		   CONJ complex-conj,
		   CONT [ TOP #chand,
			  INDEX #cind,
			  LISZT <! #altkey & relation &
				 [ HANDEL #chand,
                                   LABEL *cons*,
				   C-ARG #cind,
				   R-INDEX #ind ] !>,
			  H-CONS <! !> ],
		   KEYS.ALTKEY #altkey ] ].

conj_atomic_le := conj_word &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS.FIRST.LOCAL [ CONT.TOP #hand,
					       CONJ cnil ],
		   CONJ atomic-conj,
		   CONT [ TOP #hand,
			  LISZT <! !>,
			  H-CONS <! !> ] ] ].

; two hundred and fifty
conj_and_num_le := conj_word &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS.FIRST.LOCAL [ CAT.HEAD intadj2-,
					       KEYS.ALTKEY #altkey &
                                                        [ HANDEL #hand ],
                                               CONJ cnil ],
		   CONJ num-conj,
		   KEYS [ KEY integer_rel,
                          ALTKEY #altkey ],
		   CONT [ TOP #hand,
			  LISZT <! !>,
			  H-CONS <! !> ] ] ].


etc_conj_le := phrase &
  [ INFLECTED +,
    SYNSEM synsem &
     [ LOCAL [ CAT.VAL.COMPS < >,
               CONJ complex-conj & [ CHEAD.LEFT < cnil, cnil > ],
               AGR.PNG png & [ PN 3pl ],
               CONT [ TOP #chand,
                      INDEX #cind,
                      LISZT <! #altkey & relation &
                             [ HANDEL #chand,
                               C-ARG #cind ] !>,
                      H-CONS <! !> ],
               KEYS.ALTKEY #altkey ] ] ].



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; NEG
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; not

; "not" requires its VP complement to be [VFORM non_fin], which is a supersort
; for all non-finite vforms except imp_vform, which is a subsort of bse; the
; other subsort of bse, bse_only, is a subsort of non_fin.  Most verbs with
; base morphology are [VFORM bse], and hence (the VPS they project) can be
; modified by "not", and they can also head imperative clauses, which require
; the head to be [VFORM imp_vform].  Only if modified by "not" will a base
; VP be unable to project an imperative clause; in contrast, "never sleep" is 
; fine as an imperative, since "never" does not further restrict its VP 
; complement leaving it [VFORM bse], which is consistent with the imperative 
; rule.  This fine distinction is due to Rob M.
; DPF 2-Feb-01 - Changed MOD..HEAD from verbal to v_or_g, to allow e.g.
; "I tried not hiring kim"

adv_not_le := norm_mod_nonque_no_affix_word &
  [ SYNSEM lex_synsem & 
	 [ LOCAL [ CAT [ HEAD negadv &
			      [ MOD < [ LOCAL scopal_mod &
					      [ CAT [ HEAD v_or_g & strict_type &
							   [ VFORM non_fin ],
						      VAL [ SUBJ < synsem_min >,
							    COMPS *olist* ],
                                                      MC na ],
						CONT [ TOP #mhand,
						       INDEX #vevent ],
						CONJ cnil,
						KEYS.MESSAGE #msg ] ] > ],
			 VAL [ SUBJ < >,
			       COMPS < >,
			       SPR < > ],
                         MC na,
			 POSTHEAD - ],
		   CONT [ INDEX #vevent,
			  LISZT <! neg_rel & #key &
				 [ ARG #hand ] !>,
			  H-CONS <! qeq &
				  [ SC-ARG #hand,
				    OUTSCPD #mhand ] !> ],
		   KEYS [ KEY #key,
			  MESSAGE #msg ] ] ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; COMPLEMENTIZERS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Complementizers except for 'to' are marked [SUBJ < >] which prevents them
; from having PP modifiers.

; ERB (07-10-96) 'whether' clauses are made with the regular hcomp rule, so
; the polar_q_rel must come from 'whether' itself.  This means that whether
; can't identify its KEY with its COMPKEY.  Also, I made complementizer_word
; inherit from msg_word (see comments near that type).  This has the effect of
; making non_affix_bearing be inherited by this type, instead of the lower
; types.

; ERB (14-12-97) to_c_prop needs to be MC + for infinitival relatives
; (see notes near to and non_wh_rel_cl) so this type can't say MC -.
; There is no convenient supertype to all of the MC - ones, so I'm
; just going to put it in on each for now.

; ERB (14-12-97) Complementizer_word can't be nonmod_local because 
; to_c_nonprop is going to have a non-empty mod value.  Again, For
; now I'm scattering this constraint among the lower types as appropriate.

; ERB (13-01-98) In fact, complementizer_word can't even be MC bool,
; because to_c_nonprop has to be MC na in order to head hcomps that
; serve as modifiers, as in "a teacher to talk to Sandy".

;;; DPF 01-Mar-00 - Reconsider, now that support_rel has been eliminated.
; DPF (27-Sept-98) Made KEY be v_event_rel, since "to" needs to pass up the
; KEY.HANDEL of its complement, in order to get the desired semantics for e.g.
; "Kim is going to lose" where the copula's support_rel identified its HANDEL
; with that of its complement's KEY.
; 
; DPF (13-Jan-99) Generalized to make KEY be v_event_rel for all complementizers,
; needed in particular to avoid treating complementizers as particles, which
; are selected by KEY value.

complementizer_word := msg_word & 
  [ SYNSEM.LOCAL local_min &
		 [ CAT [ HEAD comp &
			      [ POSS -,
				INV - ],
			 VAL.SPR *olist* ],
		   AGR.PNG.PN 3sg*,
		   CONT.INDEX non_expl,
		   KEYS.KEY v_event_rel ] ].

plain_compl_word := complementizer_word &
  [ SYNSEM.LOCAL [ CAT.VAL [ SUBJ < #subj >,
			     COMPS #comps & 
				    < [ LOCAL local_min &
					      [ KEYS.KEY #ckey,
						CONT.INDEX #ind ],
					OPT - ], ... > ],
		   CONT.INDEX #ind,
		   ARG-S < #subj . #comps >,
		   KEYS [ --COMPKEY #ckey ] ] ].

; These are the subject-to-object raising complementizers.  We're giving a
; funny three-constituent analysis to CPs: "that [Kim] [walks]" and "for [Kim]
; [to walk]".  One (beneficial) side effect of this is that finite and
; non-finite CPs get a uniform analysis, and we also can do real subject
; extraction without running afoul of the dreaded that-trace filter
; (Perlmutter 1971).  

; We also need another "that", one that combines with an adverbial and an
; S/Adv.  This accounts for why you get preposed modifiers with that clauses
; and not with for/to clauses, and why you don't get the that-t effect when
; you have an adverb in there.  But, that second "that" is going to have to
; bind off a SLASH value, sort of like the tough-movement cases, and that will
; have to wait for lexicalized SLASH passing.

; ERB (14-12-97) See notes at complementizer_word.

; DPF (25-Mar-00) Removed re-entrancy between --COMPKEY and KEY - not clear what
; it was supposed to do, but it prevents LABEL from working as intended.
; AAC - killed LABEL 
; DPF 2-Apr-01 - Added PP modifier argument to COMPS, to allow for sentence-
; initial modification, as in "I knew that on Tuesday I would be early", and in
; particular for "how come when I arrive he leaves" (given that we treat this
; "how come" as a main-clause-compatible complementizer).

two_place_compl_word := complementizer_word & 
  [ SYNSEM basic_two_arg & 
	 [ LOCAL nomod_local &
	   [ ARG-S #comps,
	     CAT [ HEAD.VFORM #vform,
		   VAL [ SUBJ < >,
			 COMPS 
			      < synsem &
                                [ LOCAL [ CAT.HEAD prep_or_modnp &
                                          [ TAM.TENSE no_tense,
                                            MOD < [ LOCAL intersective_mod ]>],
                                          CONT [ TOP #chand,
                                                 INDEX #event ] ],
                                  OPT + ] . #comps & 
                                < synsem & [ NON-LOCAL #nloc &
                                                       [ SLASH 0-dlist ] ],
                                  [ LOCAL local_min &
					[ CAT [ HEAD verbal & strict_type &
						     [ VFORM #vform ],
						VAL [ SUBJ 
                                                       < [ NON-LOCAL #nloc ] >,
                                                      COMPS *olist* ] ],
                                          CONT [ TOP #chand,
                                                 INDEX #event ],
					  KEYS [ KEY #ckey,
						 MESSAGE 0-dlist ] ],
                                    OPT - ] > > ] ],
	     CONT [ TOP #mhand,
		    LISZT.LIST < #msg, ... >,
		    H-CONS.LIST < qeq &
                                  [ SC-ARG #soahand,
                                    OUTSCPD #chand ], ... > ],
	     KEYS [ --COMPKEY #ckey,
		    MESSAGE 1-dlist &
			<! #msg & [ HANDEL #mhand,
				    SOA #soahand ] !> ] ] ] ].

; Instead of doing raising here by identifying whole synsems, only identify
; semantic indices, as with regular raising for verbs above.  This lets us get
; "I prefer that he be here early" treating the second (VP) complement of 
; "that" as a simple base-form VP, rather than some new morphological form.

sor_compl_word := two_place_compl_word &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < synsem,
                                 synsem &
				 [ LOCAL [ CAT nomp_cat_min,
                                           CONT.INDEX #ind,
                                           AGR #agr,
					   CONJ cnil ],
				   OPT - ],
				 [ LOCAL.CAT.VAL.SUBJ 
                                      < synsem &
                                        [ LOCAL [ AGR #agr, 
                                                  CONT.INDEX #ind ] ] > ] > ].

comp_that_le := sor_compl_word & 
  [ SYNSEM.LOCAL [ CAT [ HEAD [ VFORM fin,
				TAM.MOOD #mood & ind_or_mod_subj,
				AUX - ],
			 VAL.COMPS < synsem, 
                                     [ LOCAL.CAT np_cat_nom_min ],
				     [ LOCAL.CAT.HEAD.TAM.MOOD #mood ] >,
                         MC - ],
                   CONT [ LISZT <! relation !>,
                          H-CONS <! qeq !> ],
		   KEYS.MESSAGE 1-dlist &
			    <! prpstn_rel !> ] ].

;; "I prefer that he be here early"
comp_that_subj_le := sor_compl_word & 
  [ SYNSEM.LOCAL [ CAT [ HEAD [ VFORM bse,
				TAM.MOOD #mood & subjunctive,
				AUX - ],
			 VAL.COMPS < synsem, 
                                     [ LOCAL.CAT np_cat_nom_min ],
                                     [ LOCAL.CAT.HEAD.TAM.MOOD #mood ] >,
                         MC - ],
                   CONT [ LISZT <! relation !>,
                          H-CONS <! qeq !> ],
		   KEYS.MESSAGE 1-dlist &
			    <! hypo_rel !> ] ].

comp_for_le := sor_compl_word & 
  [ SYNSEM.LOCAL [ CAT [ HEAD [ VFORM inf,
                                AUX - ],
                         VAL.COMPS < [ LOCAL.KEYS.KEY no_rel ],
                                     [ LOCAL.CAT np_cat_acc_min ],
                                     [ NON-LOCAL.SLASH 0-dlist ] >,
                         MC - ],
                   CONT [ LISZT <! relation !>,
                          H-CONS <! qeq !> ],
		   KEYS.MESSAGE 1-dlist &
			    <! hypo_rel !> ] ].

comp_like_le := sor_compl_word & 
  [ SYNSEM.LOCAL [ CAT [ HEAD [ VFORM fin,
				AUX - ],
			 VAL.COMPS < synsem,
                                     [ LOCAL.CAT np_cat_nom_min ],
				     [ LOCAL.CAT.HEAD.TAM.MOOD ind_or_mod_subj ] >,
                         MC - ],
                   CONT [ LISZT <! relation !>,
                          H-CONS <! qeq !> ],
		   KEYS.MESSAGE 1-dlist &
			    <! _like_mood_rel !> ] ].

; ERB (13-01-98) to_compl_word says SUBJ < unexpressed >, but I don't
; how that can be, unless the analysis of raising has changed drastically.
; (With it that way, raising is broken).  So I am getting rid of it.

; ERB (19-01-98) SUBJ < unexpressed > was in there because something
; had to take care of giving that subject a SLASH value.  The solution
; we've opted for is this: the subject of to_c_prop is always PRO, and
; the subject of to_c_nonprop is always raised (that is, all
; infinitival tos except those in raising constructions are to_c_prop).
; So there is no constraint on to_compl_word, but to_c_prop says
; SUBJ < pro_ss > and to_c_nonprop says SUBJ canonical_synsem.

; ERB (03-02-98) KEY should be no_rel.  It used to be identified with
; COMPKEY, but that causes at least one problem: to was able to be the
; SPR of the, since the constrains its choice of SPR by a semantic
; constraint on the KEY.
; DPF (13-Jan-99) This problem should be solved by the above decision to
; make all complementizers have the KEY be of type v_event_rel.

to_compl_word := plain_compl_word &
  [ SYNSEM basic_two_arg &
     [ LOCAL [ CAT [ HEAD [ AUX +,
			    PRD - ],
		     VAL [ SUBJ < [ LOCAL.CONT #cont ] >,
			   COMPS 
				< synsem &
				  [ LOCAL 
					  [ CAT vp_bse_cat &
						[ VAL.SUBJ 
							   < [ LOCAL.CONT #cont,
							       NON-LOCAL
								 [ SLASH 0-dlist,
								   REL 0-dlist,
								   QUE 0-dlist ] ] > ],
					    CONT.TOP #hand ] ] > ] ],
	       KEYS.KEY.HANDEL #hand ] ] ].

; ERB (14-12-97) Why is the SUBJ here specified as acc?? I'm taking it
; out because that follows now from pro_ss.

; ERB (08-01-98) Presumably that had to do with "for him/*he to sleep..."
; but that's the other to: to_c_nonprop.

; ERB (14-12-97) In order to implement infinitival relatives (I found
; a teacher to talk to Sandy, I found a teacher to talk to), I am
; making to_c_prop MC +.  The reasons for this is that the relevant
; cx (non_wh_rel_cl) needs to be able to take subjh_decl and hcomps
; headed by to_c_prop as its head-dtr, but not an extrasubj phrase.

; ERB (19-01-98) This to has to have a mod value so that it can
; head the infinitival relatives.  Something needs to be said about
; the features mentioned in nomod_local, probably, but I'm not sure
; what yet.  It also needs to be MC na so it can feed extrasubj.

basic_comp_to_prop_le := to_compl_word & 
  [ SYNSEM.LOCAL [ CAT [ HEAD.MOD < [ LOCAL intersective_mod &
                                            [ CAT nbar_cat ] ] >,
			 VAL [ SUBJ < pro_ss >,
			       COMPS < [ LOCAL.CONT.TOP #chand ] > ],
			 MC na_or_-,
			 POSTHEAD + ],
		   CONT [ --TOPKEY #topkey,
			  LISZT <! #topkey !>,
			  H-CONS <! qeq &
				  [ SC-ARG #soahand,
				    OUTSCPD #chand ] !> ],
		   KEYS.MESSAGE 1-dlist &
				   <! hypo_rel & #topkey &
				    [ SOA #soahand ] !> ] ].

comp_to_prop_le := basic_comp_to_prop_le &
  [ SYNSEM.LOCAL.CAT.HEAD.VFORM inf ].

comp_inf_and_le := basic_comp_to_prop_le &
  [ SYNSEM.LOCAL.CAT.HEAD.VFORM inf_and ].

; ERB (14-12-97) This one will have a non-empty MOD value in order to
; build things like "Kim found a friend to talk to Sandy" where the
; subject of talk has to be bound to friend (unlike in "Kim found a
; friend to talk to" where we're convinced it is discourse bound.)
;
; The semantics are probably not right -- as a place holder in the
; modcont I put a appropriate_rel.  The idea is that "I found a person
; to talk to Sandy" means something like "I found a person who is willing/
; appropriate/suitable to talk to Sandy."  But I don't see how to make
; (something like) the appropriate_rel take the comlement of the to
; as an argument.  There is also the question of whether there should be
; any message associated with this.  One would think not, given that we've
; decided the relevant to is the nonprop one...
;   For now, we're not including this rel.

; ERB (13-01-98) to_compl_nonprop_word must be MC na if it is
; to head hcomps that serve as modifiers as in "a teacher to talk to Sandy".
;
; ERB (19-01-98) All infinitival relatives are now headed by
; to_c_prop.  To_c_nonprop only appears in raising constructions.
; Accordingly, it has no MOD value, and its subject is constrained
; to be canonical_synsem.  I'm not sure the latter constraint is
; actually doing any work for us, but I believe it to be true.
; It can also now inherit from nomod_local.  The MC value might
; could be changed, as well.

comp_to_nonprop_le := to_compl_word & 
  [ SYNSEM.LOCAL nomod_local &
		 [ CAT [ HEAD.VFORM inf,
                         VAL [ SUBJ < canonical_synsem >,
			       COMPS < [ LOCAL local_min &
					       [ CONT.TOP #hand ] ] > ],
			 MC na ],
		   CONT [ TOP #hand,
			  LISZT <! !>,
			  H-CONS <! !> ],
		   KEYS.MESSAGE 0-dlist ] ].

;;; DPF (21-Oct-98) Added types for elided "to", since we can't use the lexical
;; rule of auxiliary verbs, since we need to block the NP "the book to", which
;; would be built by that lexical rule, then by the application of subject
;; extraction, and then the infinitival relative rule.

to_compl_elided_word := msg_word &
  [ SYNSEM basic_one_arg &
	   [ LOCAL nomod_local &
		   [ CAT [ HEAD comp &
				[ VFORM inf,
				  INV -,
				  AUX +,
				  PRD - ],
			   VAL [ SUBJ #subj & 
				      < [ LOCAL.CONT.INDEX #index,
					  NON-LOCAL.SLASH 0-dlist ] >,
				 SPR *olist*,
				 COMPS < > ],
			   MC na ],
		     CONT [ INDEX #event,
			    LISZT.LIST < ellipsis_rel & #key &
					 [ EVENT #event,
					   ROLE #index ], ... > ],
		     ARG-S #subj,
		     KEYS.KEY #key ] ] ].

comp_to_prop_elided_le := to_compl_elided_word & 
  [ SYNSEM.LOCAL [ CAT.VAL.SUBJ < pro_ss >,
		   CONT [ LISZT <! ellipsis_rel &
				   [ HANDEL #chand ], 
				   #msg !>,
			  --TOPKEY #topkey,
			  H-CONS <! qeq &
				    [ SC-ARG #soahand,
				      OUTSCPD #chand ] !> ],
		   KEYS.MESSAGE 1-dlist &
				<! hypo_rel & #msg & #topkey &
				     [ SOA #soahand ] !> ] ].

comp_to_nonprop_elided_le := to_compl_elided_word & topkey &
  [ SYNSEM.LOCAL [ CAT.VAL.SUBJ < canonical_synsem >,
		   CONT [ LISZT <! ellipsis_rel !>,
			  H-CONS <! !> ],
		   KEYS.MESSAGE 0-dlist ] ].

; ERB (08-10-96) Whether was previously a que_word so that things selecting
; for questions could select by QUE values.  This is clearly a hack, and
; further, one that is no longer necessary now that we have semantic selection.
; I have split the old type whether_compl_word into two types: finite and
; nonfinite.  I have also gotten rid of the feature CMP.  
; As for the semantics, whether contributes a polar_q_rel (see notes near
; polar_q_rel in fundamentals.tdl).  Now, according to Rob, things that
; take complements with message-type semantics usually take the handle of
; those complements as the argument in their KEYs.  However, also according
; to Rob, if we did that here, it would imply that "whether Kim left every
; party" has more readings than "Kim left every party", which doesn't seem
; to be true.  Accordingly, whether will identify its handle with the
; handle of its complement, and take the complement's INDEX as the value
; of the ARG in the polar_q_rel.  Now, if whether further identified its
; INDEX with that one, things would be getting pretty twisty, so whether
; will have its own INDEX of type event.

; ERB (24-10-96) MESSAGE now functions like KEY as a pointer to
; a certain rel on the LISZT.  Both wh and yn questions will have an
; int_rel on their LISZT.  This is actually sewn into the LISZT
; by the constructions.  Whether just carries it in its MESSAGE value
; so that it unifies with the right constructions.  In this way, we
; even kind of get Rob's intuition -- whether doesn't, in a sense,
; have a message, because that message isn't on its LISZT.
; Anyway, all of this makes the polar_q_rel irrelevant, so I am removing
; it.  The hcomp_yn_nr construction will make sure the SOA value
; of the int_rel takes the HANDEL of whether's complement.   I don't
; know what to do about whether's HANDEL, KEY, and EVENT now, so I am 
; leaving them unspecified.

; ERB (14-12-97) See notes at complementizer_word.

whether_compl_word := complementizer_word &
  [ SYNSEM [ LOCAL nomod_local &
		   [ CONT.LISZT.LIST < #msg, ... >,
		     KEYS.MESSAGE 1-dlist &
				    <! int_rel & #msg &
				     [ PARAMS <! !> ] !> ],
	     NON-LOCAL.SLASH 0-dlist ] ].

comp_whether_if_fin_lexent := whether_compl_word & sor_compl_word &
  [ SYNSEM.LOCAL [ CAT [ HEAD [ VFORM fin,
                                TAM.MOOD #mood & strict_sort ],
			 VAL.COMPS < synsem, 
                                     [ LOCAL.CAT np_cat_nom_min ],
                                     [ LOCAL.CAT.HEAD.TAM.MOOD #mood ] >,
                         MC - ],
                   CONT [ LISZT <! relation !>,
                          H-CONS <! qeq !> ] ] ].

comp_whether_fin_le := comp_whether_if_fin_lexent &
  [ SYNSEM.LOCAL.CAT 
        [ HEAD.TAM.MOOD ind_or_mod_subj,
          VAL.COMPS < synsem, 
                      synsem, 
                      [ LOCAL.CAT.HEAD.TAM ind_or_mod_subj_tam ] > ] ].

comp_if_indic_le := comp_whether_if_fin_lexent &
  [ SYNSEM.LOCAL.CAT [ HEAD.CASE acc,
                       VAL.COMPS < synsem, 
                                   synsem, 
                                   [ LOCAL.CAT.HEAD.TAM 
                                                 ind_or_mod_subj_tam ] > ] ].

comp_if_subjnct_le := comp_whether_if_fin_lexent &
  [ SYNSEM.LOCAL.CAT [ HEAD.CASE acc,
                       VAL.COMPS < synsem, 
                                   synsem, 
                                   [ LOCAL.CAT.HEAD.TAM subjnct_tam ] > ] ].

how_come_le := whether_compl_word & sor_compl_word &
  [ SYNSEM.LOCAL [ CAT [ HEAD [ VFORM fin,
                                TAM.MOOD ind_or_mod_subj ],
                         VAL.COMPS < synsem, 
                                     [ LOCAL.CAT np_cat_nom_min ],
                                    [ LOCAL [ CAT.HEAD.TAM.MOOD ind_or_mod_subj
                                                                & strict_sort,
                                              CONT [ TOP #ctop,
                                                     INDEX #cind ] ] ] >,
                         MC + ],
                   CONT [ INDEX #cind,
                          LISZT <! message,
                                 _for_rel & [ HANDEL #ctop,
                                              ARG #cind,
                                              ARG3 #objind ],
                                 which_rel & [ BV #objind,
                                               RESTR #rhand ],
                                 reason_rel &
                                 [ HANDEL #nhand,
                                   INST #objind ] !>,
                          H-CONS <! qeq, 
                                  qeq &
                                  [ SC-ARG #rhand,
                                    OUTSCPD #nhand ] !> ] ] ].

; ERB (08-10-96) In the older version, I had whether_compl_word_inf 
; selecting for something that was just vp_inf_cat.  I never
; tested for this, but this probably allowed Sandy knew whether whether
; to leave, etc.  One way to get around this is for whether to
; select for something that is MESSAGE 0-dlist.  In order to do 
; this, I need to build to CPs that are no_rel, but we want this
; anyway (even though I talked Dan out of it at one point).
; DPF 09-Jan-00 - Made "whether" be MC -, not MC na, since e.g. "know"
; wants [MC -] CP complements.

comp_whether_inf_le := whether_compl_word & plain_compl_word &
  [ SYNSEM ssr_two_arg_subst &
	   [ LOCAL [ CAT [ HEAD.VFORM inf,
			   VAL.COMPS < [ LOCAL [ CAT [ HEAD comp &
							  [ VFORM inf ],
						       VAL [ COMPS *olist*,
							     SPR *olist* ] ],
						 CONT.TOP #chand ] ] >,
			   MC - ],
		     CONT [ TOP #mhand,
			    LISZT <! #msg !>,
			    H-CONS <! qeq &
				    [ SC-ARG #soahand,
				      OUTSCPD #chand ] !> ],
		     KEYS.MESSAGE 1-dlist &
				<! #msg & [ HANDEL #mhand,
					    SOA #soahand ] !> ] ] ].
	    
s_compl_phrase_le := msg_word &
  [ SYNSEM [ LOCAL nomod_local &
		 [ CAT [ HEAD verb & 
			      [ INV -,
				VFORM fin ],
			 VAL [ SUBJ < >,
			       SPR *olist*,
			       COMPS < > ] ],
		   CONT [ TOP #hand,
			  INDEX #event,
			  LISZT <! #key, #msg !>,
			  H-CONS <! qeq &
				  [ SC-ARG #soa,
				    OUTSCPD #khand ] !> ],
		   KEYS [ KEY ellipsis_rel & #key & 
			      [ HANDEL #khand,
				EVENT #event ],
			  MESSAGE <! message & #msg &
				   [ HANDEL #hand,
				     SOA #soa ] !> ],
		   ARG-S < > ],
	     NON-LOCAL.SLASH 0-dlist ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Imperative words "don't" and "let's"

; ERB (05-10-97) In order to conflate hcomp_root and hcomp_nc into just one
; hcomp, root_marker_words have to identify their value for mc with that of
; their complement.  (Hcomp_root identified MC for mother and non-head
; daughter.)

root_marker_word := nonque & non_affix_bearing & nonrel & nonconj & hc-to-phr &
  [ SYNSEM synsem &
	 [ LOCAL [ CAT [ HEAD verb* & [ MOD < >,
					TAM.MOOD #mood,
					AUX -,
					INV - ],
			 VAL [ SPR < >,
			       COMPS < synsem &
				       [ LOCAL local_min &
					       [ CAT [ HEAD.TAM.MOOD #mood,
						       VAL.COMPS *olist* ],
						 CTXT #ctxt ],
					 NON-LOCAL [ SLASH 0-dlist,
						     QUE 0-dlist,
						     REL 0-dlist ] ] > ] ],
		   CTXT #ctxt ],
	   NON-LOCAL.SLASH 0-dlist ] ].

lex_imperative := root_marker_word &
  [ SYNSEM.LOCAL 
	   [ CAT [ VAL 
		    [ SUBJ < >,
		      COMPS < [ LOCAL [ CAT [ HEAD verb &
						   [ VFORM bse_only ],
					      VAL.SUBJ 
						 < synsem &
						   [ LOCAL local_min &
							   [ CONT.INDEX #inst ],
						     NON-LOCAL [ SLASH 0-dlist,
								 QUE 0-dlist,
								 REL 0-dlist]] >,
					      MC na ],
					CONT [ TOP #khand,
                                               INDEX #ind ] ],
                                OPT + ] > ],
		   MC + ],
	     AGR #inst,
	     CONT [ TOP #hand,
		    INDEX #ind,
		    LISZT.LIST < #msg, 
                                 def_rel & [ RESTR #rhand,
                                             BV #inst ],
				 pron_rel & 
				 [ HANDEL #prohand,
				   INST #inst &
					  [ PRONTYPE std_pron ] ], ... >,
		    H-CONS.LIST < qeq &
				  [ SC-ARG #soa ], 
                                  qeq &
                                  [ SC-ARG #rhand,
                                    OUTSCPD #prohand ], ... > ],
	     KEYS [ KEY no_rel & [ HANDEL #khand ],
		    MESSAGE 1-dlist &
			  <! imp_rel & #msg &
			     [ HANDEL #hand,
			       SOA #soa ] !> ] ] ].

va_lets_imp_le := lex_imperative &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.TOP #chand ] >,
		   AGR [ PNG png & [ PN 1pl* ],
			 PRONTYPE std_1pl ],
		   CONT [ H-CONS <! [ OUTSCPD #chand ], qeq !>,
			  LISZT <! relation, relation, relation !> ] ] ].

va_dont_imp_le := lex_imperative &
  [ SYNSEM.LOCAL [ CAT.VAL.COMPS < [ LOCAL.CONT.TOP #chand ] >,
		   AGR [ PNG png & [ PN 2per ],
			 PRONTYPE std_2 ],
		   CONT [ LISZT <! relation,
				   relation,
                                   relation,
				   neg_rel &
				 [ HANDEL #neghand,
				   ARG #arg ] !>,
			  H-CONS <! [ OUTSCPD #neghand ],
                                    qeq,
				    qeq &
				    [ SC-ARG #arg,
				      OUTSCPD #chand ] !> ] ] ].

;; 09-09-97  Added BV and SCOPE attributes temporarily for VM2, since the
;; whq external relation for how_about is like a quantifier, needing four args.
;; Had to change the relation introduced by "how about" to '_which_rel'
;; rather than '_how_about_rel' because of some property of vitADT.  Similar
;; change made to the 'frag-msg' instance in syntax.tdl.
;; 06-Oct-98 DPF - Changed MC value from na to +, to allow "okay, how about kim".

how_about_word := root_marker_word &
  [ SYNSEM.LOCAL [ CAT [ VAL [ SUBJ < >,
                               COMPS 
                                    < [ LOCAL [ CAT [ HEAD subst,
                                                      VAL [ SPR *olist*,
                                                            COMPS *olist* ] ],
                                                CONT.INDEX #ind ] ] > ],
                         MC + ],
		   CONT [ TOP #top,
			  INDEX #ind & event_or_index,
			  LISZT <! #msg,
                                   arg13_rel & 
                                     [ HANDEL #arghand,
                                       ARG3 #ind ],
				   relation & #key &
					 [ HANDEL #rhand,
					   ARG #arg ] !>,
                          H-CONS <! qeq &
                                  [ SC-ARG #ctop,
                                    OUTSCPD #rhand ],
                                    qeq &
                                  [ SC-ARG #arg,
                                    OUTSCPD #arghand ]
 !> ],
		   KEYS [ KEY #key,
                          MESSAGE 1-dlist &
				<! int_rel & #msg &
				   [ HANDEL #top,
				     SOA #ctop ] !> ] ] ].

comp_how_about_n_or_p_le := how_about_word &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT.HEAD n_or_p & strict_type,
                                   OPT - ] > ].

comp_how_about_s_le := how_about_word &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT [ HEAD verb & strict_type,
					       MC bool ],
                                   OPT - ] > ].

comp_how_about_or_why_vp_lexent := how_about_word &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT [ HEAD verb & strict_type,
                                               VAL.SUBJ < synsem > ],
                                   OPT + ] > ].

; DPF 17-Apr-01 - Changed COMPS.FIRST..SUBJ from canonical_synsem to synsem
; since the former blocked "Why be late"

comp_how_about_vp_le := comp_how_about_or_why_vp_lexent &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT.HEAD.VFORM prp ] > ].

comp_why_vp_le := comp_how_about_or_why_vp_lexent &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < [ LOCAL.CAT.HEAD.VFORM bse_only ] > ].

; Discourse-related sentential adverbs
;   (e.g. 'then, but')
; DPF 06-Oct-98 - Added MC + to MOD, to block "Kim says but sandy left"
; DPF 15-Oct-98 - Changed HEAD from adv to root-marker, to get "yes, okay"
; DPF 22-Oct-98 - Added MC + to the SYNSEM itself to block the relative clause
;   in "a programmer but he hired".
; DPF 09-Jun-99 - Added SLASH 0-dlist to MOD, to block e.g. "when yes Kim left"
; DPF 03-Oct-99 - Added SUBJ *olist* to MOD specific. - why wasn't it here before?
; DPF 17-Apr-00 - Removed CONJ cnil to enable e.g. "yes and Kim left"

basic_disc_adv_synsem := lex_synsem &
  [ LOCAL [ CAT [ HEAD root-marker &
		       [ PRD -,
			 MOD < [ LOCAL scopal_mod &
				       [ CAT.VAL [ SUBJ *olist*,
                                                     SPR *olist*,
                                                   COMPS *olist* ],
                                         CONT.INDEX #ind,
					 KEYS.MESSAGE #msg ],
				 NON-LOCAL.SLASH 0-dlist ] > ],
		  VAL [ SUBJ < >,
			SPR < >,
			COMPS < > ] ],
	    CONT.INDEX event_or_index & #ind,
	    KEYS.MESSAGE #msg,
	    CONJ cnil ],
    NON-LOCAL [ SLASH 0-dlist,
		REL 0-dlist,
		QUE 0-dlist ] ].

disc_adv_synsem := basic_disc_adv_synsem &
  [ LOCAL.CAT [ HEAD.MOD < [ LOCAL.CAT.HEAD disc_adverbee & strict_type ] >,
                MC + ] ].

;; DPF 24-Oct-98 Removed [MC +] from disc_adv_word, since this runs afoul of
;; the carefully balanced constraint on head-modifier structures which requires
;; [MC na], to allow relative clauses as modifiers while preventing ordinary
;; finite clauses from being modifiers.

disc_adv_word := msg_word &
  [ SYNSEM disc_adv_synsem &
	   [ LOCAL [ CAT.HEAD.MOD < [ LOCAL.CONT [ TOP #mtop,
						   INDEX #mindex ] ] >,
		     CONT [ TOP #chand & handle,
			    INDEX #cind,
			    LISZT <! #key & [ HANDEL #khand ],
				     discourse_rel &
				   [ HANDEL #chand,
				     C-ARG #cind,
				     L-HANDEL #khand,
				     R-HANDEL #mtop,
				     R-INDEX #mindex ] !>,
                            H-CONS <! !> ],
                     KEYS.KEY #key & abstr_excl_rel ] ] ].

adv_disc_le :< disc_adv_word.

disc_adv_sat_le := disc_adv_word &
  [ SYNSEM.LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT.VAL.SUBJ < > ] > ].

; This is like really necessary
disc_adv_like_synsem := basic_disc_adv_synsem &
  [ LOCAL.CAT.HEAD.MOD < [ LOCAL.CAT [ HEAD subst,
                                       MC na ] ] > ].

adv_disc_like_le := msg_word & topkey &
  [ SYNSEM disc_adv_like_synsem &
	   [ LOCAL [ CAT.HEAD.MOD < [ LOCAL [ CONT.TOP #top ] ] >,

		     CONT [ TOP #top & handle,
			    LISZT <! #key !> ],
		     KEYS.KEY #key & abstr_excl_rel ] ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;   Temporal nouns
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Extend to COMPS of temporal nouns the hack blocking extraction of subjects of 
; inverted auxiliaries, which exploits CASE value.
; Block N-N compound of "a two meeting" by stipulating that hour_words are
; PRD +, incompatible with requirement of compound rule.  In contrast, e.g.,
; day_of_week words are unmarked for PRD.
;
; Made CONT.TOP be the handle of the numbered_hour_rel rather than that of
; the implicit def_rel, since hour_words can be modified, as in "two o'clock
; on the sixteenth".  Since the PP unifies its TOP handle with the TOP handle
; of the phrase it modifies, that handle can't be the handle of the quantifier,
; so must be that of the numbered_hour_rel.

; DPF (18-Oct-98) - Removed PRD + from hour_word, so it can combine with its
; minute or am-pm complements and still be a prenominal adjective, as in
; "a ten thirty am meeting"

;; DPF (22-Dec-98) - SYNSEM is typed phr_synsem in order to block "a two meeting"
;; while admitting "a two o'clock meeting" - this works because "two o'clock"
;; combines with "meeting" using the noun-noun compound rule, which requires
;; the left member to have SYNSEM of type lex_synsem.

;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing
;; DPF 13-May-01 - Added [PERIPH +] to prevent hour words from being modified
;; (especially by relative clauses), and therefore added optional prd-pp 
;; complement to still get e.g. "three in the morning"

abstr_hour_word := hc_word & non_affix_bearing &
  [ SYNSEM phr_synsem &
	 [ LOCAL nomod_local &
		 [ STEMHEAD nstem,
		   CAT [ HEAD noun* & [ POSS - ],
			 VAL [ SUBJ < >,
			       SPR < [ LOCAL local_min &
					     [ CAT [ HEAD adv,
						     VAL [ SPR  *olist*,
							   COMPS *olist* ] ],
                                               CONT.TOP #nhand,
					       KEYS.KEY degree_rel &
						   [ DARG #darg ] ],
				       OPT + ] >,
			       COMPS < synsem &
				       [ LOCAL [ CAT [ HEAD intadj2,
						       VAL.COMPS < > ],
						 CONJ cnil,
                                                 CONT [ TOP #nhand,
                                                        INDEX #minute ],
						 KEYS.KEY minute_rel ],
					 NON-LOCAL.SLASH 0-dlist,
					 OPT + ],
				       synsem &
				       [ LOCAL [ CAT.HEAD no_head,
						 CONJ cnil,
                                                 CONT [ TOP #nhand,
                                                        INDEX #ampm ],
						 KEYS.KEY am_pm_rel ],
					 NON-LOCAL.SLASH 0-dlist,
					 OPT + ],
                                       synsem & 
				       [ LOCAL [ CAT prd_cat &
						   [ HEAD prep &
                                                          [ MOD 
                                                      < [ LOCAL [ CONT
                                                           [ TOP #nhand,
                                                             INDEX #index],
                                                                  KEYS.KEY
                                                                   #key ]]>]],
                                                 CONJ cnil ],
					 NON-LOCAL.SLASH 0-dlist ] > ],
                         HC-LEX + ],
                   CONT nom-obj &
		       [ INDEX #index,
			 LISZT.LIST < numbered_hour_rel & #key &
				      [ HANDEL #nhand,
					INST #index,
					MIN #minute,
					AM-PM #ampm,
					DIM #darg ],
				      def_rel &
				      [ BV #index,
					RESTR #rhand ], ... >,
			 H-CONS <! qeq &
                                  [ SC-ARG #rhand,
                                    OUTSCPD #nhand ] !> ],
		   AGR #index,
		   ARG-S < >,
		   KEYS.KEY #key ],
           MODIFIED notmod & [ PERIPH + ] ] ].

n_hour_le := abstr_hour_word &
  [ SYNSEM.LOCAL.CONT.LISZT <! relation, relation !> ].

; sixish
n_approx_hour_le := abstr_hour_word &
  [ SYNSEM.LOCAL.CONT.LISZT <! relation &
			         [ HANDEL #hand,
				   INST #inst ],
			         relation, 
			         approx_grad_rel &
			         [ HANDEL #hand,
				   DARG #inst ] !> ].

abstr_minute_word := np_word_no_quant &
  [ SYNSEM [ LOCAL [ CAT.HEAD intadj2,
		     KEYS.KEY minute_rel &
			      [ HANDEL #hand,
				ARG #inst ],
		     CONT [ TOP #hand,
			    INDEX #inst ] ],
	     MODIFIED notmod ] ].

adj_minute_le := abstr_minute_word &
  [ SYNSEM.LOCAL.CONT.LISZT <! relation !> ].

adj_approx_minute_le := abstr_minute_word &
  [ SYNSEM.LOCAL.CONT [ TOP #hand,
			INDEX #inst,
			LISZT <! relation, approx_grad_rel &
			       [ HANDEL #hand,
				 DARG #inst ] !> ] ].

xp_am_pm_le := np_word_no_quant &
  [ SYNSEM.LOCAL [ CAT.HEAD no_head,
		   KEYS.KEY am_pm_rel,
		   CONT [ INDEX #inst,
			  LISZT <! [ INST #inst ] !> ] ] ].

;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing

n_mealtime_le := que_word & nonque &
  [ SYNSEM noun_synsem & nomod_synsem &
	 [ LOCAL [ CAT [ HEAD [ POSS -,
				PRD + ],
			 VAL [ SPR < [ LOCAL local_min &
					     [ CAT [ HEAD adv,
						     VAL [ SPR  *olist*,
							   COMPS *olist* ] ],
                                               CONT.TOP #nhand,
					       KEYS.KEY degree_rel &
						   [ DARG #index ] ],
				       OPT + ] >,
			       COMPS < > ] ],
		   CONT nom-obj &
		       [ INDEX #index,
			 LISZT <! hour_rel & #key &
				  [ HANDEL #nhand,
				    INST #index ],
				  def_rel &
				  [ BV #index,
				    RESTR #rhand ] !>,
			 H-CONS <! qeq &
			   [ SC-ARG #rhand,
			     OUTSCPD #nhand ] !> ],
		   ARG-S < >,
		   KEYS.KEY #key ],
	     MODIFIED notmod ] ].

month_synsem := nonpro_nomod_onearg_synsem &
  [ LOCAL [ ARG-S #comps,
            CAT [ HEAD noun*,
                  VAL [ SPR < [ LOCAL.KEYS.KEY quant_or_wh_rel,
                                OPT - ] >,
                        COMPS #comps &
                            < [ LOCAL local_min &
                                      [ CAT [ VAL [ SUBJ < >,
                                                    COMPS *olist*,
                                                    SPR *olist* ],
                                              MC na ],
                                        CONJ cnil,
                                        CONT.INDEX #ind ],
                                NON-LOCAL.SLASH 0-dlist ] > ] ],
            AGR #ind,
            CONT [ INDEX #ind,
                   LISZT <! #key !>,
                   H-CONS <! !> ],
            KEYS.KEY #key &
                  [ INST #ind &
                         [ PNG png & [ PN 3sg*,
                                       GEN neut* ] ] ] ],
    MODIFIED notmod ].

; June, June 1996, June of 1996
month_no_of_year_synsem := month_synsem &
  [ LOCAL [ CAT.VAL.COMPS < synsem & 
                            [ LOCAL [ CAT nomp_cat_acc_min,
                                      KEYS.KEY yofc_rel ],
                              OPT - ] >,
            KEYS.KEY mofy_rel ] ].


;;n_month_le := nontopkey &
;;  [ SYNSEM month_of_year_synsem ].

n_month_year_le := nontopkey &
  [ SYNSEM month_no_of_year_synsem ].

; DPF 22-May-01 Added ALTKEY relation to block year names appearing as heads
; in n-n compounds.
n_year_le := non_affixed_np & 
  [ SYNSEM noun_nocomp_synsem &
         [ LOCAL [ CAT.HEAD.MOD < >,
                   AGR.DIVISIBLE -,
		   CONJ cnil,
		   KEYS [ KEY yofc_rel,
                          ALTKEY relation ],
		   CONT [ INDEX.PNG png & [ PN 3sg* ],
                          LISZT <! relation !>,
                          H-CONS <! !> ] ] ] ].

; 'the first', 'October first', 'the first of October'
; Need to find way to block '*October first of October'
; DPF 20-Nov-99 - Added PRD + to block membership in N-N-compounds

basic_dom_synsem := common_noun_synsem & two_arg &
  [ LOCAL [ CAT.VAL.SPR < [ LOCAL.KEYS.KEY quant_or_wh_rel ] >,
            CONT [ INDEX.DIVISIBLE -*,
		   LISZT.LIST < dofm_rel &
                                [ INST.PNG png & [ PN 3sg*,
                                                   GEN neut* ] ], ... > ] ] ].
                             
basic_dom_ord_synsem := basic_dom_synsem &
  [ LOCAL.CAT [ HEAD.PRD +,
                VAL [ SPR < [ LOCAL.KEYS.KEY abstr_def_rel ] >,
                      COMPS < [ NON-LOCAL.SLASH 0-dlist ] > ] ] ].

dom_ord_synsem := basic_dom_ord_synsem & temp_noun_ppcomp_synsem & 
  [ LOCAL.CAT.VAL.COMPS < [ LOCAL.KEYS [ KEY _of_rel,
                                         --COMPKEY mofy_rel ],
                            OPT + ] > ].

dom_ord_yofc_synsem := basic_dom_ord_synsem & basic_noun_npcomp_synsem &
  [ LOCAL.CAT.VAL [ SPR < [ LOCAL.KEYS.ALTKEY mofy_rel ] >,
                    COMPS < [ LOCAL.KEYS.KEY yofc_rel,
                              OPT - ] > ] ].

dom_card_synsem := basic_dom_synsem & basic_noun_npcomp_synsem & 
  [ LOCAL [ CAT [ HEAD.PRD -,
                  VAL [ SPR < [ LOCAL.KEYS.ALTKEY mofy_rel ] >,
                        COMPS < [ LOCAL.KEYS.KEY yofc_rel,
                                  OPT + ] > ] ],
            KEYS.ALTKEY relation ] ].

abstr_n_day_of_month_le := hc_word & non_affix_bearing &
  [ SYNSEM basic_dom_synsem ].

n_day_of_month_le := abstr_n_day_of_month_le &
  [ SYNSEM dom_ord_synsem ].

n_day_of_month_card_le := abstr_n_day_of_month_le &
  [ SYNSEM dom_card_synsem ].

; 'Tuesday (the first (of October))', 'Tuesday (October first)'
; 'Tuesday of that week'
; DPF 31-Jul-99 - Made SYNSEM be nonpro_nomod_onearg_synsem instead of 
; nomod_basic_onearg_synsem since they can appear in noun-noun compounds,
; and also in appositive constructions.


; 'morning' of 'each morning', 'Tuesday morning'
n_day_part_le := que_word &
  [ SYNSEM noun_two_arg_nomod_synsem &
	   [ LOCAL [ STEMHEAD countnstem,
                     ARG-S < #spr . #comps >,
                     CAT [ HEAD noun*,
			   VAL 
			    [ SPR < synsem & #spr &
				  [ LOCAL local_min &
					  [ CAT [ HEAD det,
						  VAL [ SUBJ < >,
							COMPS *olist* ] ],
                                            CONT.INDEX #ind,
					    KEYS [ KEY quant_or_wh_rel,
						   ALTKEY dofw_rel ] ],
				    OPT - ] >,
			      COMPS #comps &
                                 < [ LOCAL [ CAT.VAL.COMPS < >,
					     CONT [ TOP #nhand,
                                                    INDEX #ind ],
					     KEYS [ KEY _of_rel,
						    --COMPKEY dofm_rel ] ],
				     NON-LOCAL.SLASH 0-dlist ] > ] ],
		     AGR #ind,
                     CONT [ INDEX #ind,
			    LISZT <! #key !>,
			    --TOPKEY #key,
                            H-CONS <! !> ],
		     KEYS.KEY #key & day_part_rel &
			   [ HANDEL #nhand,
			     INST #ind ] ],
	     MODIFIED notmod ] ].

n_holiday_le := np_sing_word &
  [ SYNSEM.LOCAL [ KEYS.KEY holiday_rel,
		   CONT.INDEX.PNG png & [ PN 3sg* ] ] ].

; 'Advent', 'summer'
n_season_le := np_sing_word &
  [ SYNSEM.LOCAL [ KEYS.KEY season_gen_rel,
		   CONT.INDEX.PNG png & [ PN 3sg* ] ] ].

; "the morning" etc.

n_def_day_part_le := norm_no_affix_word &
  [ SYNSEM nomod_onearg_synsem &
	   [ LOCAL [ ARG-S < #spr >,
		     CAT [ HEAD noun*,
			   VAL 
			    [ SPR < synsem & #spr &
				  [ LOCAL local_min &
					  [ CAT [ HEAD det,
						  VAL [ SUBJ < >,
							COMPS *olist* ] ],
					    CONT [ INDEX #inst,
                                                   LISZT <! relation !> ],
					    KEYS.KEY abstr_def_rel ],
                                    OPT - ] >,
			      COMPS < > ],
			   POSTHEAD + ],
		     AGR #inst,
                     CONT [ INDEX #inst,
			    LISZT <! #key !> ],
		     KEYS.KEY def_day_part_rel & #key &
			   [ INST #inst ] ],
	     MODIFIED notmod ] ].


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Degree specifiers for adjectives
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; 'very, really, much, a lot, almost';
; Treated as measure phrases, selected as specifiers by adjectives.

;; DPF 10-Oct-00 - SPR.KEYS.KEY and SPEC.KEYS.KEY need fixing

basic_degree_spec_synsem := canonical_synsem &
  [ LOCAL nomod_local &
          [ CAT [ HEAD adv,
                  VAL [ SUBJ < >,
                        SPR < [ LOCAL local_min &
                                      [ CAT [ HEAD adv,
                                              VAL.SPR *olist* ],
                                        CONT.TOP #khand,
                                        KEYS.KEY degree_rel &
                                            [ DARG #darg ] ],
                                NON-LOCAL.QUE 0-dlist,
                                OPT + ] >,
                        SPEC < [ LOCAL [ KEYS.KEY.HANDEL #khand,
                                         CONT [ TOP #hand,
                                                INDEX #specind ] ] ] > ] ],
            CONT [ TOP #hand,
                   INDEX #specind ],
            KEYS.KEY degree_rel &
                     [ HANDEL #khand,
                       DIM #darg ] ] ].

degree_spec_synsem := basic_degree_spec_synsem &
  [ LOCAL.CAT.VAL.COMPS < > ].

lex_degree_spec_synsem := degree_spec_synsem & lex_synsem.

; For measure_nps as in "two foot tall"
phr_degree_spec_synsem := degree_spec_synsem & phr_synsem.

eq_degree_spec_synsem := basic_degree_spec_synsem & lex_synsem &
  [ LOCAL [ CAT.VAL [ SPEC < [ LOCAL.CONT.INDEX #adjind ] >,
                      COMPS < [ LOCAL [ CAT.VAL.COMPS *olist*,
                                        KEYS.KEY _as_comp_rel &
                                                      [ ARG3 #cind ] ] ] > ],
            KEYS.KEY eq_degree_rel & [ arg #adjind,
                                       arg3 #cind ] ] ].

basic_degree_spec_word := que_word &
  [ SYNSEM lex_degree_spec_synsem &
           [ LOCAL [ KEYS.KEY #key,
                     CONT.LISZT.LIST < #key, ... > ] ] ].

degree_spec_word := basic_degree_spec_word & nonque &
  [ SYNSEM.LOCAL.CONT [ LISZT <! degree_rel &
                               [ DARG deg-ind ] !>,
                        H-CONS <! !> ] ].

adv_degree_spec_le :< degree_spec_word.

; "how tall"
adv_wh_degree_spec_le := basic_degree_spec_word &
  [ SYNSEM [ LOCAL.CONT [ LISZT <! abstr_deg_rel &
				 [ HANDEL #hand,
				   DARG #darg ],
			         which_rel &
			         [ BV #darg,
				   RESTR #rhand ] !>,
			  H-CONS <! qeq & [ SC-ARG #rhand,
					    OUTSCPD #hand ] !> ],
	     NON-LOCAL.QUE 1-dlist & [ LIST < handle > ] ] ].

adv_degree_spec_noprep_le := degree_spec_word &
  [ SYNSEM.LOCAL.CAT.VAL.SPEC < [ LOCAL.CAT.HEAD adverbee & strict_type ] > ]. 

eq_degree_spec_le := que_word &
  [ SYNSEM eq_degree_spec_synsem &
           [ LOCAL [ KEYS.KEY #key,
                     CONT [ LISZT <! #key !>,
                            H-CONS <! !> ] ] ] ].


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Numbers
;;
;; DPF 14-Apr-01 - Numerals combine with their complements and specifiers as
;; laid out in Smith 2000, but since the specifier-head rule makes the TOP
;; handle of the phrase be that of the spr dtr, we can't have numerals simply
;; identify their TOP value with their KEY.HANDEL value or something similar.
;; Hence, numerals combine using the HANDEL of the attribute ALTKEY, which
;; points to the KEY for simple numerals like "two", but to the plus_rel or
;; the times_rel for more interesting numerals like "hundred".  
;;   The TOP handle is identified with the SPEC's handle, so when a numeral is
;; built using the specifier-head rule, the resulting TOP handle will be the 
;; same as the TOP handle of the head-dtr.  This is useful because of two other
;; regularities:
;;   (1) the MOD's handle is identified with the ALTKEY's handle; and
;;   (2) the intersective modifier-head rule identifies the MOD's handle with
;;       the modifier's TOP handle.
;; This ensures that the TOP handle of a numeral modifier will be identified
;; with the ALTKEY's handle, as desired.

; For numerals
num_synsem := zero_arg &
  [ LOCAL [ CAT.VAL.SUBJ < >,
	    CONT.LISZT.LIST < integer_rel, ... > ] ].

int_adj_synsem := canonical_synsem &
  [ LOCAL [ CAT [ HEAD intadj,
                  HC-LEX +,
                  VAL.SUBJ < >,
		  POSTHEAD - ],
	    CONT.INDEX #index & ref-ind,
	    KEYS.KEY integer_rel &
                       [ ARG #index ] ] ].

; DPF 6-Jun-01 - Added MODIFIED.PERIPH - to avoid unwanted reading for 
; "two hundred three chairs".
; But this blocks "two competent programmers", so make [PERIPH na].

norm_num_synsem := num_synsem & int_adj_synsem &
  [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL intersective_mod &
                                     [ CAT nbar_cat,
                                       CONT [ TOP #mhand,
                                              INDEX #index ],
                                       KEYS.KEY nonpro_rel,
                                       CONJ cnil,
                                       AGR #index & [ DIVISIBLE +* ] ],
                               MODIFIED.PERIPH na ] >,
                  VAL.SPEC < [ LOCAL.CONT.TOP #top ] > ],
                           
            CONT [ TOP #top,
                   INDEX #index,
                   LISZT.LIST.FIRST #key ],
            KEYS [ KEY #key,
                   ALTKEY.HANDEL #mhand ] ],
    MODIFIED.PERIPH + ].

norm_num_word := nontopkey &
  [ SYNSEM norm_num_synsem &
           [ LOCAL.CAT.HEAD.PRD - ] ].

;; some norm_card-words must remain unspecified for ORDINAL

norm_card_word := norm_num_word &
  [ SYNSEM.LOCAL.KEYS.KEY card_rel ].

;; those cardinals that take no complements may be safely specified as 
;; -ORDINAL, and as plural (since they're normal)

card_only_word := norm_card_word &
  [ SYNSEM.LOCAL [ CAT.HEAD.ORDINAL -,
                   CONT.INDEX.PNG png & [ PN 3pl ] ] ].

; should be POSTHEAD -
; These are adjectives only

ord_word := norm_num_word & 
  [ SYNSEM.LOCAL [ CAT [ HEAD.ORDINAL +,
			 VAL.COMPS < > ],
		   KEYS.KEY ord_rel ] ].

;;  Specifiers and modifiers will agree with heads on the value
;; of the head feature ORDINAL -- this allows propagation
;; of such a feature from complements, where (on the current account)
;; the ORDINAL feature gets a value from the lexicon.

;; distribution of ORDINAL:
;;   ordinal lex_entries are ORDINAL +
;;   complement_free lex_entries are ORDINAL -
;;   all lex_entries for "one" are ORDINAL - 
;;   other lex_entries are unspecified for ORDINAL
;; Since (the HEAD feature of) the determiner "one" is outside this 
;; hierarchy, it can't appear as a specifier or complement.


;; The sorts below refer only to lexical entries, so there's presumably no
;; harm in making conventions about the order of the RELs on their LISZTs
;; No particular order is assumed for any phrase containing these entries.

;; It will be hard to do optional complements here and get the LISZTs to
;; come out right.  Another problem with optional complementation is that
;; entries with complements must be -ORD, while their complement-taking
;; counterparts must be underspecified for ORD 
;;   (cf. "twenty" vs. "twenty one" vs. "twenty first")


complement_free_number := norm_num_word &
			  [ SYNSEM.LOCAL.CAT.VAL.COMPS < > ].

complemented_number := norm_num_word &
  [ SYNSEM 
    [ LOCAL [ CAT [ HEAD.ORDINAL #ord,
		    VAL.COMPS < [ OPT -,
				  LOCAL [ CAT [ HEAD intadj & 
						    [ ORDINAL #ord ],
						VAL [ SPR *olist*,
						      COMPS < > ] ],
					  CONT.INDEX #arg & [ PNG #num ] ],
                                  NON-LOCAL.SLASH 0-dlist ] > ],
	      CONT [ INDEX.PNG #num,
		     LISZT.LIST < relation, plus_rel, ... > ],
	      KEYS.KEY.ARG #arg ] ] ].
		   

; Non-identity stipulated for handle of SPR and handle of CONT in order to
; prevent the regular head-spec rule (non-head-compositional) from applying.
; Need head-compositional since the handle of the phrase "two hundred" must
; be the handle of the times_rel from the head "hundred" (which contains as
; arguments the other two handles).

specified_number := norm_num_word &
  [ SYNSEM.LOCAL.CAT.VAL.SPR < synsem &
			       [ OPT -,
				 LOCAL [ CAT [HEAD intadj & 
						   [ ORDINAL - ],
					      VAL [ COMPS < >,
						    SPR *olist* ] ] ] ] >].
				 
;; DPF 10-Oct-00 - SPR.KEYS.KEY needs fixing

unspecified_num := norm_num_word &
  [ SYNSEM.LOCAL [ CAT.VAL.SPR < [ LOCAL local_min &
					   [ CAT [ HEAD adv,
						   VAL [ SPR  *olist*,
							 COMPS *olist* ] ],
                                             CONT.TOP #khand,
					     KEYS.KEY degree_rel &
						 [ DARG #darg ] ],
				   OPT + ] >,
		   KEYS.KEY [ HANDEL #khand,
			      DIM #darg ] ] ].

unspecified_num_without_complements := complement_free_number & 
                                       unspecified_num &

  [ SYNSEM.LOCAL [ CONT.LISZT <! const_rel !>,
                   KEYS [ KEY #key,
                          ALTKEY #key ] ] ].

approx_unspecified_num_without_complements := complement_free_number & 
                                              unspecified_num &
  [ SYNSEM.LOCAL [ CONT [ INDEX #inst,
                          LISZT <! const_rel,
                                 approx_grad_rel &
                                 [ DARG #inst ]  !> ],
                   KEYS [ KEY #key,
                          ALTKEY #key ] ] ].

unspecified_num_with_complements := complemented_number & unspecified_num &
  [ SYNSEM.LOCAL 
     [ CAT.VAL.COMPS < [ LOCAL.KEYS [ KEY const_rel,
                                      ALTKEY.HANDEL #chandle ] ] >,
       CONT.LISZT <! const_rel &
                     [ HANDEL #khandle,
                       ARG #arg ],
                     #altkey &
                     [ ARG #arg,
                       TERM1  #khandle,
                       TERM2  #chandle] !>,
       KEYS.ALTKEY #altkey ] ].

specified_num_without_complements := complement_free_number & 
                                     specified_number &
  [ SYNSEM.LOCAL 
	[ CAT.VAL.SPR < [ LOCAL.KEYS [ KEY.ARG #arg,
                                       ALTKEY.HANDEL #shandle ] ] >,
          CONT.LISZT <! const_rel & [ HANDEL #khandle,
                                      ARG #arg ],
                        times_rel & #altkey &
                        [ FACTOR1  #shandle,
                          FACTOR2  #khandle,
                          ARG #arg & [ PNG.PN 3pl ] ] !>,
          KEYS.ALTKEY #altkey ] ].

specified_num_with_complements := complemented_number & specified_number & 
  [ SYNSEM.LOCAL 
    [ CAT.VAL [ SPR < [ LOCAL.KEYS [ KEY.ARG #arg,
                                     ALTKEY.HANDEL #shandle ] ] >,
                COMPS < [ LOCAL.KEYS.ALTKEY.HANDEL #chandle ] > ],
      CONT.LISZT <! [ HANDEL #khandle,
                      ARG #arg ],
                    #altkey &
		    [ TERM1  #thandle,
		      TERM2  #chandle,
                      ARG #arg ],
		    times_rel &
                    [ HANDEL #thandle,
                      FACTOR1 #shandle,
                      FACTOR2 #khandle,
                      ARG #arg ] !>,
      KEYS.ALTKEY #altkey ] ].

adj_bare_unspecified_card_le := card_only_word & 
			    unspecified_num_without_complements.
adj_bare_specified_card_le := card_only_word & 
			  specified_num_without_complements.
adj_complemented_unspecified_card_le := norm_card_word & 
				    unspecified_num_with_complements.
adj_complemented_specified_card_le := norm_card_word & 
				  specified_num_with_complements.
adj_bare_unspecified_ord_le := ord_word & unspecified_num_without_complements.
adj_bare_specified_ord_le := ord_word & specified_num_without_complements.

adj_approx_bare_unspecified_card_le := card_only_word & 
			           approx_unspecified_num_without_complements.

;; the constraints appropriate for all lexical entries for "one"
;; DPF 2-Feb-01 - Removed [ SPEC..OPT - ], since blocked "the one year old b0y"
;; and it's not clear what it was for.

one_num_synsem := num_synsem & int_adj_synsem &
  [ LOCAL [ CAT [ HEAD intadj & 
                       [ MOD < >,
                         ORDINAL -,
                         PRD - ],
                  VAL [ SPR < >,
                        COMPS < >,
                        SPEC < [ LOCAL.CONT.TOP #top ] > ] ],
            CONT [ TOP #top,
                   LISZT <! card_rel & #key &
                          [ CONST_VALUE "1" ] !> ],
            KEYS [ KEY #key,
                   ALTKEY #key ] ] ].

one_word := non_affix_bearing &
  [ SYNSEM one_num_synsem ].

adj_one_sing_le := one_word & 
  [ SYNSEM.LOCAL [ CAT.HEAD intadj,
                   CONT.INDEX.PNG.PN 3sg* ] ].

adj_one_plur_le := one_word & 
  [ SYNSEM.LOCAL [ CAT.HEAD intadj1,
                   CONT.INDEX.PNG.PN 3pl* ] ].

; Temporary types to cope with unknown words - used in PET
basic_unknown_word := mcna & nonconj & nonmsg & hc-to-phr &
  [ INFLECTED +,
    SYNSEM.LOCAL.KEYS.KEY.LABEl *cons* ].

unknown_word := basic_unknown_word & topkey.

generic_v_np*_trans_lex_entry := unknown_word &
  [ SYNSEM np_trans_verb &
	   [ LOCAL [ CAT.VAL.COMPS < [ OPT + ] >,
		     CONT.LISZT <! #key !>,
                     KEYS.KEY #key & generic_verb_rel ] ] ].

v_np*_trans_bse_nale := generic_v_np*_trans_lex_entry &
  [ SYNSEM.LOCAL bse_verb ].
v_np*_trans_pres3sg_nale := generic_v_np*_trans_lex_entry &
  [ SYNSEM.LOCAL third_sg_fin_verb ].
v_np*_trans_presn3sg_nale := generic_v_np*_trans_lex_entry &
  [ SYNSEM.LOCAL non_third_sg_fin_verb ].
v_np*_trans_past_nale := generic_v_np*_trans_lex_entry &
  [ SYNSEM.LOCAL past_or_subj_verb ].
v_np*_trans_psp_nale := generic_v_np*_trans_lex_entry &
  [ SYNSEM.LOCAL psp_verb ].
v_np*_trans_prp_nale := generic_v_np*_trans_lex_entry &
  [ SYNSEM.LOCAL prp_verb ].

generic_n_intr_lex_entry := unknown_word &
  [ SYNSEM mass_or_count_ppcomp_synsem & 
	   [ LOCAL [ CAT.VAL.SPR < [ LOCAL.KEYS.KEY quant_or_wh_rel ] >,
                     CONT.LISZT <! #key !>,
		     KEYS.KEY #key & generic_noun_rel ],
	     MODIFIED notmod ] ].

n_intr_sg_nale := generic_n_intr_lex_entry &
  [ SYNSEM.LOCAL mass_count_noun ].

n_intr_pl_nale := generic_n_intr_lex_entry &
  [ SYNSEM.LOCAL plur_noun ].

n_proper_nale := basic_unknown_word & nonslash & nonrel & nonque &
  [ SYNSEM proper_n_synsem &
           [ LOCAL.KEYS.KEY named_abb_rel &
                            [ NAMED 'noname ] ] ].

adj_intrans_nale := unknown_word &
  [ SYNSEM intrans_adj_synsem &
           [ LOCAL [ CAT [ HEAD.MOD < [ LOCAL.CONT.INDEX #ind ] >,
                           VAL.SUBJ < [ LOCAL.CONT.INDEX #ind ] > ],
                     CONT.LISZT <! #key !>,
                     KEYS.KEY #key & generic_adj_rel & [ ARG #ind ] ] ] ].


adv_word_nale := unknown_word & nonque & nonslash & nonrel &
   [ SYNSEM intersect_adverb_synsem & 
            [ LOCAL [ CAT.HEAD.MOD < [ LOCAL.CAT.HEAD v_or_g_or_a & 
                                                    strict_type ] >,
                      KEYS.KEY generic_adv_rel ] ] ].
