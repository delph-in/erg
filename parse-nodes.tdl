;;; -*- Mode: TDL; Package: DISCO -*-
;;;
;;;  parsenodes.tdl
;;;
;;;  Parse node definitions to support node labels in printed parse trees
;;;
;;;  Created: Dan Flickinger 15-Aug-94
;;;  Last revised: Dan Flickinger, 24-Feb-97
;;;
;;;  $Id$

;;; AAC - for the LKB, the order of definitions in the file is significant
;;; because the definitions are tried in order until one matches.
;;; This replaces the functionality of the variable *node-labels*
;;; in the PAGE machinery

:begin :instance.

slash := meta &
  [ NON-LOCAL.SLASH 1-dlist &
		    [ LIST < local > ],
    META-PREFIX "/",
    META-SUFFIX "" ].

;;; labels
;;; "ADV" "PP" "CONJ" "NP" "S" "VP" "V" "P" "DET" "N" "ADJ" "COMP" "NEG"


adv1 := label &
  [ LOCAL.CAT.HEAD adv,
    LABEL-NAME "ADV" ].

adv2 := label &
  [ LOCAL.CAT [ HEAD root-marker,
		VAL.COMPS *cons* ],
    LABEL-NAME "ADV" ].

adv3 := label &
  [ LOCAL.CAT.HEAD detspec,
    LABEL-NAME "ADV" ].


pp :=  label &
   [ LOCAL.CAT [ HEAD prep,
		 VAL.COMPS *olist* ], 
     LABEL-NAME "PP" ].

conj := label &
  [ LOCAL.CAT [ HEAD conjh,
		VAL.COMPS *cons* ],
    LABEL-NAME "CONJ" ].

np :=  label &
  [ LOCAL.CAT [ HEAD noun,
		VAL [ SUBJ < >,
			  SPR *olist* ] ],
    LABEL-NAME "NP" ].


s1 := label &
  [ LOCAL.CAT [ HEAD verbal & strict_head,
		VAL [ SUBJ < >,
			  SPR *olist*,
			  COMPS *olist* ] ],
    LABEL-NAME "S" ].

s2 := label &
  [ LOCAL.CAT [ HEAD root-marker &
		     [ MOD no-mod ],
		VAL [ SUBJ < >,
			  COMPS *olist* ] ],
    LABEL-NAME "S" ].

vp :=  label &
   [ LOCAL.CAT [ HEAD verbal & strict_head,
		 VAL [ SUBJ < synsem >,
			   COMPS *olist* ] ],
     LABEL-NAME "VP" ].

v := label &
  [ LOCAL.CAT [ HEAD verb & strict_head,
		VAL.COMPS *cons* ],
    LABEL-NAME "V" ].


p := label &
  [ LOCAL.CAT [ HEAD prep,
		VAL.COMPS *cons* ],
    LABEL-NAME "P" ].

det := label &
  [ LOCAL.CAT.HEAD det &
		   [ MOD no-mod ],
    LABEL-NAME "DET" ].

n := label &
  [ LOCAL.CAT [ HEAD nominal,
		VAL [ SUBJ < >,
			  SPR < synsem > ] ],
    LABEL-NAME "N" ].

adj := label &
  [ LOCAL.CAT.HEAD adj,
    LABEL-NAME "ADJ" ].

comp := label &
  [ LOCAL.CAT.HEAD comp,
    LABEL-NAME "COMP" ].

neg := label &
  [ LOCAL.CAT.HEAD neg,
    LABEL-NAME "NEG" ].

prdp :=  label &
  [ LOCAL.CAT [ HEAD.PRD +,
		VAL.SUBJ < synsem > ],
    LABEL-NAME "XP" ].

:end :instance.
